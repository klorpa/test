;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:AUTO_SPLIT.ERB
;	Facility	:引数として渡されたARGSから、特定の文字列ARGS:1で区切った場合のARG番目の文字列を返す関数@AUTO_SPLIT
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2011/09/25		Ｎ鳥					新規作成
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;@AUTO_SPLIT
;ARGS   操作する文字列
;ARGS:1 ARGSを区切る文字列
;ARG 　 ARGSを区切った後で、操作する箇所。ARGS:2が指定されている場合、発見したARGS:2の場所+ARG番目の文字列を返す。
;ARGS:2 検索する文字列
;================================================
@AUTO_SPLIT(ARGS , ARGS:1 , ARG = 0 , ARGS:2 = "")
#FUNCTIONS
#LOCALSSIZE 200
#LOCALSIZE 1
#DIMS 保存文字列, 1
IF ARGS != "再利用"
	VARSET LOCALS
	SPLIT ARGS , ARGS:1 , LOCALS
	保存文字列 = %ARGS%
ENDIF
IF ARGS:2 != ""
	LOCAL = FINDELEMENT(LOCALS , ARGS:2 , , , 1)
	IF LOCAL != -1
		SIF ARG + LOCAL < 0
			PRINTFORML エラー：関数@AUTO_SPLITにて{LOCAL}番目の文字列に%ARGS:2%を発見しましたが、第三引数{ARG}の指定が不正です。
		RETURNF LOCALS:(LOCAL + ARG)
	ELSE
		RETURNF
	ENDIF
ELSE
	SIF ARG < 0
		PRINTFORML エラー：関数@AUTO_SPLITの第三引数として{ARG}が渡されました
	RETURNF LOCALS:ARG
ENDIF

