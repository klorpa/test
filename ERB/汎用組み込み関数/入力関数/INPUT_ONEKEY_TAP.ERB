;--------------------------------------------------------------------------------------------------
;キー入力ウィンドウ関数　@INPUT_ONEKEY_TAP
;ゲームパッドめいた配置のボタンと入力待機状況を生成
;キー入力でもマウスボタンでも入力可
;
;ARG 　　 "十字キー"の扱い。
;  　値が0なら　　それ以外の値なら
;
; 　　　8　　　　　　W
;　　　4 6          A D (大文字小文字の区別なし)
; 　　　2　　　　    S
;
;ARGS　 上下のウィンドウ装飾に用いる文字
;ARGS:1 以降の文字列の区切りに用いる文字。デフォルトでは"_"
;ARG_T  入力対応ボタン。"キー_ボタンとしての表示文字_添付テキスト_色"の形式で記述
;--------------------------------------------------------------------------------------------------
@INPUT_ONEKEY_TAP(ARG:0 = 0,ARGS:0 = "-",ARGS:1 = "_",ARG_T:0 = "",ARG_T:1 = "",ARG_T:2 = "",ARG_T:3 = "",ARG_T:4 = "",ARG_T:5 = "",ARG_T:6 = "",ARG_T:7 = "",ARG_T:8 = "",ARG_T:9 = "",ARG_T:10 = "",ARG_T:11 = "",ARG_T:12 = "",ARG_T:13 = "",ARG_T:14 = "",ARG_T:15 = "",ARG_T:16 = "",ARG_T:17 = "",ARG_T:18 = "",ARG_T:19 = "")
#DIMS ARG_T,20
#DIMS ボタン,20
#DIMS キー,20
#DIMS ボタンタグ,20
#DIM ボタンカラー,20
#DIM ボタン文字数,1 = 0
#DIM 一列幅,1 = 0
#DIM 横,1
#DIM 縦,1
#DIM リターンキー有効,1
#LOCALSSIZE 4
#LOCALSIZE 1
ARG:1 = LIMIT(ARG:1,1,20)
FOR LOCAL,0,20
	ARRAYREMOVE LOCALS,0,4
	SPLIT ARG_T:LOCAL,ARGS:1,LOCALS
	キー:LOCAL         = %LOCALS:0%
	ボタン:LOCAL       = %LOCALS:1 == "" ? "" # "["+LOCALS:1+"]" %
	ボタンタグ:LOCAL   = %LOCALS:2%
	ボタンカラー:LOCAL = ISNUMERIC(LOCALS:3) == 1 && LOCALS:3 != "" ? TOINT(LOCALS:3) # GETCOLOR()
	一列幅 = MAX(一列幅,STRLENS(LOCALS:0)+STRLENS(LOCALS:1)+STRLENS(LOCALS:2)+2)
NEXT

DRAWLINEFORM %ARGS:0%
FOR 縦,0,4
	SELECTCASE 縦
		CASE 1
			PRINTPLAIN 　 　
			PRINTBUTTON \@ ARG == 0 ? [８] # [Ｗ] \@,\@ ARG == 0 ? 8 # w \@
			PRINTPLAIN 　 　
		CASE 2
			PRINTPLAIN 　
			PRINTBUTTON \@ ARG == 0 ? [４] # [Ａ] \@,\@ ARG == 0 ? 4 # a \@
			PRINTPLAIN ┼
			PRINTBUTTON \@ ARG == 0 ? [６] # [Ｄ] \@,\@ ARG == 0 ? 6 # d \@
			PRINTPLAIN 　
		CASE 3
			PRINTPLAIN 　 　
			PRINTBUTTON \@ ARG == 0 ? [２] # [Ｓ] \@,\@ ARG == 0 ? 2 # s \@
			PRINTPLAIN 　 　
		CASEELSE
			PRINTPLAIN 　　 　 　　
	ENDSELECT
	FOR 横,縦*5,縦*5+5,1
		SETCOLOR ボタンカラー:横
		PRINTBUTTON ボタン:横,キー:横
		PRINTPLAINFORM %ボタンタグ:横,一列幅-STRLENS(ボタンタグ:横),LEFT%
		RESETCOLOR
	NEXT
	PRINTL
NEXT
DRAWLINEFORM %ARGS:0%

リターンキー有効 = 0
LOCALS = \@ ARG == 0 ? 8462 # WASDwasd \@
FOR LOCAL,0,20
	LOCALS = %LOCALS%%キー:LOCAL%
	SIF キー:LOCAL == "" && ボタン:LOCAL != ""
		リターンキー有効 = 1
NEXT
CALL INPUT_CHAR(\@ FLAG:ダンジョン内操作設定 == 0 ? 8462 # wasdWASD \@,リターンキー有効)
IF RESULTS != ""
	REUSELASTLINE 
	CLEARLINE 1
ENDIF

;入力用シンタックスシュガー。
;ARG_T配列にRESULTS:0〜19を代入する
@INPUT_ONEKEY_TAP_RESULTS(ARG = 0,ARGS = "-",ARGS:1 = "_")
CALL INPUT_ONEKEY_TAP(ARG,ARGS,ARGS:1,RESULTS:0,RESULTS:1,RESULTS:2,RESULTS:3,RESULTS:4,RESULTS:5,RESULTS:6,RESULTS:7,RESULTS:8,RESULTS:9,RESULTS:10,RESULTS:11,RESULTS:12,RESULTS:13,RESULTS:14,RESULTS:15,RESULTS:16,RESULTS:17,RESULTS:18,RESULTS:19)
