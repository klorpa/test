;-------------------------------------------------
;2択提示関数　@INPUT_YN
;いちいち、INPUT_LOOP書くのめんどいよ！
;っていうか、2択提示はめちゃめちゃ多いんだから、関数化しちゃえばいいじゃないかときづく
;INPUTINTでもまぁほとんど同じだけど、これほんとよく使う定形だから、別関数として独立させる
;0、1しかこないのでONEINPUTに変更
;-------------------------------------------------
@INPUT_YN,ARGS = "はい",ARGS:1 = "いいえ",ARG = 1 
REDRAW 0
SELECTCASE ARG
	CASE 1
		PRINTFORML [0] %ARGS%
		PRINTFORML [1] %ARGS:1%
	CASE 2
		PRINTFORM [0] %ARGS% 
		PRINTFORML [1] %ARGS:1%
ENDSELECT
$INPUT_LOOP

ONEINPUTS
IF RESULTS == "0" || RESULTS == " " || (FLAG:二択入力設定 == 1 && (ARGS == "はい" || ARGS == "Yes") && (RESULTS == "y" || RESULTS == "Y")) || (FLAG:二択入力設定 == 2 && (RESULTS == "y" || RESULTS == "Y"))
	REDRAW 1
	RETURN 0
ELSEIF RESULTS == "1" || (FLAG:二択入力設定 == 1 && (ARGS:1 == "いいえ" || ARGS:1 == "No") && (RESULTS == "n" || RESULTS == "N")) || (FLAG:二択入力設定 == 2 && (RESULTS == "n" || RESULTS == "N"))
	REDRAW 1
	RETURN 1
ELSE
	SIF RESULTS != ""
		CLEARLINE 1
	REUSELASTLINE 
	GOTO INPUT_LOOP
ENDIF