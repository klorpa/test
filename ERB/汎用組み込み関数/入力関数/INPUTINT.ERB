;-------------------------------------------------
;数値型インプット関数@INPUTINT
;　引数0〜19：入力可能な数字
;数値の入力待ちをします。引数に含まれない数字を選んだら自動的に入力待ちをし直します
;有効な数字が入力されたらそれをRESULTへ返して戻ります
;選択肢を汎用処理するため、ループの構文を書く必要がなくなります
;PANCTIONからの移植
;引数が全部一桁のときはONEINPUTにするに変更
;-------------------------------------------------
;注意：0を選択肢に含む場合は引数の並びの最後に配置しないこと
;（例：INPUTINT(3, 2, 1, 0)はダメ。INPUTINT(0, 1, 2, 3)はＯＫ）
;-------------------------------------------------
@INPUTINT(ARG:0 = 0, ARG:1 = 0, ARG:2 = 0, ARG:3 = 0, ARG:4 = 0, ARG:5 = 0, ARG:6 = 0, ARG:7 = 0, ARG:8 = 0, ARG:9 = 0, ARG:10 = 0, ARG:11 = 0, ARG:12 = 0, ARG:13 = 0, ARG:14 = 0, ARG:15 = 0, ARG:16 = 0, ARG:17 = 0, ARG:18 = 0, ARG:19 = 0)
#LOCALSIZE 2
LOCAL:1 = 0
FOR LOCAL, 0, 20
	IF ARG:LOCAL < 0 || ARG:LOCAL > 9
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1
	INPUT
ELSE
	ONEINPUT
ENDIF
SIF EQUALCHECK(RESULT, ARG:0, ARG:1, ARG:2, ARG:3, ARG:4, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9, ARG:10, ARG:11, ARG:12, ARG:13, ARG:14, ARG:15, ARG:16, ARG:17, ARG:18, ARG:19)
	RETURN RESULT
CLEARLINE 1
REUSELASTLINE 
RESTART