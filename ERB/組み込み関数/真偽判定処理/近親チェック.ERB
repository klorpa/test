@近親チェック(ARG , ARG:1)
#FUNCTION
#DIM CHARA , 2
#DIM 親ＩＤ , 4

FOR LOCAL , 0 , 2
	CHARA = ARG:LOCAL
	CHARA:1 = ARG:(!LOCAL)
	FOR LOCAL:1 , 0 , 7
		;続柄を決定
		LOCALS:LOCAL = %AUTO_SPLIT("父_母_兄弟_姉妹_娘_息子" , "_" , LOCAL:1)%
		;相手が続柄に合う存在か判定
		IF AUTO_SPLIT(CSTR:(CHARA):@"近親判定_%LOCALS:LOCAL%" , "_" , 0 , CSVNAME(NO:(CHARA:1) , 0) ) != ""
			;性別と続柄が合わない場合ここで変更
			;父・母は種になったか・産んだかだけで判定する
			IF GROUPMATCH(LOCALS:LOCAL , "兄弟" , "息子") && !IS_MALE(CHARA:1)
				;兄弟or息子なのに性別女なら姉妹or娘
				LOCALS:LOCAL = \@ LOCALS:LOCAL == "兄弟" ? 姉妹 # 娘 \@
			ELSEIF GROUPMATCH(LOCALS:LOCAL , "姉妹" , "娘") && IS_MALE(CHARA:1)
				;その逆
				LOCALS:LOCAL = \@ LOCALS:LOCAL == "姉妹" ? 兄弟 # 息子 \@
			ENDIF
			BREAK
		ENDIF
		;ここまでに見つけて無かったら、CSVでは無関係のキャラなので妊娠関係の変数から判断する
		IF LOCAL:1 == 5
			IF CFLAG:CHARA:娘の父親の固有番号娘 == CFLAG:(CHARA:1):キャラ固有の番号
				LOCALS:LOCAL = 父
				LOCALS:(!LOCAL) = \@ IS_MALE(CHARA) ? 息子 # 娘 \@
			ELSEIF CFLAG:CHARA:娘の産みの親の固有番号娘 == CFLAG:(CHARA:1):キャラ固有の番号
				LOCALS:LOCAL = 母
				LOCALS:(!LOCAL) = \@ IS_MALE(CHARA) ? 息子 # 娘 \@
			ELSEIF CFLAG:CHARA:キャラ固有の番号 == CFLAG:(CHARA:1):娘の父親の固有番号娘
				LOCALS:LOCAL = \@ IS_MALE(CHARA:1) ? 息子 # 娘 \@
				LOCALS:(!LOCAL) = 父
			ELSEIF CFLAG:CHARA:キャラ固有の番号 == CFLAG:(CHARA:1):娘の産みの親の固有番号娘
				LOCALS:LOCAL = \@ IS_MALE(CHARA:1) ? 息子 # 娘 \@
				LOCALS:(!LOCAL) = 母
			ELSE
				親ＩＤ:0 = (CFLAG:CHARA:娘の父親の固有番号娘 == -1 ? 9999 # CFLAG:CHARA:娘の父親の固有番号娘 )
				親ＩＤ:1 = (CFLAG:(CHARA:1):娘の父親の固有番号娘 == -1 ? 9998 # CFLAG:(CHARA:1):娘の父親の固有番号娘 )
				親ＩＤ:2 = (CFLAG:CHARA:娘の産みの親の固有番号娘 == -1 ? 9997 # CFLAG:CHARA:娘の産みの親の固有番号娘 )
				親ＩＤ:3 = (CFLAG:(CHARA:1):娘の産みの親の固有番号娘 == -1 ? 9996 # CFLAG:(CHARA:1):娘の産みの親の固有番号娘 )
				IF GROUPMATCH(親ＩＤ:0 , 親ＩＤ:1 , 親ＩＤ:3) || GROUPMATCH(親ＩＤ:2 , 親ＩＤ:1 , 親ＩＤ:3)
					;二人の父・母のいずれかが一つでも一致したら兄弟姉妹扱い
					LOCALS:LOCAL = \@ IS_MALE(CHARA:1) ? 兄弟 # 姉妹 \@
					LOCALS:(!LOCAL) = \@ IS_MALE(CHARA) ? 兄弟 # 姉妹 \@
				ELSE
					RETURNF 0
				ENDIF
			ENDIF
			GOTO HERE
		ENDIF
	NEXT
NEXT
$HERE
RESULTS = %LOCALS%
RESULTS:1 = %LOCALS:1%
RETURNF 1
