;-------------------------
;画面表示更新用関数
;ARGS
;	D/DUNGEON/ダンジョン	ダンジョンの一般的な画面表示
;	NONFLOORD				ダンジョンからMAP表示を抜いたもの
;								ARGS:1 入力した場合フロア名の代わりに表示
;								ARGS:2 入力した場合、MAPの代わりに表示
;								ARGS:3 ARGS:2を使う際の区切り文字
;								ARGS:4 ARGS:2を使う際のALIGNMENT
;	再利用					前回の引数を使って実行する
;-------------------------
@SHOW_PICTURE, ARGS, ARGS:1, ARGS:2, ARGS:3 = "/" , ARGS:4 = "LEFT"
#LOCALSIZE 1
#LOCALSSIZE 30
#DIMS 前回ARGS, 4
IF ARGS == "再利用" || ARGS == "再利用する"
	FOR LOCAL, 0, 4
		ARGS:LOCAL = %前回ARGS:LOCAL%
	NEXT
ELSE
	FOR LOCAL, 0, 4
		前回ARGS:LOCAL = %ARGS:LOCAL%
	NEXT
ENDIF
SELECTCASE ARGS
	CASE "D", "DUNGEON", "ダンジョン"
		REDRAW 0
		CLEARLINE FLAG:コマンド表示行数 - LINECOUNT
		CALL SHOW_FLOOR
		CALL SHOW_NOW_FORMATION_P,0,2,,2
		FLAG:コマンド表示行数 = LINECOUNT
		REDRAW 1
	CASE "NONFLOORD"
		CUSTOMDRAWLINE =
		IF ARGS:1 == ""
			CALLFORM FLOORNAME_{FLAG:現ダンジョン}
		ELSE
			PRINTFORML %ARGS:1%
		ENDIF
		PRINTFORML   ￥{MONEY,8}    ＣＯＭＰ capacity: {NUM_NAKAMA() + ソフト容量()}/{FLAG:ＣＯＭＰ容量} Used
		PRINTFORM MAG:{BASE:MASTER:ＭＡＧ,8}    
		IF FLAG:月齢 != 0 && FLAG:月齢 != 8
			PRINTFORML {FLAG:月齢}/8 \@ FLAG:月齢ベクトル == 0 ? ＹＯＵＮＧ # ＯＬＤ \@ ＭＯＯＮ
		ELSE
			PRINTFORML \@ FLAG:月齢 == 8 ? ＦＵＬＬ ＭＯＯＮ # ＮＥＷ ＭＯＯＮ\@
		ENDIF
		IF ARGS:2 == ""
			FOR LOCAL, 0, 18
			;FOR LOCAL, MAX(0, FLAG:現Y - 10 - MAX(0, ((FLAG:現Y+10)-FLAG:最大Y))), MIN(FLAG:現Y+10+MAX(0, 10-FLAG:現Y), FLAG:最大Y)
				PRINTL 
			NEXT
		ELSE
			SELECTCASE ARGS:4
				CASE "CENTER"
					ALIGNMENT CENTER
				CASE "RIGHT"
					ALIGNMENT RIGHT
				CASEELSE
					ALIGNMENT LEFT
			ENDSELECT
			SPLIT ARGS:2, ARGS:3, LOCALS 
			FOR LOCAL, 0, RESULT
				PRINTFORML %LOCALS:LOCAL%
			NEXT
			ALIGNMENT LEFT
		ENDIF
		CALL SHOW_NOW_FORMATION_P,0,2,,2
		FLAG:コマンド表示行数 = LINECOUNT
	CASE "blank"
		CUSTOMDRAWLINE =
		IF ARGS:1 == ""
			CALLFORM FLOORNAME_{FLAG:現ダンジョン}
		ELSE
			PRINTFORML %ARGS:1%
		ENDIF
		IF ARGS:2 == ""
			FOR LOCAL, 0, 18
			;FOR LOCAL, MAX(0, FLAG:現Y - 10 - MAX(0, ((FLAG:現Y+10)-FLAG:最大Y))), MIN(FLAG:現Y+10+MAX(0, 10-FLAG:現Y), FLAG:最大Y)
				PRINTL 
			NEXT
		ELSE
			SELECTCASE ARGS:4
				CASE "CENTER"
					ALIGNMENT CENTER
				CASE "RIGHT"
					ALIGNMENT RIGHT
				CASEELSE
					ALIGNMENT LEFT
			ENDSELECT
			SPLIT ARGS:2, ARGS:3, LOCALS 
			FOR LOCAL, 0, RESULT
				PRINTFORML %LOCALS:LOCAL%
			NEXT
			ALIGNMENT LEFT
		ENDIF
		FLAG:コマンド表示行数 = LINECOUNT
	CASE "EMPTY"
		REDRAW 0
		FOR LOCAL, 0, 50
			PRINTL 
		NEXT
		REDRAW 2
		TWAIT 500, 1
		CLEARLINE 50
	CASEELSE
		PRINTW 引数の指定が間違っています！
ENDSELECT
