;-------------------------------------------------
;2択提示関数　@INPUT_YN_M
;枠囲み版も作る。とりあえず行数も可変です
;-------------------------------------------------
@INPUT_YN_M,ARGS = "はい",ARGS:1 = "いいえ", 区切り文字 = "/", オプション = "ログを残さない/ボタンを利用する", 位置 = "LEFT", 行数 = 2, 字幅 = 72
#DIM ログ残し
#DIMS 位置
#DIM 字幅
#DIM 行数
#DIMS 区切り文字
#DIMS オプション
#DIM LINE
#DIMS ログ
#DIM ログ取得済み
#LOCALSIZE 1
LINE = LINECOUNT
REDRAW 0
;幅取得
字幅 = MAX(STRLENS(ARGS)+4, STRLENS(ARGS:1)+4, 字幅)
字幅 = (字幅+1)/2*2
ログ取得済み = 0
$START
ログ = 
CLEARLINE LINECOUNT - LINE
CALL SET_ALIGNMENT(位置)
PRINTFORML ┌%"─" * (字幅/2)%┐
PRINT │
PRINTBUTTON @"%"[0] "+ARGS,字幅,LEFT%", "0"
PRINTL │
PRINT │
PRINTBUTTON @"%"[1] "+ARGS:1,字幅,LEFT%", "1"
ログ += "[0] "+ARGS+区切り文字+"[1] "+ARGS:1
PRINTL │
FOR LOCAL, 2, 行数
	ログ += 区切り文字+" "*字幅
	PRINTFORML │%" ",字幅%│
NEXT
;下表示
PRINTFORM └%"─"*(字幅/2-23)%
IF STRCOUNT(オプション, "ボタンを利用する")
	PRINTBUTTON "[+] LOG", "+"
	PRINTBUTTON @" [-] AUTO<\@FLAG:オート送り ?%" ON"%#OFF\@>", "-"
	PRINTBUTTON @" [*] SKIP<\@FLAG:ウィンドウメッセージスキップ ?%" ON"%#OFF\@>", "*"
	PRINTBUTTON " [/] CONFIG", "/"
ELSE
	PRINTFORM %"─"*23%
ENDIF
PRINTL ┘
CALL SET_ALIGNMENT("PREV")
IF ログ取得済み++ == 0
	CALL MESSAGE_WINDOW_LOG("" , ログ, 区切り文字, 行数, 字幅)
	;誤クリック防止に少しウェイトをかける
	TWAIT 250, 1
ENDIF
$INPUT_LOOP
ONEINPUTS
IF (GROUPMATCH(RESULTS, "+", "-", "/" , "*")) && STRCOUNT(オプション, "ボタンを利用する")
	;ログ閲覧
	IF RESULTS == "+"
		CALL MESSAGE_WINDOW_LOG(, , , , , 1)
	;オート送りモード切替
	ELSEIF RESULTS == "-"
		INVERTBIT FLAG:オート送り, 0
	;スキップモード切替
	ELSEIF RESULTS == "*"
		INVERTBIT FLAG:ウィンドウメッセージスキップ, 0
	ELSEIF RESULTS == "/"
		CALL MESSAGE_WINDOW_CONFIG
	ENDIF
	GOTO START
ENDIF
IF RESULTS == "0" || RESULTS == " " || (FLAG:二択入力設定 == 1 && (ARGS == "はい" || ARGS == "Yes") && (RESULTS == "y" || RESULTS == "Y")) || (FLAG:二択入力設定 == 2 && (RESULTS == "y" || RESULTS == "Y"))
	LOCAL = 0
ELSEIF RESULTS == "1" || (FLAG:二択入力設定 == 1 && (ARGS:1 == "いいえ" || ARGS:1 == "No") && (RESULTS == "n" || RESULTS == "N")) || (FLAG:二択入力設定 == 2 && (RESULTS == "n" || RESULTS == "N"))
	LOCAL = 1
ELSE
	SIF RESULTS != ""
		CLEARLINE 1
	GOTO INPUT_LOOP
ENDIF
CALL MESSAGE_WINDOW_LOG("", @"[{LOCAL}] "+ ARGS:LOCAL, 区切り文字, 行数 ,字幅)
SIF !STRCOUNT(オプション, "ログを残す")
	CLEARLINE LINECOUNT - LINE
REDRAW 1
RETURN LOCAL
