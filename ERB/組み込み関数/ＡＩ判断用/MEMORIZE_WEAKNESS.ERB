;-----------------------------------------------------------
;弱点学習関数　@MEMORIZE_WEAKNESS(ARG, ARG:1, ARGS, ARG:2)
;敵の弱点を覚える関数
;ARGは覚えるキャラのキャラ番号、ARG:1は覚える対象のポジション、ARGSは相性、ARG:2が真のときフラグ更新をしない
;99個まで覚えます。RESULTSあたりの要素数増やせば、まぁ300くらいまでは余裕ではいりそうだけど、まぁ99で大体足りると信じたい
;/登録番号_相性番号_数値/でCSTR:50に登録していく
;戻り値は指定された範囲の中での相性のうち最大だったもの、ただし、相性が記録されていなかった場合は1000
;またRESULT:1に最大値、RESULT:2に最大値の持ち主のポジション
;RESULT:3に最小値、RESULT:4に最小値の持ち主のポジションを入れて返す
;-----------------------------------------------------------
@MEMORIZE_WEAKNESS, ARG, ARG:1, ARGS, ARG:2
#FUNCTION
RESULT:1 = 1000
RESULT:2 = -1
RESULT:3 = 1000
RESULT:4 = -1
IF ARG:1 < 7
	LOCAL:3 = ARG:1
	LOCAL:4 = ARG:1 + 1
;前列
ELSEIF ARG:1 == 20
	LOCAL:3 = 1
	LOCAL:4 = 4
;後列
ELSEIF ARG:1 == 21
	LOCAL:3 = 4
	LOCAL:4 = 7
;全体
ELSE
	LOCAL:3 = 1
	LOCAL:4 = 7
ENDIF
FOR LOCAL:2, LOCAL:3, LOCAL:4
	LOCAL:5 = 0
	LOCAL:6 = 0
	SIF LOCAL:2 <= 0
		CONTINUE
	ARGS:4 = ポジション{LOCAL:2}
	SIF FLAG:(ARGS:4) < 0
		CONTINUE
	LOCAL:1 = 0
	ARGS:1 = {FLAG:(ARGS:4)}_%ARGS%
	ARGS:2 = %ARGS:1%_{MAXBASE:(FLAG:(ARGS:4)):GETNUM(BASE, ARGS)}
	;LOCALSが埋まってるので変わりに使う
	ARGS:3 = 
	SPLIT CSTR:ARG:50, "/", LOCALS
	SIF !STRLENS(LOCALS)
		LOCALS = %CSTR:ARG:50%
	FOR LOCAL, 0 , RESULT + 1
		;とりあえず、最初の二つが一致してるかを見る
		IF STRLENS(LOCALS) && STRCOUNT(LOCALS:LOCAL, ARGS:1)
			;ちゃんと一致してる場合,その数値を返す。ただし、複数をまとめてやっている場合は返さずに進む
			IF STRCOUNT(LOCALS:LOCAL, ARGS:2)
				;RESULT退避
				LOCAL:5 = RESULT
				SPLIT LOCALS:LOCAL, "_", RESULTS
				LOCAL:6 = TOINT(RESULTS:2)
				RESULT = LOCAL:5
				;初期値だったら、ここで代入
				IF RESULT:1 == 1000
					;相性の最大値
					RESULT:1 = LOCAL:6
					RESULT:2 = LOCAL:2
					;相性の最小値
					RESULT:3 = LOCAL:6
					RESULT:4 = LOCAL:2
				ELSE
					IF RESULT:1 < LOCAL:6
						RESULT:1 = LOCAL:6
						RESULT:2 = LOCAL:2
					ENDIF
					IF RESULT:3 > LOCAL:6
						RESULT:3 = LOCAL:6
						RESULT:4 = LOCAL:2
					ENDIF
				ENDIF
				CONTINUE
			;相性が違ってる（ペルソナ使いとかデビルシフター）場合はその相性を忘れる
			ELSE
				SIF ARG:2 == 0
					LOCALS:LOCAL = 
			ENDIF
		ENDIF
		IF ARG:2 == 0
			SIF RESULT == 99
				LOCALS = 
			SIF LOCAL == RESULT
				LOCALS:LOCAL = %ARGS:2%
			IF STRLENS(LOCALS:LOCAL)
				SIF LOCAL:1 ++
					LOCALS:LOCAL = /%LOCALS:LOCAL%
				ARGS:3 += LOCALS:LOCAL
			ENDIF
		ENDIF
	NEXT
	SIF ARG:2 == 0
		CSTR:ARG:50 = %ARGS:3%
NEXT
;相性のうち最大値を返す。誰も相性が記録されてなかったら1000が帰る
RETURNF RESULT:1

@CHECK_WEAKNESS(ARG)
#FUNCTION
IF ARG == 999
	RETURNF -4
ELSEIF ARG < 0
	RETURNF -3
ELSEIF ARG == 0
	RETURNF -2
ELSEIF ARG < 100
	RETURNF -1
ELSEIF ARG == 100
	RETURNF 0
ELSEIF ARG > 100 && ARG < 999
	RETURNF 1
;メモライズでの初記憶の場合
ELSEIF ARG == 1000
	RETURNF 100
ENDIF
