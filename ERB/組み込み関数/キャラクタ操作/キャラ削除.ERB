;-------------------------------------------------
;@キャラ削除　ARGのキャラクターを削除し、変数の移動まで済ませる関数
;-------------------------------------------------
@キャラ削除, ARG
#LOCALSIZE 2
;ペルソナ使いの場合はペルソナをひっぺがし、つくっていた初期ペルソナを消す
IF TALENT:ARG:ペルソナ使い
	FOR LOCAL, 1, 4
		LOCALS = 所持ペルソナ{LOCAL}
		IF EQUIP:ARG:LOCALS
			DITEMTYPE:(EQUIP:ARG:LOCALS):ペルソナ("装備状態") = 1
			DITEMTYPE:(EQUIP:ARG:LOCALS):ペルソナ("ペルソナ所持者") = -1
		ENDIF
	NEXT
	IF CFLAG:ARG:初期ペルソナナンバー
		FOR LOCAL, 0, 50
			DITEMTYPE:(CFLAG:ARG:初期ペルソナナンバー):LOCAL = 0
		NEXT
	ENDIF
ENDIF

;デビルシフター関連
IF CFLAG:ARG:被リンクフラグ > -1 && FINDCHARA_ID(CFLAG:ARG:被リンクフラグ) > -1
	CFLAG:FINDCHARA_ID(CFLAG:ARG:被リンクフラグ):リンク悪魔 = -1
	;CFLAG:FINDCHARA_ID(CFLAG:ARG:被リンクフラグ):悪魔変身 = 0	
	;スキルを復元
	;FOR LOCAL,1, FLAG:スキル数 + 1
	;	ABL:ARG:("スキル" + TOSTR(LOCAL)) = ABL:ARG:("人間時スキル" + TOSTR(LOCAL))
	;NEXT
	;CALL SYNC_STATUS,FINDCHARA_ID(CFLAG:ARG:被リンクフラグ)
	SIF CFLAG:FINDCHARA_ID(CFLAG:ARG:被リンクフラグ):悪魔変身 == 1
		CALL ACTION_2310 , FINDCHARA_ID(CFLAG:ARG:被リンクフラグ) , 0
ENDIF
IF CFLAG:ARG:リンク悪魔 > -1 && TALENT:ARG:デビルシフター
	CFLAG:FINDCHARA_ID(CFLAG:ARG:リンク悪魔):被リンクフラグ = -1
	CFLAG:FINDCHARA_ID(CFLAG:ARG:リンク悪魔):被変身フラグ = 0
ENDIF

;親の数値の破棄
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:娘の産みの親の固有番号娘 == CFLAG:ARG:キャラ固有の番号
		CFLAG:LOCAL:娘の産みの親の固有番号娘 = -1
	SIF CFLAG:LOCAL:娘の父親の固有番号娘 == CFLAG:ARG:キャラ固有の番号
		CFLAG:LOCAL:娘の父親の固有番号娘 = -1
	IF CFLAG:LOCAL:父親の固有番号親 == CFLAG:ARG:キャラ固有の番号
		CFLAG:LOCAL:父親の固有番号親 = -1
		CFLAG:LOCAL:父親失踪 = 1
	ENDIF
NEXT
;好感度を削除
FOR LOCAL, 0, CHARANUM
	CFLAG:(LOCAL):(CFLAG:ARG:キャラ固有の番号+2100) = 0
NEXT

SIF CFLAG:ARG:ポジション > 0
	CALL REMOVE_POSITION, CFLAG:ARG:ポジション
;予備ターゲットから外す
FOR LOCAL, 0, 10
	IF LOCAL == 0
		SIF TARGET == ARG
			TARGET = -1
		SIF ASSI == ARG
			ASSI = -1
	ENDIF
	SIF TARGET:LOCAL == CFLAG:ARG:キャラ固有の番号
		TARGET:LOCAL = -1
NEXT

DELCHARA ARG
	IF ARG != -1
		IF TARGET > ARG
			TARGET -= 1
		ELSEIF TARGET == ARG
			TARGET = -1
		ENDIF
	ENDIF
	IF ARG != -1
		IF ASSI > ARG
			ASSI -= 1
		ELSEIF ASSI == ARG
			ASSI = -1
		ENDIF
	ENDIF
;フォーメーション再読み込み
CALL REFRESH_POS
