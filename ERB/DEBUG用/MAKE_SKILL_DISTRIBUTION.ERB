@SHOPCOMABLE_193
[IF_DEBUG]
	RESULTS = スキル分布作成
	RETURN 1
[ENDIF]
IF FLAG:DEBUG
	RESULTS = スキル分布作成
	RETURN 1
ENDIF
RETURN -1

@SHOP_COM_193
;LOCAL スキル番号回し
;LOCAL:1 キャラクタ番号回し
;LOCAL:2 スキル検索用
;PRINTL [0]オトコ [1]女　[2]両方　[3]キャラを表示しない
PRINTL [0]Male [1]Female　[2]Both [3]キャラを表示しない
CALL INPUTINT,0,1,2,3
LOCAL:4 = RESULT
PRINTL [0]スキルをすべて表示 [1]表示するスキル番号を決める
CALL INPUTINT,0,1
IF RESULT == 0
	LOCAL:6 = 1
	LOCAL:7 = 3000
ELSE
	PRINTL 開始するスキル番号入力
	INPUT
	IF RESULT < 1
		PRINTL 番号入力が少ないので最初からにします
		LOCAL:6 = 1
	ELSEIF RESULT > 2999
		PRINTL 番号入力が多いので終わりの直前からにします
		LOCAL:6 = 2999
	ELSEIF RESULT > 1 && RESULT < 3000
		LOCAL:6 = RESULT
	ELSE
		PRINTL 番号入力が不正なので最初からにします
		LOCAL:6 = 1
	ENDIF
	PRINTL 終了するスキル番号入力
	INPUT
	IF RESULT <= LOCAL:6
		PRINTL 番号入力が少ないので初めの直後に変えます
		LOCAL:7 = LOCAL:6 + 1
	ELSEIF RESULT > 3000
		PRINTL 番号入力が多いので最後までにします
		LOCAL:7 = 3000
	ELSEIF RESULT > 1 && RESULT < 3000
		LOCAL:7 = RESULT
	ELSE
		PRINTL 番号入力が不正なので最後までにします
		LOCAL:7 = 3000
	ENDIF
ENDIF

PRINTL [0]一気に表示する [1]4相性ごとに止まる
CALL INPUTINT,0,1
LOCAL:5 = RESULT

CLEARLINE 5000
PRINTFORML ;-------------------------------
PRINTFORML ;%GAMEBASE_TITLE% {GAMEBASE_VERSION/1000},{GAMEBASE_VERSION % 1000}
GETTIME
PRINTFORML ;%RESULTS%
PRINTFORML ;-------------------------------
PRINTFORML ;-------------------------------
PRINTFORML ;この資料は資料フォルダ内にある
PRINTFORML ;SORT_DISTRIBUTION.ERBを利用して作成されています
PRINTFORML ;何か追加する際はコピペするのではなく、
PRINTFORML ;SORT_DISTRIBUTION.ERBを利用して出力してください
PRINTFORML ;-------------------------------
;FOR LOCAL,1,3000
FOR LOCAL,LOCAL:6,LOCAL:7
	SIF LOCAL == 2101
		LOCAL = 2400
	TRYCCALLFORM SKILL_NAME_{LOCAL}
		;ランク0か、継承タイプ23のスキルは無視
		CALLFORM SKILL_RANK_{LOCAL}
		SIF RESULT == 0
			CONTINUE
		CALLFORM SKILL_SUCCESSION_TYPE_{LOCAL}
		SIF RESULT == 23
			CONTINUE
		
		PRINTFORML [{LOCAL}] %RESULTS%
		;性能を記述
		;========================================================================
		
		TRYCALLFORM SKILL_RANK_{LOCAL}		
		PRINTFORM ランク:{RESULT,2}　
		RESULT = 0
		TRYCALLFORM SKILL_HITRATE_{LOCAL}
		PRINTFORM Hit:{RESULT,4}　
		RESULT = 0
		TRYCALLFORM SKILL_POWER_{LOCAL}
		PRINTFORM 威力:{RESULT,4}　
		RESULT = 0
		TRYCALLFORM SKILL_COSTTYPE_{LOCAL}
		LOCAL:1 = RESULT
		RESULT = 0
		TRYCALLFORM SKILL_COST_{LOCAL}
		PRINTFORML \@ LOCAL:1 == 2 ? 消費HP:{RESULT,3}％ # 消費MP:{RESULT,3} \@

		SELECTCASE LOCAL
			;罪味方の専用魔法シリーズはターゲットいねーと落ちるので
			CASE 1520, 1620, 1720 ,1923, 2025
				PRINTFORM 相性:特殊　
			CASEELSE
				RESULT = -1
				TRYCALLFORM SKILL_TYPE_{LOCAL}
				IF RESULT == -1
					PRINTFORM 相性:特殊　
				ELSE
					PRINTFORM 相性:%GET_TYPE_E(RESULT), 6%　
				ENDIF
		ENDSELECT
		TRYCALLFORM SKILL_DECIDE_TYPE_{LOCAL}
		PRINTFORM \@RESULT == 1 ? EX # MA\@　
		RESULT = 0
		TRYCALLFORM SKILL_SPHERE_{LOCAL}
		PRINT 範囲:
		SELECTCASE RESULT
			CASE 1
				PRINTFORM 単体　　
			CASE 2
				PRINTFORM 列　　　
			CASE 3
				PRINTFORM 全体　　
			CASE 4
				PRINTFORM 敵味方　
			CASEELSE
				PRINT なし　　
		ENDSELECT
		TRYCALLFORM SKILL_EFECT_{LOCAL}
		PRINT 種別:
		SELECTCASE RESULT
			CASE 1
				PRINTFORM Attack　　　　
			CASE 2
				PRINTFORM 回復　　　　
			CASE 3
				PRINTFORM 状態変化　　
			CASE 4
				PRINTFORM 特殊　　　　
			CASE 5
				PRINTFORM 状態回復　　
			CASEELSE
				PRINTFORM その他　　　
		ENDSELECT
		TRYCCALLFORM SKILL_MINATTACKNUMBER_{LOCAL},-1
			PRINTFORM 攻撃回数:{RESULT}〜
			CALLFORM SKILL_MAXATTACKNUMBER_{LOCAL},-1
			PRINTFORM {RESULT}
			TRYCCALLFORM SKILL_MAXATK_PER_{LOCAL},-1
				PRINTFORM 　1体当たり{RESULT}HITまで
			CATCH
			ENDCATCH
		CATCH
		ENDCATCH
		PRINTL
		
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL}
		PRINTL 
		;========================================================================
		IF LOCAL:4 != 3
			FOR LOCAL:1,1,4500
				;特定の種族を飛ばす処理
				SELECTCASE (LOCAL:1+99)/100
					CASE 0
						LOCAL:1 += 99
						CONTINUE
					CASE IS < 19
					CASE 40,42,44
					CASEELSE
						LOCAL:1 += 99
						CONTINUE
				ENDSELECT
				SIF EXISTCSV(LOCAL:1,0) == 0
					CONTINUE
				SIF CSVTALENT(LOCAL:1,GETNUM(TALENT,"オトコ"),0) == LOCAL:4
					CONTINUE
				SIF CSVCFLAG(LOCAL:1,GETNUM(CFLAG,"全書召喚不可"),0) == 1
					CONTINUE
				
				LOCAL:3 = -1
				FOR LOCAL:2,1,FLAG:スキル数+1
					SIF CSVABL(LOCAL:1,GETNUM(ABL,"スキル"+ TOSTR(LOCAL:2)),0) == LOCAL
						LOCAL:3 = 0
					SIF LOCAL:3 > -1
						BREAK
				NEXT
				FOR LOCAL:2,1,21
					SIF CSVABL(LOCAL:1,GETNUM(ABL,"習得スキル"+ TOSTR(LOCAL:2)),0) == LOCAL
						LOCAL:3 = CSVABL(LOCAL:1,GETNUM(ABL,"習得LV"+ TOSTR(LOCAL:2)),0)
					SIF LOCAL:3 > -1
						BREAK
				NEXT
				IF LOCAL:3 == 0
					PRINTFORML 　　　[{LOCAL:1,4}] %CSVCALLNAME(LOCAL:1,0),20,LEFT% LV{CSVBASE(LOCAL:1,GETNUM(BASE,"LV"),0)}　　　初期習得
				ELSEIF LOCAL:3 > 0
					PRINTFORML 　　　[{LOCAL:1,4}] %CSVCALLNAME(LOCAL:1,0),20,LEFT% LV{CSVBASE(LOCAL:1,GETNUM(BASE,"LV"),0)}　　　習得LV:{LOCAL:3}
				ENDIF
				
			NEXT
		ENDIF
		PRINTL
	CATCH
	ENDCATCH
	IF LOCAL % 400 == 0 && LOCAL:5 == 1
		PRINTW
	ENDIF
NEXT
WAIT





