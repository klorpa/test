@POKER
#LOCALSIZE 10
#LOCALSSIZE 10
;山札
#DIM DECK,53
;手札
#DIM HAND,5
#DIM HOLD,5
;勝ち枚数
#DIM WIN, 1
#DIM LINE,2
PRINTL バニー:
PRINTL 「めくるめくラックの世界へようこそ
PRINTW 　このポーカーでは度胸と引き際を見極める眼力が試されるわよ！」
PRINTL 「どうするの？」
$INPUT_START
CALL INPUT_SELECT(1, "ゲームスタート", 2, "アドバイスを聞く", 3, "画面がずれる", 9, "外に出る")
IF RESULT > 1
	PRINTL バニー:
	IF RESULT == 2
		PRINTL 「ポーカーＨ&Ｌでは、ゲームで使うコインを
		PRINTL 　１コインと５コインと１０コインの中から選べるわ」
		PRINTL 「このポーカーではＰＯＫＥＲと
		PRINTW 　ＥＸＴＲＡゲームがついてるのよ」
		PRINTL 「ＰＯＫＥＲでは残しておきたい
		PRINTL 　カードがあればＨＯＬＤしておけるの
		PRINTW 　左から[1]･[2]･[3]･[4]･[5]で残すカードを決めてね」
		PRINTL 「決めたら、ＤＲＡＷしてネ！
		PRINTL 　捨てたカードとおんなじ枚数だけカードをひけるの
		PRINTW 　それで役ができてたらコインをゲット！」
		PRINTW 「役を教えてあげるね！」
		CALL SHOW_CARD(1, 101, 5, 305, 211)
		PRINTL バニー:
		PRINTL 「２ ＰＡＩＲはこんな感じでおんなじ数のカードが２枚づつあるときね
		PRINTW 　安いけど、その分よくお世話になるわヨ」
		CALL SHOW_CARD(1, 101, 201, 305, 211)
		PRINTL バニー:
		PRINTL 「３ ＯＦ ＡＫＩＮＤはこんな感じでおんなじ数のカードが３枚あるときね
		PRINTW 　日本じゃ３ＣＡＲＤって呼ばれることのほうがおおいかも」
		CALL SHOW_CARD(1, 101, 201, 301, 211)
		PRINTL バニー:
		PRINTL 「４ ＯＦ ＡＫＩＮＤはおんなじ数のカードが４枚あるとき
		PRINTW 　３まではよく見れてもこの子はなかなかお目にかかれないわ」
		CALL SHOW_CARD(1, 101, 201, 301, 9999)
		PRINTL バニー:
		PRINTL 「これが５ ＯＦ ＡＫＩＮＤ、ワイルドカード+４ ＯＦ ＡＫＩＮＤよ
		PRINTL 　ワイルドカードがないポーカーじゃ出来ない役ね
		PRINTL 　ワイルドカードっていうのは、なんにでも使えるカードのことで
		PRINTW 　ＪＯＫＥＲの札が使われることが多いわ」
		CALL SHOW_CARD(1, 101, 201, 105, 205)
		PRINTL バニー:
		PRINTL 「ＦＵＬＬ ＨＯＵＳＥは３枚と２枚の組み合わせ
		PRINTW 　なかなか強い手だから、できると嬉しいわね」
		CALL SHOW_CARD(1, 102, 103, 204, 305)
		CALL SHOW_CARD(10, 11, 212, 313, 301)
		CALL SHOW_CARD(112,113,201,202,303)
		PRINTL バニー:
		PRINTL 「ＳＴＲＡＩＧＨＴは５枚のカードが連番になっているとき、
		PRINTL 　一番上のはもちろん、Ａは２だけじゃなくて、Ｋとも続いてるから、２番目でもあがれるの
		PRINTW 　でも、Ｋ-Ａ-２とはつながらないから、３番目のはあがれないから注意してネ」
		CALL SHOW_CARD(202, 205, 207, 209, 211)
		PRINTL バニー:
		PRINTL 「全部のスートがおんなじだったらＦＬＵＳＨ
		PRINTW 　わかりやすいけど、意外にそろわないわ」
		CALL SHOW_CARD(302, 303, 304, 305, 306)
		PRINTL バニー:
		PRINTL 「ＦＬＵＳＨとＳＴＲＡＩＧＨＴの条件を同時に満たすと
		PRINTW 　ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ、ふふ、そのまんまね」
		CALL SHOW_CARD(10, 11, 12, 13, 1)
		PRINTL バニー:
		PRINTL 「１０・Ｊ・Ｑ・Ｋ・Ａの組み合わせでＳＴＲＡＩＧＨＴ ＦＬＵＳＨをやると
		PRINTW 　ＲＯＹＡＬ ＦＬＵＳＨよ！　このゲームで一番強い役！」
		PRINTL 「さらにこの１０枚かけてるときにＲＯＹＡＬ ＦＬＵＳＨで、ＪＡＣＫ ＰＯＴ
		PRINTL  ＪＡＣＫ ＰＯＴの点数を全部取れるわ！
		PRINTW  ＪＡＣＫ ＰＯＴは誰かがコインをいれるたびに成長していくから、育ってたらすごいことになるわ」
		PRINTL 「あとは…そうね・さっきもちょっといったけど、ＪＯＫＥＲは
		PRINTW 　どんなカードの代わりにもなるから、上手く使ってね」
		PRINTL 「で、役を揃えたら、それに応じたコインがもらえるわけだけど
		PRINTW 　ここからがＥＸＴＲＡゲームのスタートよ」
		PRINTL 「ＥＸＴＲＡゲームはＨＩＧＨ ＆ ＬＯＷ
		PRINTL 　５枚のカードから一枚を選んで
		PRINTW 　それが親がだしたカードより大きな数字だったらダブルアップ！」
		PRINTL 「もらえるコインが倍になるワヨ
		PRINTL 　で、ダブルアップは連続してできるの
		PRINTW 　勝ち続けることで、２倍、４倍、８倍…どんどんコインが増えていくワ」
		PRINTL 「でも、１回でも負けたら、コインは０枚に
		PRINTL 　１枚ももらえなくなっちゃうの
		PRINTW 　負ける前にＴＡＫＥ ＯＵＴしてコインを持ち帰ってね」
		PRINTW 「ＬＥＴ’Ｓ ＴＲＹ！！」
	ELSEIF RESULT == 3
		PRINTW 「EMUERAの描画に使ってるGDI+問題で、色をつけるとどうしてもずれちゃうの」
		PRINTL 「ウィンドウの上部にあるコンフィグをえらんで、表示タブを選んで
		PRINTW 　描画をGDI+からGDIにすればずれなくなるから、気になる人は試してみてネ」
	ELSEIF RESULT  == 9
		PRINTFORMW 「あなたのコインは{MONEY:3}枚よ。また来てね！」
		REDRAW 1
		RETURN 1
	ENDIF
	PRINTL バニー:
	PRINTL 「どうするの？」
	GOTO INPUT_START
ENDIF
REDRAW 0
FONTSTYLE 3
PRINTL ＰＯＫＥＲ
FONTSTYLE 0
DRAWLINE
PRINTFORML ┌────────┬───┬───┬───┐
PRINTFORML │%"OODS", 16,LEFT%│%"BET1",6%│%"BET5",6%│%"BET10",6%│
PRINTFORML ├────────┼───┼───┼───┤
PRINTFORML │%"ROYAL FLASH", 16,LEFT%│{300,6}│{1500,6}│%"ＪＰ",6%│
PRINTFORML │%"5 OF AKIND", 16,LEFT%│{100,6}│{500,6}│{1000,6}│
PRINTFORML │%"STRAIGHT FLASH", 16,LEFT%│{50,6}│{250,6}│{500,6}│
PRINTFORML │%"4 OF AKIND", 16,LEFT%│{30,6}│{150,6}│{300,6}│
PRINTFORML │%"FLASH", 16,LEFT%│{10,6}│{50,6}│{100,6}│
PRINTFORML │%"STRAIGHT", 16,LEFT%│{5,6}│{25,6}│{50,6}│
PRINTFORML │%"3 OF AKIND", 16,LEFT%│{3,6}│{15,6}│{30,6}│
PRINTFORML │%"2 PAIR", 16,LEFT%│{2,6}│{10,6}│{20,6}│
PRINTFORML │%"HIGH CARD", 16,LEFT%│{0,6}│{0,6}│{0,6}│
PRINTFORML ├────────┼───┴───┴───┤
PRINTFORML │%"JACK POT", 16,LEFT%│{FLAG:ジャックポット/100,17}COINS│
PRINTFORML └────────┴───────────┘
LINE = LINECOUNT
$START
VARSET HOLD
CLEARLINE LINECOUNT - LINE
PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
DRAWLINE
PRINTL 
PRINTL 
PRINTL 
PRINTL
PRINTL
DRAWLINE
CALL SHOW_CARD(0,0,0,0,0)
DRAWLINE
PRINTL ＢＥＴ [1] １ ＣＯＩＮ [2] ５ ＣＯＩＮＳ [3] １０ ＣＯＩＮＳ
PRINTL [9] ＥＸＩＴ
CALL INPUTINT(1,2,3,9)
IF RESULT == 9
	PRINTFORMW 「あなたのコインは{MONEY:3}枚よ。また来てね！」
	REDRAW 1
	RETURN 1
ELSEIF RESULT == 1
	LOCAL = 1
ELSEIF RESULT == 2
	LOCAL = 5
ELSE
	LOCAL = 10
ENDIF
SIF MONEY:3 < LOCAL
	GOTO START
WIN = LOCAL
MONEY:3 -= LOCAL
CLEARLINE LINECOUNT - LINE

FLAG:ジャックポット += 10 * WIN
;山札の作成
;カードはスート(スペード0, クラブ1,ハート2,ダイア3)*100+2~13の数で登録する
FOR LOCAL, 0, 53
	DECK:LOCAL = (LOCAL/13 * 100)+LOCAL % 13+1
NEXT
;53枚目の山札はJOKER(9999)にする
DECK:52 = 9999
;山札をシャッフルする
FOR LOCAL, 0, 53
	LOCAL:1 = RAND:53
	SWAP DECK:LOCAL, DECK:(LOCAL:1)
NEXT
;最初の手札を配る
FOR LOCAL, 0, 5
	HAND:LOCAL = DECK:LOCAL
NEXT
;山札から札を削除
ARRAYREMOVE DECK, 0, 5
;手札の整列
DO 
	LOCAL:1 = 0
	FOR LOCAL, 0, 4
		IF HAND:LOCAL > HAND:(LOCAL+1)
			SWAP HAND:LOCAL, HAND:(LOCAL+1)
			LOCAL:1 = 1
		ENDIF
	NEXT
LOOP LOCAL:1
$CHOICE_HOLD
CLEARLINE LINECOUNT-LINE
PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
DRAWLINE
PRINTL 
PRINTL 
PRINTL 
PRINTL
PRINTL
DRAWLINE
CALL SHOW_CARD(HAND:0,HAND:1,HAND:2,HAND:3,HAND:4, HOLD*COLOR("aqua"), HOLD:1*COLOR("aqua"), HOLD:2*COLOR("aqua"), HOLD:3*COLOR("aqua"), HOLD:4*COLOR("aqua"))
DRAWLINE
FOR LOCAL, 0, 5
	IF HOLD:LOCAL
		SETCOLOR COLOR("aqua")
	ELSE
		SETCOLOR COLOR("gray")
	ENDIF
	PRINTFORM [{LOCAL+1}] 
	PRINT HELD　
NEXT
RESETCOLOR
PRINTL 
PRINTL [0] ＤＲＡＷ
CALL INPUTINT(0 ,1 ,2 ,3, 4, 5)
IF RESULT > 0
	INVERTBIT HOLD:(RESULT-1),0
	GOTO CHOICE_HOLD
ENDIF
IF MATCH(HOLD, 0)
	VARSET LOCAL, -1, 2, 7
	LOCAL:1 = 0
	FOR LOCAL, 0, 5
		IF !HOLD:LOCAL
			HAND:LOCAL = DECK:(LOCAL:1)
			LOCAL:(LOCAL:1+2) = DECK:(LOCAL:1)
			LOCAL:1++ 
		ENDIF
	NEXT
	;山札から札を削除
	ARRAYREMOVE DECK, 0, MATCH(HOLD, 0)
	;手札の整列
	DO 
		LOCAL:1 = 0
		FOR LOCAL, 0, 4
			IF HAND:LOCAL > HAND:(LOCAL+1)
				SWAP HAND:LOCAL, HAND:(LOCAL+1)
				LOCAL:1 = 1
			ENDIF
		NEXT
	LOOP LOCAL:1
	CLEARLINE LINECOUNT-LINE
	PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
	DRAWLINE
	CALL MESSAGE_BL, 1, "ＤＲＡＷ ＣＡＲＤＳ"
	PRINTL
	PRINTL 
	;ひいたカードの提示
	DRAWLINE
	CALL SHOW_CARD, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5, LOCAL:6
	DRAWLINE
	SETCOLOR COLOR("gray")
	FOR LOCAL, 0, 5
		PRINTFORM [{LOCAL+1}] 
		PRINT HELD　
	NEXT
	RESETCOLOR
	PRINTL
	PRINTL [0] ＤＲＡＷ
	RESETCOLOR
	WAIT
ENDIF
CLEARLINE LINECOUNT-LINE
PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
DRAWLINE
;役の判定
;JOKERがある場合　HAND:4はJOKERになってるはずなので、HAND:3までのみをみる
IF MATCH(HAND,9999)
	;10-J-Q-K-Aのストレートは別判定しておく。それぞれが含まれているかをチェック
	VARSET LOCAL,0, 1, 6
	FOR LOCAL, 0, 4
		SELECTCASE HAND:LOCAL % 100
			CASE 1
				LOCAL:1 = 1
			CASE 10
				LOCAL:2 = 1
			CASE 11	
				LOCAL:3 = 1
			CASE 12
				LOCAL:4 = 1
			CASE 13
				LOCAL:5 = 1
		ENDSELECT
	NEXT
	;以上のカードの内４枚が含まれていれば、10-Aの並びのストレート
	LOCAL:8 = LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:4+LOCAL:5
	VARSET LOCAL, 0, 1, 5
	;同一カード系の判定 LOCAL:1~4に1枚~4枚揃いのカードがあるごとに+1
	FOR LOCAL, 0, 5
		LOCAL:GROUPMATCH(HAND:LOCAL%100, HAND%100, HAND:1%100, HAND:2%100, HAND:3%100) += 1
	NEXT
	;最小と最大の差が４以下かつ４枚全てが別カードならストレート
	IF LOCAL:8 == 4 || (MAX(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100) - MIN(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100) <= 4 && LOCAL:1 == 4)
		;フラッシュの判定　カードの最小と最大の差が13未満ならフラッシュ
		IF MAX(HAND, HAND:1, HAND:2, HAND:3) - MIN(HAND, HAND:1, HAND:2, HAND:3) < 13
			;ロイヤルフラッシュの判定　上記で判定してるのを流用
			IF LOCAL:8 == 4
				LOCALS = ＲＯＹＡＬ ＦＬＵＳＨ！
				WIN *= 300
			;ストレートフラッシュ
			ELSE
				LOCALS = ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ！
				WIN *= 50
			ENDIF
		;ストレート
		ELSE
			LOCALS = ＳＴＲＡＩＧＨＴ！
			WIN *= 5
		ENDIF
	ELSE
		;5 OF AKINDの判定
		IF LOCAL:4 == 4
			LOCALS = ５ ＯＦ ＡＫＩＮＤ！
			WIN *= 100
		;4 OF AKINDの判定
		ELSEIF LOCAL:3 == 3
			LOCALS = ４ ＯＦ ＡＫＩＮＤ！
			WIN *= 30
		;フルハウスの判定
		ELSEIF LOCAL:2 == 4
			LOCALS = ＦＵＬＬ ＨＯＵＳＥ！
			WIN *= 20
		ELSE
			;フラッシュの判定　カードの最小と最大の差が13未満ならフラッシュ
			IF MAX(HAND, HAND:1, HAND:2, HAND:3) - MIN(HAND, HAND:1, HAND:2, HAND:3) < 13
				LOCALS = ＦＬＵＳＨ！
				WIN *= 10
			ELSE
				;3 OF AKINDの判定
				IF LOCAL:2 == 2
					LOCALS = ３ ＯＦ ＡＫＩＮＤ！
					WIN *= 3
				;役なし
				ELSE
					LOCALS = ＨＩＧＨ ＣＡＲＤ
					WIN *= 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSE
	;10-J-Q-K-Aのストレートは別判定しておく。それぞれが含まれているかをチェック
	VARSET LOCAL,0, 1, 6
	FOR LOCAL, 0, 5
		SELECTCASE HAND:LOCAL % 100
			CASE 1
				LOCAL:1 = 1
			CASE 10
				LOCAL:2 = 1
			CASE 11
				LOCAL:3 = 1
			CASE 12
				LOCAL:4 = 1
			CASE 13
				LOCAL:5 = 1
		ENDSELECT
	NEXT
	;以上のカードの内５枚が含まれていれば、10-Aの並びのストレート
	LOCAL:8 = LOCAL:1+LOCAL:2+LOCAL:3+LOCAL:4+LOCAL:5
	VARSET LOCAL, 0, 1, 6
	;同一カード系の判定 LOCAL:1~4に1枚~4枚揃いのカードがあるごとに+1
	FOR LOCAL, 0, 5
		LOCAL:GROUPMATCH(HAND:LOCAL%100, HAND%100, HAND:1%100, HAND:2%100, HAND:3%100, HAND:4%100) += 1
	NEXT
	;最小と最大の差が4かつ、全てのカードが別のカードであるならストレート
	IF LOCAL:8 == 5 || (MAX(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100, HAND:4%100) - MIN(HAND%100, HAND:1%100, HAND:2%100, HAND:3%100, HAND:4%100) == 4 && LOCAL:1 == 5)
		;フラッシュの判定　カードの最小と最大の差が13未満ならフラッシュ
		IF MAX(HAND, HAND:1, HAND:2, HAND:3, HAND:4) - MIN(HAND, HAND:1, HAND:2, HAND:3, HAND:4) < 13
			;ロイヤルフラッシュの判定 上記判定の流用
			IF LOCAL:8 == 5
				LOCALS = ＲＯＹＡＬ ＦＬＵＳＨ！
				WIN *= 300
			;ストレートフラッシュ
			ELSE
				LOCALS = ＳＴＲＡＩＧＨＴ ＦＬＵＳＨ！
				WIN *= 50
			ENDIF
		;ストレート
		ELSE
			LOCALS = ＳＴＲＡＩＧＨＴ！
			WIN *= 5
		ENDIF
	ELSE
		;4 OF AKINDの判定
		IF LOCAL:4 == 4
			LOCALS = ４ ＯＦ ＡＫＩＮＤ！
			WIN *= 30
		;フルハウス
		ELSEIF LOCAL:3 == 3 && LOCAL:2 == 2
			LOCALS = ＦＵＬＬ ＨＯＵＳＥ！
			WIN *= 20
		ELSE
			;フラッシュの判定　カードの最小と最大の差が13未満ならフラッシュ
			IF MAX(HAND, HAND:1, HAND:2, HAND:3, HAND:4) - MIN(HAND, HAND:1, HAND:2, HAND:3, HAND:4) < 13
				LOCALS = ＦＬＵＳＨ！
				WIN *= 10
			ELSE
				;3 OF AKINDの判定
				IF LOCAL:3 == 3
					LOCALS = ３ ＯＦ ＡＫＩＮＤ！
					WIN *= 3
				;2ペアの判定。2枚ぞろいのカードは4枚あることに注意
				ELSEIF LOCAL:2 == 4
					LOCALS = ２ ＰＡＩＲ！
					WIN *= 2
				;役なし
				ELSE
					LOCALS = ＨＩＧＨ ＣＡＲＤ
					WIN *= 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF
IF WIN
	IF WIN == 3000
		CALL MESSAGE_BL, 3, LOCALS, "ＪＡＣＫ ＰＯＴ！！", "ＧＥＴ "+TOSTR(FLAG:ジャックポット/100)+"ＣＯＩＮＳ！！"
		WIN = 0
		MONEY:3 += FLAG:ジャックポット/100
		FLAG:ジャックポット = 10000 * 100
	ELSE
		CALL MESSAGE_BL, 3, LOCALS, "ＧＥＴ "+TOSTR(WIN)+"ＣＯＩＮＳ！！", "ＥＸＴＲＡ ＧＡＭＥ ＳＴＡＲＴ！！"
	ENDIF
ELSE
	CALL MESSAGE_BL, 2, LOCALS, "ＹＯＵ ＬＯＳＥＤ！"
	PRINTL 
ENDIF
DRAWLINE
CALL SHOW_CARD, HAND, HAND:1, HAND:2, HAND:3, HAND:4
DRAWLINE
SETCOLOR COLOR("gray")
FOR LOCAL, 0, 5
	PRINTFORM [{LOCAL+1}] 
	PRINT HELD　
NEXT
RESETCOLOR
PRINTL
PRINTL [0] ＤＲＡＷ
RESETCOLOR
WAIT
SIF WIN == 0
	GOTO START
;Aは14にする
FOR LOCAL,0, 53
	SIF DECK:LOCAL % 100 == 1
		DECK:LOCAL += 13
NEXT
WHILE WIN
	;イカサマあり→降りやすいし、なにより必要なコイン枚数が原作ほど多くないしでサマなしの勝率50%に変更しておきます
	;親が最も強いカードである場合、子のカードのいずれかと入れ替わる
	;引き分けと山札の在庫を置いておけば、20%~100%が当分にでるので、これで勝率6割
	;異聞録の実測が70%なんで、10%ほど負けてるけど、そのかわりドロップのタイミングを優遇しておく
	;TAKE OUTはいつでも可能にしておきます
	;IF DECK % 100 > MAX(DECK:1 % 100, DECK:2 % 100 ,  DECK:3 % 100 , DECK:4 % 100, DECK:5 %100)
	;	LOCAL = RAND:5
	;	SWAP DECK, DECK:LOCAL
	;ENDIF
	CLEARLINE LINECOUNT - LINE
	PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
	DRAWLINE
	CALL SHOW_CARD , , , DECK
	DRAWLINE
	CALL SHOW_CARD , 0, 0, 0, 0, 0
	DRAWLINE
	PRINTL [1] RAIZE [2] RAIZE [3] RAIZE [4] RAIZE [5] RAIZE
	PRINTL [9] ＴＡＫＥ ＯＵＴ
	CALL INPUTINT(1,2,3,4,5,9)
	IF RESULT == 9
		CLEARLINE LINECOUNT - LINE
		PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
		DRAWLINE
		CALL MESSAGE_BL, 1, "ＴＡＫＥ "+TOSTR(WIN)+"ＣＯＩＮＳ！！"
		PRINTL 
		MONEY:3 += WIN
		WIN = 0
	ELSE
		VARSET LOCAL, 0, 1, 6
		LOCAL = RESULT
		CLEARLINE LINECOUNT - LINE
		PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
		DRAWLINE
		LOCAL:RESULT = COLOR("aqua")
		CALL SHOW_CARD , , , DECK,
		DRAWLINE
		CALL SHOW_CARD , DECK:1 , DECK:2, DECK:3, DECK:4, DECK:5, LOCAL:1, LOCAL:2, LOCAL:3, LOCAL:4, LOCAL:5
		DRAWLINE
		FOR LOCAL:6, 1, 6
			SIF LOCAL:(LOCAL:6)
			SETCOLOR COLOR("aqua")
			PRINTFORM [{LOCAL:6}]
			PRINT  RAIZE 
			RESETCOLOR
		NEXT
		PRINTL 
		SETCOLOR COLOR("gray")
		PRINTL [9] ＴＡＫＥ ＯＵＴ
		RESETCOLOR
		WAIT
		CLEARLINE LINECOUNT - LINE
		PRINTFORML ＣＯＩＮＳ：{MONEY:3} ＢＥＴ：{WIN}
		DRAWLINE
		IF DECK % 100 < DECK:LOCAL % 100
			WIN *= 2
			CALL MESSAGE_BL, 2, "ＤＯＵＢＬＥ ＵＰ！", "ＧＥＴ "+TOSTR(WIN)+"ＣＯＩＮＳ！！"
		ELSEIF DECK % 100 == DECK:LOCAL % 100
			CALL MESSAGE_BL, 2, "ＤＲＡＷＮ　ＭＡＴＣＨ！", "ＲＥＴＲＹ"
		ELSE
			CALL MESSAGE_BL, 2, "ＹＯＵ ＬＯＳＥ", "ＥＸＴＲＡ ＧＡＭＥ ＥＮＤ"
			WIN = 0
		ENDIF
		;山札から札を削除
		ARRAYREMOVE DECK, 0, 6
	ENDIF
	PRINTL 
	DRAWLINE
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
	DRAWLINE
	FOR LOCAL:6, 1, 6
		SIF LOCAL:(LOCAL:6)
			SETCOLOR COLOR("aqua")
		PRINTFORM [{LOCAL:6}]
		PRINT  RAIZE 
		RESETCOLOR
	NEXT
	PRINTL 
	SETCOLOR COLOR("gray")
	PRINTL [9] ＴＡＫＥ ＯＵＴ
	RESETCOLOR
	WAIT
	;山札が切れたら再生成
	IF MATCH(DECK, 0) > 46
		CALL MESSAGE_B, 2, "ＤＥＣＫ ＯＵＴ！", "ＭＡＫＥ ＮＥＷ ＤＥＣＫ！"
		;山札の作成
		FOR LOCAL, 0, 53
			DECK:LOCAL = (LOCAL/13 * 100)+LOCAL % 13+2
		NEXT
		;53枚目の山札はJOKER(9999)にする
		DECK:52 = 9999
		;山札をシャッフルする
		FOR LOCAL, 0, 53
			LOCAL:1 = RAND:53
			SWAP DECK:LOCAL, DECK:(LOCAL:1)
		NEXT
	ENDIF
WEND
GOTO START
@SHOW_CARD, ARG = -1, ARG:1 = -1, ARG:2 = -1, ARG:3= -1, ARG:4= -1, ARG:5, ARG:6, ARG:7, ARG:8, ARG:9
#LOCALSIZE 10
#LOCALSSIZE 10
GETCOLOR
LOCAL:9 = RESULT
VARSET LOCALS
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  J──┐　
		CASE 0
			PRINT  ┌-─┐　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINT  
			CALL SHOW_TRUMP_SUIT(ARG:LOCAL)
			PRINT ──┐　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  │o  │　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINT  │   │　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  │ k │　
		CASE 0
			PRINT  │
			SETCOLOR COLOR("red")
			PRINT DD
			SETCOLOR COLOR("aqua")
			PRINT S
			IF ARG:(LOCAL+5)
				SETCOLOR ARG:(LOCAL+5)
			ELSE
				SETCOLOR LOCAL:9
			ENDIF
			PRINT │　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINTFORM  │
			CALL SHOW_TRUMP_NUM(ARG:LOCAL)
			PRINTFORM  │　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  │  e│　
		CASE 0
			PRINT  │   │　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINT  │   │　
	ENDSELECT
NEXT
PRINTL 
FOR LOCAL, 0 ,5
	IF ARG:(LOCAL+5)
		SETCOLOR ARG:(LOCAL+5)
	ELSE
		SETCOLOR LOCAL:9
	ENDIF
	SELECTCASE ARG:LOCAL
		CASE 9999
			PRINT  └─-r 　
		CASE 0
			PRINT  └─-┘　
		CASE -1
			PRINT 　　　　　
		CASEELSE
			PRINTFORM  └──
			CALL SHOW_TRUMP_SUIT(ARG:LOCAL)
			PRINT 　
	ENDSELECT
NEXT
PRINTL 
SETCOLOR LOCAL:9
@SHOW_TRUMP_SUIT, ARG
#LOCALSIZE 10
#LOCALSSIZE 10
GETCOLOR
SIF ARG == 0
	PRINTFORM EMPTY
SIF ARG == 9999
	PRINTFORM JOKER
SELECTCASE ARG / 100
	;スペード
	CASE 0
		SETCOLOR 0x0000ff
		PRINTFORM %UNICODE(0x2660)%
	;クラブ
	CASE 1
		SETCOLOR 0x228b22
		PRINTFORM %UNICODE(0x2663)%
	;ハート
	CASE 2
		SETCOLOR 0xff69b4
		PRINTFORM %UNICODE(0x2665)%
	CASE 3
	;ダイヤ
		SETCOLOR COLOR("red")
		PRINTFORM %UNICODE(0x2666)%
ENDSELECT
SETCOLOR RESULT
@SHOW_TRUMP_NUM, ARG
#LOCALSIZE 10
#LOCALSSIZE 10
GETCOLOR
SIF ARG == 0
	PRINTFORM EMPTY
SIF ARG == 9999
	PRINTFORM JOKER
SELECTCASE ARG % 100
	CASE 1, 14
		SETCOLOR COLOR("red")
		PRINTFORM  A
	CASE 11
		PRINTFORM  J
	CASE 12
		SETCOLOR 0xff69b4
		PRINTFORM  Q
	CASE 13
		PRINTFORM  K
	CASE 10
		PRINT 10
	CASEELSE
		PRINTFORM  {ARG%100}
ENDSELECT
SETCOLOR RESULT
