;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:DEVIL_COMPENDIUM.ERB
;	Facility	:悪魔全書一般の処理
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		N鳥						新規作成
;	002		2011/03/ 8		P						一部表記ずれを対策
;	003		2011/06/13		名無し					３身逆引き検索を追加
;	004		2011/10/01		黒天					全書閲覧で登録データとオリジナル(CSV)データの切り替え機能を追加
;	005		2013/12/02		ひみつ					全書閲覧が2列で見れてページ送りも出来るように変更。スキルの説明も見れるように修正。
;													スキル属性表示の適用範囲拡大。スキル属性表示にキャラ情報を渡すように
;			2017/08/30		輝心深紺				全書番号範囲を7101まで（勝手に）拡張。
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;================================================================
;悪魔全書予定地
;解析度:1 〜 名称識別、会話可能 100 〜 アナライズ可能 1000〜全書呼び出し可能




;=============================================================
;悪魔全書
;=============================================================
@DEVIL_COMPENDIUM
#LOCALSIZE 31
;LOCAL ループ用
;LOCAL:1 選択種族
;LOCAL:2 選択した悪魔
;LOCAL:3 召喚した悪魔の番号
;LOCAL:4 召喚費用
;LOCAL:5 召喚可能フラグ
;LOCAL:6 既に仲魔にいるか
;LOCAL:7 全書登録有無
;LOCAL:8 参照データ(0:オリジナル(CSV)データ、1:登録データ)

;LOCAL:10
;LOCAL:20
;LOCAL:30 表示ページ番号
;C 閲覧可能な種族のリスト
;D 選択した種族のリスト
;LOCAL配列の初期化
VARSET LOCAL,0
DRAWLINE
PRINTL [1] Browse
PRINTL [2] ２ ingredient reverse search
PRINTL [3] ３ ingredient reverse search
PRINTL [4] Register demons
PRINTL [5] Trait search
PRINTL [6] Skill search
PRINTL [7] Elemental resistance search
PRINTL [8] Alignment search
DRAWLINE
PRINTL [0] Return
$INPUT_LOOP
ONEINPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 2
	CALL REVERSE_LOOKUP_DEVIL, 2
	RESTART
ELSEIF RESULT == 3
	CALL REVERSE_LOOKUP_DEVIL, 3
	RESTART
ELSEIF RESULT == 4
	CALL REGISTRATION_COMPENDIUM
	RESTART
ELSEIF RESULT == 5
	CALL TALENT_LOOKUP_DEVIL
	RESTART
ELSEIF RESULT == 6
	CALL SKILL_LOOKUP_DEVIL
	RESTART
ELSEIF RESULT == 7
	CALL RESIST_LOOKUP_DEVIL
	RESTART
ELSEIF RESULT == 8
	CALL ALI_LOOKUP_DEVIL
	RESTART
ELSEIF RESULT > 8
	GOTO INPUT_LOOP
ENDIF

LOCAL:25 = LINECOUNT	;行数
$COMPENDIUM_START
CUSTOMDRAWLINE ･

;リスト初期化
VARSET C, 0
FOR LOCAL,1,17
	C:LOCAL = 1
NEXT

FOR LOCAL,1,7101
	IF EXISTCSV(LOCAL,0)
		SIF FLAG:(20000+LOCAL) > 0 && (CSVABL(LOCAL,GETNUM(ABL,"種族"),0)) > -1
			C:(CSVABL(LOCAL,GETNUM(ABL,"種族"),0)) = 1
	ENDIF
NEXT


;HalfDemon・Element（？）・Zomaは閲覧不可
C:36 = 0
C:40 = 0
C:45 = 0

PRINTL Which demon race do you want to browse?
FOR LOCAL,1,99
	SIF C:LOCAL != 1
		CONTINUE
	PRINTFORMLC [{LOCAL,2}] %STR:LOCAL,6,LEFT%
NEXT
PRINTL

DRAWLINE
PRINTL [0] Return

$INPUT_LOOP1
INPUT
SIF RESULT > 999
	GOTO INPUT_LOOP1
IF RESULT == 0
	RESTART
ELSEIF C:RESULT != 1
	GOTO INPUT_LOOP1
ENDIF

LOCAL:1 = RESULT

LOCAL:26 = 0					;表示ページ
LOCAL:27 = CURRENTREDRAW()		;REDRAW値
REDRAW 2
LOCAL:28 = LINECOUNT			;行数
$PRINT_LIST

;リストの作成
VARSET D, -1
FOR LOCAL,1,7101
	IF EXISTCSV(LOCAL,0) && CSVABL(LOCAL,GETNUM(ABL,"種族"),0) == LOCAL:1
;		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"全書召喚不可")),0) == 1 || FLAG:(20000+LOCAL) < 1000 || (CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL) == 0)
;			CONTINUE
		SIF FLAG:(20000+LOCAL) < 1
			CONTINUE
		FOR LOCAL:10,0,7000
			IF D:(LOCAL:10) == -1
					D:(LOCAL:10) = LOCAL
					BREAK
			ELSEIF CSVBASE(LOCAL,GETNUM(BASE,"LV"),0) <= CSVBASE(D:(LOCAL:10),GETNUM(BASE,"LV"),0)
				CONTINUE
			ELSE
					FOR LOCAL:20,4999,LOCAL:10,-1
						SIF D:(LOCAL:20-1) > -1
							D:(LOCAL:20) = D:(LOCAL:20-1)
					NEXT
					D:(LOCAL:10) = LOCAL
					BREAK
			ENDIF
		NEXT
	ENDIF
NEXT
LOCAL:29 = FINDELEMENT(D, -1)	;閲覧可能数

CLEARLINE LINECOUNT - LOCAL:28
LOCAL:6 = 0
;リストを表示
DRAWLINE
PRINTFORML □%STR:(LOCAL:1)% (page.{LOCAL:26 + 1}/{LOCAL:29 / 2 / リスト表示数() + 1})
DRAWLINE
FOR LOCAL, LOCAL:26 * 2 * リスト表示数(), MIN(LOCAL:29, LOCAL:26 * 2 * リスト表示数() + リスト表示数())
	SIF D:LOCAL == -1
		BREAK
	CALL PRINTDEVIL_ELEMENT(LOCAL, LOCAL:1)
	IF D:(LOCAL + リスト表示数()) >= 0
		PRINT 　
		CALL PRINTDEVIL_ELEMENT(LOCAL + リスト表示数(), LOCAL:1)
	ENDIF
	PRINTL 
NEXT
SIF LOCAL:26 && (LOCAL:26 == LOCAL:29 / 2 / リスト表示数())
	PRINTFORM %"\n" * (リスト表示数() + 3 + LOCAL:28 - LINECOUNT)%
DRAWLINE
PRINTFORMLC \@ LOCAL:26 > 0 ? [1007]Previous page # %" " * 16% \@
PRINTFORMLC [1000]Return
PRINTFORMLC \@ LOCAL:26 < LOCAL:29 / 2 / リスト表示数() ? [1009]Next page # %" " * 16% \@
$INPUT_LOOP2
INPUT
IF RESULT == 1000
	CLEARLINE LINECOUNT - LOCAL:25
	REDRAW LOCAL:27
	GOTO COMPENDIUM_START
ELSEIF RESULT == 1007 && LOCAL:26 > 0
	LOCAL:26--
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL:26 < LOCAL:29 / 2 / リスト表示数()
	LOCAL:26++
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= 5000 || D:RESULT == -1 || FLAG:(20000+D:RESULT) < 100
	CLEARLINE 1
	GOTO INPUT_LOOP2
ENDIF

LOCAL:2 = D:RESULT
LOCAL:3 = CHARANUM

SIF CSVCFLAG(LOCAL:2 , GETNUM(CFLAG,"１体のみ"),0) && FINDCHARA(NO,LOCAL:2) > 0
	LOCAL:9 = 1

;選んだ悪魔が既に仲魔である場合、後に渡すフラグを立てておく
SIF FINDCHARA_NO_C(LOCAL:2) > -1
	LOCAL:6 = 1

;最初は全書登録されているデータを表示するように
LOCAL:8 = 1
LOCAL:7 = 0

$CHANGE_REFER_DATA
;一旦悪魔をキャラ登録
CALL ADD_NEW_COMPANION, LOCAL:2, 0, -1, 1
;敵専用スキルを含めて一旦再設定
FOR LOCAL, 1, FLAG:スキル数 + 1
	LOCALS = スキル{LOCAL}
	ABL:(LOCAL:3):LOCALS = CSVABL(LOCAL:2, GETNUM(ABL, LOCALS))
NEXT

;指定悪魔を全書登録してるかチェック
SIF STRFLAG_NUM_CPD_FIND("NO", NO:(LOCAL:3)) > -1
	LOCAL:7 = 1

;全書登録されている悪魔で、全書登録データを閲覧する場合、登録情報を参照
SIF LOCAL:7 && LOCAL:8
	CALL STRFLAG_NUM_CPD(LOCAL:3,"REFER")

;初期化する能力を記述
CFLAG:(LOCAL:3):初期LV = BASE:(LOCAL:3):LV
BASE:(LOCAL:3):ＥＸＰ = BASE:(LOCAL:3):LV* (BASE:(LOCAL:3):LV+1) * (BASE:(LOCAL:3):LV-1) * 5 /3

CFLAG:(LOCAL:3):能力強化回数 = 0
FOR LOCAL,0,6
	CFLAG:(LOCAL:3):(GET_BASESTATUS(LOCAL) + "強化回数") = 0
NEXT
CALL SYNC_STATUS,LOCAL:3
CALL HEALTH_CHARA,LOCAL:3

LOCAL:24 = LINECOUNT
LOCAL:30 = 2
$PRINT_STATUS
CLEARLINE LINECOUNT - LOCAL:24
CUSTOMDRAWLINE =
U = LOCAL:3
LOCALS = \@ CSTR:U:種族名 != "" ? %CSTR:U:種族名% # %STR:(ABL:U:種族)% \@
PRINTFORML %LOCALS%　　%NAME:U%　　
CALLFORM SHOW_INFO_PAGE_\@ LOCAL:30 == 4 ? {LOCAL:30 + 1} # {LOCAL:30}\@
PRINTFORM %"\n" * MAX(0, 26 - LINECOUNT + LOCAL:24)%

;召喚費用を算出
LOCAL:4 = MAX(POWER(BASE:(LOCAL:3):LV,3),BASE:(LOCAL:3):LV*300, POWER(CSVBASE(LOCAL:2 , GETNUM(BASE , "LV"),0) ,3) ,CSVBASE(LOCAL:2,GETNUM(BASE,"LV"),0) * 300)
;精霊は2.5倍
SIF LOCAL:1 == 13
	TIMES LOCAL:4 , 2.50
IF FLAG:(20000+LOCAL:2) < 1000
	LOCAL:4 *= 2 * 1000
	LOCAL:4 /= MAX(1,FLAG:(20000+LOCAL:2))
ENDIF
;基本能力値合計が18 + Lv * 1.5 + 5 + 264 ( = Lv * 1.5 + 287)より大きい場合1ptにつき10万円追加
LOCAL:4 += MAX(0, BASE:(LOCAL:3):力 + BASE:(LOCAL:3):知恵 + BASE:(LOCAL:3):魔力 + BASE:(LOCAL:3):耐力 + BASE:(LOCAL:3):速さ + BASE:(LOCAL:3):運 - BASE:(LOCAL:3):LV * 3 / 2 - 287) * 100000 / POWER(2, FLAG:戦闘難易度-1)

;FUSION.ERBからページ送り他を輸入

;ステータスの表示切替用
;PRINTFORMLC \@ LOCAL:30 > 1 ? [1007]Previous page # %" " * 16% \@
;PRINTFORMLC                 
;PRINTFORMLC \@ LOCAL:30 < 4 ? [1009]Next page # %" " * 16% \@
CALL PRINTBUTTON_CONCOL(" [1004] Training stats ",1,LOCAL:30 == 1,0,"LEFT")
CALL PRINTBUTTON_CONCOL("[1005] Combat stats ",1,LOCAL:30 == 2,0,"LEFT")
CALL PRINTBUTTON_CONCOL("[1006] Character profile ",1,LOCAL:30 == 3,0,"LEFT")
CALL PRINTBUTTON_CONCOL("[1007] Relations ",1,LOCAL:30 == 4,0,"LEFT")
PRINTL
PRINTL

IF CSVCFLAG(LOCAL:2,(GETNUM(CFLAG,"全書召喚不可")),0) == 1 || (CSVCFLAG(LOCAL:2,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL:2) == 0) || FLAG:(20000+LOCAL:2) < 100 || LOCAL:1 > 18
	PRINTFORM [9] Return
	LOCAL:5 = 0
ELSE
	SIF LOCAL:6 == 1
		PRINT 既に同じ仲魔がいますが、
	PRINTFORM Summon this demon?（Summoning cost:￥{LOCAL:4}) [0]Yes [9]No
	LOCAL:5 = 1
ENDIF

IF LOCAL:7
	PRINT  [10] Erase registry
	PRINTFORM  [11] View \@ LOCAL:8 ? original # registration \@ data
ENDIF
PRINTL

$INPUT_LOOP3
INPUTS
IF RESULTS == ""
	GOTO INPUT_LOOP3
ELSEIF !ISNUMERIC(RESULTS)
	CLEARLINE 1
	GOTO INPUT_LOOP3
ENDIF
RESULT = TOINT(RESULTS)
IF (RESULTS == "1004" || RESULTS == "1005" || RESULTS == "1006" || RESULTS == "1007")
CALL SHOW_INFO_PAGE_HELPER(LOCAL:30,RESULTS)
IF RESULT != -1
	LOCAL:30 = RESULT
	GOTO PRINT_STATUS
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP3
ENDIF
;IF RESULT == 1007 && LOCAL:30 > 1
;	LOCAL:30--
;	GOTO PRINT_STATUS
;ELSEIF RESULT == 1009 && LOCAL:30 < 4
;	LOCAL:30++
;	GOTO PRINT_STATUS
ELSEIF INRANGE(RESULT, 1, FLAG:スキル数)
	LOCALS = スキル{RESULT}
	IF ABL:U:LOCALS > 0
		SIF FLAG:スキル属性表示設定 == 1
			CALL SKILL_EXPLAIN_PERFORMANCE, ABL:U:LOCALS, U
		TRYCALLFORM SKILL_EXPLAIN_{ABL:U:LOCALS}
		WAIT
		GOTO PRINT_STATUS
	ELSE
		CLEARLINE 1
		GOTO INPUT_LOOP3
	ENDIF
ELSEIF RESULT == 9
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ELSEIF RESULT == 10 && LOCAL:7
	CALL STRFLAG_NUM_CPD(LOCAL:3,"CLEAR")
	PRINTFORML %LOCALS%%NAME:U%'s registry erased
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ELSEIF RESULT == 11 && LOCAL:7
	;表示内容(登録⇔オリジナル)切替
	INVERTBIT LOCAL:8, 0
	DELCHARA LOCAL:3
	GOTO CHANGE_REFER_DATA
ELSEIF RESULT == 0 && LOCAL:5 == 1
ELSE
	CLEARLINE 1
	GOTO INPUT_LOOP3
ENDIF

;200体対策
IF CHARANUM > 200
	PRINTL ※Maximum characters reached
	PRINTW Summoning canceled
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;悪魔のレベル>主人のレベルだった場合駄目
IF LOCAL:9
	PRINTW Sorry but you can't have more than one of this demon
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;同じ仲魔がいる場合の除外をコメントアウト。代わりに自動ストレージ送りとします
;IF LOCAL:6 == 1
;	PRINTW You already have this demon
;	DELCHARA LOCAL:3
;	GOTO PRINT_LIST
;ENDIF

;悪魔のレベル>主人のレベルだった場合駄目
IF BASE:(LOCAL:3):LV > BASE:MASTER:LV || CSVBASE(LOCAL:2,GETNUM(BASE,"LV"),0) > BASE:MASTER:LV
	PRINTW Sorry but...your level is too low
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;ＣＯＭＰ容量が一杯
IF FLAG:ＣＯＭＰ容量 < NUM_NAKAMA()+ソフト容量()
	PRINTW The ＣＯＭＰ is full
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;足りなければ不可能
IF MONEY < LOCAL:4
	PRINTW Not enough money
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;￥を消費
MONEY -= LOCAL:4

;敵専用スキルを削除
FOR LOCAL, 1, FLAG:スキル数 + 1
	LOCALS = スキル{LOCAL}
	RESULT = 0
	TRYCALLFORM 敵専用_{ABL:(LOCAL:3):LOCALS}
	ABL:(LOCAL:3):LOCALS = RESULT ? 0 # ABL:(LOCAL:3):LOCALS
NEXT

;作成時のセリフを表示
TRYCCALLFORM FUSION_MESSAGE_K{NO:(LOCAL:3)},LOCAL:3
CATCH
	TRYCALLFORM FUSION_MESSAGE_PUB{ABL:(LOCAL:3):会話タイプ},LOCAL:3
ENDCATCH
PRINTFORMW %STR:(LOCAL:1)% %CALLNAME:(LOCAL:3)% became an intermediary.

IF LOCAL:6 == 1
	PRINTW 既に同じ仲魔がいるのでストレージに送られた
	CFLAG:(LOCAL:3):所属ＣＯＭＰ = -1
ENDIF

;種族リストに戻るよ
GOTO PRINT_LIST



;=============================================================
;悪魔解析度増加処理
;=============================================================
@INCREASE_ANALYZE,ARG,ARG:1
;ARG　増加させたい悪魔の登録番号
;ARG:1 増加させたい値

SIF NUM_HAVESKILL([[スキル:民俗学]])
	ARG:1 *= 2
FLAG:(20000+ARG) += ARG:1
SIF FLAG:(20000+ARG) > 1000
	FLAG:(20000+ARG) = 1000

@CAN_ANALYZE_TL,ARG
#FUNCTION
;helper function to make it easier to check if analyzing matters(=is shown on stat screen) and can be performed actively(=the analzye skills work). TL is just to indicate this was added by the translation and not the devs
IF ABL:ARG:種族 > 0 && ABL:ARG:種族 < 45 && !TALENT:ARG:子供
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@PRINTDEVIL_ELEMENT(ARG, ARG:1)
#LOCALSIZE 1
LOCAL = MAX(POWER(CSVBASE(D:ARG, GETNUM(BASE, "LV")), 3), CSVBASE(D:ARG, GETNUM(BASE, "LV")) * 300)	;召喚費用を算出
SIF ARG:1 == 13	;精霊は2.5倍
	TIMES LOCAL , 2.50
SIF FLAG:(20000 + D:ARG) < 100
	SETCOLOR 0x404040
IF FLAG:(20000 + D:ARG) < 1000
	LOCAL *= 2 * 1000
	LOCAL /= MAX(1, FLAG:(20000 + D:ARG))
ENDIF
PRINTFORM [{ARG, 3}] %CSVCALLNAME(D:ARG), 20, LEFT%Lv{CSVBASE(D:ARG, GETNUM(BASE, "LV")), 3, RIGHT}　{FLAG:(20000 + D:ARG) / 10, 3, RIGHT}％
IF CSVTALENT(D:ARG, GETNUM(TALENT, "オトコ"))
	PRINTS \@ CSVTALENT(D:ARG, GETNUM(TALENT, "男の娘")) ? ？　 # ♂　 \@
ELSE
	PRINTS \@ CSVTALENT(D:ARG, GETNUM(TALENT, "ふたなり")) ? 双　 # ♀　 \@
ENDIF
IF FLAG:(20000 + D:ARG) < 100
	PRINT  (Unknown) 
	RESETCOLOR
ELSEIF CSVCFLAG(D:ARG, GETNUM(CFLAG, "全書召喚不可")) || (CSVCFLAG(D:ARG, GETNUM(CFLAG,"合体条件有り")) && !FLAG:(10000 + D:ARG)) || ARG:1 > 18
	PRINT  (Cannot be summoned) 
ELSE
	PRINTFORM ￥{LOCAL, 10, RIGHT}
ENDIF
