;イケニエ合体用処理を記述したファイルです


@FUSION_SACRIFICE
;FLAG:合体予定悪魔1、FLAG:合体予定悪魔2　契約済みの造魔以外の悪魔、素材の登録番号
FLAG:合体予定悪魔1 = -1
FLAG:合体予定悪魔2 = -1
$START_FUSION

IF FLAG:合体予定悪魔1 > -1
	CALL INPUT_CHARA_LIST("Please select the character that should be sacrificed", "CASTING_FUSION_SACRIFICE", "", "UP_PRINT_FUSION_SACRIFICE")
ELSE
	CALL INPUT_CHARA_LIST("Please select the character that should receive a sacrifice", "CASTING_FUSION_SACRIFICE", "", "UP_PRINT_FUSION_SACRIFICE")
ENDIF
IF RESULT == 1000
	IF FLAG:合体予定悪魔1 > -1
		FLAG:合体予定悪魔1 = -1
		GOTO START_FUSION
	ELSE
		RETURN 0
	ENDIF
ENDIF

IF FLAG:合体予定悪魔1 > 0
	FLAG:合体予定悪魔2 = RESULT
	
	;取得する経験値を計算
	LOCAL:4 = CFLAG:(FLAG:合体予定悪魔2):初期LV
	LOCAL:3 = BASE:(FLAG:合体予定悪魔2):ＥＸＰ - 5*(LOCAL:4-1)*(LOCAL:4+1)*(LOCAL:4)/3
	;香で強化されている場合経験値にボーナス
	LOCAL:3 += MAX(0, BASE:(FLAG:合体予定悪魔2):力 + BASE:(FLAG:合体予定悪魔2):知恵 + BASE:(FLAG:合体予定悪魔2):魔力 + BASE:(FLAG:合体予定悪魔2):耐力 + BASE:(FLAG:合体予定悪魔2):速さ + BASE:(FLAG:合体予定悪魔2):運 - 18 - MAXBASE:(FLAG:合体予定悪魔2):LV * 3 / 2) * 100
	SIF BASE:(FLAG:合体予定悪魔1):LV > LOCAL:4
		LOCAL:3 /= (BASE:(FLAG:合体予定悪魔1):LV - LOCAL:4)/4+1
	SIF BASE:(FLAG:合体予定悪魔1):LV < LOCAL:4
		LOCAL:3 *= POWER(2, MIN(( LOCAL:4 - BASE:(FLAG:合体予定悪魔1):LV)/4,4))
	TIMES LOCAL:3 , 0.70
	;取得するＭＡＧを記録
	LOCAL:4 = BASE:(FLAG:合体予定悪魔2):ＭＡＧ
	
	CUSTOMDRAWLINE =
	PRINTFORML %CALLNAME:(FLAG:合体予定悪魔1)% LV{MAXBASE:(FLAG:合体予定悪魔1):LV}({BASE:(FLAG:合体予定悪魔1):LV})
	PRINTFORML 　　　　ＥＸＰ:＋{MAX(LOCAL:3,0)}　　ＭＡＧ：＋{LOCAL:4}
	PRINTFORML 　　　　Character modification points:{BASE:(FLAG:合体予定悪魔2):忠誠度/200}
	CUSTOMDRAWLINE =
	;合体結果のステータスを表示
	;U = FLAG:合体予定悪魔1
	;PRINTFORML %STR:(ABL:U:80)%　　%CALLNAME:U%　　
	;CALL SHOW_INFO_PAGE_2
	;CUSTOMDRAWLINE =
	
	
	;合体するかどうか　→　合体実行（実際には既に終わっている）
	PRINTL Confirm the above results and perform the sacrifice?
	CALL INPUT_YN, "Yes", "No",2

	IF RESULT == 1
		RESTART
	ENDIF
	
;	;作成時のセリフを表示
;	TRYCCALLFORM FUSION_MESSAGE_K{NO:(FLAG:合体予定悪魔1)},LOCAL:3
;	
;	
;	
;	CATCH
;		TRYCALLFORM FUSION_MESSAGE_PUB{ABL:(FLAG:合体予定悪魔1):会話タイプ},LOCAL:3
;	ENDCATCH
;	
;	;ここに忠誠度による素質変化とか予定
;	CALL MAKE_TALENT,LOCAL:3,(BASE:(FLAG:合体予定悪魔1):忠誠度+BASE:(FLAG:合体予定悪魔2):忠誠度)
;	BASE:(LOCAL:3):忠誠度 = MAX(100,RESULT/2)
;	
;	PRINTFORMW %STR:(ABL:(LOCAL:3):種族)% %CALLNAME:(LOCAL:3)%が仲魔になった。
	
	;経験値加算
	BASE:(FLAG:合体予定悪魔1):ＥＸＰ += LOCAL:3
	CALL CHECK_LEVEL_UP, FLAG:合体予定悪魔1
	;ＭＡＧ加算
	BASE:(FLAG:合体予定悪魔1):ＭＡＧ += LOCAL:4
	;忠誠度による素質変更後、忠誠度を加算する
	CALL MAKE_TALENT, FLAG:合体予定悪魔1, BASE:(FLAG:合体予定悪魔2):忠誠度
	BASE:(FLAG:合体予定悪魔1):忠誠度 += MIN(BASE:(FLAG:合体予定悪魔2):忠誠度, RESULT)
	
	;手紙システム
	CALL FUSION_LETTER, FLAG:合体予定悪魔2,6
	
	;合体素材の悪魔を消去
	CALL キャラ削除, FLAG:合体予定悪魔2

	FLAG:合体予定悪魔1 = -1
	FLAG:合体予定悪魔2 = -1
	RETURN 0
ELSE
	FLAG:合体予定悪魔1 = RESULT
ENDIF

GOTO START_FUSION

@CASTING_FUSION_SACRIFICE
;素材は契約済みで無い、造魔以外の悪魔に限る
SIF (契約(COUNT) > 0 || (ABL:COUNT:種族 == 0 || ABL:COUNT:種族 > 44)) && FLAG:合体予定悪魔1 > -1 
	RETURN 0
;一体目は契約済みの造魔以外の悪魔であること
SIF FLAG:合体予定悪魔1 == -1 && (ABL:COUNT:種族 == 45 || ABL:COUNT:種族 == 0 || 契約(COUNT) == 0)
	RETURN 0
SIF COUNT == FLAG:合体予定悪魔1 || COUNT == FLAG:合体予定悪魔2
	RETURN 0
SIF CFLAG:COUNT:労役フラグ == 3
	RETURN 0
SIF CFLAG:COUNT:合体不可
	RETURN 0
SIF CFLAG:COUNT:この場に居ないフラグ == 1
	RETURN 0
RETURN 1

@UP_PRINT_FUSION_SACRIFICE
SIF FLAG:合体予定悪魔1 > -1
	PRINTFORML Receiver:%CALLNAME:(FLAG:合体予定悪魔1)%
;これ表示する場面も必要もないよね？
;SIF FLAG:合体予定悪魔2 > -1
;	PRINTFORML Sacrifice:%CALLNAME:(FLAG:合体予定悪魔2)%