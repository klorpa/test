
; 相性パターン
; 0:不問(なんでも)
; 1:弱点(100%以上かつ999じゃない)
; 2:非弱点(100%以下あるいは999)
; 3:耐性以上(75%以下あるいは999)
; 4:無効(0%)
; 5:吸収(-1以下)
; 6:反射(999)
;オプション
; B.適合率(すべて一致or不一致1つまで)←耐性変化で何とかできるレベルの算出用
; C.より上位の条件を含むか(含むor含まない)
@RESIST_LOOKUP_DEVIL
#DIM LINE,2
#DIM RESIST
#DIM 相性パターン,19
#DIM オプション
#DIM 条件数,2
#DIM COL
$TOP
LINE = LINECOUNT
FOR RESIST,0,18
	SELECTCASE 相性パターン:RESIST
	CASE 0
		COL = 0
		LOCALS = ----
	CASE 1
		COL = FLAG:弱点カラー
		LOCALS = Weak
	CASE 2
		COL = 0
		LOCALS = Neutral
	CASE 3
		COL = FLAG:耐性カラー
		LOCALS = Resist
	CASE 4
		COL = FLAG:無効カラー
		LOCALS = Null
	CASE 5
		COL = FLAG:吸収カラー
		LOCALS = Absorb
	CASE 6
		COL = FLAG:反射カラー
		LOCALS = Reflect
	ENDSELECT
	SIF COL
		SETCOLOR COL / 1000000 , COL /1000 % 1000 , COL % 1000
	PRINTFORM [{RESIST,2}]%GET_TYPE_E(RESIST)%%LOCALS,8%　
	RESETCOLOR
	SIF RESIST % 6 == 5
		PRINTL
NEXT
PRINTL [25] Start Search
PRINTL [26] Reset Search Condition
PRINTFORML [27] Option １：\@ GETBIT(オプション,0) ? 耐性以上は1つまで不一致でも許容 # すべて一致する物のみ検索 \@
PRINTFORML [28] Option ２：\@ GETBIT(オプション,1) ? 上位の条件を含まない # 上位の条件を含む \@
PRINTL [29] Return
INPUT
LOCAL:1 = RESULT
IF INRANGE(RESULT,0,18)
	CALL RESIST_LOOKUP_DEVIL_SETPETTERN(LOCAL:1,相性パターン:(LOCAL:1))
	SIF RESULT >= 0
		相性パターン:(LOCAL:1) = RESULT
ELSEIF RESULT == 26
	VARSET 相性パターン,0
ELSEIF RESULT == 27
	INVERTBIT オプション,0
ELSEIF RESULT == 28
	INVERTBIT オプション,1
ELSEIF RESULT == 29
	RETURN
ELSEIF RESULT == 25
	LINE:1 = LINECOUNT
	$SEARCH
	CUSTOMDRAWLINE =
	LOCAL:1 = 0
	FOR LOCAL, 1, 1900
		SIF FLAG:(20000+LOCAL) < 100
			CONTINUE
		SIF !EXISTCSV(LOCAL, 0)
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"全書召喚不可")),0) == 1
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL) == 0
			CONTINUE

		;ここに条件を入れる
		VARSET 条件数
		FOR RESIST,0,18
			SIF 相性パターン:RESIST == 0
				CONTINUE
			条件数:0++
			CALL CHECK_DEVIL_RESIST(LOCAL,RESIST,相性パターン:RESIST,!GETBIT(オプション,0))
			SIF RESULT == 1
				条件数:1++
		NEXT
		SIF 条件数:1 < 条件数:0
			CONTINUE
		IF CSVCSTR(LOCAL, GETNUM(CSTR, "種族名"), 0) == ""
			PRINTFORM %@"[{LOCAL,4}] "+ CSVNAME(LOCAL, 0) + "(" + STR:CSVABL(LOCAL, GETNUM(ABL, "種族"), 0) + ")",36,LEFT%
		ELSE
			PRINTFORM %@"[{LOCAL,4}] "+ CSVNAME(LOCAL, 0) + "(" + CSVCSTR(LOCAL, GETNUM(CSTR, "種族名"), 0) + ")",36,LEFT%
		ENDIF
		;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "種族", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
		SIF LOCAL:1 % 3 == 2
			PRINTL 
		LOCAL:1++
	NEXT
	SIF !LINEISEMPTY()
		PRINTL
	CUSTOMDRAWLINE =
	IF LOCAL:1
		PRINTFORML {LOCAL:1} matches found.
		PRINTL [   0] Return
		$INP
		INPUT
		IF RESULT == 0
			GOTO TOP
		ELSEIF FLAG:(20000+RESULT) >= 100
			CUSTOMDRAWLINE =
			LOCAL = RESULT
			PRINTFORM \@ CSVCSTR(LOCAL,GETNUM(CSTR,"種族名"),0) != "" ? %CSVCSTR(LOCAL,GETNUM(CSTR,"種族名"),0)% # %STR:CSVABL(LOCAL,GETNUM(ABL,"種族"),0)% \@ 
			PRINTFORML %CSVNAME(LOCAL,0)%
			FOR RESIST,0,18
				LOCAL:1 = CSVBASE(LOCAL,GETNUM(BASE,"剣撃")+RESIST,0)
				SELECTCASE LOCAL:1
				CASE 999
					COL = FLAG:反射カラー
					LOCALS = Reflect
					;反射
				CASE IS < 0
					COL = FLAG:吸収カラー
					LOCALS = Absorb
					;吸{ABS(LOCAL:1)}\%
				CASE 0
					COL = FLAG:無効カラー
					LOCALS = Null
					;無効
				CASE IS < 100
					COL = FLAG:耐性カラー
					LOCALS = Resist
					;{LOCAL:1}\%
				CASE 100
					COL = 0
					LOCALS = Normal
					;{LOCAL:1}\%
				CASE IS > 100
					COL = FLAG:弱点カラー
					LOCALS = Weak
					;{LOCAL:1}\%
				ENDSELECT
				SIF COL
					SETCOLOR COL / 1000000 , COL /1000 % 1000 , COL % 1000
				PRINTFORM %GET_TYPE_E(RESIST)%%LOCALS,8%　
				RESETCOLOR
				SIF RESIST % 6 == 5
					PRINTL
			NEXT
			WAIT
		ELSE
			CLEARLINE 1
			REUSELASTLINE [{RESULT}] is an invalid value
			;は無効な値です
			GOTO INP
		ENDIF
		CLEARLINE LINECOUNT-LINE:1
		GOTO SEARCH
	ELSE
		PRINTFORMW No matches.
		;該当なし
		GOTO TOP
	ENDIF
ENDIF
CLEARLINE LINECOUNT-LINE
GOTO TOP

@RESIST_LOOKUP_DEVIL_SETPETTERN(ARG,ARG:1)
IF LOCAL:1 == 0
	LOCAL:1 = 1
	LOCALS:0 =   Reset
	;不問
	LOCALS:1 =   Weak
	;弱点
	LOCALS:2 =   Neutral
	;通常
	LOCALS:3 =   Resist
	;耐性
	LOCALS:4 =   Null
	;無効
	LOCALS:5 =   Absorb
	;吸収
	LOCALS:6 =   Reflect
	;反射
	LOCALS:7 =   Cancel
	;キャンセル
ENDIF
FOR LOCAL,0,8
	SIF LOCAL == ARG:1
		SETCOLOR COLOR("aqua")
	PRINTFORML [{LOCAL,2}] %LOCALS:LOCAL%
	RESETCOLOR
NEXT
ONEINPUT
SIF RESULT == 7
	RETURN -1
RETURN RESULT

@CHECK_DEVIL_RESIST(ARG,ARG:1,ARG:2,ARG:3)
#DIM CSVRES
CSVRES = CSVBASE(ARG,GETNUM(BASE,"剣撃")+ARG:1,0)
;パターン1：弱点
SIF ARG:2 == 1 && CSVRES > 100 && CSVRES != 999
	RETURN 1
;パターン2:通常
SIF ARG:2 == 2 && ( CSVRES == 100 || ( ARG:3 && CSVRES < 100 ) || ( ARG:3 && CSVRES == 999 ) )
	RETURN 1
;パターン3:耐性
SIF ARG:2 == 3 && ( INRANGE(CSVRES,1,99) || ( ARG:3 && CSVRES <= 0 ) || ( ARG:3 && CSVRES == 999 ) )
	RETURN 1
;パターン4:無効 
SIF ARG:2 == 4 && ( CSVRES == 0 || ( ARG:3 && CSVRES < 0 ) || ( ARG:3 && CSVRES == 999 ) )
	RETURN 1
;パターン5:吸収 
SIF ARG:2 == 5 && CSVRES < 0
	RETURN 1
;パターン6:反射
SIF ARG:2 == 6 && CSVRES == 999
	RETURN 1
RETURN 0

;=========================================================================================
@ALI_LOOKUP_DEVIL
#DIM LINE,2
#DIM ALI_LC
#DIM ALI_LD
#DIM SEX
$TOP
LINE = LINECOUNT
PRINT L-D Alignment 
FOR LOCAL,0,4
	SIF ALI_LD == LOCAL
		SETCOLOR COLOR("aqua")
	IF !LOCAL
		PRINTBUTTON @"[{LOCAL,2}] %"Ignore",8,LEFT%",LOCAL
	ELSE
		PRINTBUTTON @"[{LOCAL,2}] %GET_ALI1(LOCAL),8,LEFT%",LOCAL
	ENDIF
	RESETCOLOR
NEXT
PRINTL
PRINT L-C Alignment 
FOR LOCAL,0,4
	SIF ALI_LC == LOCAL
		SETCOLOR COLOR("aqua")
	IF !LOCAL
		PRINTBUTTON @"[{LOCAL+4,2}] %"Ignore",8,LEFT%",LOCAL+4
	ELSE
		PRINTBUTTON @"[{LOCAL+4,2}] %GET_ALI2(LOCAL),8,LEFT%",LOCAL+4
	ENDIF
	RESETCOLOR
NEXT
PRINTL
PRINT Gender  
LOCALS = ♂＋♀
LOCALS:1 = ♀ only
LOCALS:2 = ♂ only
FOR LOCAL,0,3
	SIF SEX == LOCAL
		SETCOLOR COLOR("aqua")
	PRINTBUTTON @"[{LOCAL+10,2}] %LOCALS:LOCAL,8,LEFT%",LOCAL+10
	RESETCOLOR
NEXT
PRINTL
IF !ALI_LD && !ALI_LC
	SETCOLOR COLOR("gray")
	PRINTPLAIN [18] Start Search
	PRINTL
	RESETCOLOR
ELSE
	PRINTL [18] Start Search
ENDIF
PRINTL [19] Return
INPUT
LOCAL:1 = RESULT
IF INRANGE(RESULT,0,3)
	ALI_LD = RESULT
ELSEIF INRANGE(RESULT,4,7)
	ALI_LC = RESULT-4
ELSEIF INRANGE(RESULT,10,12)
	SEX = RESULT-10
ELSEIF RESULT == 19
	RETURN
ELSEIF RESULT == 18 && (ALI_LC || ALI_LD)
	LINE:1 = LINECOUNT
	$SEARCH
	CUSTOMDRAWLINE =
	LOCAL:1 = 0
	FOR LOCAL, 1, 1900
		SIF FLAG:(20000+LOCAL) < 100
			CONTINUE
		SIF !EXISTCSV(LOCAL, 0)
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"全書召喚不可")),0) == 1
			CONTINUE
		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL) == 0
			CONTINUE

		;ここに条件を入れる
		SIF ALI_LD && CSVABL(LOCAL,GETNUM(ABL,"属性LD"),0) != ALI_LD
			CONTINUE
		SIF ALI_LC && CSVABL(LOCAL,GETNUM(ABL,"属性LC"),0) != ALI_LC
			CONTINUE
		SIF SEX == 1 && CSVTALENT(LOCAL,GETNUM(TALENT,"オトコ"),0)
			CONTINUE
		SIF SEX == 2 && !CSVTALENT(LOCAL,GETNUM(TALENT,"オトコ"),0)
			CONTINUE
		;条件ここまで

		IF CSVTALENT(LOCAL,GETNUM(TALENT,"オトコ"),0)
			IF CSVTALENT(LOCAL,GETNUM(TALENT,"男の娘"),0)
				SETCOLOR 0x80FF80
			ELSE
				SETCOLOR 0x8080FF
			ENDIF
		ELSE
			SETCOLOR 0xFF8080
		ENDIF
		PRINTPLAINFORM %@"[{LOCAL,4}] %CSVNAME(LOCAL, 0)%(%CSV種族名(LOCAL,0)%)",36,LEFT%
		;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "種族", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
		SIF LOCAL:1 % 3 == 2
			PRINTL 
		LOCAL:1++
		RESETCOLOR
	NEXT
	SIF !LINEISEMPTY()
		PRINTL
	CUSTOMDRAWLINE =
	IF LOCAL:1
		PRINTFORML {LOCAL:1} matches found.
		;件が該当しました
		;WAIT
		;implement a result viewing like with the other search?
	ELSE
		PRINTFORMW No matches.
		;該当なし
	ENDIF
	GOTO TOP
ENDIF
CLEARLINE LINECOUNT-LINE
GOTO TOP

