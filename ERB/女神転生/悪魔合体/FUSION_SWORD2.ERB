;================================================================
;魔晶装備２　別仕様の魔晶装備。魔晶による強化が可能
;================================================================
@MAKE_MASHO2
FLAG:合体予定悪魔1 = -1
;LOCAL ループ用
;LOCAL:1 素材の登録番号
LOCAL:1 = -1
;LOCAL:2 悪魔の表示用変数
;LOCAL:3 合体結果の剣枠
;LOCAL:4 作成する魔晶装備の種類
;G 合体剣のフラグを管理する変数 20-25:バッドステータス村正用

$START_MAKE
LOCAL:6 = 0

;キャラリストを表示・選択
CALL INPUT_CHARA_LIST("素体にする悪魔を選んでください","CASTING_MASHO2")
SIF RESULT == 1000
	JUMP SELECT_MASHO2
LOCAL:1 = RESULT


LOCAL:3 = -1
;空いている合体剣枠を参照
FOR LOCAL,0,50
	IF DC:LOCAL:49 == 0
		LOCAL:3 = LOCAL
		BREAK
	ENDIF
NEXT
IF LOCAL:3 == -1
	PRINTW これ以上合体剣を持てません
	RETURN 0
ENDIF

;合体剣の種類を決める
;習得スキルによってGを設定
VARSET G,-1
G:0 = 1
FOR LOCAL,1,FLAG:スキル数+1
	LOCALS = スキル{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による条件
		TRYCALLFORM SKILL_TYPE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
		G:RESULT = 1
	ENDIF
NEXT
;剣撃以外の物理は無し
G:1 = -1
G:2 = -1
G:3 = -1


;神経・精神は特殊
G:8 = -1
G:9 = -1
FOR LOCAL,1,FLAG:スキル数+1
	LOCALS = スキル{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による条件
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
			SELECTCASE GET_STATE(RESULT)
				CASE "POISON"
					G:20 = 1
				CASE "SLEEP"
					G:21 = 1
				CASE "PANIC"
					G:22 = 1
				CASE "HAPPY"
					G:23 = 1
				CASE "BIND"
					G:24 = 1
				CASE "CHARM"
					G:25 = 1
			ENDSELECT
		CATCH
		ENDCATCH
	ENDIF
NEXT

;剣のタイプを選択
$PRINT_TYPE
DRAWLINE
PRINTL 剣の攻撃相性を選択してください
FOR LOCAL,0,FLAG:相性数
	SIF G:LOCAL > 0
		PRINTFORM [{LOCAL}] 【%GET_TYPE(LOCAL)%】
	SIF G:LOCAL > 0 && LOCAL == 5
		PRINT & FREEZE ２０％
	SIF G:LOCAL > 0 && LOCAL == 6
		PRINT & SHOCK ２０％
	SIF G:LOCAL > 0
		PRINTL
NEXT
SIF G:20 > 0
	PRINTL [20] 【剣撃】& POISON ２０％
SIF G:21 > 0
	PRINTL [21] 【剣撃】& SLEEP ２５％
SIF G:22 > 0
	PRINTL [22] 【剣撃】& PANIC ２５％
SIF G:23 > 0
	PRINTL [23] 【剣撃】& HAPPY ３５％
SIF G:24 > 0
	PRINTL [24] 【剣撃】& BIND ２５％
SIF G:25 > 0
	PRINTL [25] 【剣撃】& CHARM ２０％
DRAWLINE
PRINTL [100] キャンセル

$INPUT_TYPE
INPUT

IF RESULT == 100
	RESTART
ELSEIF G:RESULT > 0
	;DCにデータを入力
	;まずは名称と相性、追加効果を決めておく
	SELECTCASE RESULT
		CASE 0
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 1
		CASE 4,16
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 2
		CASE 5
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 3
			DC:(LOCAL:3):45 = GET_STATE_NUM("FREEZE")
			DC:(LOCAL:3):46 = 5
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 6
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 4
			DC:(LOCAL:3):45 = GET_STATE_NUM("SHOCK")
			DC:(LOCAL:3):46 = 6
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 7
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 5
		CASE 10,11
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT
		CASE 12 TO 15
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT - 6
		CASE 20
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 12
			DC:(LOCAL:3):45 = GET_STATE_NUM("POISON")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 21
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 13
			DC:(LOCAL:3):45 = GET_STATE_NUM("SLEEP")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 22
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 14
			DC:(LOCAL:3):45 = GET_STATE_NUM("PANIC")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 23
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 15
			DC:(LOCAL:3):45 = GET_STATE_NUM("HAPPY")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 35
			DC:(LOCAL:3):48 = 95
		CASE 24
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 16
			DC:(LOCAL:3):45 = GET_STATE_NUM("BIND")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 25
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 17
			DC:(LOCAL:3):45 = GET_STATE_NUM("CHARM")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 17
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 18
	ENDSELECT

	;仕様タイプを１に。引き継ぎ可能
	DC:(LOCAL:3):90 = 1

	;装備可能レベルを設定、陥落資質で緩和
	DC:(LOCAL:3):50 = BASE:(LOCAL:1):LV

	SIF 陥落(LOCAL:1) == 1
		DC:(LOCAL:3):50 -= 20
	SIF 陥落(LOCAL:1) == 2
		DC:(LOCAL:3):50 -= 30
	SIF 陥落(LOCAL:1) == 3
		DC:(LOCAL:3):50 = MIN(DC:(LOCAL:3):50/2, DC:(LOCAL:3):50 - 30)

	;かなりの長期間、連れ添っていると装備可能レベルが完全に免除される
	SIF 陥落(LOCAL:1) == 3 && BASE:COUNT:忠誠度 >= 50000
		DC:(LOCAL:3):50 = 1

	DC:(LOCAL:3):50 = MAX(DC:(LOCAL:3):50, 1)

	;基本能力値修正、戦闘能力値修正を決定
	;基本
	DC:(LOCAL:3):0 += (MIN(ABL:(LOCAL:1):従順 , 10) + MIN(ABL:(LOCAL:1):欲望 , 10) + MIN(ABL:(LOCAL:1):技巧 , 10) +3)/6
	DC:(LOCAL:3):1 += (MIN(ABL:(LOCAL:1):欲望 , 10)+1)/5
	DC:(LOCAL:3):3 += (MIN(ABL:(LOCAL:1):欲望 , 10)+1)/5
	DC:(LOCAL:3):6 += (MIN(ABL:(LOCAL:1):従順 , 10)+1)/5
	DC:(LOCAL:3):4 += (MIN(ABL:(LOCAL:1):従順 , 10)+1)/5
	DC:(LOCAL:3):2 += (MIN(ABL:(LOCAL:1):技巧 , 10)+1)/5
	DC:(LOCAL:3):5 += (MIN(ABL:(LOCAL:1):技巧 , 10)+1)/5

	DC:(LOCAL:3):1 += MAX(BASE:(LOCAL:1):力/20 - 2, 0)
	DC:(LOCAL:3):3 += MAX(BASE:(LOCAL:1):魔力/20 - 2, 0)
	DC:(LOCAL:3):6 += MAX(BASE:(LOCAL:1):運/20 - 2, 0)
	DC:(LOCAL:3):4 += MAX(BASE:(LOCAL:1):耐力/20 - 2, 0)
	DC:(LOCAL:3):2 += MAX(BASE:(LOCAL:1):知恵/20 - 2, 0)
	DC:(LOCAL:3):5 += MAX(BASE:(LOCAL:1):速さ/20 - 2, 0)

	;戦闘
	DC:(LOCAL:3):10 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*3/2 + BASE:(LOCAL:1):力*3/2 + BASE:(LOCAL:1):耐力 / 2
	DC:(LOCAL:3):11 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*6/5 + BASE:(LOCAL:1):速さ*3/2 + BASE:(LOCAL:1):運/2
	DC:(LOCAL:3):14 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*3/2 + BASE:(LOCAL:1):魔力*3/2 + BASE:(LOCAL:1):知恵 / 2
	DC:(LOCAL:3):15 = (MIN(BASE:(LOCAL:1):LV,255)+5+DC:(LOCAL:3):0*2)*6/5 + BASE:(LOCAL:1):知恵 + BASE:(LOCAL:1):運/2 + BASE:(LOCAL:1):魔力/2

	;攻撃形態は素材のものをコピー
;	DC:(LOCAL:3):41 = ABL:(LOCAL:1):最低攻撃回数
	DC:(LOCAL:3):41 = 1
;	DC:(LOCAL:3):42 = ABL:(LOCAL:1):最大攻撃回数
	DC:(LOCAL:3):42 = 1
	DC:(LOCAL:3):43 = ABL:(LOCAL:1):射程
	DC:(LOCAL:3):44 = ABL:(LOCAL:1):攻撃範囲

	; 運特化で最大２回攻撃
	SIF BASE:(LOCAL:1):運 >= 100
		DC:(LOCAL:3):42 = 2

	; 耐力特化で防除を上昇（運に比べて弱いのは仕方なし）
	SIF BASE:(LOCAL:1):耐力 >= 100
		DC:(LOCAL:3):12 = BASE:(LOCAL:1):耐力*2/3+1

	; 力一定以上で、ダメージディーラー用の武器と見做す
	SIF DC:(LOCAL:3):44 == 1 && BASE:(LOCAL:1):力 >= 50
		DC:(LOCAL:3):10 += BASE:(LOCAL:1):力
	SIF DC:(LOCAL:3):44 == 2 && BASE:(LOCAL:1):力 >= 50
		DC:(LOCAL:3):10 += BASE:(LOCAL:1):力/2

	; 速さは申し訳程度の強化で
	SIF BASE:(LOCAL:1):速さ >= 100
		DC:(LOCAL:3):11 += 20

	;単体は命中を強化
	SIF DC:(LOCAL:3):44 == 1
		TIMES DC:(LOCAL:3):11 , 1.10

	; 全体は火力弱体化
	SIF DC:(LOCAL:3):44 == 3
		TIMES DC:(LOCAL:3):10 , 0.80

	; 命中での調整案も置いておく。無条件での割合減少は拙いので、MINで上限掛けるとか
	;SIF DC:(LOCAL:3):44 == 3
	;	DC:(LOCAL:3):11 -= MIN(DC:(LOCAL:3):10/8, 30)

	;剣仕様に変更 独自仕様として、性能を半減
	TIMES DC:(LOCAL:3):10 , 0.50
	TIMES DC:(LOCAL:3):11 , 0.50
	DC:(LOCAL:3):11 += 5
	TIMES DC:(LOCAL:3):14 , 0.40
	TIMES DC:(LOCAL:3):15 , 0.20

	;;射程・攻撃範囲によって命中と攻撃に修正が入る
	;SELECTCASE DC:(LOCAL:3):43
	;	CASE 2
	;		TIMES DC:(LOCAL:3):11 , 0.8
	;	CASE 3
	;		TIMES DC:(LOCAL:3):11 , 0.6
	;ENDSELECT
	;SELECTCASE DC:(LOCAL:3):44
	;	CASE 2
	;		TIMES DC:(LOCAL:3):10 , 0.7
	;		TIMES DC:(LOCAL:3):11 , 0.8
	;	CASE 3
	;		TIMES DC:(LOCAL:3):10 , 0.4
	;		TIMES DC:(LOCAL:3):11 , 0.5
	;ENDSELECT
	
	DC:(LOCAL:3):39 = GET_EQUIPNUM("剣")

	;追加効果付きの剣は修正を加える
	SELECTCASE DC:(LOCAL:3):49
		CASE 3,4
			TIMES DC:(LOCAL:3):10 , 0.85
			TIMES DC:(LOCAL:3):11 , 0.85
		CASE 12 TO 18
			TIMES DC:(LOCAL:3):10 , 0.80
			TIMES DC:(LOCAL:3):11 , 0.80
	ENDSELECT
	;防御相性
	FOR LOCAL,0,FLAG:相性数
		DC:(LOCAL:3):(20+LOCAL) = 100
	NEXT

	;おそらくは万能をさらに弱体化させるための記述
	;ミスで魅了が下がっていたが、ここでは削除
	;SIF DC:(LOCAL:3):40 == 18
	;	TIMES DC:(LOCAL:3):10 , 0.75
	
ELSE
	CLEARLINE 1
	GOTO INPUT_TYPE
ENDIF

;名称設定
CALL NAME_MASHO2,LOCAL:1,LOCAL:3

;合体結果の剣を表示
CALL 装備説明,(2450+LOCAL:3+450*DC:(LOCAL:3):39)
CALL 装備条件説明_魔晶装備,LOCAL:3
PRINTL 

;合体するかどうか　→　合体実行（実際には既に終わっている）
PRINT この剣を作りますか？ 

CALL INPUT_YN,"Yes","No",2

SELECTCASE RESULT
	CASE 0
		ITEM:(2450+LOCAL:3+450*DC:(LOCAL:3):39) = 1
	CASE 1
		REPEAT 50
			DC:(LOCAL:3):COUNT = 0
		REND
		RESTART
ENDSELECT


PRINTFORMW %EQ_GETNAME_魔晶装備(LOCAL:3)%を手に入れた

CALL FUSION_LETTER,LOCAL:1,1

;合体素材の悪魔を消去
CALL キャラ削除, LOCAL:1

ITEM:"Nameless Sword" -= 1

RETURN 0

GOTO START_MAKE


@SELECT_MASHO2
IF DAY > 50
	PRINTL インストールソフト『魔匠の工房』の使用期間、50日が経過しています。
	PRINTL 現在、この機能を使用する事はできません。
	PRINTL 通常の魔晶武器作成を行う場合、ソフトをoffにしてください。拡張モードを終了します……
	PRINTL 
	RETURN 0
ENDIF

PRINTL インストールソフト『魔匠の工房』を確認。使用期間内である事を確認。
PRINTL 拡張モードを起動します。
PRINTL
PRINTL [ 1]魔晶剣作成
;PRINTL [ 2]魔晶杖作成（未実装）
;PRINTL [ 5]合体剣作成（未実装）
PRINTL [10]魔晶剣強化
;PRINTL []魔晶装備の命名
PRINTL [99]解説
PRINTL [ 0]戻る

$INPUT_LOOP
INPUT
IF RESULT == 1
	IF ITEM:"Nameless Sword" >= 1
		CALL MAKE_MASHO2
	ELSE
		PRINTW 作成には無銘の刀が必要です。
		PRINTL 
		RETURN 0
	ENDIF
ELSEIF RESULT == 10
	CALL PLUS_MASHO2
ELSEIF RESULT == 99
	CALL KAISETU_MASHO2
ELSEIF RESULT == 0
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF


@PLUS_MASHO2
;[10]魔晶剣強化
;LOCAL:3 魔晶武器用のデータ
;LOCAL:4 消費される仲魔
;LOCAL:5 消費魔貨

;生贄
CALL INPUT_CHARA_LIST("素材にする悪魔を選んでください","CASTING_MASHO2_2")

; デバッグ用の抜け道を作っておく
SIF FLAG:DEBUG == 0 && RESULT == 1000
	JUMP SELECT_MASHO2
SIF FLAG:DEBUG == 1 && RESULT == 1000
	RESULT = 0

LOCAL:4 = RESULT

PRINTFORML どの装備を強化しますか？　＜page.{P + 1}＞
DRAWLINE
;持っているアイテムを表示
VARSET G,-1
FOR LOCAL,2000,VARSIZE("ITEM")
	IF 魔晶装備(LOCAL)
		IF (ITEM:LOCAL > 0) && (DC:(LOCAL-2450-450*((LOCAL-2000)/500)):90 == 1)
			PRINTFORMLC [{LOCAL}]%EQ_GETNAME_魔晶装備(LOCAL-2450-450*((LOCAL-2000)/500))%

			G:LOCAL = 1
		ENDIF
	ELSE
	ENDIF
NEXT

$PRINT_LIST
DRAWLINE
PRINTL [1000] 戻る
$INPUT_LOOP_1
INPUT
IF RESULT == 1000
		JUMP SELECT_MASHO2
ELSEIF RESULT < 0 || RESULT >= VARSIZE("G")
	GOTO INPUT_LOOP_1
ELSEIF G:RESULT != 1
	GOTO INPUT_LOOP_1
ENDIF
LOCAL:2 = RESULT
LOCAL:3 = (LOCAL:2-2450-450*((LOCAL:2-2000)/500))
CALL 装備説明,LOCAL:2
CALL 装備条件説明_魔晶装備,LOCAL:3
PRINTL 

;価格決定
LOCAL:5 = 1000 + (DC:(LOCAL:3):52)*(DC:(LOCAL:3):52-2)*200
SIF LOCAL:5 < 1000
	LOCAL:5 = 1000
SIF DC:(LOCAL:3):10 < 100 && DC:(LOCAL:3):11 < 100 && DC:(LOCAL:3):14 < 100 && DC:(LOCAL:3):15 < 100
	LOCAL:5 = LOCAL:5/10


PRINTFORML この装備を強化しますか？　[強化回数：{DC:(LOCAL:3):52}]　[魔貨{LOCAL:5}＋仲魔消費]
PRINTL [0]Yes [1]No
$INPUT_LOOP2
INPUT
IF RESULT == 1
	RESTART
ELSEIF RESULT != 0
	GOTO INPUT_LOOP2
ENDIF

IF MONEY:1 < LOCAL:5
	PRINTW 魔貨が足りていない。
	PRINTL 

	RESTART
ENDIF

	;基本能力値修正、戦闘能力値修正を決定
	;基本
	TIMES DC:(LOCAL:3):1 , 1.15
	TIMES DC:(LOCAL:3):3 , 1.15
	TIMES DC:(LOCAL:3):6 , 1.15
	TIMES DC:(LOCAL:3):4 , 1.15
	TIMES DC:(LOCAL:3):2 , 1.15
	TIMES DC:(LOCAL:3):5 , 1.15

	IF DC:(LOCAL:3):52 % 10 == 9
		FOR LOCAL, 1, 7
			SIF DC:(LOCAL:3):LOCAL >= 1
			DC:(LOCAL:3):LOCAL += 1
		NEXT
	ENDIF

	;戦闘
	IF  DC:(LOCAL:3):52 <= 9
		TIMES DC:(LOCAL:3):10 , 1.15
		TIMES DC:(LOCAL:3):11 , 1.15
		TIMES DC:(LOCAL:3):14 , 1.15
		TIMES DC:(LOCAL:3):15 , 1.15
	ELSE
		TIMES DC:(LOCAL:3):10 , 1.03
		TIMES DC:(LOCAL:3):11 , 1.03
		TIMES DC:(LOCAL:3):14 , 1.03
		TIMES DC:(LOCAL:3):15 , 1.03
	ENDIF

;生贄の種族による変化

	SELECTCASE ABL:(LOCAL:4):80
		;地母神
		CASE 1
			DC:(LOCAL:3):10 -= 5
			DC:(LOCAL:3):11 += 5
		;女神
		CASE 2
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):2 += 1
		;魔王
		CASE 3
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):3 += 1
		;天使
		CASE 4
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):4 += 1

			SIF DC:(LOCAL:3):40 == 10
				DC:(LOCAL:3):10 += 10
		;妖魔
		CASE 5
			DC:(LOCAL:3):15 += 5
			DC:(LOCAL:3):14 -= 5

			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):6 += 1
		;妖精
		CASE 6
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):2 += 1
		;妖鬼
		CASE 7
			DC:(LOCAL:3):10 += 5
			DC:(LOCAL:3):11 -= 5

			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 1
		;龍王
		CASE 8
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 2
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):6 -= 1
		;魔獣
		CASE 9
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):4 += 1
		;妖鳥
		CASE 10
			DC:(LOCAL:3):15 -= 5
			DC:(LOCAL:3):11 += 5
		;外道
		CASE 11
			SIF DC:(LOCAL:3):40 == 11
				DC:(LOCAL:3):10 += 10
		;マシン
		CASE 12
			;特になし
		;精霊
		CASE 13
			DC:(LOCAL:3):11 += 1
			DC:(LOCAL:3):15 += 1
		;御霊
		CASE 14
			;特になし
		;鬼神
		CASE 15
			DC:(LOCAL:3):10 += 2

			IF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 2
				DC:(LOCAL:3):4 += 2
				DC:(LOCAL:3):2 -= 1
				DC:(LOCAL:3):3 -= 1
			ENDIF
		;魔神
		CASE 16
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):3 += 1
		;堕天使
		CASE 18
			SIF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):6 += 1

		;魔人
		CASE 42
			DC:(LOCAL:3):10 += 1
			DC:(LOCAL:3):14 += 1
			IF DC:(LOCAL:3):52 % 10 == 4 || DC:(LOCAL:3):52 % 10 == 9
				DC:(LOCAL:3):1 += 1
				DC:(LOCAL:3):2 += 1
				DC:(LOCAL:3):3 += 1
				DC:(LOCAL:3):4 += 1
				DC:(LOCAL:3):5 += 1
				DC:(LOCAL:3):6 += 1
			ENDIF

	ENDSELECT

;月齢による変化
IF FLAG:月齢 == 8
	DC:(LOCAL:3):10 += 5
	DC:(LOCAL:3):11 -= 5
	DC:(LOCAL:3):14 += 5
	DC:(LOCAL:3):15 -= 5
ELSEIF FLAG:月齢 == 0
	DC:(LOCAL:3):10 -= 5
	DC:(LOCAL:3):11 += 5
	DC:(LOCAL:3):14 -= 5
	DC:(LOCAL:3):15 += 5
ENDIF

CALL 装備説明,LOCAL:2
PRINTW 指定の魔晶武器が強化されました！
PRINTL 

MONEY:1 -= LOCAL:5
SIF LOCAL:4 != 0
	CALL キャラ削除, LOCAL:4
;強化回数増加
DC:(LOCAL:3):52 += 1

@KAISETU_MASHO2
PRINTL 魔晶武器作成、拡張モード解説
PRINTL 
PRINTL ・無銘の刀と悪魔を合体させ、魔晶武器を作り出す事ができる。
PRINTL ・魔晶武器は魔晶を用いる事でさらに強化する事ができる。
PRINTL ・魔晶は悪魔を宝石や特殊な金属に封印する事で生成される。
PRINTL ・このソフト上では、宝石や金属は魔貨によって代用される。
PRINTL 
PRINTL ●作成される魔晶武器の特徴
PRINTL ・作成時の性能が大幅に減少
PRINTL ・素材のレベルに基づき、装備可能レベルが設定される
PRINTL ・引き継ぎ可能
PRINTL ・魔貨と仲魔を消費し、段階的な強化が可能
PRINTL ・消費できる仲魔は陥落、または忠誠度が一定以上の者のみ
PRINTL 
PRINTL ●注意点
PRINTL このソフトには使用期間が存在し、50日以降には停止します。
PRINTW

;================================================================
;魔晶武器の名称設定
;================================================================
@NAME_MASHO2,ARG,ARG:1
PRINTL 魔晶武器の名称を指定してください
PRINTL 
PRINTFORML [0]%STR:(DC:(ARG:1):49+100)%
PRINTFORML [1]魔晶剣：%NAME:(ARG)%
PRINTFORML [2]自分で指定する
$INPUT_LOOP
INPUT
IF RESULT == 0

ELSEIF RESULT == 1
	SAVESTR:(550+ARG:1) = 魔晶剣：%NAME:(ARG)%
	DC:(ARG:1):49 = 19
ELSEIF RESULT == 2
	PRINTL 魔晶武器の名称を入力してください。
	$INPUT_LOOP2

	INPUTS
	IF RESULTS == ""
		GOTO INPUT_LOOP2
	ELSE
		SAVESTR:(550+ARG:1) = %RESULTS%
		DC:(ARG:1):49 = 19
	ENDIF

ELSE
	GOTO INPUT_LOOP
ENDIF
PRINTL 

;================================================================
;材料にできる悪魔の条件
;================================================================
; 作成用、拡張モードでは契約していても可
@CASTING_MASHO2
SIF ABL:COUNT:種族 == 0 || ABL:COUNT:種族 > 44
	RETURN 0
SIF CFLAG:COUNT:労役フラグ == 3
	RETURN 0
SIF CFLAG:COUNT:この場に居ないフラグ == 1
	RETURN 0
SIF CFLAG:COUNT:合体不可
	RETURN 0
SIF EQUIP:COUNT:剣 != 0
	RETURN 0
SIF EQUIP:COUNT:銃 != 0
	RETURN 0
SIF EQUIP:COUNT:頭 != 0
	RETURN 0
SIF EQUIP:COUNT:胴 != 0
	RETURN 0
SIF EQUIP:COUNT:腕 != 0
	RETURN 0
SIF EQUIP:COUNT:足 != 0
	RETURN 0
RETURN 1

;こちらは強化用、陥落しているか忠誠度が必要
@CASTING_MASHO2_2
SIF 契約(COUNT) > 0
	RETURN 0
SIF 陥落(COUNT) <= 0 && BASE:COUNT:忠誠度 <= 14999
	RETURN 0
SIF ABL:COUNT:種族 == 0 || ABL:COUNT:種族 > 44
	RETURN 0
SIF CFLAG:COUNT:労役フラグ == 3
	RETURN 0
SIF CFLAG:COUNT:この場に居ないフラグ == 1
	RETURN 0
SIF CFLAG:COUNT:合体不可
	RETURN 0
SIF EQUIP:COUNT:剣 != 0
	RETURN 0
SIF EQUIP:COUNT:銃 != 0
	RETURN 0
SIF EQUIP:COUNT:頭 != 0
	RETURN 0
SIF EQUIP:COUNT:胴 != 0
	RETURN 0
SIF EQUIP:COUNT:腕 != 0
	RETURN 0
SIF EQUIP:COUNT:足 != 0
	RETURN 0
RETURN 1
