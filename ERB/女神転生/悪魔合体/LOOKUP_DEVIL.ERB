;-------------------------
;素質から悪魔を検索
;対象は召喚できる悪魔
;-------------------------
@TALENT_LOOKUP_DEVIL
#LOCALSIZE 2
PRINTL Enter trait name to search
PRINTL (Will exit if you enter nothing)
INPUTS
SIF RESULTS == ""
	RETURN 0
IF TALENTNAME_R(RESULTS) == -1
;GETNUM(TALENT, RESULTS) == -1 && 
	PRINTFORMW [%RESULTS%] doesn't exist
	CLEARLINE 5
	RESTART
ENDIF
CLEARLINE 3
DRAWLINE
PRINTFORML □%RESULTS%
LOCAL:1 = 0
FOR LOCAL, 0, 1900
	SIF FLAG:(20000+LOCAL) < 100
		CONTINUE
	SIF !EXISTCSV(LOCAL, 0)
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"全書召喚不可")),0) == 1
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL) == 0
		CONTINUE
	;GETNUM(TALENT, RESULTS)
	SIF !CSVTALENT(LOCAL, TALENTNAME_R(RESULTS), 0)
		CONTINUE
	IF CSVCSTR(LOCAL, GETNUM(CSTR, "種族名"), 0) == ""
		PRINTPLAINFORM %@"[{LOCAL:1,2}] "+ CSVNAME(LOCAL, 0) + "(" + STR:CSVABL(LOCAL, GETNUM(ABL, "種族"), 0) + ")",36,LEFT%
	ELSE
		PRINTPLAINFORM %@"[{LOCAL:1,2}] "+ CSVNAME(LOCAL, 0) + "(" + CSVCSTR(LOCAL, GETNUM(CSTR, "種族名"), 0) + ")",36,LEFT%
	ENDIF
	;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "種族", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
	SIF LOCAL:1 % 3 == 2
		PRINTL 
	LOCAL:1++
NEXT
SIF LINEISEMPTY() == 0
	PRINTL  
SIF LOCAL:1 == 0
	PRINTFORML No demon with %RESULTS% registered
RESTART
;-------------------------
;スキルから悪魔を検索
;対象は召喚できる悪魔
;-------------------------
@SKILL_LOOKUP_DEVIL
#LOCALSIZE 4
#LOCALSSIZE 200
#DIMS SKILLNAME, 1
#DIM SKILL, 1
#DIM DEM, 7000	   
VARSET DEM, 0
PRINTL Enter skill name to search
PRINTL (Will exit if you enter nothing)
INPUTS
SIF RESULTS == ""
	RETURN 0
SKILL = -1
SKILLNAME = %RESULTS%
;入力されたスキル名が存在するものかどうかを確かめつつ、スキル番号を取得する
FOR LOCAL, 0, 2500
	TRYCCALLFORM SKILL_NAME_{LOCAL}
		;ややこしいので、「・」は省略した場合も通るようにする
		RESULTS:1 = %REPLACE(RESULTS, "・", "")%
		SIF RESULTS != SKILLNAME && RESULTS:1 != SKILLNAME
			CONTINUE
		SKILL = LOCAL
		SKILLNAME = %RESULTS%
		BREAK
	CATCH
	ENDCATCH
NEXT
IF SKILL == -1
	PRINTFORMW [%SKILLNAME%] does not exist
	CLEARLINE 5
	RESTART
ENDIF
CLEARLINE 3
DRAWLINE
PRINTFORML □%SKILLNAME%
;登録悪魔で持っているものを検索
;名前は出さず、D配列にヒットした悪魔を記録
FOR LOCAL, 2000, 7000
	;空番号ひいたら終了
	SIF SAVESTR:LOCAL == ""
		BREAK
	SPLIT SAVESTR:LOCAL, ",", LOCALS
	FOR LOCAL:1, 1, 9
		IF TOINT(LOCALS:GET_CPD_STRFLAG_NUM(@"スキル{LOCAL:1}")) == SKILL
			DEM:TOINT(LOCALS:GET_CPD_STRFLAG_NUM("NO")) = 1
			BREAK
		ENDIF
	NEXT
NEXT
LOCAL:3 = 0
;通常の全書検索
FOR LOCAL, 0, 7000
	SIF !EXISTCSV(LOCAL , 0)
		CONTINUE
	SIF FLAG:(20000+LOCAL) < 100
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"全書召喚不可")),0) == 1
		CONTINUE
	SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL) == 0
		CONTINUE
	SIF !EXISTCSV(LOCAL, 0)
		CONTINUE
	;さきほどの登録悪魔検索でひっかかった悪魔はスキル捜査を飛ばす
	IF !DEM:LOCAL
		LOCAL:2 = 0
		FOR LOCAL:1, 1, 9
			IF CSVABL(LOCAL, GETNUM(ABL, @"スキル{LOCAL:1}"), 0) == SKILL
				LOCAL:2 = 1
				BREAK
			ENDIF
		NEXT
		SIF !LOCAL:2
			CONTINUE
	ENDIF
	IF CSVCSTR(LOCAL, GETNUM(CSTR, "種族名"), 0) == ""
		PRINTFORM %@"[{LOCAL:3,2}] "+ CSVNAME(LOCAL, 0) + "(" + STR:CSVABL(LOCAL, GETNUM(ABL, "種族"), 0) + ")",36,LEFT%
	ELSE
		PRINTFORM %@"[{LOCAL:3,2}] "+ CSVNAME(LOCAL, 0) + "(" + CSVCSTR(LOCAL, GETNUM(CSTR, "種族名"), 0) + ")",36,LEFT%
	ENDIF
	;PRINTFORM %@"%CSVNAME(LOCAL, 0)%(\@CSVCSTR(LOCAL, 0) == "" ? %STR:(CSVABL(LOCAL, GETNUM(ABL, "種族", 0), 0)%#%CSVCSTR(LOCAL, 0)%\@)",30,LEFT%
	SIF LOCAL:3 % 3 == 2
		PRINTL 
	LOCAL:3++
NEXT
SIF LINEISEMPTY() == 0
	PRINTL  
SIF LOCAL:3 == 0
	PRINTFORML No demon with %SKILLNAME% registered
RESTART

