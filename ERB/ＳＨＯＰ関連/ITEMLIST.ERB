
@SHOPCOMABLE_149
RESULTS = Check Inventory
RETURN 1

@SHOP_COM_149
#LOCALSIZE 7
CUSTOMDRAWLINE =
PRINTL Please select the type of item you want to see.
PRINTL Or you can direcly input the name of the item.
DRAWLINE
FOR LOCAL,0,8
	PRINTFORML [{LOCAL}] %GET_EQUIP_E(LOCAL)%
NEXT

PRINTL [8] Item
PRINTL [9] Skill Cards
PRINTL [10] Event Items
PRINTL [11] Miscellaneous
PRINTL [12] Tool Collection
PRINTL [100] Return
INPUTS
IF GROUPMATCH(RESULTS, "0", "1","2","3","4","5","6","7","8","9","10","11","12","100")
	LOCAL:6 = 0
	TOINT RESULTS
	;CALL INPUTINT(0,1,2,3,4,5,6,7,8,9,10,11,12,100)
	;LOCAL:1 種別
	LOCAL:1 = RESULT
	SELECTCASE LOCAL:1
		CASE 100
			RETURN 0
		CASE 9
			CALL SHOW_SKILL_CARD
			RESTART
		CASE 10,11
			VARSET アイテムリスト,-1
			LOCAL:3 = 0
			FOR LOCAL, RESULT == 10 ? 300 # 200 , RESULT == 10 ? 500 # 300
				SIF ITEMNAME:LOCAL == ""
					CONTINUE
				IF ITEM:LOCAL > 0
					アイテムリスト:(LOCAL:3) = LOCAL
					LOCAL:3 += 1
				ENDIF
			NEXT
			LOCAL:2 = LOCAL:3
		CASE 12
			CALL EQUIPMENT_COMPENDIUM
			RESTART
		CASEELSE
			CALL MAKE_SALERPGITEMLIST,LOCAL:1
			LOCAL:2 = RESULT
	ENDSELECT
ELSE
	CLEARLINE 1
	LOCAL:6 = 1
	GETNUM ITEM, RESULTS
	SIF RESULT == -1
		RESTART
	IF ITEM:RESULT < 1
		PRINTFORML %RESULTS% is not held.
		RESTART
	ENDIF
	LOCAL:3 = RESULT
	SELECTCASE LOCAL:3
		CASE 200 TO 299
			PRINT □Others
			LOCAL:1 = 11
		CASE 300 TO 499
			LOCAL:1 = 10
			PRINT □Event item
		CASE 1001 TO 1999
			PRINT □Item
			LOCAL:1 = 9
		CASE 2000 TO 5999
			LOCAL:1 = (LOCAL:3-2000)/500
			PRINTFORM □%GET_EQUIP_E(LOCAL:1)%
		CASEELSE
			RESTART
	ENDSELECT
	PRINTFORMW 　%RESULTS%
	GOTO EXPLANATION
ENDIF
P = 0
LOCAL:5 = LINECOUNT
$PRINT_LIST
CLEARLINE LINECOUNT - LOCAL:5
DRAWLINE
CALL ITEMLIST,LOCAL:2
$INPUT_LOOP2
INPUT
LOCAL:3 = RESULT
IF RESULT == 0
	RESTART
ELSEIF RESULT == 7 && P > 0
	P -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 9 && P < LOCAL:2/20
	P += 1
	GOTO PRINT_LIST
ELSEIF MATCH(アイテムリスト,RESULT,0) > 0
	$EXPLANATION
	DRAWLINE
	IF LOCAL:1 == 10 || LOCAL:1 == 11
		TRYCCALLFORM アイテム解説_{RESULT}
			WAIT
		CATCH
		ENDCATCH
;		CLEARLINE 1
;		GOTO INPUT_LOOP2
;	ENDIF
	ELSEIF LOCAL:1 < 7
		CALL 装備説明,LOCAL:3
		WAIT
	ELSEIF LOCAL:1 == 7
		CALLFORM 装備解説_{LOCAL:3}
		WAIT
	ELSE
		CALLFORM 最大所持数_{LOCAL:3+2000}
		LOCAL:4 = RESULT
		PRINTFORML %ITEMNAME_E(LOCAL:3),20,LEFT%   Amount held：{ITEM:(LOCAL:3)}/{LOCAL:4}
		SIF FLAG:スキル属性表示設定 == 1
				CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL:3 + 2000, -1
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL:3+2000}
		DRAWLINE
		WAIT
	ENDIF
ELSE
	GOTO INPUT_LOOP2
ENDIF
SIF LOCAL:6
	RESTART
GOTO PRINT_LIST






;======================================================
;所持しているアイテム・装備品全てをリスト化して表示
;======================================================

@ITEMLIST,ARG
PRINTFORML Please choose the item you want to confirm　＜page.{P + 1}＞
DRAWLINE
LOCAL:1 = 0
FOR LOCAL,P*20,MIN(P*20+20,ARG)
	PRINTFORMLC [{アイテムリスト:LOCAL}] %ITEMNAME_E(アイテムリスト:LOCAL),20,LEFT%×{ITEM:(アイテムリスト:LOCAL),2}　
NEXT
PRINTL
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 13% # [7]Previous page\@
PRINTLC [0]Return
PRINTFORMLC \@(P >= ARG / 20) ? %" " * 13% # [9]Next page\@



