;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20xx/xx/xx		----					新規作成
;	002		2011/01/02		旅人					複数画面に纏め買い対応
;	003		2011/02/04		P						衣装購入にマヌカンを配置
;	004		2013/11/30		ひみつ					スキル属性表示の適用範囲拡大
;	005		2016/05/02		ネトリス				結界石の交換処理を追加
;	006		2017/05/16		ネトリス				インストールソフト：セカンドハローの処理を追加
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
; 特記:
;  メモが必要な変数一覧
;  M:0		SHOP_RPG_2.ERB内の『画面モード関連』にある説明を参照
;  M:1		同上
;-------------------------------------------------

;==========================================================================
;魔貨の使い道を考える会
;==========================================================================

@SHOPCOMABLE_114
;---- EDIT 002 ADD START -------------------------
;初回訪問フラグ:初期化
;M:1 = 0
;---- EDIT 002 ADD END ---------------------------
RESULTS = Macca Exchange
;SIF MONEY:1 == 0
;	RETURN 0
RETURN 1

@SHOP_COM_114
DRAWLINE
PRINTFORML You have {MONEY:1} macca available. What will you do?
DRAWLINE

;---- EDIT 002 ADD START -------------------------
;画面モード:初回訪問時
;CALL RPG_SHOP_SCREENMODE_DEFAULT

;画面モード:表示
CALL RPG_SHOP_SCREENMODE_PRINT
PRINTL (This affects the "Item exchange")
PRINTL 
;---- EDIT 002 ADD END ---------------------------

PRINTL [1] ￥ exchange
PRINTL [2] Item exchange
PRINTL [3] Software exchange
PRINTFORML [4] ＣＯＭＰ expansion (Needed macca：{POWER(FLAG:ＣＯＭＰ容量 - 7,2)*100})
PRINTL [5] Clothing exchange
PRINTL [6] Dolly Kadmon exchange（Needed macca：500）
PRINTFORML [7] Border stone exchange（Needed macca：1000）({ITEM:結界石}/3)
;PRINTL [7] 後生の土鈴と交換（Needed macca：1000）
DRAWLINE
PRINTL [0] Return

$INPUT_LOOP
INPUT

;---- EDIT 002 ADD START -------------------------
;初回訪問フラグ:訪問済み
;M:1 = 1
;---- EDIT 002 ADD END ---------------------------

IF RESULT == 0
	RETURN 0
;---- EDIT 002 ADD START -------------------------
ELSEIF RESULT == 800
	;画面モード:変更
	CALL RPG_SHOP_SCREENMODE_CHANGE
	RESTART
;---- EDIT 002 ADD END ---------------------------
ELSEIF RESULT == 1
	CALL EXCHANGE_MONEY
	RESTART
ELSEIF RESULT == 2
;---- EDIT 002 MOD START -------------------------
;	CALL EXCHANGE_ITEM

	IF FLAG:ショップ画面モード設定 == 0
		CALL EXCHANGE_ITEM
	ELSE
		;アイテム購入:通常 (1:魔貨交換)
		CALL RPG_SHOP_NOMAL,FLAG:ショップ画面モード設定,1
	ENDIF
;---- EDIT 002 MOD END ---------------------------
	RESTART
ELSEIF RESULT == 3
	CALL EXCHANGE_SOFT
	RESTART
ELSEIF RESULT == 4
	IF POWER(FLAG:ＣＯＭＰ容量 - 7,2)*100 > MONEY:1
		REUSELASTLINE Not enough macca.
		GOTO INPUT_LOOP
	ENDIF
	MONEY:1 -= POWER(FLAG:ＣＯＭＰ容量 - 7,2)*100
	FLAG:ＣＯＭＰ容量 += 1
	PRINTFORMW ＣＯＭＰ capacity increased to {FLAG:ＣＯＭＰ容量}.
	RESTART
ELSEIF RESULT == 5
	CALL EXCHANGE_CLOTHES
	RESTART
ELSEIF RESULT == 6
	IF MONEY:1 < 500
		PRINTW Not enough macca.
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	MONEY:1 -= 500
	ITEM:ドリー・カドモン += 1
	PRINTFORMW One 『Dolly Kadmon』 has been purchased.
;結界石交換
ELSEIF RESULT == 7
	IF MONEY:1 < 1000
		PRINTW Not enough macca.
		CLEARLINE 1
		GOTO INPUT_LOOP
	ELSEIF ITEM:結界石 == 3
		PRINTW You can't have more than 3 border stones.
		CLEARLINE 1
		GOTO INPUT_LOOP
	ENDIF
	MONEY:1 -= 1000
	ITEM:結界石 += 1
	PRINTFORMW A 『Border stone』 has been purchased.
;ELSEIF RESULT == 7
;	IF MONEY:1 < 1000
;		PRINTW Not enough macca
;		CLEARLINE 1
;		GOTO INPUT_LOOP
;	ENDIF
;	MONEY:1 -= 1000
;	ITEM:後生の土鈴 += 1
;	PRINTFORMW 『後生の土鈴』を手に入れた
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART

@EXCHANGE_MONEY
; 魔貨レート変動設定が有効
IF SETTING_IS_MAKKA_RATE()
	;レートが0の場合は30に修正し、他のレートも再設定する
	IF FLAG:魔貨レート1 == 0
		FLAG:魔貨レート1 = 30
		FLAG:魔貨レート2 = 10+RAND:51
		FLAG:魔貨レート3 = 10+RAND:51
	ENDIF
	PRINTFORML How much macca will you exchange for ￥?(1 macca ＝￥{FLAG:魔貨レート1})
	CALL INPUT_MANY(0,MONEY:1)
	IF RESULT == 0
		RETURN 0
	ELSE
		PRINTFORMW {RESULT} macca was exchanged for ￥{RESULT*FLAG:魔貨レート1}.
		MONEY += RESULT*FLAG:魔貨レート1
		MONEY:1 -= RESULT
	ENDIF
; 魔貨レート変動設定が無効(通常)
ELSE
	PRINTL How much macca will you exchange for ￥?(1Macca＝￥50)
	CALL INPUT_MANY(0,MONEY:1)
	IF RESULT == 0
		RETURN 0
	ELSE
		PRINTFORMW {RESULT}macca was exchanged for ￥{RESULT*50}.
		MONEY += RESULT*50
		MONEY:1 -= RESULT
	ENDIF
ENDIF

@EXCHANGE_SOFT

DRAWLINE
PRINTFORML You have {MONEY:1} macca available.
DRAWLINE
LOCAL:1 = 0

FOR LOCAL,500,600
	SIF !SOFT_SELLABLE(LOCAL)
		CONTINUE
	IF ITEMPRICE:LOCAL > 0 && ITEM:LOCAL == 0
		PRINTFORM [{LOCAL}] %ITEMNAME_E(LOCAL),20,LEFT% {ITEMPRICE:LOCAL,7}macca　　
		ITEMSALES:LOCAL = 1
		LOCAL:1 += 1
	ELSE
		ITEMSALES:LOCAL = 0
	ENDIF
	IF LOCAL:1 == 2
		PRINTL
		LOCAL:1 = 0
	ENDIF
NEXT
PRINTL
DRAWLINE
PRINTL [100] Return
$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT > -1 && RESULT < VARSIZE("ITEMSALES") && ITEMSALES:RESULT == 1 
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML %ITEMNAME_E(LOCAL)%
	TRYCALLFORM SOFT_EXPLAIN_{LOCAL}
	DRAWLINE
	IF ITEMPRICE:LOCAL > MONEY:1
		PRINTW Not enough macca.
		RESTART
	ENDIF
	PRINTL Purchase this? 

	CALL INPUT_YN,"Yes","No",2

	SELECTCASE RESULT
		CASE 0
			PRINTFORMW %ITEMNAME_E(LOCAL)% purchased.
			MONEY:1 -= ITEMPRICE:LOCAL
			ITEM:LOCAL = 1
			ITEMSALES:LOCAL = 0
		CASE 1
			RESTART
	ENDSELECT

ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART



@EXCHANGE_ITEM
DRAWLINE
PRINTFORML You have {MONEY:1}  macca available.
PRINTL What will you trade for?
DRAWLINE

FOR LOCAL,0,8
	PRINTFORML [{LOCAL}] %GET_EQUIP_E(LOCAL)%
NEXT
PRINTL [8] Item
DRAWLINE
PRINTL [100] Return


$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < 9
	LOCAL = RESULT
	CALL MAKE_EXCHANGEITEMLIST,RESULT
	LOCAL:1 = RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF
P = 0
LOCAL:5 = LINECOUNT
$PRINT_LIST
CLEARLINE LINECOUNT - LOCAL:5
DRAWLINE
CALL PRINT_EXCHANGEITEMLIST,LOCAL:1
$INPUT_LOOP2
INPUT
LOCAL:2 = RESULT
IF RESULT == 0
	RESTART
ELSEIF RESULT == 7 && P > 0
	P -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 9 && P < LOCAL:1/20
	P += 1
	GOTO PRINT_LIST
ELSEIF MATCH(アイテムリスト,RESULT,0)> 0
	DRAWLINE
	IF LOCAL < 7
		CALL 装備説明,LOCAL:2
		LOCAL:3 = 999
		IF ITEM:(LOCAL:2) >= 999
			PRINTW You can't hold any more of this item.
			GOTO PRINT_LIST
		ENDIF
	ELSEIF LOCAL == 7
		CALLFORM 装備解説_{LOCAL:2}
		PRINTL
		LOCAL:3 = 999
		IF ITEM:(LOCAL:2) >= 999
			PRINTW You can't hold any more of this item.
			GOTO PRINT_LIST
		ENDIF
	ELSE
		CALLFORM 最大所持数_{LOCAL:2+2000}
		LOCAL:3 = RESULT
		PRINTFORML %ITEMNAME_E(LOCAL:2),20,LEFT%   Currently have：{ITEM:(LOCAL:2)}/{LOCAL:3}
		SIF FLAG:スキル属性表示設定 == 1
			CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL:2 + 2000, -1
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL:2+2000}
		DRAWLINE
		IF ITEM:(LOCAL:2) >= LOCAL:3
			PRINTW You can't hold any more of this item.
			GOTO PRINT_LIST
		ENDIF
	ENDIF

	IF MONEY:1 < ITEMPRICE:(LOCAL:2)
		PRINTW Not enough macca.
		GOTO PRINT_LIST
	ELSE
		PRINTFORML How many do you purchase? (Owned: {ITEM:(LOCAL:2)})
		CALL INPUT_MANY(0,MIN(MONEY:1/ITEMPRICE:(LOCAL:2),LOCAL:3 - ITEM:(LOCAL:2)))
		IF RESULT == 0
			GOTO PRINT_LIST
		ELSE
			MONEY:1 -= (RESULT * ITEMPRICE:(LOCAL:2))
			IF RESULT == 1
				PRINTFORMW ＜%ITEMNAME_E(LOCAL:2)% purchased＞
			ELSE
				PRINTFORMW ＜{RESULT} %ITEMNAME_E(LOCAL:2)% purchased＞
			ENDIF
			CALL GET_ITEM,LOCAL:2,RESULT
			GOTO PRINT_LIST
		ENDIF
	ENDIF
ELSE
	GOTO INPUT_LOOP2
ENDIF
GOTO PRINT_LIST





@MAKE_EXCHANGEITEMLIST,ARG
VARSET アイテムリスト,-1
LOCAL:1 = 0
IF ARG < 8
	FOR LOCAL,2000,VARSIZE("ITEM")
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		RESULT = 12
		TRYCCALLFORM 装備箇所_{LOCAL}
			IF RESULT == ARG
				RESULT = 0
				TRYCALLFORM EQUIP_EXCHANGE_{LOCAL}
				SIF RESULT == 0
					CONTINUE
				アイテムリスト:(LOCAL:1) = LOCAL
				LOCAL:1 += 1
			ENDIF
		CATCH
		ENDCATCH
	NEXT
ELSE
	FOR LOCAL,1001,2000
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		RESULT = 0
		TRYCALLFORM ITEM_EXCHANGE_{LOCAL+2000}
		SIF RESULT == 0
			CONTINUE
		アイテムリスト:(LOCAL:1) = LOCAL
		LOCAL:1 += 1
	NEXT
ENDIF
RETURN LOCAL:1

@PRINT_EXCHANGEITEMLIST,ARG
PRINTFORML Please choose an item to buy　＜page.{P + 1}＞
PRINTFORML {MONEY:1} macca held.
DRAWLINE
FOR LOCAL,P*20,MIN(P*20+20,ARG)
	PRINTFORM [{アイテムリスト:LOCAL}] %ITEMNAME_E(アイテムリスト:LOCAL),20,LEFT% ×{ITEM:(アイテムリスト:LOCAL),2} {ITEMPRICE:(アイテムリスト:LOCAL),7} macca　　
	SIF LOCAL % 2 == 1
		PRINTL
NEXT
PRINTL
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 13% # [7]Previous page\@
PRINTLC [0]Return
PRINTFORMLC \@(P >= ARG / 20) ? %" " * 13% # [9]Next page\@



@EXCHANGE_CLOTHES
CUSTOMDRAWLINE =
PRINTL Mannequin：
PRINTL 「Welcome. Our store only has many beautiful clothes. We sell them specially with a kiss.」
DRAWLINE
PRINTL
PRINTL
DRAWLINE
REPEAT 12
	PRINTL
REND
DRAWLINE
PRINTL
TWAIT 2000,0
CLEARLINE 19
$START
PRINTL 「How is it? Isn't is beautiful? Aren't they something no ordinary man can resist?」
DRAWLINE
PRINTFORML You have {MONEY:1} macca available.
PRINTL What do you buy?
DRAWLINE

FOR LOCAL,0,12
	PRINTFORML [{LOCAL}] %GET_CLOTHESNAME(LOCAL)%
NEXT
DRAWLINE
PRINTL [100] Return


$INPUT_LOOP
INPUT
IF RESULT == 100
	CUSTOMDRAWLINE =
	PRINTL Mannequin：
	PRINTL 「You should acquire a better fashion sense. I'll lecture you gently this time. See you.」
	DRAWLINE
	PRINTL
	PRINTL
	DRAWLINE
	REPEAT 12
		PRINTL
	REND
	DRAWLINE
	PRINTL
	TWAIT 2000,0
	RETURN 0
ELSEIF RESULT >= 0 && RESULT < 12
	LOCAL = RESULT
	CALL MAKE_CLOTHESLIST,RESULT
	LOCAL:1 = RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF
P = 0
LOCAL:5 = LINECOUNT
$PRINT_LIST
CLEARLINE LINECOUNT - LOCAL:5
DRAWLINE
CALL PRINT_EXCHANGEITEMLIST,LOCAL:1
$INPUT_LOOP2
INPUT
LOCAL:2 = RESULT
IF RESULT == 0
	CUSTOMDRAWLINE =
	PRINTL Mannequin：
	GOTO START
ELSEIF RESULT == 7 && P > 0
	P -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 9 && P < LOCAL:1/20
	P += 1
	GOTO PRINT_LIST
ELSEIF MATCH(アイテムリスト,RESULT,0)> 0
	DRAWLINE
	IF MONEY:1 < ITEMPRICE:(LOCAL:2)
		PRINTW Not enough macca.
		GOTO PRINT_LIST
	ELSE
		PRINTFORMW %ITEMNAME_E(LOCAL:2)% purchased.
		MONEY:1 -= (ITEMPRICE:(LOCAL:2))
		ITEM:(LOCAL:2) += 1
		CALL MAKE_CLOTHESLIST,LOCAL
		LOCAL:1 = RESULT
		GOTO PRINT_LIST
	ENDIF
ELSE
	GOTO INPUT_LOOP2
ENDIF
GOTO PRINT_LIST


@MAKE_CLOTHESLIST,ARG
VARSET アイテムリスト,-1
LOCAL:1 = 0
	FOR LOCAL,6001,VARSIZE("ITEM")
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		SIF ITEM:LOCAL > 0
			CONTINUE
		RESULT = 12
		TRYCCALLFORM CLOTHES_PART_{LOCAL-6000}
			IF RESULT == ARG
				アイテムリスト:(LOCAL:1) = LOCAL
				LOCAL:1 += 1
			ENDIF
		CATCH
		ENDCATCH
	NEXT
RETURN LOCAL:1