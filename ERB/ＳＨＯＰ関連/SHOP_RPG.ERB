;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20xx/xx/xx		----					新規作成
;	002		2010/12/28		旅人					纏め買い対応
;	   		2011/01/02		旅人					複数画面に纏め買い対応
;	003		2013/11/30		ひみつ					スキル属性表示の適用範囲拡大
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
; 特記:
;  メモが必要な変数一覧
;  M:0		SHOP_RPG_2.ERB内の『画面モード関連』にある説明を参照
;  M:1		同上
;-------------------------------------------------


@SHOPCOMABLE_115
;---- EDIT 002 ADD START -------------------------
;初回訪問フラグ:初期化
M:1 = 0
;---- EDIT 002 ADD END ---------------------------
RESULTS = Junk Shop
RETURN 1


@SHOP_COM_115
RESULT = 0
CUSTOMDRAWLINE =
PRINTL Shopkeeper：
PRINTFORML 「Oh hey, a customer!」
DRAWLINE
PRINTL
PRINTL
PRINTL
DRAWLINE
;ALIGNMENT CENTER
PRINTL [CLICK]
;ALIGNMENT LEFT
TWAIT 2000,0
CLEARLINE 8
$SHOP_START
PRINTL Shopkeeper：
PRINTFORML \@ RESULT ? 「What are you buyin'?」#「What do you want?」\@
DRAWLINE
;---- EDIT 002 ADD START -------------------------
;画面モード:初回訪問時
;CALL RPG_SHOP_SCREENMODE_DEFAULT

;画面モード:表示
CALL RPG_SHOP_SCREENMODE_PRINT
;---- EDIT 002 ADD END ---------------------------

PRINTL [1]Purchase Item
PRINTL [2]Sell Item
DRAWLINE
PRINTL [0]Leave the store
CALL INPUTINT(0,1,2,800)
;---- EDIT 002 ADD START -------------------------
;初回訪問フラグ:訪問済み
M:1 = 1
;---- EDIT 002 ADD END ---------------------------
;---- EDIT 002 ADD START -------------------------
IF RESULT == 0
	CLEARLINE 9
	PRINTL Shopkeeper：
	PRINTL 「Goodbye!」
	DRAWLINE
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL
	TWAIT 2000,0
	RETURN 0
ELSEIF RESULT == 800
	;画面モード:変更
	CALL RPG_SHOP_SCREENMODE_CHANGE
	RESULT = -1
;---- EDIT 002 ADD END ---------------------------
ELSEIF RESULT == 1
;---- EDIT 002 MOD START -------------------------
;	CALL BUY_RPG

	IF FLAG:ショップ画面モード設定 == 0
		CALL BUY_RPG
	ELSE
		;アイテム購入:通常 (0:ジャンクSHOP)
		CALL RPG_SHOP_NOMAL,FLAG:ショップ画面モード設定,0
	ENDIF
;---- EDIT 002 MOD END ---------------------------
ELSEIF RESULT == 2
	CALL SALE_RPG
ELSE
	CLEARLINE 1
ENDIF
IF RESULT == 0
	DRAWLINE
	PRINTL
	PRINTL Shopkeeper：
	PRINTL 「Goodbye!」
	DRAWLINE
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL
	TWAIT 2000,0
	RETURN 0
ENDIF
IF RESULT == -1
	CUSTOMDRAWLINE =
	GOTO SHOP_START
ENDIF

@BUY_RPG
PRINTFORML What do you want to buy?		Total Money Carried ￥{MONEY}
DRAWLINE
FOR LOCAL,0,8
	PRINTFORML [{LOCAL}] %GET_EQUIP_E(LOCAL)%
NEXT
PRINTL [8] Item
PRINTL [99] Return
PRINTL [100] Leave the store　[101]Change sort criteria

$INPUT_LOOP
INPUT

IF RESULT == 99
	RETURN -1
ELSEIF RESULT == 100
	RETURN 0
ELSEIF RESULT == 101
	CALL CHANGE_CONDITION_EQUIP_SORT
	CALL EQUIP_SORT
	RESTART
ELSEIF RESULT >= 0 && RESULT < 9
	LOCAL = RESULT
	CALL MAKE_RPGITEMLIST,RESULT
	LOCAL:1 = RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF
P = 0
LOCAL:5 = LINECOUNT
$PRINT_LIST
CLEARLINE LINECOUNT - LOCAL:5
DRAWLINE
CALL PRINT_RPGITEMLIST,LOCAL:1
$INPUT_LOOP2
INPUT
LOCAL:2 = RESULT
IF RESULT == 0
	RESTART
ELSEIF RESULT == 1
	CALL CHANGE_CONDITION_EQUIP_SORT
	CALL EQUIP_SORT
	GOTO PRINT_LIST
ELSEIF RESULT == 7 && P > 0
	P -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 9 && P < (LOCAL:1-1)/20
	P += 1
	GOTO PRINT_LIST
ELSEIF MATCH(アイテムリスト,RESULT,0)> 0
	DRAWLINE
	IF LOCAL < 7
		CALL 装備説明,LOCAL:2
		LOCAL:3 = 999
		IF ITEM:(LOCAL:2) >= 999
			PRINTW You can't hold anymore.
			GOTO PRINT_LIST
		ENDIF
	ELSEIF LOCAL == 7
		CALLFORM 装備解説_{LOCAL:2}
		PRINTL
		LOCAL:3 = 999
		IF ITEM:(LOCAL:2) >= 999
			PRINTW You can't hold anymore.
			GOTO PRINT_LIST
		ENDIF
	ELSE
		CALLFORM 最大所持数_{LOCAL:2+2000}
		LOCAL:3 = RESULT
		PRINTFORML %ITEMNAME_E(LOCAL:2),20,LEFT%   Amount held：{ITEM:(LOCAL:2)}/{LOCAL:3}
		SIF FLAG:スキル属性表示設定 == 1
			CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL:2 + 2000, -1
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL:2+2000}
		DRAWLINE
		IF ITEM:(LOCAL:2) >= LOCAL:3
			PRINTW You can't hold anymore.
			GOTO PRINT_LIST
		ENDIF
	ENDIF

	IF MONEY < ITEMPRICE:(LOCAL:2)
		PRINTW You don't have enough money.
		GOTO PRINT_LIST
	ELSE
		PRINTFORML How many do you want to purchase? (Owned: {ITEM:(LOCAL:2)})
		CALL INPUT_MANY(0,MIN(MONEY/ITEMPRICE:(LOCAL:2),LOCAL:3 - ITEM:(LOCAL:2)))
		IF RESULT == 0
			GOTO PRINT_LIST
		ELSE
			MONEY -= (RESULT * ITEMPRICE:(LOCAL:2))
			IF RESULT == 1
				PRINTFORMW ＜%ITEMNAME_E(LOCAL:2)% purchased＞
			ELSE
				PRINTFORMW ＜{RESULT} %ITEMNAME_E(LOCAL:2)% purchased＞
			ENDIF
			CALL GET_ITEM,LOCAL:2,RESULT
			GOTO PRINT_LIST
		ENDIF
	ENDIF
ELSE
	GOTO INPUT_LOOP2
ENDIF
GOTO PRINT_LIST


@MAKE_RPGITEMLIST,ARG
VARSET アイテムリスト,-1
LOCAL:1 = 0
IF ARG < 8
	FOR LOCAL,2000,VARSIZE("ITEM")
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		RESULT = 12
		TRYCCALLFORM 装備箇所_{LOCAL}
			IF RESULT == ARG
				TRYCCALLFORM 購入条件_{LOCAL}
					IF RESULT == 1
						アイテムリスト:(LOCAL:1) = LOCAL
						LOCAL:1 += 1
					ENDIF
				CATCH
					CONTINUE
				ENDCATCH
			ENDIF
		CATCH
		ENDCATCH
	NEXT
ELSE
	FOR LOCAL,1001,2000
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		TRYCCALLFORM 道具購入条件_{LOCAL+2000}
			IF RESULT == 1
				アイテムリスト:(LOCAL:1) = LOCAL
				LOCAL:1 += 1
			ENDIF
		CATCH
			CONTINUE
		ENDCATCH
	NEXT
ENDIF
CALL EQUIP_SORT
RETURN LOCAL:1

@PRINT_RPGITEMLIST,ARG
PRINTFORML What do you want to buy?　＜page.{P + 1}＞
PRINTFORML Total Money Carried ￥{MONEY}
DRAWLINE
LOCAL:1 = 0
FOR LOCAL,P*20,MIN(P*20+20,ARG)
	PRINTFORM [{アイテムリスト:LOCAL}] %ITEMNAME_E(アイテムリスト:LOCAL),20,LEFT% ×{ITEM:(アイテムリスト:LOCAL),2} %"￥" + TOSTR(ITEMPRICE:(アイテムリスト:LOCAL)),9 %　　
	SIF LOCAL:1 % 2 == 1
		PRINTL 
	LOCAL:1 += 1
NEXT
SIF LOCAL:1 % 2 == 1
	PRINTL 
;PRINTL
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 13% # [7]Previous page\@
PRINTLC [0]Return  [1]Change sort criteria
PRINTFORMLC \@(P >= (ARG-1) / 20) ? %" " * 13% # [9]Next page\@


@SALE_RPG
PRINTFORML What do you want to sell?		Total Money Carried ￥{MONEY}
DRAWLINE
FOR LOCAL,0,8
	PRINTFORML [{LOCAL}] %GET_EQUIP_E(LOCAL)%
NEXT
PRINTL [8] Item
PRINTL [99] Return
PRINTL [100] Leave store

$INPUT_LOOP
INPUT

IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 99
	RETURN -1
ELSEIF RESULT >= 0 && RESULT < 9
	LOCAL = RESULT
ELSE
	GOTO INPUT_LOOP
ENDIF
P = 0
LOCAL:5 = LINECOUNT
$PRINT_LIST
CLEARLINE LINECOUNT - LOCAL:5
DRAWLINE
CALL MAKE_SALERPGITEMLIST,LOCAL
LOCAL:1 = RESULT
CALL PRINT_SALERPGITEMLIST,LOCAL:1
$INPUT_LOOP2
INPUT
LOCAL:2 = RESULT
IF RESULT == 0
	RESTART
ELSEIF RESULT == 7 && P > 0
	P -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 9 && P < (LOCAL:1-1)/20
	P += 1
	GOTO PRINT_LIST
ELSEIF MATCH(アイテムリスト,RESULT,0) > 0
	DRAWLINE
	IF LOCAL < 7
		CALL 装備説明,LOCAL:2
	ELSEIF LOCAL == 7
		CALLFORM 装備解説_{LOCAL:2}
	ELSE
		CALLFORM 最大所持数_{LOCAL:2+2000}
		LOCAL:3 = RESULT
		PRINTFORML %ITEMNAME_E(LOCAL:2),20,LEFT%   Amount held：{ITEM:(LOCAL:2)}/{LOCAL:3}
		SIF FLAG:スキル属性表示設定 == 1
			CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL:2 + 2000, -1
		TRYCALLFORM SKILL_EXPLAIN_{LOCAL:2+2000}
		DRAWLINE
	ENDIF
	PRINTFORML How many do you want to sell?  (Owned: {ITEM:(LOCAL:2)})
	CALL INPUT_MANY(0,ITEM:(LOCAL:2))
	IF RESULT == 0
		GOTO PRINT_LIST
	ELSE
		PRINTFORMW {RESULT} %ITEMNAME_E(LOCAL:2)% \@ RESULT == 1 ? was # where \@ sold for ￥{RESULT * (ITEMPRICE:(LOCAL:2)/2)}.
		MONEY += (RESULT * (ITEMPRICE:(LOCAL:2)/2))
		ITEM:(LOCAL:2) -= RESULT
		GOTO PRINT_LIST
	ENDIF
ELSE
	GOTO INPUT_LOOP2
ENDIF
GOTO PRINT_LIST


@MAKE_SALERPGITEMLIST,ARG
VARSET アイテムリスト,-1
LOCAL:1 = 0
IF ARG < 8
	FOR LOCAL,2000,VARSIZE("ITEM")
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		RESULT = 12
		IF 魔晶装備(LOCAL)
		ELSE
			TRYCALLFORM 装備箇所_{LOCAL}
			IF RESULT == ARG && ITEM:LOCAL > 0
				アイテムリスト:(LOCAL:1) = LOCAL
				LOCAL:1 += 1
			ENDIF
		ENDIF
	NEXT
ELSE
	FOR LOCAL,1001,2000
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		IF ITEM:LOCAL > 0
			アイテムリスト:(LOCAL:1) = LOCAL
			LOCAL:1 += 1
		ENDIF
	NEXT
ENDIF
RETURN LOCAL:1

@PRINT_SALERPGITEMLIST,ARG
PRINTFORML Please select the item you want to sell.　＜page.{P + 1}＞
PRINTFORML Total Money Carried ￥{MONEY}
DRAWLINE
LOCAL:1 = 0
FOR LOCAL,P*20,MIN(P*20+20,ARG)
	PRINTFORM [{アイテムリスト:LOCAL}] %ITEMNAME_E(アイテムリスト:LOCAL),20,LEFT% ×{ITEM:(アイテムリスト:LOCAL),2} %"￥" + TOSTR(ITEMPRICE:(アイテムリスト:LOCAL)/2),9 %　　
	SIF LOCAL:1 % 2 == 1
		PRINTL 
	LOCAL:1 += 1
NEXT
SIF LOCAL:1 % 2 == 1
	PRINTL 
;PRINTL
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 13% # [7]Previous page\@
PRINTLC [0]Return
PRINTFORMLC \@(P >= (ARG-1) / 20) ? %" " * 13% # [9]Next page\@

