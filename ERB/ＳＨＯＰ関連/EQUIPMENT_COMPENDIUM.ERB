@EQUIPMENT_COMPENDIUM
CUSTOMDRAWLINE =
PRINTL □ Tool Collection
PRINTL Please select the type of item you want to see
DRAWLINE
FOR LOCAL,0,8
	PRINTFORML [{LOCAL}] %GET_EQUIP_E(LOCAL)%
NEXT
PRINTL [8] Item
;PRINTL [9] Collection Registration
PRINTL [100] Return
CALL INPUTINT(0,1,2,3,4,5,6,7,8,100)
;LOCAL:1 種別
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
	CASE 100
		RETURN 0
;	CASE 9
;		CALL ENTRY_EQUIPMENT_COMPENDIUM
	CASEELSE
		CALL MAKE_EQUIPMENT_COMPENDIUM_LIST,LOCAL:1
		IF RESULT == 0
			PRINTL There are no registered items
			RESTART
		ENDIF
		LOCAL:2 = RESULT

		P = 0
		$PRINT_LIST
		PRINTFORML Please select the type of item you want to see　＜page.{P + 1}＞
		DRAWLINE
		FOR LOCAL,P*20,MIN(P*20+20,LOCAL:2)
			PRINTFORMLC [{アイテムリスト:LOCAL}] %ITEMNAME_E(アイテムリスト:LOCAL)%
		NEXT
		PRINTL
		DRAWLINE
		PRINTFORMLC \@(P <= 0) ? %" " * 13% # [7]Previous page\@
		PRINTLC [0]Return
		PRINTFORMLC \@(P >= (LOCAL:2 - 1) / 20) ? %" " * 13% # [9]Next page\@
		PRINTL

		$INPUT_LOOP2
		INPUT
		LOCAL:3 = RESULT
		IF RESULT == 0
			RESTART
		ELSEIF RESULT == 7 && P > 0
			P -= 1
			GOTO PRINT_LIST
		ELSEIF RESULT == 9 && P < LOCAL:2/20
			P += 1
			GOTO PRINT_LIST
		ELSEIF MATCH(アイテムリスト,RESULT,0) > 0
			DRAWLINE
			IF LOCAL:1 < 7
				CALL 装備説明,LOCAL:3
				WAIT
			ELSEIF LOCAL:1 == 7
				CALLFORM 装備解説_{LOCAL:3}
				WAIT
				PRINTL
				DRAWLINE
			ELSE
				PRINTFORML %ITEMNAME_E(LOCAL:3),20,LEFT%
				SIF FLAG:スキル属性表示設定 == 1
					CALL SKILL_EXPLAIN_PERFORMANCE, LOCAL:3 + 2000, -1
				TRYCALLFORM SKILL_EXPLAIN_{LOCAL:3+2000}
				DRAWLINE
				WAIT
			ENDIF
		ELSE
			GOTO INPUT_LOOP2
		ENDIF
		GOTO PRINT_LIST
ENDSELECT

RESTART

@MAKE_EQUIPMENT_COMPENDIUM_LIST,ARG
VARSET アイテムリスト,-1
LOCAL:1 = 0
IF ARG < 8
	FOR LOCAL,2000,VARSIZE("ITEM")
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		RESULT = 12
		IF 魔晶装備(LOCAL)
		ELSE
			TRYCALLFORM 装備箇所_{LOCAL}
			IF RESULT == ARG && FLAG:(LOCAL + 40000) > 0
				アイテムリスト:(LOCAL:1) = LOCAL
				LOCAL:1 += 1
			ENDIF
		ENDIF
	NEXT
ELSE
	FOR LOCAL,1001,2000
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		IF FLAG:(LOCAL + 40000) > 0
			アイテムリスト:(LOCAL:1) = LOCAL
			LOCAL:1 += 1
		ENDIF
	NEXT
ENDIF

RETURN LOCAL:1

@ENTRY_EQUIPMENT_COMPENDIUM
FOR LOCAL,1000,9999
	SIF ITEM:LOCAL > 0
		FLAG:(LOCAL + 40000) = 1
NEXT
