;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:SHOP_REST.ERB
;	Facility	:休憩関連の関数群です。
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2011/04/08		黒天					休憩確認オプション対応 +「朝まで休憩」追加
;	003		2011/05/05		黒天					休憩確認設定オプション拡張。[休憩]コマンドの[朝まで休憩]変更を追加
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

@SHOPCOMABLE_105
IF FLAG:休憩確認フラグ == 2
	RESULTS = Rest till Morning
ELSE
	RESULTS = Rest
ENDIF
RETURN 1

@SHOP_COM_105
IF !FLAG:休憩確認フラグ
	PRINTL Are you sure you want to rest? 

	PRINT [0] Rest
	PRINT [1] Rest till Morning
	SIF TIME < 2
		PRINT [2] Rest till Night
	PRINTL [3] Don't rest

	CALL INPUTINT(0, 1, 2 * (TIME < 2), 3)
	;CALL INPUT_YN,"Yes","No",2

	SELECTCASE RESULT
		CASE 0
			PRINTW You rested
			;休憩フラグを立てる（ここは特別）
			FLAG:0 = 2
			BEGIN TURNEND
			RETURN 1
		CASE 1
			PRINTW You rested until morning
			FLAG:0 = 3
			BEGIN TURNEND
			RETURN 1
		CASE 2
			PRINTW You rested until night
			TIME = 2
			FLAG:0 = 2
			BEGIN TURNEND
			RETURN 1
		CASE 3
			RETURN 0
	ENDSELECT
ELSE
	PRINTL You rested
	;休憩フラグを立てる（ここは特別）
	IF FLAG:休憩確認フラグ == 2
		;朝まで休憩
		FLAG:0 = 3
	ELSE
		FLAG:0 = 2
	ENDIF
	BEGIN TURNEND
	RETURN 1
ENDIF
