@DICTIONARY_FALLED_MAIN
#DIM PAGE
#DIM RACE
#DIM LINE
#DIM C_NO
#DIM L_COUNT, 3
#LOCALSIZE 320
LINE = LINECOUNT
RACE = -1
PAGE = 0
$START
VARSET LOCAL, -1
CLEARLINE LINECOUNT
DRAWLINE
L_COUNT:1 = 0
;リスト登録
FOR L_COUNT, 0, 5300
	;一応CSVの存在で弾く
	SIF !EXISTCSV(L_COUNT, 0)
		CONTINUE
	;調教したことがあるキャラのみ
	SIF !FLAG:(50000+L_COUNT)
		CONTINUE
	;種族が一致していない
	SIF RACE != CSVABL(L_COUNT, GETNUM(ABL, "種族"), 0)
		CONTINUE
	;魔人の娘も除外
	SIF L_COUNT == 3502
		CONTINUE
	LOCAL:(L_COUNT:1) = L_COUNT
	L_COUNT:1++
NEXT
;リスト表示
FOR L_COUNT, 0 , 40
	C_NO = LOCAL:(PAGE * 40 + L_COUNT)
	IF C_NO >= 0
		SIF FLAG:(50000+C_NO) == 255
			SETCOLOR COLOR("PINK")
		PRINTPLAINFORM [{C_NO,4}] %CSVCALLNAME(C_NO, 0), 20, LEFT%
		RESETCOLOR
		FOR L_COUNT:1, 1, 9
			SETCOLOR GETBIT(FLAG:(50000+C_NO), L_COUNT:1) ? COLOR("aqua") # COLOR("light-gray")
			PRINTFORM %AUTO_SPLIT("/Aff/ Lov/ Lew/ Who/ Sub/ Slv/ Virgin", "/", L_COUNT:1)%
			;PRINTFORM %AUTO_SPLIT("/恋/親/淫/娼/服/隷/ Virgin", "/", L_COUNT:1)%
			RESETCOLOR
		NEXT
		PRINT 　　
	ELSE
		PRINTFORM %" "*53%
	ENDIF
	SIF L_COUNT % 2
		PRINTL 
NEXT
DRAWLINE
;とりあえず人間を含んだ一般種族のみ
L_COUNT:1 = 0
FOR L_COUNT, 0, 45
	SIF STR:L_COUNT == ""
		CONTINUE
	;とりあず除外
	SIF GROUPMATCH(STR:L_COUNT, "Machine", "Mitama", "七星", "Kyojin", "ボス専用", "ペルソナ専用", "ペルソナ２", "Meta")
		CONTINUE
	PRINTFORM [{L_COUNT,2}] %STR:L_COUNT,8,LEFT%
	SIF L_COUNT:1 % 6 == 5
		PRINTL 
	L_COUNT:1++
NEXT
SIF !LINEISEMPTY()
	PRINTL 
DRAWLINE
IF PAGE > 0
	PRINTFORM %"[ 99] Previous page",30,LEFT%
ELSE
	PRINTFORM %"",30,LEFT%
ENDIF
PRINTFORM %"[100] Return",30,LEFT%
SIF LOCAL:((PAGE+1) * 40) >= 0
	PRINTFORM %"[101] Next page",30,LEFT%
PRINTL 
INPUT
SIF RESULT == 99 && PAGE > 0
	PAGE--
SIF RESULT == 100
	RETURN 0
SIF RESULT == 101 && PAGE < 8 && LOCAL:((PAGE+1) * 40) >= 0
	PAGE++
IF RANGE(RESULT, 0, 45) && !GROUPMATCH(STR:L_COUNT, "Machine", "Mitama", "七星", "Kyojin", "ボス専用", "ペルソナ専用", "ペルソナ２", "Meta") && STR:RESULT != ""
	RACE = RESULT
	PAGE = 0
ENDIF
GOTO START
