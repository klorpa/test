;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:EQUIPMENT_FUNC.ERB
;	Facility	:装備品関数。
;
;				 装備品処理で使用する共通関数を纏める。
;				 記載関数には先頭に"EQ_"を付ける。(EQUIPMENTの略)
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2011/10/23		旅人					新規作成
;	002		2015/10/02		魔晶拡張パッチ				EQ_GETNAME_魔晶装備に名称自由変更の処理を追加
;
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;===============================================================================
;  ◇記載関数一覧(記載順)
;
;		Module Name						Explanation
;
;	・ EQ_装備箇所_魔晶装備				EQ:装備箇所(魔晶装備)
;	・ EQ_GETID_魔晶装備				EQ:アイテムID取得(魔晶装備)
;	・ EQ_GETNAME_魔晶装備_ITEM			EQ:名称取得(魔晶装備)-ITEM由来
;	・ EQ_GETNAME_魔晶装備_CHARA		EQ:名称取得(魔晶装備)-CHARA由来
;	・ EQ_GETNAME_魔晶装備				EQ:名称取得(魔晶装備)
;
; *- その他
;
;	・ EQ_画面モード切替				EQ:画面モード切替
;	・ EQ_射程名称						EQ:射程名称
;
;===============================================================================

;=================================================
;   sub EQ_装備箇所_魔晶装備
;=================================================
;   EQ:装備箇所(魔晶装備)
;-------------------------------------------------
; Input:
;  ARGS					アイテムNo
; Output:
;  RETURNF				装備箇所
;-------------------------------------------------
@EQ_装備箇所_魔晶装備(ARG)
#FUNCTION
#DIM L_I
#DIM L_TMP
L_TMP=0

FOR L_I,0,7
	IF ARG < 2500 + L_I*500
		L_TMP = L_I
		BREAK
	ENDIF
NEXT
RETURNF L_TMP


;=================================================
;   sub EQ_GETID_魔晶装備
;=================================================
;   EQ:アイテムID取得(魔晶装備)
;-------------------------------------------------
;   アイテムNoから魔晶装備のIDを取得する
;-------------------------------------------------
; Input:
;  ARGS					アイテムNo
; Output:
;  RETURNF				装備箇所
;-------------------------------------------------
@EQ_GETID_魔晶装備(ARG)
#FUNCTION
#DIM L_POINT

;- EQ:装備箇所(魔晶装備)
L_POINT = EQ_装備箇所_魔晶装備(ARG)

RETURNF ARG - 2450 - L_POINT * 450


;=================================================
;   sub EQ_GETNAME_魔晶装備_ITEM
;=================================================
;   EQ:名称取得(魔晶装備)-ITEM由来
;-------------------------------------------------
;   アイテムNoから魔晶装備の名称を返す
;-------------------------------------------------
; Input:
;  ARG					アイテムNo
; Output:
;  RETURNF				名称
;-------------------------------------------------
@EQ_GETNAME_魔晶装備_ITEM(ARG)
#FUNCTIONS
#DIM L_TMP

L_TMP = EQ_GETID_魔晶装備(ARG)

RETURNF EQ_GETNAME_魔晶装備(L_TMP)


;=================================================
;   sub EQ_GETNAME_魔晶装備_CHARA
;=================================================
;   EQ:名称取得(魔晶装備)-CHARA由来
;-------------------------------------------------
;   指定したキャラの装備箇所にセットしてある装備から魔晶装備の名称を返す
;-------------------------------------------------
; Input:
;  ARG:0				キャラ
;  ARG:1				装備箇所
; Output:
;  RETURNF				名称
;-------------------------------------------------
@EQ_GETNAME_魔晶装備_CHARA(ARG:0, ARG:1)
#FUNCTIONS
#DIM L_TMP

;- 装備中のアイテムNoを取得
L_TMP = EQUIP:ARG:(GET_EQUIP(ARG:1))

RETURNF EQ_GETNAME_魔晶装備_ITEM(L_TMP)


;=================================================
;   sub EQ_GETNAME_魔晶装備
;=================================================
;   EQ:名称取得(魔晶装備)
;-------------------------------------------------
; Input:
;  ARG					データベース位置
; Output:
;  RETURNF				名称
;-------------------------------------------------
@EQ_GETNAME_魔晶装備(ARG)
#FUNCTIONS
	SIF DC:ARG:49 == 19
	RETURNF SAVESTR:(550+ARG)
RETURNF STR:(DC:ARG:49+100)


;-------------------------------------------------------------------------------
;   その他
;-------------------------------------------------------------------------------

;=================================================
;   sub EQ_画面モード切替
;=================================================
;   EQ:画面モード切替
;-------------------------------------------------
@EQ_画面モード切替

;- 反転
FLAG:アイテムリスト画面モード設定 = !FLAG:アイテムリスト画面モード設定



;=================================================
;   sub EQ_射程名称
;=================================================
;   EQ:射程名称
;-------------------------------------------------
; Input:
;  ARG					射程
; Output:
;  RETURNF				名称
;-------------------------------------------------
@EQ_射程名称(ARG)
#FUNCTIONS

SELECTCASE ARG
	CASE 1
		RETURNF "Ｓ"
	CASE 2
		RETURNF "Ｍ"
	CASE 3
		RETURNF "Ｌ"

	CASEELSE
		;- 引っかからない場合はERROR
		RETURNF "E!"
ENDSELECT



