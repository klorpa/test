;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:EQUIPMENT.ERB
;	Facility	:装備の脱着などのシステム
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2011/05/19		P						プラグインソフトの処理を追加
;	003		2011/10/23		旅人					装備品のリスト表示化
;	004		2011/10/23		旅人					処理を共通化
;	005		2012/05/20		P						装備の変更時に変動ステータスの表示画面を追加
;	006		2013/03/27		GunP						生得武器所持にエレジー追記
;	007		2013/12/02		ひみつ					バステ耐性の表示の仕方を少し変更。装備リスト(キャラクタ版)もページで表示するように。
;													キャラクタ表示の際にHalfDemonのアクセサリが変更出来ないのを修正。
;													装備選択中に戻るを選んだ場合に変動ステータス値表示が残る問題を修正
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;===================================================================
;装備品の変更等
;===================================================================

@CASTING_EQUIPMENT
SIF CFLAG:COUNT:PTフラグ == 0
	RETURN 0
SIF ABL:COUNT:種族 > 0 && ABL:COUNT:種族 < 46 && ABL:COUNT:種族 != 36 && SKILL_EQUIPTHEORY_IS_HAVE_SKILL(COUNT) == 0
	RETURN 0
;ダンジョン攻略中はPT内のキャラ以外ダメ
;SIF FLAG:ショップコマンド == [[ショップ:探索]] && CFLAG:COUNT:PTフラグ != 2
;	RETURN 0
SIF CFLAG:COUNT:労役フラグ == 3
	RETURN 0
SIF TALENT:COUNT:非戦闘員
	RETURN 0
SIF CFLAG:COUNT:この場に居ないフラグ == 1
	RETURN 0
RETURN 1

@EQUIPMENT , ARG = -1 , ARG:1 = -1
#DIM LINE
;キャラリストを表示・選択
IF ARG == -1
	CALL INPUT_CHARA_LIST("Please select whose equipment you want to change", "CASTING_EQUIPMENT")
	SIF RESULT == 1000
		RETURN 0
	LOCAL:10 = RESULT
ELSE
	;ステータス画面から来た人用
	LOCAL:10 = ARG
	
	;キャラ送り用のリスト作成
	VARSET Q , -1
	REPEAT CHARANUM
		CALL CASTING_EQUIPMENT
		SIF RESULT
			Q:FINDELEMENT(Q, -1) = COUNT
	REND
	
ENDIF
LOCAL:1 = FINDELEMENT(Q, LOCAL:10)

SIF GETBIT(FLAG:ステータス画面タイプ, 2)
	GOTO PRINT_ELIST

CUSTOMDRAWLINE =
LINE = LINECOUNT
LOCAL:1 = FINDELEMENT(Q, LOCAL:10)
変更装備箇所 = ARG:1

$RESET
VARSET アイテムリスト , -1
SIF 変更装備箇所 != -1
	CALL STATUS_WINDOW_EQUIP_ITEM_LIST , LOCAL:10 , 0 , 0 , 変更装備箇所 , "リスト作成"
VARSET 変更予定装備 , -1
CALL 装備変更時ステータス更新 , LOCAL:10
;ここまでが変数など初期化処理

$PRINTLOOP
CLEARLINE LINECOUNT - LINE

CALL SET_PICTURE , , , "消去"

;CALL STATUS_WINDOW_MAIN , LOCAL:10 , 0 , 0

CALL STATUS_WINDOW_EQUIP , LOCAL:10 , 0 , 0 , 1
CALL STATUS_WINDOW_BATTLE_STATUS , LOCAL:10 , 13 , 0 , 1
CALL STATUS_WINDOW_DEFTYPE , LOCAL:10 , 24 , 0 , 1
CALL STATUS_WINDOW_EQUIP_ITEM_LIST , LOCAL:10 , 0 , 52

;
IF 変更装備箇所 != -1
	CALL STATUS_WINDOW_EQUIP_DATA , LOCAL:10 , 13 , 76 , EQUIP:(LOCAL:10):GET_EQUIP(変更装備箇所) , ""
	CALL SET_PICTURE , "[-],,,-_ToggleInfo_[+],,,+" , 13 , , 100
ENDIF
IF 変更装備箇所 > -1 && 変更予定装備:変更装備箇所 != -1
	CALL SET_PICTURE , "↓↓↓" , 23 , , 90
	CALL STATUS_WINDOW_EQUIP_DATA , LOCAL:10 , 24 , 76 , 変更予定装備:変更装備箇所 , ""
ENDIF

CALL SET_PICTURE ,  , 0 , "表示" , 35 ;35行まで表示

PRINT Equipment description　：　
IF 変更装備箇所 != -1
	TRYCCALLFORM 装備解説_{EQUIP:(LOCAL:10):GET_EQUIP(変更装備箇所)}
	CATCH
		PRINTL
	ENDCATCH
	CALL 装備強化_CSTR, "解説表示", (LOCAL:10),EQUIP:(LOCAL:10):GET_EQUIP(変更装備箇所)
ELSE
	PRINTL
ENDIF
IF 変更装備箇所 > -1 && 変更予定装備:変更装備箇所 != -1
	PRINT 　　　　　→　
	TRYCCALLFORM 装備解説_{変更予定装備:変更装備箇所}
	CATCH
		PRINTL
	ENDCATCH
ELSE
	PRINTL
ENDIF

CUSTOMDRAWLINE =

PRINTFORM \@(LOCAL:1 == 0) ? %" " * 16% # [1001]Previous character \@
PRINT [100] Return [200]Confirm changes [300]Unequip all
PRINTFORML \@(LOCAL:1 == FINDELEMENT(Q, -1) - 1) ? %" " * 16% # [1003]Next character\@

$INPUTS_LOOP
INPUTS
ENCODETOUNI %RESULTS%
IF RESULTS == "100"
	変更装備箇所 = -1
	VARSET 変更予定装備, -1
	CALL 装備変更時ステータス更新(LOCAL:10)
	RETURN 0
ELSEIF RESULTS == "200"
	FOR LOCAL , 0 , 7
		SIF 変更予定装備:LOCAL != -1
			CALL 装備変更 , LOCAL:10 , LOCAL , 変更予定装備:LOCAL
	NEXT
	GOTO RESET
ELSEIF RESULTS == "300"
	CALL 全装備解除(LOCAL:10)
	GOTO RESET
ELSEIF RESULTS == "1001" && LOCAL:1 > 0
	変更装備箇所 = -1
	LOCAL:1 -= 1
	LOCAL:10 = Q:(LOCAL:1)
	GOTO RESET
ELSEIF RESULTS == "1003" && LOCAL:1 < FINDELEMENT(Q, -1) -1
	変更装備箇所 = -1
	LOCAL:1 += 1
	LOCAL:10 = Q:(LOCAL:1)
	GOTO RESET
ELSEIF RANGE(TOINT(RESULTS) , 50 , 56)
	変更装備箇所 = TOINT(RESULTS) - 50
	CALL STATUS_WINDOW_EQUIP_ITEM_LIST , LOCAL:10 , 0 , 0 , 変更装備箇所 , "リスト作成"
	GOTO PRINTLOOP

;ELSEIF RANGE(TOINT(RESULTS) , 2000 , 5499) && FINDELEMENT(アイテムリスト , TOINT(RESULTS)) > -1
ELSEIF RESULT == 1 && RANGE(RESULT:1 , 97 , 97 + 19) && アイテムリスト:(アイテムリストページ * 20 + RESULT:1 - 97) > -1
	;変更予定装備:変更装備箇所 = TOINT(RESULTS)
	変更予定装備:変更装備箇所 = アイテムリスト:(アイテムリストページ * 20 + RESULT:1 - 97)
	CALL 装備変更時ステータス更新 , LOCAL:10
	
	GOTO PRINTLOOP
ELSEIF RESULTS == "57" && TALENT:(LOCAL:10):ガンスリンガー > 0 && EQUIP:(LOCAL:10):銃 > 0
	CALL EQUIP_SPECIAL_BULLET, LOCAL:10
	GOTO RESET
ELSEIF RESULTS == "<"
	アイテムリストページ = LIMIT(アイテムリストページ-1 , 0 , (FINDELEMENT(アイテムリスト , -1)-1) / 20)
	GOTO PRINTLOOP
ELSEIF RESULTS == ">"
	アイテムリストページ = LIMIT(アイテムリストページ+1 , 0 , (FINDELEMENT(アイテムリスト , -1)-1) / 20)
	GOTO PRINTLOOP
ELSEIF RESULTS == "+" || RESULTS == "-"
	CALL STATUS_WINDOW_EQUIP_DATA , , , , , @"表示切替%RESULTS%"
	GOTO PRINTLOOP
ELSEIF RESULTS == "ソート条件変更"
	CALL CHANGE_CONDITION_EQUIP_SORT
	CALL EQUIP_SORT , LOCAL:10 , 1
	GOTO PRINTLOOP
ELSE
	GOTO INPUTS_LOOP
ENDIF

;=========================================
$PRINT_ELIST
CUSTOMDRAWLINE =
PRINTFORML %NAME:(LOCAL:10)%
PRINTL
CALL PRINT_EQUIPLIST,LOCAL:10
IF FLAG:カスタムゲーム画面
	IF TALENT:(LOCAL:10):ガンスリンガー && EQUIP:(LOCAL:10):銃
		PRINTL 　[7][Special Bullet]
	ELSE
		PRINTL 　[-][----------]
	ENDIF
ELSE
	IF TALENT:(LOCAL:10):ガンスリンガー && EQUIP:(LOCAL:10):銃
		PRINTL [7][Special Bullet]
	ELSE
		PRINTL [-][----------]
	ENDIF
ENDIF
DRAWLINE
SIF FLAG:カスタムゲーム画面
	PRINT 　
PRINTL [300]Unequip all
DRAWLINE

PRINTFORMLC \@(LOCAL:1 == 0) ? %" " * 16% # [1001]Previous character \@
PRINTLC [100]Cancel　　[200]Confirm changes　　
PRINTFORMLC \@(LOCAL:1 == A-1) ? %" " * 16% # [1003]Next character\@
$INPUT_LOOP2
INPUT

IF RESULT == 100
	RESTART
ELSEIF RESULT == 200
	RETURN 0
ELSEIF RESULT == 300
	CALL 全装備解除(LOCAL:10)
	GOTO PRINT_ELIST
ELSEIF RESULT == 1001 && LOCAL:1 > 0
	LOCAL:1 -= 1
	LOCAL:10 = Q:(LOCAL:1)
	GOTO PRINT_ELIST
ELSEIF RESULT == 1003 && LOCAL:1 < A-1
	LOCAL:1 += 1
	LOCAL:10 = Q:(LOCAL:1)
	GOTO PRINT_ELIST
ELSEIF RESULT < 0 || RESULT >= 8
	GOTO INPUT_LOOP2
ENDIF
IF RESULT == 7 && TALENT:(LOCAL:10):ガンスリンガー && EQUIP:(LOCAL:10):銃
	CALL EQUIP_SPECIAL_BULLET,LOCAL:10
	GOTO PRINT_ELIST
ELSEIF RESULT == 7
	GOTO INPUT_LOOP2
ENDIF
LOCAL:11 = RESULT
CALL EQUIP_CHANGE, LOCAL:11, LOCAL:10
SIF RESULT == 0
	GOTO PRINT_ELIST
RETURN 1

;多分こんなノリ
;ARG:0…装備カテゴリ(0.Sword/1.Gun/2.Headgear/3.Armor/4.Bracer/5.Footwear/6.Accessory)
;ARG:1…キャラ登録番号
;ARG:2…呼び元(0.装備変更/1.ステータス画面)
@EQUIP_CHANGE, ARG, ARG:1, ARG:2
#DIM アイテム確認表示
#DIM 元戦闘能力, 8
#DIM L_COUNT
#DIM LLINE
#DIM LPAGE
#DIM LREDRAW
#DIM LSIZE
#LOCALSIZE 2
;---- EDIT 003 ADD START -------------------------
#DIM L_NG
;---- EDIT 003 ADD END   -------------------------

CALL STATUS_WINDOW_EQUIP_ITEM_LIST(ARG:1, 0, 0, ARG:0, "リスト作成")
ARRAYREMOVE アイテムリスト, 0, 1
LLINE = LINECOUNT
LPAGE = 0
LREDRAW = CURRENTREDRAW()
SIF LREDRAW != 2
	REDRAW 2
LSIZE = FINDELEMENT(アイテムリスト, -1)
ARRAYSORT アイテムリスト, FORWARD, 0, LSIZE

$PRINT_EMLIST
CLEARLINE LINECOUNT - LLINE
;---- EDIT 003 ADD START -------------------------
L_NG=0
CUSTOMDRAWLINE =
PRINTFORM セットする装備品を選んでください (現在の画面モード:\@ !FLAG:アイテムリスト画面モード設定 ? 通常 # 簡易リスト \@) 
PRINTFORML ＜page.{LPAGE + 1}/{(LSIZE - 1) / リスト表示数() + 1}＞
DRAWLINE
;---- EDIT 003 ADD END   -------------------------

CALL PRINT_EQUIPMENTLIST2(ARG:0, ARG:1, LPAGE, LSIZE)
IF LPAGE == (LSIZE - 1) / リスト表示数()
	IF FLAG:アイテムリスト画面モード設定
		PRINTFORM \@ LINECOUNT - LLINE < 4 + リスト表示数() ? %"\n" * MAX(0, 4 + リスト表示数() + LLINE - LINECOUNT)% # \@
	ELSE
		LOCAL:1 = LSIZE % リスト表示数()
		LOCAL = (リスト表示数() / PRINTCPERLINE()) + ((リスト表示数() % PRINTCPERLINE()) != 0)
		LOCAL -= (LOCAL:1 / PRINTCPERLINE()) + ((LOCAL:1 % PRINTCPERLINE()) != 0)
		PRINTFORM %"\n" * MAX(0, LOCAL)%
	ENDIF
ENDIF
DRAWLINE
PRINTL [0] Remove
;---- EDIT 003 MOD START -------------------------
PRINTFORMLC \@ LPAGE <= 0 ? %" " * 16% # [1007]Previous page \@
IF !ARG:2
	PRINTFORMLC [1000]Cancel
	PRINTFORMLC [1001] Confirm Changes
ELSE
	PRINTFORMLC [1000] End
	PRINTFORMLC 
ENDIF
PRINTFORMLC \@ LPAGE >= (LSIZE - 1) / リスト表示数() ? %" " * 16% # [1009]Next page \@
PRINTFORMLC [1002] 画面モード変更(\@ FLAG:アイテムリスト画面モード設定 ? 通常 # 簡易リスト \@)
PRINTL
;---- EDIT 003 MOD END   -------------------------


$INPUT_LOOP3
INPUT

IF RESULT == 1000
	GOTO END_FUNC
ELSEIF RESULT == 1001
	SIF LREDRAW != 2
		REDRAW LREDRAW
	RETURN 1
;---- EDIT 003 ADD START -------------------------
ELSEIF RESULT == 1002
	CALL EQ_画面モード切替
	CLEARLINE 1
	GOTO PRINT_EMLIST
;---- EDIT 003 ADD END   -------------------------
ELSEIF RESULT == 1007 && LPAGE > 0
	LPAGE--
	GOTO PRINT_EMLIST
ELSEIF RESULT == 1009 && LPAGE < (LSIZE - 1) / リスト表示数()
	LPAGE++
	GOTO PRINT_EMLIST
ELSEIF RESULT == 0
	;Gunの場合、特殊弾を外す
	IF GET_EQUIP(ARG) == "銃"
		FOR LOCAL,1,8
			LOCALS = 特殊弾{LOCAL}
			EQUIP:(ARG:1):LOCALS = 0
		NEXT
	ENDIF
	;外した装備の所持数を増加
	SIF (EQUIP:(ARG:1):GET_EQUIP(ARG)) != 0 && (EQUIP:(ARG:1):GET_EQUIP(ARG)) != [[アイテム:Innate Weapon]]
		ITEM:(EQUIP:(ARG:1):GET_EQUIP(ARG)) += 1
	EQUIP:(ARG:1):GET_EQUIP(ARG) = 0
	;装備箇所が"剣"で対象キャラが生得武器所持条件を満たす場合、装備を「Innate Weapon」に変更
	SIF ARG == 0 && 生得武器所持(ARG:1)
		EQUIP:(ARG:1):GET_EQUIP(ARG) = [[アイテム:Innate Weapon]]
	
	CALL SYNC_STATUS,ARG:1
	GOTO END_FUNC
ENDIF
LOCAL = RESULT


IF 魔晶装備(LOCAL)
	CALLFORM 装備条件_魔晶装備,ARG:1,LOCAL - 2450 - 450*ARG
	IF LOCAL == 1000
		GOTO END_FUNC
	ELSEIF LOCAL < 2000+ARG*500 || LOCAL >= 2000+(ARG+1)*500 || RESULT == 0 || ITEM:(LOCAL) <= 0
		;---- EDIT 003 MOD START -------------------------
;		GOTO INPUT_LOOP3
		L_NG=1
		;---- EDIT 003 MOD END   -------------------------
	ENDIF
ELSE
	TRYCCALLFORM 装備条件_{LOCAL},ARG:1
		IF LOCAL == 1000
			GOTO END_FUNC
		ELSEIF LOCAL < 2000+ARG*500 || LOCAL >= 2000+(ARG+1)*500 || RESULT == 0 || ITEM:(LOCAL) <= 0
			;---- EDIT 003 MOD START -------------------------
;			GOTO INPUT_LOOP3
			L_NG=1
			;---- EDIT 003 MOD END   -------------------------
		ENDIF
	CATCH
		;---- EDIT 003 MOD START -------------------------
;		GOTO INPUT_LOOP3
		L_NG=1
		;---- EDIT 003 MOD END   -------------------------
	ENDCATCH
ENDIF

;---- EDIT 003 ADD START -------------------------
IF L_NG
	L_NG=0

	;- 装備中の装備は許可
	IF EQUIP:(ARG:1):GET_EQUIP(ARG) == LOCAL
	ELSE
		CLEARLINE 1
		REUSELASTLINE （入力値 {LOCAL} は範囲外、または個数0のアイテムです）
		GOTO INPUT_LOOP3
	ENDIF

ENDIF
;---- EDIT 003 ADD END   -------------------------
$PRINT_STATUS
IF アイテム確認表示
	IF EQUIP:(ARG:1):GET_EQUIP(ARG) > 0
		CALL 装備説明,EQUIP:(ARG:1):GET_EQUIP(ARG),ARG:1
		PRINTL
		CUSTOMDRAWLINE ↓
		PRINTL
	ENDIF
	CALL 装備説明,LOCAL,ARG:1
ELSE
	FOR L_COUNT,0, FLAG:相性数
		CALLF 装備変更時相性(L_COUNT, 1, MAXBASE:(ARG:1):GET_TYPE(L_COUNT))
	NEXT
	FOR L_COUNT,0,FLAG:戦闘能力数+2
		元戦闘能力:L_COUNT = MAXBASE:(ARG:1):GET_BATTLESTATUS(L_COUNT)
	NEXT
	DRAWLINE
	PRINTFORML \@ EQUIP:(ARG:1):GET_EQUIP(ARG) == 0 ? %AUTO_SPLIT("素手_ＥＭＰＴＹ" , "_" , GET_EQUIP(ARG) != "剣" )% # %ITEMNAME_E(EQUIP:(ARG:1):GET_EQUIP(ARG))% \@ → %ITEMNAME_E(LOCAL)%
	DRAWLINE
	SWAP EQUIP:(ARG:1):GET_EQUIP(ARG), LOCAL
	CALL SYNC_STATUS, ARG:1
	FOR L_COUNT,0,FLAG:戦闘能力数 + 2
		CALLF 装備変更時ステータス変化値(L_COUNT, 1, MAXBASE:(ARG:1):GET_BATTLESTATUS(L_COUNT) - 元戦闘能力:L_COUNT)
	NEXT
	U = ARG:1
	CALL SHOW_INFO_NAME
	CALL SHOW_INFO_PAGE_2, 1
	SWAP EQUIP:(ARG:1):GET_EQUIP(ARG), LOCAL
	CALL SYNC_STATUS, ARG:1
ENDIF
PRINTFORML [0] Change equipment　[1] %\@アイテム確認表示 ? Check Status # Confirm changes \@% [9]Cancel
CALL INPUTINT, 0, 1, 9

SELECTCASE RESULT
	CASE 0
		
	CASE 1
		INVERTBIT アイテム確認表示, 0
		GOTO PRINT_STATUS
	CASE 9
		GOTO PRINT_EMLIST
ENDSELECT

CALL 装備変更,ARG:1,ARG,LOCAL
;Gunの場合、特殊弾を外すかを判定
IF GET_EQUIP(ARG) == "銃" && EQUIP:(ARG:1):銃 > 0
	CALLFORM 特殊弾倉_{EQUIP:(ARG:1):銃},ARG:1
	LOCAL:1 = RESULT
	FOR LOCAL,1,8
		LOCALS = 特殊弾{LOCAL}
		SIF LOCAL > LOCAL:1
			EQUIP:(ARG:1):LOCALS = 0
		SIF EQUIP:(ARG:1):LOCALS == 0
			CONTINUE
		CALLFORM 装備条件_{EQUIP:(ARG:1):LOCALS},ARG:1
		SIF RESULT == 0
			EQUIP:(ARG:1):LOCALS = 0
	NEXT
ENDIF
$END_FUNC
SIF LREDRAW != 2
	REDRAW LREDRAW
RETURN 0


@PRINT_EQUIPLIST,ARG
;1行目にレベル・属性を表示
CALL PRINT_BASE_STATUS,ARG

IF FLAG:カスタムゲーム画面
	DRAWLINE
	PRINTFORML 　ＨＰ[{BASE:ARG:ＨＰ,5}/{MAXBASE:ARG:ＨＰ,5}]　　ＭＰ[{BASE:ARG:ＭＰ,5}/{MAXBASE:ARG:ＭＰ,5}]　　\@MAXBASE:ARG:CP>0?ＣＰ:{MAXBASE:ARG:CP,3}#\@
	DRAWLINE
	PRINTFORM 　Attack：%GET_TYPE_E(ABL:ARG:攻撃相性)% {ABL:ARG:最低攻撃回数}-{ABL:ARG:最大攻撃回数} hits　Range:
	IF ABL:ARG:射程 == 1
		PRINT Ｓ
	ELSEIF ABL:ARG:射程 == 2
		PRINT Ｍ
	ELSEIF ABL:ARG:射程 == 3
		PRINT Ｌ
	ENDIF
	PRINTFORML 　%GET_SPHERE(ABL:ARG:攻撃範囲)%

	SIF ABL:ARG:追加効果 > 0
		PRINTFORML 　　　　AddedEffect:%GET_STATE(ABL:ARG:追加効果), 6, LEFT%　Type:%GET_TYPE_E(ABL:ARG:追加効果相性)%
		;追加効果:%GET_STATE(ABL:ARG:追加効果), 6, LEFT%　追加効果相性:%GET_TYPE(ABL:ARG:追加効果相性)%
	
	;Humanならばここに銃攻撃の性能と銃攻撃力・銃命中を2行つかって表示
	IF EQUIP:ARG:銃 > 0
		DRAWLINE
		PRINTFORML 　Gun：攻撃力 {MAXBASE:ARG:銃攻撃}　Hit {MAXBASE:ARG:銃命中}
		PRINT 　　　　
		IF 魔晶装備(EQUIP:ARG:銃)
			CALLFORM 最低攻撃回数_魔晶装備,EQUIP:ARG:銃 - 2900
			PRINTFORM 攻撃回数 {RESULT}-
			CALLFORM 最大攻撃回数_魔晶装備,EQUIP:ARG:銃 - 2900
			PRINTFORM {RESULT} hits
			CALLFORM 射程_魔晶装備,EQUIP:ARG:銃 - 2900
			PRINTFORM 　Range:
			IF RESULT == 1
				PRINT Ｓ
			ELSEIF RESULT == 2
				PRINT Ｍ
			ELSEIF RESULT == 3
				PRINT Ｌ
			ENDIF
			CALLFORM 攻撃範囲_魔晶装備,EQUIP:ARG:銃 - 2900
		ELSE
			CALLFORM 最低攻撃回数_{EQUIP:ARG:銃},ARG
			PRINTFORM 攻撃回数 {RESULT}-
			CALLFORM 最大攻撃回数_{EQUIP:ARG:銃},ARG
			PRINTFORM {RESULT} hits
			CALLFORM 射程_{EQUIP:ARG:銃},ARG
			PRINTFORM 　Range:
			IF RESULT == 1
				PRINT Ｓ
			ELSEIF RESULT == 2
				PRINT Ｍ
			ELSEIF RESULT == 3
				PRINT Ｌ
			ENDIF
			CALLFORM 攻撃範囲_{EQUIP:ARG:銃},ARG
		ENDIF
		PRINTFORM 　%GET_SPHERE(RESULT),4,LEFT%
		PRINTL
	ENDIF
	DRAWLINE
	;5〜10行目に基本能力値と戦闘能力値を表示
	REPEAT 6
		PRINTFORM %GET_BASESTATUS_E(COUNT+1),5%{MAXBASE:ARG:(GET_BASESTATUS(COUNT+1)),5}({BASE:ARG:(GET_BASESTATUS(COUNT+1)),5})　　　　　　　　
		PRINTFORM %GET_BATTLESTATUS_E(COUNT),10,LEFT% 
		PRINTFORML {MAXBASE:ARG:(GET_BATTLESTATUS(COUNT)),6}
	REND
	DRAWLINE
	;11、12行目に被攻撃相性と継承可能表示
	PRINTL  Resistances
	LOCAL:1 = GETCOLOR()
	;現在の色を退避しておく（敵のステータスを表示した時など）
	FOR LOCAL,0, FLAG:相性数
		SIF FLAG:カスタムゲーム画面 && LOCAL % 6 == 0
			PRINT 　 
		CALL SET_AISYOU_COLOR, MAXBASE:U:(GET_TYPE(LOCAL))
		PRINTFORM %GET_TYPE_E(LOCAL),5,LEFT% \@(MAXBASE:U:(GET_TYPE(LOCAL)) < 0) ? Dr # 　\@
		PRINTFORM \@ ((MAXBASE:U:(GET_TYPE(LOCAL)) != 999)) ? {ABS(MAXBASE:U:(GET_TYPE(LOCAL))),4}％ # %" " *2%Refl \@　
		;PRINTFORM {ABS(MAXBASE:U:(GET_TYPE(LOCAL)),4}％
		SIF LOCAL % 6 == 5
			PRINTL
	NEXT
	;色を戻す
	SETCOLOR LOCAL:1
	DRAWLINE
	;装備が可能でないキャラ
	IF ABL:ARG:種族 > 0 && ABL:ARG:種族 < 46 && ABL:ARG:種族 != 36 && SKILL_EQUIPTHEORY_IS_HAVE_SKILL(ARG) == 0
		PRINT  [PLUG-IN] 
		IF EQUIP:ARG:アクセサリ == 0
			SETCOLOR 0x404040
			PRINTFORML ＥＭＰＴＹ
			RESETCOLOR
		ELSE
			PRINTFORML 　%PLUGINNAME_E(EQUIP:ARG:アクセサリ)%
		ENDIF
	ELSE
		FOR LOCAL,0,7
			SIF FLAG:カスタムゲーム画面
				PRINT 　
			PRINTFORM [{LOCAL}]%"[" + GET_EQUIP_E(LOCAL) + "]",14,LEFT%
			IF (EQUIP:ARG:(GET_EQUIP(LOCAL))) == 0
				SETCOLOR 0x404040
				PRINTFORML ＥＭＰＴＹ
				RESETCOLOR
			ELSE
				IF 魔晶装備(EQUIP:ARG:(GET_EQUIP(LOCAL)))
					;---- EDIT 004 MOD START -------------------------
	;				PRINTFORML %STR:(DC:(EQUIP:ARG:(GET_EQUIP(LOCAL)) - 2450 - LOCAL*450):49+100)%
					PRINTFORML %EQ_GETNAME_魔晶装備_CHARA(ARG, LOCAL)%
					;---- EDIT 004 MOD END   -------------------------
				ELSE
					PRINTFORML %ITEMNAME_E(EQUIP:ARG:(GET_EQUIP(LOCAL)))%
				ENDIF
			ENDIF
		NEXT
	ENDIF
ELSE
	PRINTFORML ＨＰ[{BASE:ARG:ＨＰ,4}/{MAXBASE:ARG:ＨＰ,4}]　　ＭＰ[{BASE:ARG:ＭＰ,4}/{MAXBASE:ARG:ＭＰ,4}]　　\@MAXBASE:ARG:CP>0?ＣＰ:{MAXBASE:ARG:CP,3}#\@
	;4行目に通常攻撃の性能を表示
	PRINTFORM Attack：%GET_TYPE_E(ABL:ARG:攻撃相性),6,LEFT%　{ABL:ARG:最低攻撃回数,2}-{ABL:ARG:最大攻撃回数,2}hits　Range:
	IF ABL:ARG:射程 == 1
		PRINT Ｓ
	ELSEIF ABL:ARG:射程 == 2
		PRINT Ｍ
	ELSEIF ABL:ARG:射程 == 3
		PRINT Ｌ
	ENDIF
	PRINTFORML  %GET_SPHERE(ABL:ARG:攻撃範囲),4,LEFT%
	;Humanならばここに銃攻撃の性能と銃攻撃力・銃命中を2行つかって表示
	IF EQUIP:ARG:銃 > 0
		DRAWLINE
		PRINTFORML Gun：Attack {MAXBASE:ARG:銃攻撃,4} Hit {MAXBASE:ARG:銃命中}
		IF 魔晶装備(EQUIP:ARG:銃)
			CALLFORM 最低攻撃回数_魔晶装備,EQUIP:ARG:銃 - 2900
			PRINTFORM 攻撃回数 {RESULT,2}-
			CALLFORM 最大攻撃回数_魔晶装備,EQUIP:ARG:銃 - 2900
			PRINTFORM {RESULT,2}
			CALLFORM 射程_魔晶装備,EQUIP:ARG:銃 - 2900
			PRINTFORM 　　Range：
			IF RESULT == 1
				PRINT Ｓ
			ELSEIF RESULT == 2
				PRINT Ｍ
			ELSEIF RESULT == 3
				PRINT Ｌ
			ENDIF
			CALLFORM 攻撃範囲_魔晶装備,EQUIP:ARG:銃 - 2900
		ELSE
			CALLFORM 最低攻撃回数_{EQUIP:ARG:銃},ARG
			PRINTFORM 攻撃回数 {RESULT,2}-
			CALLFORM 最大攻撃回数_{EQUIP:ARG:銃},ARG
			PRINTFORM {RESULT,2}
			CALLFORM 射程_{EQUIP:ARG:銃},ARG
			PRINTFORM 　　Range：{RESULT,2}
			CALLFORM 攻撃範囲_{EQUIP:ARG:銃},ARG
		ENDIF
		PRINTFORM 　　%GET_SPHERE(RESULT),4,LEFT%
		PRINTL
	ENDIF
	DRAWLINE
	;5〜10行目に基本能力値と戦闘能力値を表示
	REPEAT 6
		PRINTFORM %GET_BASESTATUS_E(COUNT+1),4%{MAXBASE:ARG:(GET_BASESTATUS(COUNT+1)),4}({BASE:ARG:(GET_BASESTATUS(COUNT+1)),4})　　　　　　　　
		PRINTFORM %GET_BATTLESTATUS_E(COUNT),8,LEFT% 
		PRINTFORML {MAXBASE:ARG:(GET_BATTLESTATUS(COUNT)),5}
	REND
	DRAWLINE
	;11、12行目に被攻撃相性と継承可能表示
	PRINTL Resistances
	FOR LOCAL,0,FLAG:相性数
			PRINTFORM %GET_TYPE_E(LOCAL),5,LEFT% \@(MAXBASE:ARG:(GET_TYPE(LOCAL)) < 0) ? Dr # 　\@
			PRINTFORM \@ ((MAXBASE:ARG:(GET_TYPE(LOCAL)) != 999)) ? {ABS(MAXBASE:ARG:(GET_TYPE(LOCAL))),4}％ # %" " *2%Refl \@　
			;PRINTFORM {ABS(MAXBASE:ARG:(GET_TYPE(LOCAL)),4}％
		SIF LOCAL % 6 == 5
			PRINTL
	NEXT
	DRAWLINE
	;装備が可能でないキャラ
	IF ABL:ARG:種族 > 0 && ABL:ARG:種族 < 46 && ABL:ARG:種族 != 36 && SKILL_EQUIPTHEORY_IS_HAVE_SKILL(ARG) == 0
		PRINT  [PLUG-IN] 
		IF EQUIP:ARG:アクセサリ == 0
			SETCOLOR 0x404040
			PRINTFORML ＥＭＰＴＹ
			RESETCOLOR
		ELSE
			PRINTFORML %PLUGINNAME_E(EQUIP:ARG:アクセサリ)%
		ENDIF
	ELSE
		FOR LOCAL,0,7
			PRINTFORM [{LOCAL}]%"[" + GET_EQUIP_E(LOCAL) + "]",14,LEFT%
			IF (EQUIP:ARG:(GET_EQUIP(LOCAL))) == 0
				SETCOLOR 0x404040
				PRINTFORML ＥＭＰＴＹ
				RESETCOLOR
			ELSE
				IF 魔晶装備(EQUIP:ARG:(GET_EQUIP(LOCAL)))
					;---- EDIT 004 MOD START -------------------------
	;				PRINTFORML %STR:(DC:(EQUIP:ARG:(GET_EQUIP(LOCAL)) - 2450 - LOCAL*450):49+100)%
					PRINTFORML %EQ_GETNAME_魔晶装備_CHARA(ARG, LOCAL)%
					;---- EDIT 004 MOD END   -------------------------
				ELSE
					PRINTFORML %ITEMNAME_E(EQUIP:ARG:(GET_EQUIP(LOCAL)))%
				ENDIF
			ENDIF
		NEXT
	ENDIF
ENDIF

;---- EDIT 003 DEL START -------------------------
;;装備できるアイテムを表示する
;@PRINT_EQUIPMENTLIST, ARG, ARG:1
;#LOCALSIZE 1
;FOR LOCAL,2000+ARG*500,2000+(ARG+1)*500
;	SIF ITEMPRICE:LOCAL == 0
;		CONTINUE
;	SIF ITEM:LOCAL == 0
;		CONTINUE
;	IF 魔晶装備(LOCAL)
;		CALLFORM 装備条件_魔晶装備,ARG:1,LOCAL - 2450 - ARG*450
;		SIF RESULT == 1
;			PRINTFORMC [{LOCAL}] %STR:(DC:(LOCAL - 2450 - ARG*450):49+100),20,LEFT% × {ITEM:LOCAL,3} 
;	ELSE
;		CALLFORM 装備条件_{LOCAL},ARG:1
;		SIF RESULT == 1
;			PRINTFORMC [{LOCAL}] %ITEMNAME_E(LOCAL),20,LEFT% × {ITEM:LOCAL,3} 
;	ENDIF
;NEXT
;PRINTL
;DRAWLINE
;---- EDIT 003 DEL END ---------------------------
;---- EDIT 003 ADD START -------------------------
;=================================================
;   sub PRINT_EQUIPMENTLIST
;=================================================
;   表示:装備可能アイテム
;-------------------------------------------------
; Input:
;  ARG:0				アイテム種別
;  ARG:1				キャラ
;-------------------------------------------------
@PRINT_EQUIPMENTLIST(ARG, ARG:1)
#LOCALSIZE 1
#DIM L_J
#DIM L_TMP
#DIM L_MODE

;- モード(0:通常 1:簡易リスト)
L_MODE = FLAG:アイテムリスト画面モード設定

;- 簡易リスト時
IF L_MODE

	;- 装備箇所で分岐
	SELECTCASE ARG
		;- 0:剣
		CASE 0

			;- 表示:装備可能アイテム(武器)(リスト形式:ヘッダ)
			CALL PRINT_EQUIPMENTLIST_TYPELIST_ARMS_HEADER

			FOR LOCAL,2000+ARG*500,2000+(ARG+1)*500
				SIF ITEMPRICE:LOCAL == 0
					CONTINUE
				IF ITEM:LOCAL == 0
					;- 装備中の装備以外は非表示
					SIF !(EQUIP:(ARG:1):(GET_EQUIP(ARG)) == LOCAL)
						CONTINUE
				ENDIF
				;- 表示:装備可能アイテム(武器)(リスト形式:行単位)
				CALL PRINT_EQUIPMENTLIST_TYPELIST_ARMS_LINE(LOCAL, ARG:1)

			NEXT
		;- 1:飛具
		CASE 1

			;- 表示:装備可能アイテム(Gun)(リスト形式:ヘッダ)
			CALL PRINT_EQUIPMENTLIST_TYPELIST_GUN_HEADER

			FOR LOCAL,2000+ARG*500,2000+(ARG+1)*500
				SIF ITEMPRICE:LOCAL == 0
					CONTINUE
				IF ITEM:LOCAL == 0
					;- 装備中の装備以外は非表示
					SIF !(EQUIP:(ARG:1):(GET_EQUIP(ARG)) == LOCAL)
						CONTINUE
				ENDIF
				;- 表示:装備可能アイテム(Gun)(リスト形式:行単位)
				CALL PRINT_EQUIPMENTLIST_TYPELIST_GUN_LINE(LOCAL, ARG:1)

			NEXT
		;- 2-6:防具
		CASE 2 TO 6

			;- 表示:装備可能アイテム(防具)(リスト形式:ヘッダ)
			CALL PRINT_EQUIPMENTLIST_TYPELIST_PROTECTOR_HEADER

			FOR LOCAL,2000+ARG*500,2000+(ARG+1)*500
				SIF ITEMPRICE:LOCAL == 0
					CONTINUE
				IF ITEM:LOCAL == 0
					;- 装備中の装備以外は非表示
					SIF !(EQUIP:(ARG:1):(GET_EQUIP(ARG)) == LOCAL)
						CONTINUE
				ENDIF
				;- 表示:装備可能アイテム(防具)(リスト形式:行単位)
				CALL PRINT_EQUIPMENTLIST_TYPELIST_PROTECTOR_LINE(LOCAL, ARG:1)

			NEXT

		;上記以外
		CASEELSE
			;- 通常出力
			L_MODE = 0
	ENDSELECT
ENDIF

;- 通常出力時
IF !L_MODE
	FOR LOCAL,2000+ARG*500,2000+(ARG+1)*500
		SIF ITEMPRICE:LOCAL == 0
			CONTINUE
		SIF ITEM:LOCAL == 0
			CONTINUE
		IF 魔晶装備(LOCAL)
			CALLFORM 装備条件_魔晶装備,ARG:1,LOCAL - 2450 - ARG*450
			SIF RESULT == 1
				;---- EDIT 004 MOD START -------------------------
;				PRINTFORMC [{LOCAL}] %STR:(DC:(LOCAL - 2450 - ARG*450):49+100),20,LEFT% × {ITEM:LOCAL,3} 
				PRINTFORML [{LOCAL}] %EQ_GETNAME_魔晶装備_ITEM(LOCAL), 20, LEFT% × {ITEM:LOCAL,3} 
				;---- EDIT 004 MOD END   -------------------------
		ELSE
			CALLFORM 装備条件_{LOCAL},ARG:1
			SIF RESULT == 1
				PRINTFORMC [{LOCAL}] %ITEMNAME_E(LOCAL),20,LEFT% × {ITEM:LOCAL,3} 
		ENDIF
	NEXT
	PRINTL
ENDIF

DRAWLINE

@PRINT_EQUIPMENTLIST2(ARG:0, ARG:1, ARG:2, ARG:3)
#DIMS CONST CATEGORY = "ARMS", "GUN", "PROTECTOR"
IF FLAG:アイテムリスト画面モード設定 && INRANGE(ARG:0, 0, 6)
	CALLFORM PRINT_EQUIPMENTLIST_TYPELIST_%CATEGORY:MIN(ARG:0, 2)%_HEADER
	FOR LOCAL:1, ARG:2 * リスト表示数(), MIN((ARG:2 + 1) * リスト表示数(), ARG:3)
		LOCAL = アイテムリスト:(LOCAL:1)
		CALLFORM PRINT_EQUIPMENTLIST_TYPELIST_%CATEGORY:MIN(ARG:0, 2)%_LINE(LOCAL, ARG:1)
	NEXT
ELSE
	FOR LOCAL:1, ARG:2 * リスト表示数(), MIN((ARG:2 + 1) * リスト表示数(), ARG:3)
		LOCAL = アイテムリスト:(LOCAL:1)
		LOCALS '= \@ 魔晶装備(LOCAL) ? %EQ_GETNAME_魔晶装備_ITEM(LOCAL)% # %ITEMNAME_E(LOCAL)% \@
		PRINTFORMLC [{LOCAL}] %LOCALS, 20, LEFT% × {ITEM:LOCAL, 3} 
	NEXT
	PRINTFORM \@ LINEISEMPTY() ? # \n \@
ENDIF
RETURN RESULT

;=================================================
;   sub PRINT_EQUIPMENTLIST_TYPELIST_ARMS_HEADER
;=================================================
;   表示:装備可能アイテム(武器)(リスト形式:ヘッダ)
;-------------------------------------------------
@PRINT_EQUIPMENTLIST_TYPELIST_ARMS_HEADER

;-----------------------------
;- ヘッダ
;-----------------------------
PRINTFORM %" " * 34%|
FOR LOCAL,0,FLAG:戦闘能力数
	PRINTFORM %GET_BATTLESTATUS_E(LOCAL)%|
NEXT
PRINTFORM Attacks |Type |Range|Target
PRINTL


;=================================================
;   sub PRINT_EQUIPMENTLIST_TYPELIST_ARMS_LINE
;=================================================
;   表示:装備可能アイテム(武器)(リスト形式:行単位)
;-------------------------------------------------
; Input:
;  ARG:0				アイテム番号
;  ARG:1				キャラ
;-------------------------------------------------
@PRINT_EQUIPMENTLIST_TYPELIST_ARMS_LINE(ARG:0, ARG:1)
#DIM L_J
#DIM L_ID
#DIM L_魔晶ID
#DIM L_TMP
#DIM L_POINT

L_ID = ARG:0

;-----------------------------
;- 行
;-----------------------------

IF 魔晶装備(L_ID)
	;- EQ:装備箇所(魔晶装備)
	L_POINT = EQ_装備箇所_魔晶装備(L_ID)

	;- EQ:アイテムID取得(魔晶装備)
	L_魔晶ID = EQ_GETID_魔晶装備(L_ID)

	;- CHK
	CALLFORM 装備条件_魔晶装備,ARG:1,L_魔晶ID
	SIF RESULT != 1
		RETURN

	;- 装備中の場合は水色
	SIF EQUIP:(ARG:1):(GET_EQUIP(L_POINT)) == L_ID
		SETCOLOR 0x66FFFF

	PRINTFORM [{L_ID,4}] %EQ_GETNAME_魔晶装備(L_魔晶ID),20,LEFT% × {ITEM:L_ID,3}
	RESETCOLOR
	PRINTFORM |

	FOR L_J,0,FLAG:戦闘能力数
		L_TMP = STRLENS(GET_BATTLESTATUS(L_J))
		CALLFORM 戦闘能力修正_魔晶装備,L_J,L_魔晶ID
		IF RESULT == 0
			SETCOLOR 0x404040
		ELSEIF RESULT < 0
			SETCOLOR 0xEE1010
		ENDIF
		PRINTFORM {RESULT,L_TMP}
		RESETCOLOR
		PRINTFORM |
	NEXT

	CALLFORM 最低攻撃回数_魔晶装備,L_魔晶ID
	PRINTFORM {RESULT,2} - 
	CALLFORM 最大攻撃回数_魔晶装備,L_魔晶ID
	PRINTFORM {RESULT,2} 
	PRINTFORM |
	CALLFORM 攻撃相性_魔晶装備,L_魔晶ID
	PRINTFORM %GET_TYPE_E(RESULT),5,LEFT%
	PRINTFORM |
	CALLFORM 射程_魔晶装備,L_魔晶ID
	PRINTFORM  %EQ_射程名称(RESULT)%  
	PRINTFORM |

	CALLFORM 攻撃範囲_魔晶装備,L_魔晶ID
	PRINTFORM %GET_SPHERE(RESULT)%

ELSE

	CALLFORM 装備箇所_{L_ID}
	L_POINT = RESULT

	;- CHK
	CALLFORM 装備条件_{L_ID},ARG:1
	SIF RESULT != 1
		RETURN

	;- 装備中の場合は水色
	SIF EQUIP:(ARG:1):(GET_EQUIP(L_POINT)) == L_ID
		SETCOLOR 0x66FFFF

	PRINTFORM [{L_ID,4}] %ITEMNAME_E(L_ID),20,LEFT% × {ITEM:L_ID,3}
	RESETCOLOR
	PRINTFORM |

	FOR L_J,0,FLAG:戦闘能力数
		L_TMP = STRLENS(GET_BATTLESTATUS(L_J))
		CALLFORM 戦闘能力修正_{L_ID},L_J,ARG:1
		IF RESULT == 0
			SETCOLOR 0x404040
		ELSEIF RESULT < 0
			SETCOLOR 0xEE1010
		ENDIF
		PRINTFORM {RESULT,L_TMP}
		RESETCOLOR
		PRINTFORM |
	NEXT

	CALLFORM 最低攻撃回数_{L_ID},ARG:1
	PRINTFORM {RESULT,2} - 
	CALLFORM 最大攻撃回数_{L_ID},ARG:1
	PRINTFORM {RESULT,2} 
	PRINTFORM |
	CALLFORM 攻撃相性_{L_ID},ARG:1
	PRINTFORM %GET_TYPE_E(RESULT),5,LEFT%
	PRINTFORM |
	CALLFORM 射程_{L_ID},ARG:1
	PRINTFORM  %EQ_射程名称(RESULT)%  
	PRINTFORM |
	CALLFORM 攻撃範囲_{L_ID},ARG:1
	PRINTFORM %GET_SPHERE(RESULT)%

ENDIF
PRINTL

;=================================================
;   sub PRINT_EQUIPMENTLIST_TYPELIST_GUN_HEADER
;=================================================
;   表示:装備可能アイテム(Gun)(リスト形式:ヘッダ)
;-------------------------------------------------
@PRINT_EQUIPMENTLIST_TYPELIST_GUN_HEADER

;-----------------------------
;- ヘッダ
;-----------------------------
PRINTFORM %" " * 34%|%GET_BATTLESTATUS_E(6)%|%GET_BATTLESTATUS_E(7)%|
PRINTFORM Attacks |Type |Bullets|Range|Target
PRINTL


;=================================================
;   sub PRINT_EQUIPMENTLIST_TYPELIST_GUN_LINE
;=================================================
;   表示:装備可能アイテム(Gun)(リスト形式:行単位)
;-------------------------------------------------
; Input:
;  ARG:0				アイテム番号
;  ARG:1				キャラ
;-------------------------------------------------
@PRINT_EQUIPMENTLIST_TYPELIST_GUN_LINE(ARG:0, ARG:1)
#DIM L_J
#DIM L_ID
#DIM L_TMP
#DIM L_POINT
#DIM L_魔晶ID

L_ID = ARG:0

;-----------------------------
;- 行
;-----------------------------

IF 魔晶装備(L_ID)

	;- EQ:装備箇所(魔晶装備)
	L_POINT = EQ_装備箇所_魔晶装備(L_ID)

	;- EQ:アイテムID取得(魔晶装備)
	L_魔晶ID = EQ_GETID_魔晶装備(L_ID)

	;- CHK
	CALLFORM 装備条件_魔晶装備,ARG:1,L_魔晶ID
	SIF RESULT != 1
		RETURN

	;- 装備中の場合は水色
	SIF EQUIP:(ARG:1):(GET_EQUIP(L_POINT)) == L_ID
		SETCOLOR 0x66FFFF

	PRINTFORM [{L_ID,4}] %EQ_GETNAME_魔晶装備(L_魔晶ID),20,LEFT% × {ITEM:L_ID,3}
	RESETCOLOR
	PRINTFORM |

	;-----------------------------
	;- ※ 残りの項目は魔晶装備のGunが追加されてから作成
	;-----------------------------

ELSE

	CALLFORM 装備箇所_{L_ID}
	L_POINT = RESULT

	;- CHK
	CALLFORM 装備条件_{L_ID},ARG:1
	SIF RESULT != 1
		RETURN

	;- 装備中の場合は水色
	SIF EQUIP:(ARG:1):(GET_EQUIP(L_POINT)) == L_ID
		SETCOLOR 0x66FFFF

	PRINTFORM [{L_ID,4}] %ITEMNAME_E(L_ID),20,LEFT% × {ITEM:L_ID,3}
	RESETCOLOR
	PRINTFORM |

	CALLFORM 銃攻撃_{L_ID},ARG:1
	PRINTFORM {RESULT,STRLENS(GET_BATTLESTATUS(6))}
	PRINTFORM |
	CALLFORM 銃命中_{L_ID},ARG:1
	PRINTFORM {RESULT,STRLENS(GET_BATTLESTATUS(7))}
	PRINTFORM |


	CALLFORM 最低攻撃回数_{L_ID},ARG:1
	PRINTFORM {RESULT,2} - 
	CALLFORM 最大攻撃回数_{L_ID},ARG:1
	PRINTFORM {RESULT,2} 
	PRINTFORM |
	CALLFORM 攻撃相性_{L_ID},ARG:1
	PRINTFORM %GET_TYPE_E(RESULT),5,LEFT%
	PRINTFORM |
	CALLFORM 特殊弾倉_{L_ID},ARG:1
	PRINTFORM {RESULT,4}   
	PRINTFORM |

	CALLFORM 射程_{L_ID},ARG:1
	PRINTFORM  %EQ_射程名称(RESULT)%  
	PRINTFORM |
	CALLFORM 攻撃範囲_{L_ID},ARG:1
	PRINTFORM %GET_SPHERE(RESULT)%
	RESULT:1 = RESULT
	RESULT = 0
	TRYCALLFORM GUN_RANDOMTARGET_{L_ID}
	SIF RESULT == 1
		PRINTFORM （ランダム）
ENDIF
PRINTL


;=================================================
;   sub PRINT_EQUIPMENTLIST_TYPELIST_PROTECTOR_HEADER
;=================================================
;   表示:装備可能アイテム(防具)(リスト形式:ヘッダ)
;-------------------------------------------------
@PRINT_EQUIPMENTLIST_TYPELIST_PROTECTOR_HEADER

;-----------------------------
;- ヘッダ
;-----------------------------
PRINTFORM %" " * 34%|
FOR LOCAL,0,FLAG:戦闘能力数
	PRINTFORM %GET_BATTLESTATUS_E(LOCAL)%|
NEXT
PRINTFORM Attributes
PRINTL

;=================================================
;   sub PRINT_EQUIPMENTLIST_TYPELIST_PROTECTOR_LINE
;=================================================
;   表示:装備可能アイテム(防具)(リスト形式:行単位)
;-------------------------------------------------
; Input:
;  ARG:0				アイテム番号
;  ARG:1				キャラ
;-------------------------------------------------
@PRINT_EQUIPMENTLIST_TYPELIST_PROTECTOR_LINE(ARG:0, ARG:1)
#DIM L_J
#DIM L_ID
#DIM L_TMP
#DIM L_POINT
#DIM L_魔晶ID
#DIM L_HIT

L_ID = ARG:0
L_HIT=0

;-----------------------------
;- 行
;-----------------------------

IF 魔晶装備(L_ID)

	;- EQ:装備箇所(魔晶装備)
	L_POINT = EQ_装備箇所_魔晶装備(L_ID)

	;- EQ:アイテムID取得(魔晶装備)
	L_魔晶ID = EQ_GETID_魔晶装備(L_ID)

	;- CHK
	CALLFORM 装備条件_魔晶装備,ARG:1,L_魔晶ID
	SIF RESULT != 1
		RETURN

	;- 装備中の場合は水色
	SIF EQUIP:(ARG:1):(GET_EQUIP(L_POINT)) == L_ID
		SETCOLOR 0x66FFFF

	PRINTFORM [{L_ID,4}] %EQ_GETNAME_魔晶装備(L_魔晶ID),20,LEFT% × {ITEM:L_ID,3}
	RESETCOLOR
	PRINTFORM |

	;-----------------------------
	;- ※ 残りの項目は魔晶装備の防具が追加されてから作成
	;-----------------------------

ELSE

	CALLFORM 装備箇所_{L_ID}
	L_POINT = RESULT

	;- CHK
	CALLFORM 装備条件_{L_ID},ARG:1
	SIF RESULT != 1
		RETURN

	;- 装備中の場合は水色
	SIF EQUIP:(ARG:1):(GET_EQUIP(L_POINT)) == L_ID
		SETCOLOR 0x66FFFF

	PRINTFORM [{L_ID,4}] %ITEMNAME_E(L_ID),20,LEFT% × {ITEM:L_ID,3}
	RESETCOLOR
	PRINTFORM |

	FOR L_J,0,FLAG:戦闘能力数
		L_TMP = STRLENS(GET_BATTLESTATUS(L_J))
		CALLFORM 戦闘能力修正_{L_ID},L_J,ARG:1
		IF RESULT == 0
			SETCOLOR 0x404040
		ELSEIF RESULT < 0
			SETCOLOR 0xEE1010
		ENDIF
		PRINTFORM {RESULT,L_TMP}
		RESETCOLOR
		PRINTFORM |
	NEXT

	FOR L_J,0,FLAG:相性数
		CALLFORM 防御相性_{L_ID},L_J,ARG:1
		IF RESULT != 100
			SIF L_HIT
				PRINT 、
			L_HIT ++

			;表示打ち切り
			IF L_HIT > 2
				PRINT (More)
				BREAK
			ENDIF
			CALL SET_AISYOU_COLOR, RESULT
			;耐性の場合は白表示
			SIF 0 < RESULT && RESULT < 100
				RESETCOLOR
			PRINTFORM %GET_TYPE_E(L_J),5,LEFT%
			SELECTCASE RESULT
				CASE 0
				CASE 999
					PRINT Refl
				CASEELSE
					PRINTFORM  %\@(RESULT < 0) ? Dr # 　\@%{ABS(RESULT),3}
			ENDSELECT
			RESETCOLOR
		ENDIF
	NEXT

ENDIF
PRINTL



;---- EDIT 003 ADD END ---------------------------



@装備変更,ARG,ARG:1,ARG:2

SIF EQUIP:ARG:(GET_EQUIP(ARG:1)) >= 2000 && EQUIP:ARG:(GET_EQUIP(ARG:1)) != [[アイテム:Innate Weapon]]
	ITEM:(EQUIP:ARG:(GET_EQUIP(ARG:1))) += 1
SIF ARG:2 != [[アイテム:Innate Weapon]]
	ITEM:(ARG:2) -= 1
SIF ARG:2 == 0 && GET_EQUIP(ARG:1) == "剣" && 生得武器所持(ARG)
	ARG:2 = [[アイテム:Innate Weapon]]
EQUIP:ARG:(GET_EQUIP(ARG:1)) = ARG:2
CALL SYNC_STATUS,ARG


@装備説明,ARG,ARG:1 = -1, ARG:2 = -1
IF 魔晶装備(ARG)
	;---- EDIT 004 DEL START -------------------------
;	FOR LOCAL,0,7
;		IF ARG < 2500 + LOCAL*500
;			LOCAL:10 = LOCAL
;			BREAK
;		ENDIF
;	NEXT
;	LOCAL:2 = ARG - 2450 - LOCAL:10*450
	;---- EDIT 004 DEL END   -------------------------
	;---- EDIT 004 ADD START -------------------------
	;- EQ:アイテムID取得(魔晶装備)
	LOCAL:2 = EQ_GETID_魔晶装備(ARG)
	;---- EDIT 004 ADD END   -------------------------

	;---- EDIT 004 MOD START -------------------------
;	PRINTFORML %STR:(DC:(LOCAL:2):49+100)%
	CUSTOMDRAWLINE =
	PRINTL
	PRINTFORML %EQ_GETNAME_魔晶装備(LOCAL:2)%
	;---- EDIT 004 MOD END   -------------------------
	TRYCALLFORM 装備解説_魔晶装備,LOCAL:2
	PRINTL
	FOR LOCAL,1,FLAG:基本能力数
		IF GET_BASESTATUS_NUM("速さ") > LOCAL
			PRINTFORM %GET_BASESTATUS_E(LOCAL),4,LEFT% 
		ELSE
			PRINTFORM %GET_BASESTATUS_E(LOCAL),8,LEFT% 
		ENDIF
		CALLFORM 基本能力修正_魔晶装備,LOCAL,LOCAL:2
		IF RESULT == 0
			SETCOLOR 0x404040
		ELSEIF RESULT < 0
			SETCOLOR 0xEE1010
		ENDIF
		PRINTFORM {RESULT,4}　
		RESETCOLOR
		PRINTFORM
	NEXT
	PRINTL 
	FOR LOCAL,0,FLAG:戦闘能力数
		PRINTFORM %GET_BATTLESTATUS_E(LOCAL),4,LEFT% 
		CALLFORM 戦闘能力修正_魔晶装備,LOCAL,LOCAL:2
		IF RESULT == 0
			SETCOLOR 0x404040
		ELSEIF RESULT < 0
			SETCOLOR 0xEE1010
		ENDIF
		PRINTFORM {RESULT,4}　
		RESETCOLOR
	NEXT
	PRINTFORML %LOCALS%
	CALLFORM 装備箇所_魔晶装備,LOCAL:2
	IF RESULT == 0 || RESULT == 1
		PRINTL
		IF RESULT == 1
			CALLFORM 銃攻撃_魔晶装備,LOCAL:2
			PRINTFORM 　Attack：{RESULT}　　
			CALLFORM 銃命中_魔晶装備,LOCAL:2
			PRINTFORM Hit：{RESULT}　　
		ENDIF
		PRINT Attack count:
		CALLFORM 最低攻撃回数_魔晶装備,LOCAL:2
		PRINTFORM {RESULT}-
		CALLFORM 最大攻撃回数_魔晶装備,LOCAL:2
		PRINTFORM {RESULT}
		PRINT 　　　Element:
		CALLFORM 攻撃相性_魔晶装備,LOCAL:2
		PRINTFORM %GET_TYPE_E(RESULT),4%
		PRINT 　　　Range：
		CALLFORM 射程_魔晶装備,LOCAL:2
		;---- EDIT 004 DEL START -------------------------
;		IF RESULT == 1
;			PRINT Ｓ
;		ELSEIF RESULT == 2
;			PRINT Ｍ
;		ELSEIF RESULT == 3
;			PRINT Ｌ
;		ENDIF
		;---- EDIT 004 DEL END   -------------------------
		;---- EDIT 004 ADD START -------------------------
		PRINTFORM %EQ_射程名称(RESULT)%
		;---- EDIT 004 ADD END   -------------------------
		CALLFORM 攻撃範囲_魔晶装備,LOCAL:2
		PRINTFORM 　　　Target:%GET_SPHERE(RESULT)%
		PRINTL
		PRINTL
	ENDIF
	SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
		SETCOLOR COLOR("gray")
	PRINTL 防御相性
	FOR LOCAL,0,FLAG:相性数
		SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
			SETCOLOR COLOR("gray")
		PRINTFORM %GET_TYPE_E(LOCAL),5,LEFT%
		CALLFORM 防御相性_魔晶装備,LOCAL,LOCAL:2
		CALL SET_AISYOU_COLOR, RESULT
		SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
			SETCOLOR COLOR("gray")
		IF RESULT == 999
			PRINT 　Refl　
		ELSE
			PRINTFORM  %\@(RESULT < 0) ? Dr # 　\@%{ABS(RESULT),3}　
		ENDIF
		RESETCOLOR
		IF LOCAL%6 == 5
			CALL SHOW_AISYOU_COLOR_LIST, LOCAL/6 * 2
			PRINT 　
			CALL SHOW_AISYOU_COLOR_LIST, LOCAL/6 * 2 + 1
			PRINTL 
		ENDIF
	NEXT
	PRINTL
	TRYCCALLFORM バステ耐性_魔晶装備,0,LOCAL:2
		SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
			SETCOLOR COLOR("gray")
		PRINTL Status modifiers:
		;バッドステータス：
		LOCAL:3 = 0
		FOR LOCAL,0,FLAG:ステート数
			TRYCCALLFORM バステ耐性_魔晶装備,LOCAL,LOCAL:2
				SIF RESULT == 0
					CONTINUE
				PRINTFORMLC %GET_STATE(LOCAL),8,LEFT% {100-RESULT}％　
				SIF (LOCAL:3++ % PRINTCPERLINE()) == PRINTCPERLINE() - 1
					PRINTL 
			CATCH
			ENDCATCH
		NEXT
		RESETCOLOR
		PRINTL
	CATCH
	ENDCATCH

	TRYCALLFORM 装備条件説明_{ARG}
	DRAWLINE
ELSE
	PRINTFORML \@RANGE(ARG, 8000, 8500) ? %PLUGINNAME_E(ARG)%#%ITEMNAME_E(ARG)%\@
	TRYCALLFORM 装備解説_{ARG}
	FOR LOCAL,1,FLAG:基本能力数
		IF GET_BASESTATUS_NUM("速さ") > LOCAL
			PRINTFORM %GET_BASESTATUS_E(LOCAL),4,LEFT% 
		ELSE
			PRINTFORM %GET_BASESTATUS_E(LOCAL),8,LEFT% 
		ENDIF
		CALLFORM 基本能力修正_{ARG},LOCAL,ARG:1
		IF RESULT == 0
			SETCOLOR 0x404040
		ELSEIF RESULT < 0
			SETCOLOR 0xEE1010
		ENDIF
		PRINTFORM {RESULT,4}　
		RESETCOLOR
	NEXT
	PRINTL
	FOR LOCAL,0,FLAG:戦闘能力数
		PRINTFORM %GET_BATTLESTATUS_E(LOCAL),4,LEFT% 
		CALLFORM 戦闘能力修正_{ARG},LOCAL,ARG:1
		IF RESULT == 0
			SETCOLOR 0x404040
		ELSEIF RESULT < 0
			SETCOLOR 0xEE1010
		ENDIF
		PRINTFORM {RESULT,4}　
		RESETCOLOR
	NEXT

	PRINTL
	CALLFORM 装備箇所_{ARG}
	IF RESULT == 0 || RESULT == 1
		PRINTL
		IF RESULT == 1
			CALLFORM 銃攻撃_{ARG},ARG:1
			PRINTFORM 　Attack：{RESULT}　　
			CALLFORM 銃命中_{ARG},ARG:1
			PRINTFORM Hit：{RESULT}　　
		ENDIF
		PRINT Attack count:
		CALLFORM 最低攻撃回数_{ARG},ARG:1
		PRINTFORM {RESULT}-
		CALLFORM 最大攻撃回数_{ARG},ARG:1
		PRINTFORM {RESULT}
		PRINT 　　　Element:
		CALLFORM 攻撃相性_{ARG},ARG:1
		PRINTFORM %GET_TYPE_E(RESULT),4%
		PRINT 　　　Range：
		CALLFORM 射程_{ARG},ARG:1
		;---- EDIT 004 DEL START -------------------------
;		IF RESULT == 1
;			PRINT Ｓ
;		ELSEIF RESULT == 2
;			PRINT Ｍ
;		ELSEIF RESULT == 3
;			PRINT Ｌ
;		ENDIF
		;---- EDIT 004 DEL END   -------------------------
		;---- EDIT 004 ADD START -------------------------
		PRINTFORM %EQ_射程名称(RESULT)%
		;---- EDIT 004 ADD END   -------------------------
		CALLFORM 攻撃範囲_{ARG},ARG:1
		PRINTFORM 　　　Target:%GET_SPHERE(RESULT)%
		RESULT:1 = RESULT
		RESULT = 0
		TRYCALLFORM GUN_RANDOMTARGET_{ARG}
		SIF RESULT == 1
			PRINTFORM （Random）
			;ランダム
		PRINTL
		PRINTL
	ENDIF
	SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
		SETCOLOR COLOR("gray")
	PRINTL Elemental defense:
	FOR LOCAL,0,FLAG:相性数
		SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
			SETCOLOR COLOR("gray")
		PRINTFORM %GET_TYPE_E(LOCAL),5,LEFT%
		CALLFORM 防御相性_{ARG},LOCAL,ARG:1
		CALL SET_AISYOU_COLOR, RESULT
		SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
			SETCOLOR COLOR("gray")
		IF RESULT == 999
			PRINT 　Refl　
		ELSE
			PRINTFORM  %\@(RESULT < 0) ? Dr # 　\@%{ABS(RESULT),3}　
		ENDIF
		RESETCOLOR
		IF LOCAL%6 == 5
			CALL SHOW_AISYOU_COLOR_LIST, LOCAL/6 * 2
			PRINT 　
			CALL SHOW_AISYOU_COLOR_LIST, LOCAL/6 * 2 + 1
			PRINTL 
		ENDIF
	NEXT
	PRINTL
	TRYCCALLFORM バステ耐性_{ARG},0,ARG:1
		SIF ARG:1 >= 0 && (TALENT:(ARG:1):ペルソナ使い || ABL:(ARG:1):種族 == 36)
			SETCOLOR COLOR("gray")
		PRINTL Status modifiers:
		;バッドステータス：
		LOCAL:3 = 0
		FOR LOCAL,1,FLAG:ステート数
			TRYCCALLFORM バステ耐性_{ARG},LOCAL,ARG:1
				SIF RESULT == 0
					CONTINUE
				PRINTFORMLC %GET_STATE(LOCAL),8,LEFT% {100+RESULT}％　
				SIF (LOCAL:3++ % PRINTCPERLINE()) == PRINTCPERLINE() - 1
					PRINTL 
			CATCH
			ENDCATCH
		NEXT
		RESETCOLOR
		PRINTL
	CATCH
	ENDCATCH
	TRYCCALLFORM 銃タイプ_{ARG}
		PRINT Type：
		SELECTCASE RESULT
			CASE 1
				PRINT Pistol
				;ハンドガン
			CASE 2
				PRINT Shotgun
				;ショットガン
			CASE 3
				PRINT Machine gun
				;マシンガン
			CASE 4
				PRINT Rifle
				;ライフル
			CASE 5
				PRINT Other Gun
				;その他の
		ENDSELECT
		CALLFORM 特殊弾倉_{ARG},ARG:1
		PRINTFORML 　　Special Magazine:{RESULT}
	CATCH
	ENDCATCH
	
	TRYCCALLFORM 装備追加スキル_{ARG},0,ARG:1
		IF RESULT > 0
			CALLFORM SKILL_NAME_{RESULT},ARG
			PRINTL 
			PRINTFORM  Additional skills：[%RESULTS%]
			;追加スキル
		ENDIF
		TRYCCALLFORM 装備追加スキル_{ARG},1,ARG:1
			IF RESULT > 0
				CALLFORM SKILL_NAME_{RESULT},ARG
				PRINTFORM  [%RESULTS%]
			ENDIF
			TRYCCALLFORM 装備追加スキル_{ARG},2,ARG:1
				IF RESULT > 0
					CALLFORM SKILL_NAME_{RESULT},ARG
					PRINTFORM  [%RESULTS%]
				ENDIF
			CATCH
			ENDCATCH
		CATCH
		ENDCATCH
		PRINTL 
	CATCH
	ENDCATCH
	;---- EDIT 002 ADD START -------------------------
	IF RANGE(ARG, 8000, 8500)
		CALLFORM 必要レベル_{ARG}
		PRINTFORM Required lvl: {RESULT}　
		;必要レベル
		;I'm amazed! With one exception, this really is printed only here! If you can do that, why so much redundancy in other cases japs?
		CALLFORM 増加CP_{ARG}
		PRINTFORML 　　　　ＣＰcorrection: \@RESULT < 0 ?‐#＋\@{ABS(RESULT)}\%
	ENDIF
	;---- EDIT 002 ADD END   -------------------------
	TRYCALLFORM 装備条件説明_{ARG}
	DRAWLINE
ENDIF

@装備変更時ステータス更新 , ARG , ARGS
#DIM 元戦闘能力 , 8
IF ARGS == "消去"
	FOR LOCAL , 0 , 18
		SIF LOCAL < 8
			CALLF 装備変更時ステータス変化値(LOCAL , 1 , 0)
	NEXT
	VARSET LOCAL , 0
	RETURN 0
ENDIF

FOR LOCAL , 0 , FLAG:戦闘能力数 + 2
	元戦闘能力:LOCAL = MAXBASE:ARG:GET_BATTLESTATUS(LOCAL)
NEXT
;装備を変更予定の装備と入れ替え
FOR LOCAL , 0 , 7
	SIF 変更予定装備:LOCAL != -1
		SWAP EQUIP:ARG:GET_EQUIP(LOCAL) , 変更予定装備:LOCAL
NEXT
CALL SYNC_STATUS , ARG
FOR LOCAL , 0 , FLAG:戦闘能力数 + 2
	CALLF 装備変更時ステータス変化値(LOCAL, 1, MAXBASE:ARG:GET_BATTLESTATUS(LOCAL) - 元戦闘能力:LOCAL)
NEXT
FOR LOCAL,0, FLAG:相性数
	CALLF 装備変更時相性(LOCAL, 1, MAXBASE:ARG:GET_TYPE(LOCAL))
NEXT
FOR LOCAL , 0 , 7
	SIF 変更予定装備:LOCAL != -1
		SWAP EQUIP:ARG:GET_EQUIP(LOCAL) , 変更予定装備:LOCAL
NEXT
CALL SYNC_STATUS , ARG
RETURN 1

@装備変更時ステータス変化値, ARG , ARG:1 , ARG:2
#FUNCTION
#LOCALSIZE 8
IF ARG:1
	LOCAL:ARG = ARG:2
ELSE
	RETURNF LOCAL:ARG
ENDIF
@装備変更時相性, ARG, ARG:1, ARG:2
#FUNCTION
#LOCALSIZE 18
IF ARG:1
	LOCAL:ARG = ARG:2
ELSE
	RETURNF LOCAL:ARG
ENDIF
;============================================================================
;特殊弾の装備
;============================================================================
@EQUIP_SPECIAL_BULLET,ARG
DRAWLINE

CALLFORM 特殊弾倉_{EQUIP:ARG:銃},ARG
LOCAL:1 = RESULT 
FOR LOCAL,1,9
	SIF LOCAL == 5
		PRINTL
	SIF LOCAL:1 < LOCAL
		BREAK
	LOCALS = 特殊弾{LOCAL}
	IF EQUIP:ARG:LOCALS == 0
		
		PRINTFORM [{LOCAL}] 
		SETCOLOR 0x404040
		PRINT ＥＭＰＴＹ　　　 
		RESETCOLOR
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL}] %ITEMNAME_E(EQUIP:ARG:LOCALS),16,LEFT% 
	
NEXT
PRINTL
DRAWLINE
PRINTL [0]Return

$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT < 1 || RESULT > LOCAL:1
	GOTO INPUT_LOOP
ENDIF
LOCALS:2 = 特殊弾{RESULT}

$PRINT_BLIST
CALL PRINT_EQUIPMENTLIST,7,ARG

PRINTL [0] Remove
PRINTL [1000]Cancel


$INPUT_LOOP3
INPUT

IF RESULT == 1000
	RESTART
ELSEIF RESULT == 0
	EQUIP:ARG:(LOCALS:2) = 0
	RESTART
ELSEIF RESULT < 5500 || RESULT > 5999 || ITEM:RESULT < 1
	GOTO INPUT_LOOP3
ENDIF
LOCAL:12 = RESULT

TRYCCALLFORM 装備条件_{LOCAL:12},ARG
CATCH
	GOTO INPUT_LOOP3
ENDCATCH

CALLFORM 装備解説_{LOCAL:12}

PRINT Is this ok?　
;よろしいですか？　
CALL INPUT_YN,"Yes","No",2

SELECTCASE RESULT
	CASE 0

	CASE 1
		GOTO PRINT_BLIST
ENDSELECT

EQUIP:ARG:(LOCALS:2) = LOCAL:12

RESTART

;---- EDIT 002 ADD START -------------------------
;=================================================================
;プラグインソフト
;=================================================================
@EQUIP_PLUGIN_SOFT
#LOCALSIZE 4
#DIM LINE, 3
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT-LINE
CALL INPUT_CHARA_LIST, "Please selected a character to change plugin-software", "LIST_PLUGIN_SOFT"
SIF RESULT == 1000
	RETURN 0
LOCAL:1 = FINDELEMENT(Q, RESULT)
LINE:1 = LINECOUNT
$PRINT_PLIST
CLEARLINE LINECOUNT-LINE:1
LOCAL = Q:(LOCAL:1)
DRAWLINE
PRINTFORML %NAME:LOCAL%
DRAWLINE
CALL PRINT_EQUIPLIST, LOCAL
DRAWLINE
PRINTL [300] Change Plugin [301] Remove plugin
DRAWLINE
PRINTFORMLC \@(LOCAL:1 == 0) ? %" " * 16% # [1001]Previous character \@
PRINTLC [100]Cancel [200] Exit　　　 
PRINTFORMLC \@(Q:(LOCAL:1+1) == -1) ? %" " * 16% # [1003]Next character\@
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 100
		GOTO START
	CASE 200
		RETURN 0
	CASE 300
		CALL CHANGE_PLUGIN_SOFT, LOCAL
		GOTO PRINT_PLIST
	CASE 301
		EQUIP:LOCAL:アクセサリ = 0
		CALL SYNC_STATUS, LOCAL
		GOTO PRINT_PLIST
	CASE 1001
		IF LOCAL:1 > 0
			LOCAL:1--
			GOTO PRINT_PLIST
		ENDIF
	CASE 1003
		IF Q:(LOCAL:1+1) >= 0
			LOCAL:1++
			GOTO PRINT_PLIST
		ENDIF
	CASEELSE
		CLEARLINE 1
		GOTO INPUT_LOOP
ENDSELECT
@CHANGE_PLUGIN_SOFT, ARG
#DIM LINE, 1
#LOCALSIZE 2
LINE = LINECOUNT
$CHOICE
CLEARLINE LINECOUNT-LINE
DRAWLINE
PRINTL Change Plugin
LOCAL:1 = 0
;ソフト一覧
FOR LOCAL, 8000, 8500
	SIF !ITEM:(LOCAL)
		CONTINUE
	CALLFORM 必要レベル_{LOCAL}
	SIF RESULT > BASE:ARG:LV
		SETCOLOR COLOR("gray")
	CALLFORM 装備条件_{LOCAL}, ARG
	SIF !RESULT
		SETCOLOR COLOR("gray")
	PRINTFORM [{LOCAL-8000,3}] %PLUGINNAME_E(LOCAL), 24, LEFT%
	SIF LOCAL:1 % 4 == 3
		PRINTL 
	LOCAL:1++
	RESETCOLOR
NEXT
SIF LINEISEMPTY() == 0
	PRINTL 
PRINTL [999]Cancel
$INPUT_LOOP2
INPUT
SIF RESULT == 999
	RETURN 0
LOCAL = RESULT+8000
IF !RANGE(LOCAL, 8000, 8500) || !ITEM:(LOCAL)
	GOTO INPUT_LOOP2
ENDIF
IF EQUIP:ARG:アクセサリ > 0
	CALL 装備説明, EQUIP:ARG:アクセサリ, ARG
	PRINTL
	CUSTOMDRAWLINE ↓
	PRINTL
ENDIF
CALL 装備説明, LOCAL, ARG
CALLFORM 必要レベル_{LOCAL}
IF RESULT > BASE:ARG:LV
	PRINTFORMW The level requirement for this plugin isn't met.
	GOTO CHOICE
ENDIF
CALLFORM 装備条件_{LOCAL}, ARG
IF !RESULT
	PRINTFORMW The usage conditions for this plugin aren't met.
	GOTO CHOICE
ENDIF
SIF !RESULT
	SETCOLOR COLOR("gray")
PRINTL Use this plugin?
CALL INPUT_YN, "Yes", "No"
SIF RESULT == 1
	GOTO CHOICE
PRINTFORMW %PLUGINNAME_E(LOCAL)% has been installed on %NAME:ARG%.
EQUIP:ARG:アクセサリ = LOCAL
CALL SYNC_STATUS, ARG
RETURN 1

@LIST_PLUGIN_SOFT
SIF !GET_DEVIL(COUNT)
	RETURN 0
SIF CFLAG:COUNT:この場に居ないフラグ == 1
	RETURN 0
SIF CFLAG:COUNT:フィルタリングフラグ
	RETURN 0
RETURN 1
;---- EDIT 002 ADD END   -------------------------

;============================================================================
;装備品の表示(ステータス画面)
;ARGが偽の時、装備変更出来ない
;============================================================================
@PRINT_EQUIP, ARG = 0
FOR LOCAL,0,7
	SIF FLAG:カスタムゲーム画面
		PRINT 　
	PRINTFORM \@ARG ? [{50+LOCAL}]#\@%"[" + GET_EQUIP_E(LOCAL) + "]",14,LEFT%
	IF (EQUIP:U:(GET_EQUIP(LOCAL))) == 0
		SETCOLOR 0x404040
		PRINTFORML ＥＭＰＴＹ
		RESETCOLOR
	ELSE
		IF 魔晶装備(EQUIP:U:(GET_EQUIP(LOCAL)))
			;---- EDIT 004 MOD START -------------------------
;			PRINTFORML %STR:(DC:(EQUIP:U:(GET_EQUIP(LOCAL)) - 2450 - LOCAL*450):49+100)%
			PRINTFORML %EQ_GETNAME_魔晶装備_CHARA(U, LOCAL)%
			;---- EDIT 004 MOD END   -------------------------
		ELSE
			PRINTFORML %ITEMNAME_E(EQUIP:U:(GET_EQUIP(LOCAL)))%
		ENDIF
	ENDIF
NEXT
IF ARG
	IF FLAG:カスタムゲーム画面
		IF TALENT:U:ガンスリンガー && EQUIP:U:銃
			PRINTL 　[57][Special Bullet]
		ELSE
			PRINTL 　[--][----------]
		ENDIF
	ELSE
		IF TALENT:U:ガンスリンガー && EQUIP:U:銃
			PRINTL [57][Special Bullet]
		ELSE
			PRINTL [--][----------]
		ENDIF
	ENDIF
ENDIF
DRAWLINE
@PRINT_PLUGIN
PRINTFORM \@FLAG:ショップコマンド == [[ショップ:セットアップ]]?[50]#\@[PLUG-IN]　
IF EQUIP:U:アクセサリ
	PRINTFORML [%PLUGINNAME_E(EQUIP:U:アクセサリ)%]
ELSE
	PRINTL [----------]
ENDIF
DRAWLINE
;============================================================================
;戦闘開始時効果
;============================================================================
@戦闘開始時効果
FOR LOCAL,0,CHARANUM
	FOR LOCAL:1,0,7
		IF EQUIP:LOCAL:GET_EQUIP(LOCAL:1) > 0
			TRYCCALLFORM 魔昌装備_{EQUIP:LOCAL:GET_EQUIP(LOCAL:1)}

			CATCH
				TRYCALLFORM 戦闘開始時効果_{EQUIP:LOCAL:GET_EQUIP(LOCAL:1)},LOCAL
			ENDCATCH
		ENDIF
	NEXT
NEXT

;============================================================================
;全装備解除
;ARG:0に対象者の登録番号(間違うとやばそうなので、省略時はエラー出す)
;============================================================================
@全装備解除(ARG:0 = -1)
IF ARG:0<0
	PRINTL ERROR キャラ登録番号指定なしで全装備解除をしようとしています！
	RETURN
ENDIF

;装備があれば解除して、アイテム欄の分を１個増やす
REPEAT 8
	;Innate Weaponの場合は飛ばす
	SIF COUNT == 0 && EQUIP:ARG:剣 == [[アイテム:Innate Weapon]]
		CONTINUE
	SIF (EQUIP:(ARG:0):GET_EQUIP(COUNT)) != 0
		ITEM:(EQUIP:(ARG:0):GET_EQUIP(COUNT)) += 1
	EQUIP:(ARG:0):GET_EQUIP(COUNT) = 0	
REND

;特殊弾の解除。装備してもアイテムが減るわけではないので、解除の時も増やさない
REPEAT 8
	LOCALS = 特殊弾{COUNT + 1}
	EQUIP:(ARG:0):LOCALS = 0
REND

SIF 生得武器所持(ARG)
	EQUIP:ARG:剣 = [[アイテム:Innate Weapon]]
;ステータスに反映?
CALL SYNC_STATUS,ARG:0
RETURN

;============================================================================
;生得武器所持
;ARGのキャラが生得武器所持の条件を満たしているかどうか判定
;============================================================================
;---- EDIT 006 MOD START -------------------------
@生得武器所持,ARG
#FUNCTION
SIF NO:ARG == [[キャラ:ユーリア]] || NO:ARG == [[キャラ:エレジー]] ||  NO:ARG == [[キャラ:高城ゼット]] || (ABL:ARG:種族 == 36 &&( TALENT:ARG:鉤爪 || TALENT:ARG:羽 ||  TALENT:ARG:鉤足 ||  TALENT:ARG:尾 ||  TALENT:ARG:嘴 ||  TALENT:ARG:触手 ||  TALENT:ARG:獣 ||  TALENT:ARG:鳥 ||  TALENT:ARG:爬虫類 ||  TALENT:ARG:魚))
	RETURNF 1
RETURNF 0
;---- EDIT 006 MOD END   -------------------------

;============================================================================
;@EQUIP_PROPERTY
;装備品の性能を取得する
;魔晶武器等による呼び出す関数の違いなどもここにまとめて記述する
;============================================================================
@EQUIP_PROPERTY , ARG , ARGS , ARG:1 , ARG:2 = -1
RESULTS =
RESULT = 0
IF 魔晶装備(ARG)
	ARG = EQ_GETID_魔晶装備(ARG)
	SELECTCASE ARGS
		CASE "基本能力修正" , "戦闘能力修正" , "銃攻撃" , "銃命中" , "防御相性" , "バステ耐性"
			TRYCALLFORM %ARGS%_魔晶装備 , ARG , ARG:1
		CASEELSE
	ENDSELECT
ELSE
	
	SELECTCASE ARGS
		CASE "装備箇所" , "購入条件" , "装備条件説明" , "必要レベル"
			TRYCALLFORM %ARGS%_{ARG}
		CASE "基本能力修正" , "戦闘能力修正" , "防御相性" , "バステ耐性"
			TRYCALLFORM %ARGS%_{ARG} , ARG:1 , ARG:2
			
			SIF ARGS == "戦闘能力修正"
				CALL 装備強化_展開, ARG:2, ARG, ARGS ,GET_BATTLESTATUS(ARG:1), RESULT
			SIF ARGS == "防御相性"
				CALL 装備強化_展開, ARG:2, ARG, ARGS ,GET_TYPE(ARG:1), RESULT
		CASEELSE
			TRYCCALLFORM %ARGS%_{ARG} , ARG:2
			CATCH
				RETURN 0
			ENDCATCH
			CALL 装備強化_展開, ARG:2, ARG, "戦闘能力修正" ,ARGS, RESULT
	ENDSELECT
ENDIF
RETURN RESULT

;============================================================================
;@EQUIP_SORT , ARG = -1(装備するキャラ) , ARG:1 (ソート開始位置(素手などを省くため))
;あらかじめ設定済みのソート条件に応じて装備品のリストをソートする
;アイテムリストに装備品のリストを作成した上で実行する事
;============================================================================
@EQUIP_SORT , ARG = -1 , ARG:1
SIF ソート条件 == "" || ソート条件 == "アイテム番号"
	ソート条件 = Item No.
	;アイテム番号
FOR LOCAL:2 , 2 , -1 , -1
	;条件によって、アイテムリストの数値を色々変更
	IF ソート条件:(LOCAL:2) != ""
		FOR LOCAL , ARG:1 , FINDELEMENT(アイテムリスト , -1)
			IF ソート条件:(LOCAL:2) == "Item No."
			ELSEIF ソート条件:(LOCAL:2) == "Total Stats" || RANGE(GET_BATTLESTATUS_NUM_E(ソート条件:(LOCAL:2)) , 1 , 6 )
			;合計能力値
				IF ソート条件:(LOCAL:2) == "Total Stats"
					LOCAL:3 = 1 , 9
				ELSE
					LOCAL:3 = GET_BATTLESTATUS_NUM_E(ソート条件:(LOCAL:2))
					LOCAL:4 = LOCAL:3 + 1
					;Gunなら命中と攻撃を変換
					IF GROUPMATCH(ソート条件:(LOCAL:2) , "Attack" , "Hit")
					;攻撃	命中
						TRYCALLFORM 装備箇所_{アイテムリスト:LOCAL % 10000}
						IF RESULT == GET_EQUIPNUM("銃")
							LOCAL:3 += 6
							LOCAL:4 += 6
						ENDIF
					ENDIF
				ENDIF
				FOR LOCAL:1 , LOCAL:3 , LOCAL:4
					;CALL EQUIP_PROPERTY , アイテムリスト:LOCAL % 10000 , "戦闘能力修正" , LOCAL:1 , ARG
					IF GET_BATTLESTATUS(LOCAL:1) == "銃攻撃"
						CALL EQUIP_PROPERTY , MODULO(アイテムリスト:LOCAL , 10000) , "銃攻撃" , LOCAL:1 , ARG
					ELSEIF GET_BATTLESTATUS(LOCAL:1) == "銃命中"
						CALL EQUIP_PROPERTY , MODULO(アイテムリスト:LOCAL , 10000) , "銃命中" , LOCAL:1 , ARG
					ELSE
						CALL EQUIP_PROPERTY , MODULO(アイテムリスト:LOCAL , 10000) , "戦闘能力修正" , LOCAL:1 , ARG
					ENDIF
					アイテムリスト:LOCAL += RESULT * 10000
				NEXT
				
			ELSEIF RANGE(GET_TYPE_NUM_E(ソート条件:(LOCAL:2)) , 0 , FLAG:相性数 )
				CALLFORM EQUIP_PROPERTY , MODULO(アイテムリスト:LOCAL , 10000) , "防御相性" , GET_TYPE_NUM_E(ソート条件:(LOCAL:2)) , ARG
				SELECTCASE RESULT
					CASE 999
						アイテムリスト:LOCAL += 999 * 10000
					CASEELSE
						アイテムリスト:LOCAL += (500 - RESULT) * 10000
				ENDSELECT
			ELSEIF ソート条件:(LOCAL:2) == "Price"
			;価格
				アイテムリスト:LOCAL += ITEMPRICE:(アイテムリスト:LOCAL) * 10000
			ELSEIF ソート条件:(LOCAL:2) == "Req. Level"
			;必要レベル
				CALL EQUIP_PROPERTY , MODULO(アイテムリスト:LOCAL , 10000) , "必要レベル" , , ARG
				アイテムリスト:LOCAL += RESULT * 10000
				
			ENDIF
		NEXT
		SELECTCASE ソート条件:(LOCAL:2)
			CASE "Item No."
				ARRAYSORT アイテムリスト , FORWARD , ARG:1 , MAX(0 , FINDELEMENT(アイテムリスト , -1) - ARG:1)
			CASEELSE
				ARRAYSORT アイテムリスト , BACK , ARG:1 , MAX(0 , FINDELEMENT(アイテムリスト , -1) - ARG:1)
		ENDSELECT
		FOR LOCAL , 0 , FINDELEMENT(アイテムリスト , -1)
			アイテムリスト:LOCAL = MODULO(アイテムリスト:LOCAL , 10000)
		NEXT
	ENDIF
NEXT

;============================================================================
;@CHANGE_CONDITION_EQUIP_SORT , ARG = -1(装備するキャラ) , ARG:1 (ソート開始位置(素手などを省くため))
;EQUIP_SORT内で使用するソート条件を変更する関数
;============================================================================
@CHANGE_CONDITION_EQUIP_SORT
#DIM LINE

CUSTOMDRAWLINE =
PRINTL □Change Sort Criteria□
PRINTL
PRINTL
LINE = LINECOUNT
LOCAL:1 = 0
LOCALS = Not set_Item No._Price_Req. Level_射程（未）__Total Stats_Attack_Hit_Defense_Evasion_Magic Power_Magic Effect_
LOCALS += "Slash_Gun_Strike_Skill_Fire_Ice_Elect_Force_Nerve_Mind_Light_Dark_Earth_Water_Wind_Grav_Nuclr_Almig_"
$PRINT
FOR LOCAL , 0 , 3
	SIF LOCAL:1 == LOCAL
		SETCOLOR COLOR("パ緑")
	PRINTBUTTON @"　[Condition{LOCAL+1}]%ソート条件:LOCAL,16,LEFT%　" , @"Condition{LOCAL+1}"
	;条件
	RESETCOLOR
NEXT
PRINTL
PRINTL
FOR LOCAL , 0 , STRCOUNT(LOCALS , "_")
	LOCALS:1 = [%AUTO_SPLIT(LOCALS , "_" , LOCAL)%]
	PRINTBUTTON @"　%LOCALS:1 , 18 , LEFT%　" , AUTO_SPLIT(LOCALS , "_" , LOCAL)
	SIF LOCAL % 6 == 5
		PRINTL
NEXT
PRINTL

CUSTOMDRAWLINE =
PRINTL [100]Return
$INPUTS_LOOP
INPUTS
IF RESULTS == "100"
	RETURN 0
ELSEIF GROUPMATCH(RESULTS , "Condition1" , "Condition2" , "Condition3")
	LOCAL:1 = TOINT(SUBSTRINGU(RESULTS, 9)) - 1
	;, 2
ELSEIF AUTO_SPLIT_NUM(LOCALS , "_" , RESULTS) != -1
	ソート条件:(LOCAL:1) = %RESULTS%
ELSE
	GOTO INPUTS_LOOP
ENDIF
CLEARLINE LINECOUNT - LINE
GOTO PRINT

;============================================================================
;============================================================================
@MODULO , ARG , ARG:1
#FUNCTION
SIF ARG % ARG:1 < 0
	RETURNF ARG % ARG:1 + ARG:1
RETURNF ARG % ARG:1
