;======================================================
;装備品のデータ
;Rebellion
;Hinokagutsuchiのパクリ。攻撃と魔威高め
;性能の計算は魔晶剣とほぼ同じで、ダンテのステータスの影響を受ける
;======================================================
@基本能力修正_2350,ARG,ARG:1 = -1
#LOCALSIZE 1
#LOCALSSIZE 1
LOCAL = GETCHARA([[キャラ:ダンテ]])
SIF LOCAL < 0
	RETURN 0
SIF ARG:1 < 0
	RETURN 0
SIF ARG:1 == GETCHARA([[キャラ:ダンテ]]) || (FAVORITE_1(LOCAL) == ARG:1 && 陥落(LOCAL))
	RETURN BASE:LOCAL:GET_BASESTATUS(ARG) / 10
RETURN 0
@戦闘能力修正_2350,ARG,ARG:1 = -1
#LOCALSIZE 3
LOCAL = GETCHARA([[キャラ:ダンテ]])
LOCAL:1 = 0
LOCAL:2 = 40
;剣のレベル設定。ダンテが陥落している場合、ダンテのレベルの半分が加算される
SIF ARG:1 >=0 && LOCAL >= 0 && (NO:(ARG:1) == [[キャラ:ダンテ]] || 陥落(LOCAL, ARG:1))
	LOCAL:2 += MIN(BASE:LOCAL:LV / 2, 40)
LOCAL:2 += 5
;レベルによる戦闘能力値
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "攻撃"
		LOCAL:1 = LOCAL:2 * 3/2
	CASE "命中"
		LOCAL:1 = LOCAL:2
	CASE "魔法威力"
		LOCAL:1 = LOCAL:2 * 3/2
	CASE "魔法効果"
		LOCAL:1 = LOCAL:2
ENDSELECT
;STATUSによる能力値　ダンテありはダンテ陥落でステータスによって性能が加算される
IF ARG:1 >= 0 && (NO:(ARG:1) == [[キャラ:ダンテ]] || (LOCAL >=0 && 陥落(LOCAL, ARG:1)))
	SELECTCASE GET_BATTLESTATUS(ARG)
		CASE "攻撃"
			LOCAL:1 +=  BASE:(ARG:1):力*3/2 + BASE:(ARG:1):耐力 / 2
		CASE "命中"
			LOCAL:1 += BASE:(ARG:1):速さ*3/2 + BASE:(ARG:1):運/2
		CASE "魔法威力"
			LOCAL:1 += BASE:(ARG:1):魔力*3/2 + BASE:(ARG:1):知恵 / 2
		CASE "魔法効果"
			LOCAL:1 += BASE:(ARG:1):知恵 + BASE:(ARG:1):運/2 + BASE:(ARG:1):魔力/2
	ENDSELECT
ENDIF
;剣仕様に変更
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "攻撃"
		TIMES LOCAL:1, 1.10
	CASE "命中"
		LOCAL:1 += 10
	CASE "魔法威力"
		TIMES LOCAL:1, 0.90
	CASE "魔法効果"
		TIMES LOCAL:1, 0.40
ENDSELECT
;最低でも40レベル剣の性能は保証される
SELECTCASE GET_BATTLESTATUS(ARG)
	CASE "攻撃"
		RETURN MAX(107, LOCAL:1)
	CASE "命中"
		RETURN MAX(94, LOCAL:1)
	CASE "魔法威力"
		RETURN MAX(85, LOCAL:1)
	CASE "魔法効果"
		RETURN MAX(33, LOCAL:1)
ENDSELECT
@防御相性_2350,ARG,ARG:1 = -1
SELECTCASE ARG
ENDSELECT
RETURN 100

@攻撃相性_2350, ARG = -1
RETURN GET_TYPE_NUM("剣撃")

@最低攻撃回数_2350, ARG = -1
RETURN 1

@最大攻撃回数_2350, ARG = -1
RETURN 1

@射程_2350, ARG = -1
RETURN 1

@攻撃範囲_2350, ARG = -1
RETURN 1

@装備箇所_2350
RETURN GET_EQUIPNUM("剣")

;-----------------------------
;- ショップ
;-----------------------------
@購入条件_2350
RETURN 0

@装備条件_2350,ARG
SIF TALENT:ARG:サクセサー == 0
	RETURN 0
SIF TALENT:ARG:武器 == 0
	RETURN 0
RETURN 1

@装備条件説明_2350
PRINTL Only useable by: Dante
;ダンテ専用

@装備解説_2350
PRINTL This large sword is favorite of Dante, a memento of Father Spada, named "rebellion"
;「反逆」の名を持つ、父スパーダの形見である、ダンテ愛用の大型剣
;父スパーダの形見である「反逆」の名を持つダンテ愛用の長剣。

@引継ぎ可不可_2350
RETURN 1
