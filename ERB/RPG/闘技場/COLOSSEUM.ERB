;コロシアム
;とりあえず今の所戦闘のテスト用
;クリアフラグにダンジョンフラグ:0:Xを使用


@SHOPCOMABLE_102
RESULTS = Enter Colosseum
SIF FLAG:コロシアム参加済み
	RETURN 0
;あなたが臨月だと不可
SIF (TALENT:MASTER:妊娠 && CFLAG:MASTER:出産予定日 - 2 <= DAY)
	RETURN 0
;あなた育児中だと不可
SIF TALENT:MASTER:育児中
	RETURN 0
;パーティに一人以上いるなら
LOCAL = 0
REPEAT CHARANUM
	SIF CFLAG:COUNT:PTフラグ == 2
		LOCAL += 1
REND
IF LOCAL > 0
	RETURN 1
ELSE
	RETURN 0
ENDIF


@SHOW_COLOSSEUM
CUSTOMDRAWLINE =

FOR LOCAL, 0, 999
	TFLAG:LOCAL = 0
	RESULTS = 
	TRYCCALLFORM PLAYABLE_COLOSSEUM_{LOCAL}
		IF RESULT
			TFLAG:LOCAL = 1
			IF RESULTS != ""
				PRINTFORML [{LOCAL}] - %RESULTS%
			ENDIF
		ENDIF
	CATCH
		
	ENDCATCH
NEXT

@SHOP_COM_102
CALL SHOW_COLOSSEUM
PRINTL [1000] Return

DRAWLINE
PRINTL Which match will you do?

$INPUT_LOOP
INPUT
IF RESULT == 1000
	FLAG:ショップコマンド = 0
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= VARSIZE("TFLAG")
	GOTO INPUT_LOOP
ELSEIF TFLAG:RESULT || FLAG:DEBUG 
	FLAG:進行中コロシアム = RESULT
	TRYCALLFORM COLOSSEUM_{RESULT}
	FOR LOCAL,0,CHARANUM
		SIF CFLAG:LOCAL:悪魔変身 == 1
			CALL ACTION_2310 , LOCAL , 0
	NEXT
	FLAG:進行中コロシアム = 0
	FLAG:ショップコマンド = 0
	IF RESULT != -1
		FLAG:コロシアム参加済み = 1
	ENDIF
	IF RESULT == 1
		VARSET TFLAG,-1
		BEGIN TURNEND
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF

VARSET TFLAG,-1
RETURN 0


;====================================================================
;闘技場敗北
;====================================================================
@LOSE_COLOSSEUM
#LOCALSIZE 2
#LOCALSSIZE 1
#DIM LINE
#DIM 元TARGET
LOCALS = 0
FOR LOCAL, 1, 10
	RESULT = 0
	TRYCALLFORM CL_LOSE_ABLE_{LOCAL}
	SIF RESULT
		LOCALS += @"/{LOCAL}"
NEXT
TFLAG:闘技場敗北 = TOINT(RAND_SPLIT(LOCALS, "/"))
;入場者などの描写
CALLFORM CL_LOSE_START_{TFLAG:闘技場敗北}
CUSTOMDRAWLINE ・
元TARGET = TARGET
FOR LOCAL, 1, 7
	TARGET = POS(LOCAL)
	SIF TARGET < 0
		CONTINUE
	CALL KOJO_MESSAGE_LOSE_COLOSSEUM
	PRINTL 
	DRAWLINE
NEXT
TARGET = 元TARGET
CUSTOMDRAWLINE ・
;末路などの描写
CALLFORM CL_LOSE_END_{TFLAG:闘技場敗北}
CALL GAME_OVER

@MESSAGE_LOSE_COLOSSEUM
CALLFORM CL_LOSE_MAIN_{TFLAG:闘技場敗北}




