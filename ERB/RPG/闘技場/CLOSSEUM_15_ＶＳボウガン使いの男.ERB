

;====================
;条件
;====================
@PLAYABLE_COLOSSEUM_15
IF 闘技場フラグ:15:0 == 0 && 依頼フラグ:14:0 
	RESULTS = Rank Ｅ　ＶＳ a man with a bow-gun
	;試合ランクＥ　ＶＳ ボウガン使いの男
	RETURN 1
ENDIF
RETURN 0


;====================
;内容
;====================
@COLOSSEUM_15
FLAG:逃走不可フラグ = 1
FLAG:会話不能フラグ = 1
FLAG:先制不意打ちキャンセル = 1
PRINTL 
PRINTW ＞There was a man with orange hair and a bow-gun.
;ボウガンを持ったオレンジ色の髪の男が居る。
PRINTFORMW ＞Upon seeing %CALLNAME:MASTER%, the young man screamed and backed away until he hit a wall.
;青年は%CALLNAME:MASTER%を見とめると、悲鳴を上げ闘技場から逃げ出そうとした。
PRINTW 「Don't...don't come any closer! Don't eat, don't eat me,please...!」
;「来るな、近づくな…！喰わないで、喰わないで、くれえ…！」
PRINTFORMW ＞The terrified man aimed his bow-gun towards %CALLNAME:MASTER% while trembling.
;完全に怯えきった男は、震えながらも手にしたボウガンを%CALLNAME:MASTER%に向ける。
PRINTW 「You...you are going to eat me? You can eat one of my soldiers instead.」
;「お、お前も俺を喰う気なんだろ？俺の兵隊を喰ったみてえによう」
PRINTW 「Come on, come on, say something...Oh, I won't let you eat me without fighting back!」
;「な、な、何とか言えよ…お、俺は大人しく喰われてなんかやらねえぞ！」
PRINTW ＞The man resolved to fight!
;男が襲い掛かってきた！
PRINTL
PRINTL Avatar Tuner Harley Ｑ appeared!
CUSTOMDRAWLINE =

;喰奴 ハーリーＱ
CALL SET_ENEMY,14,4569,20,0,1
CFLAG:(CHARANUM-1):ＨＰ補正 += 300
CFLAG:(CHARANUM-1):速度補正 += 10
CFLAG:(CHARANUM-1):行動回数 = 2
CFLAG:(CHARANUM-1):初期リンク悪魔 = 3801
CFLAG:(CHARANUM-1):リンク悪魔 = -1
;ボウガン弱すぎにつき補正
CFLAG:(CHARANUM-1):銃攻撃補正 = 50
CALL SYNC_STATUS, (CHARANUM-1)
CALL HEALTH_CHARA, (CHARANUM-1)
CSTR:(CHARANUM-1):思考パターン = 4569

CALL BATTLE_START
CUSTOMDRAWLINE =
PRINTW 「It burns...it burns! Oh, ah..a.!」
;「燃べだ…燃べづぎだああああああ…あ…！！」
PRINTW ＞The man collapsed and returned to his human shape.
;男は崩れ落ち、人間の姿に戻った…。
PRINTW
PRINTW ＞When you went to receive your reward, Sheffield was there.
;報酬を受け取りに行くと、そこにシェフィールドが居た。
PRINTW 「I was able to get some good data. Asking the Phantom Society for this was the right decision.」
;「なかなか良いデータが取れたよ。ファントムに依頼して正解だったかな」
PRINTW ＞You asked why the man was behaving so strangely.
;対戦相手の男の様子がおかしかった理由を尋ねてみた。
PRINTW 「I restored him after he was consumed by another Asura AI.」
;「アレは他のアスラAIに淘汰されたのをテスト用に復元したんだ」
PRINTW 「His memories seem confused from that, but it doesn't matter.」
;「多分記憶が混乱しているんだろう、どうでもいいことだけど」
PRINTW 「Oh...where you asking because of your 'profession'?」
;「ああ…君は確か、奴隷商人だったっけ？」
PRINTW 「Well, you can have him if you want.」
;「アレが欲しいなら持っていってもいいよ」
PRINTL
PRINTL Will you accept Harley Ｑ?
;ハーリーＱを受け取りますか？

CALL INPUT_YN,"Yes","No"

IF RESULT == 0
	PRINTW 「I won't ask what you'll do with him. Well, I'm done here.」
	;「何に使うかは聞かないでおくよ。それじゃ、僕はこれで」
	PRINTW ＞Sheffield walked away...
	;シェフィールドは立ち去った…。
	PRINTL
	PRINTW Harley Ｑ became a slave.
	;ハーリーＱが奴隷になった。
	CALL ADD_NEW_COMPANION,4569,500
	;さり気に治療費として持ってった
	PRINTW You received ￥15000 as price money.
	MONEY += 15000
	PRINTFORMW %CALLNAME:MASTER%'s fame increased by 80.
	FLAG:名声 += 40
	FLAG:闘技場名声 += 40
	PRINTL ※An Avatar Tuner has joined your group.
	;※喰奴が加入しました
	PRINTL 　Avatar Tuners can use swords and gun like humans and they can also transform into demons to use various skills.
	;喰奴は人間として剣やGunを扱うことができ、また悪魔に変身して各種スキルを行使できます。
	PRINTL 　However, unlike Devil Shifters, they can only turn into one demon.
	;ただし、デビルシフターとは異なり変身する悪魔を変化させることはできません。
	PRINTL 　In addition, avatar tuners get only half the MAG from winning battles.
	;また、喰奴は戦闘時に獲得できるMAGが半減します。
	PRINTW 　However, they can get more MAG by using hunting skills to kill foes.
	;戦闘中にハントスキルを使用するたび得られるMAGが上昇します。
ELSEIF RESULT == 1
	PRINTW 「Hm, a shame. I though I could save the disposal cost this way...」
	;「まぁ、貰っても困るか。処分費用が浮くと思ったんだけど…」
	PRINTW 「Ah, it's nothing. Well, I'll be on my way.」
	;「いや、こっちの話。それじゃ、僕はこれで」
	PRINTW ＞Sheffield walked away...
	PRINTL
	PRINTW You received ￥25000 as price money.
	MONEY += 25000
	PRINTFORMW %CALLNAME:MASTER%'s fame increased by 40.
	FLAG:名声 += 40
	FLAG:闘技場名声 += 40
ENDIF

闘技場フラグ:15:0 = 1
CUSTOMDRAWLINE =
FLAG:逃走不可フラグ = 0
FLAG:会話不能フラグ = 0
FLAG:先制不意打ちキャンセル = 0

SAVESTR:ランダム対戦チーム =
CALL SELECT_COLOSSEUM_RANDOMBATTLE

FOR LOCAL,0,CHARANUM
	CALL HEALTH_CHARA,LOCAL
NEXT

@SET_ACTION_4569,ARG
CFLAG:ARG:入力行動 = -1
CFLAG:ARG:ターゲット = -1

;変身しているかどうか
IF CFLAG:ARG:悪魔変身 == 0
	;Human版
	IF 現HP割合(ARG) <= 50
		CFLAG:ARG:入力行動 = [[スキル:悪魔変身]]
	ELSEIF CFLAG:ARG:行動した回数 == 0
		;１ターン目は道具を使う
		SELECTCASE RAND:100
			CASE 0 TO 9 
				CFLAG:ARG:入力行動 = [[スキル:NapalmBomb]]
			CASE 10 TO 39
				CFLAG:ARG:入力行動 = [[スキル:StunGrenade]]
			CASE 40 TO 69
				CFLAG:ARG:入力行動 = [[スキル:PoisonArrow]]
			CASE 70 TO 99
				CFLAG:ARG:入力行動 = [[スキル:SpiralBomb]]	
		ENDSELECT
	ELSE
		;弱い敵にちまちま攻撃
		CFLAG:ARG:入力行動 = [[スキル:ＧＵＮ]]
		CFLAG:ARG:ターゲット = -1
		CALL ATTACK_MIN_HP,ARG,0
	ENDIF
ELSE
	;１ターン目はかならず火の壁
	IF CFLAG:ARG:行動した回数 == 0
		CFLAG:ARG:入力行動 = [[スキル:炎の壁]]
	ELSE
		SELECTCASE RAND:100
			CASE 0 TO 19 
				CFLAG:ARG:入力行動 = [[スキル:ファイアストーム]]
			CASE 20 TO 50
				CFLAG:ARG:入力行動 = [[スキル:喰い散らし]]
			CASE 51 TO 99
				CFLAG:ARG:入力行動 = [[スキル:喰らいつき]]
		ENDSELECT
	ENDIF

ENDIF
;アクション実行不能
CALL CHECK_ACTIONABLE, ARG, CFLAG:ARG:入力行動

IF RESULT == 0 && CFLAG:ARG:悪魔変身 == 0
	;Human時
	CFLAG:ARG:入力行動 = [[スキル:ＧＵＮ]]
ELSEIF RESULT == 0 && CFLAG:ARG:悪魔変身 == 1
	CFLAG:ARG:入力行動 = 0
ENDIF

SIF CFLAG:ARG:ターゲット == -1
	CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動
	
@ENEMY_BATTLE_MESSAGE_K4569, ARG
SKIPDISP 0
LOCAL:1 = 1
LOCAL = CFLAG:ARG:入力行動
LOCALS = EMPTY
SELECTCASE LOCAL
	CASE [[スキル:StunGrenade]]
		LOCALS = ち、近寄るんじゃねええええ！
	CASE [[スキル:NapalmBomb]]
		LOCALS = く、来るなあああああ！
	CASE [[スキル:SpiralBomb]]
		LOCALS = ひ、ひぃいいい！
	CASE [[スキル:PoisonArrow]]
		LOCALS = 喰わないでくれえっ！
	CASE [[スキル:ＧＵＮ]]	
		LOCALS = や、やめてくれえっ！
	CASE [[スキル:炎の壁]]	
		LOCALS = 火火ーン！	
	CASE [[スキル:喰らいつき]]	
		LOCALS = 喰っでやるあぁあぁ！
	CASE [[スキル:喰い散らし]]
		LOCALS = 一片も残ざねええええ！
	CASE [[スキル:ファイアストーム]]	
		LOCALS = 燃べろ！燃べ尽ぎろぉぉぉ！
	CASE [[スキル:悪魔変身]]
		RESETCOLOR
		PRINTW ＞Harley Ｑ is acting strange...
		;ハーリーＱの様子がおかしい…。
		PRINTW 「Ah...I get it now...」
		;「そう、か…いっこだけ、わかったぜ…」
		PRINTW 「Before you can eat me...I will eat you!」
		;「食われる前に……喰っちまえばいいんだよなぁ！」
		PRINTL
		PRINTW ＞Harley Ｑ took on the shape of a demon!
		;ハーリーＱは悪魔に変身した！
		;HP全快に
		CALL VAR_HP,ARG,MAXBASE:ARG:ＨＰ,3
ENDSELECT
SIF LOCALS != "EMPTY"
	CALL MESSAGE_B(LOCAL:1, LOCALS, LOCALS:1, LOCALS:2, LOCALS:3, LOCALS:4)
