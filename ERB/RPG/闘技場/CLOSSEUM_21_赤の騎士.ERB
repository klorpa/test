
;====================
;条件
;====================
@PLAYABLE_COLOSSEUM_21

IF FLAG:闘技場ランク >= 3 && FLAG:闘技場名声 >= 950 && 闘技場フラグ:21:0 < 1 && ダンジョンフラグ:68:40 > 1
	RESULTS = 試合ランクＢ　ＶＳ 赤の騎士
	RETURN 1
ENDIF
RETURN 0


;====================
;内容
;====================
@COLOSSEUM_21
FLAG:逃走不可フラグ = 1
FLAG:会話不能フラグ = 1
FLAG:先制不意打ちキャンセル = 1
CUSTOMDRAWLINE =

PRINTFORML 　司会
PRINTFORML 「レディィィィス！エンッ！ジェントルメン！
PRINTFORMW 　お待たせしました！本日注目のカードはこちら！」
PRINTFORML 
PRINTFORMW ＞スクリーンに、%CALLNAME:MASTER%達と相手チームとが映し出された…"
PRINTFORML 
PRINTFORMW ＞・・・
PRINTFORMW ＞・・・メノラーの炎が揺れている
PRINTFORML 
PRINTFORML 
PRINTFORML 

PRINTFORMW 「ほぉー、まだ生きておるとはのぉー」
IF ITEM:350 || ITEM:352 || ITEM:354
	PRINTFORMW 「我ら四騎士、世に聞こえた死の担い手を退けるとはー・・・見事、見事」
	PRINTFORMW 「しかしワシは他の騎士より甘くは無いぞ」
	PRINTFORML 「闇より出でた鮮血の騎士であるワシが、
	PRINTFORMW 　おまえの息の根を・・・」
ELSE
	PRINTFORML 「ならば、闇より出でた鮮血の騎士であるワシが、
	PRINTFORMW 　おまえの息の根を・・・」
ENDIF
PRINTFORMW 「止めてやろうぞ！」

CUSTOMDRAWLINE =
WAIT

CALL SET_ENEMY,9,[[キャラ:レッドライダー]],60,0,1
CFLAG:(CHARANUM-1):ＨＰ補正 += 3000
CFLAG:(CHARANUM-1):ＭＰ補正 += 5000
CFLAG:(CHARANUM-1):行動回数 = 2
CSTR:(CHARANUM-1):増援パターン = レッドライダーの
	CSTR:(CHARANUM-1):思考パターン = 41086800
	ABL:(CHARANUM-1):アイテム1 = 351
	ABL:(CHARANUM-1):入手確率1 = 100
	ABL:(CHARANUM-1):アイテム2 = 10042
	ABL:(CHARANUM-1):入手確率2 = 50
CALL SYNC_STATUS,(CHARANUM-1)
CALL HEALTH_CHARA,(CHARANUM-1)

CALL SET_ENEMY,8,[[キャラ:パワー]],53,0,0
CFLAG:(CHARANUM-1):ＨＰ補正 += 1000
CFLAG:(CHARANUM-1):ＭＰ補正 += 500
	ABL:(CHARANUM-1):アイテム1 = 1002
	ABL:(CHARANUM-1):入手確率1 = 0
CALL SYNC_STATUS,(CHARANUM-1)
CALL HEALTH_CHARA,(CHARANUM-1)

CALL SET_ENEMY,10,[[キャラ:パワー]],53,0,0
CFLAG:(CHARANUM-1):ＨＰ補正 += 1000
CFLAG:(CHARANUM-1):ＭＰ補正 += 500
	ABL:(CHARANUM-1):アイテム1 = 1002
	ABL:(CHARANUM-1):入手確率1 = 0
CALL SYNC_STATUS,(CHARANUM-1)
CALL HEALTH_CHARA,(CHARANUM-1)

CALL BATTLE_START
CUSTOMDRAWLINE =

PRINTW Fiendレッドライダーを倒した
PRINTL 

CUSTOMDRAWLINE =
WAIT
FLAG:逃走不可フラグ = 0
FLAG:会話不能フラグ = 0
FLAG:先制不意打ちキャンセル = 0
PRINTW ￥50000を賞金として受け取った
PRINTFORMW %CALLNAME:MASTER%は100の名声を得た
MONEY += 50000
FLAG:名声 += 100
FLAG:闘技場名声 += 100
闘技場フラグ:21:0 = 1

;合体解禁しておく
FLAG:14108 = 1

FOR LOCAL,0,CHARANUM
	CALL HEALTH_CHARA,LOCAL
NEXT

;=======================================================================
;レッドライダー行動パターン
;=======================================================================
@SET_ACTION_41086800,ARG
#LOCALSIZE 2
#LOCALSSIZE 1
;HPが５０％切ったら行動回数増える
IF CFLAG:ARG:行動回数 < 3 && (BASE:ARG:ＨＰ)*100/(MAXBASE:ARG:ＨＰ) < 50
	PRINTFORML ┏>CHARA━━━━━━━━━━┓　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORM ┃[{CFLAG:ARG:ポジション,2}] %CALLNAME:ARG,21,LEFT%┃　┃
	RESULTS = 龍の眼光
	PRINTFORML %RESULTS,40,LEFT%┃
	PRINTFORMW ┗━━━━━━━━━━━━━┛　┗━━━━━━━━━━━━━━━━━━━━┛
	CFLAG:ARG:行動回数 = 4
ENDIF

;３分の１でデクンダチェック
IF RAND:3 == 1
	FOR LOCAL,7,17
		LOCALS = ポジション{LOCAL}
		SIF FLAG:LOCALS == -1
			CONTINUE
		SIF GET_STATE(CFLAG:(FLAG:LOCALS):ステート) == "DYING"
			CONTINUE
		FOR LOCAL:1,0,6
			IF CFLAG:(FLAG:LOCALS):(GET_BATTLESTATUS(LOCAL:1) + "強化") < 0
				;デクンダ
				CFLAG:ARG:入力行動 = 505
				CALL RANDOM_TARGET,ARG,CFLAG:ARG:入力行動
				RETURN 1
			ENDIF
		NEXT
	NEXT
	GOTO DECIDE_ACTION
ELSE
	$DECIDE_ACTION
	LOCAL = RAND:10
	;通常攻撃
	IF LOCAL <= 1
		CFLAG:ARG:入力行動 = 0
	;テラソ
	ELSEIF 2 <= LOCAL && LOCAL <= 3
		CFLAG:ARG:入力行動 = 42
	;悪魔の一喝
	ELSEIF 4 <= LOCAL && LOCAL <= 5
		CFLAG:ARG:入力行動 = 1010
	;雷の乱舞
	ELSEIF 6 <= LOCAL && LOCAL <= 7
		CFLAG:ARG:入力行動 = 917
	;悪魔召喚、失敗したら通常攻撃
	ELSE
		FOR LOCAL, 7, 17
			SIF POS(LOCAL) >= 0
				CONTINUE
			BREAK
		NEXT
		IF LOCAL == 17
			CFLAG:ARG:入力行動 = 0
		ELSE
			CFLAG:ARG:入力行動 = 2308
		ENDIF
	ENDIF
ENDIF

CALL CHECK_ACTIONABLE,ARG,CFLAG:ARG:入力行動
IF RESULT == 1
	CALL RANDOM_TARGET,ARG,CFLAG:ARG:入力行動
ELSE
	;なんかエラーあれば通常攻撃に
	CFLAG:ARG:入力行動 = 0
	CALL RANDOM_TARGET,ARG,CFLAG:ARG:入力行動
ENDIF
RETURN 0

@増援パターン_レッドライダーの, ARG, ARG:1, ARG:2
CALL SET_ENEMY, ARG:1, [[キャラ:パワー]],48,0,0
CFLAG:(CHARANUM-1):ＨＰ補正 = 200
	ABL:(CHARANUM-1):アイテム1 = 1002
	ABL:(CHARANUM-1):入手確率1 = 0
CALL HEALTH_CHARA,(CHARANUM-1)
CALL SYNC_STATUS,(CHARANUM-1)
SIF ARG:2 == 1
	RETURN 1
RETURN 0

