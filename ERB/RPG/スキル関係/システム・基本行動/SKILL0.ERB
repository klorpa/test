;通常攻撃
;スキルのテンプレートになっています

;スキル名
@SKILL_NAME_0,ARG
IF ARG < 0
	RESULTS = ＡＴＴＡＣＫ
	RETURN 0
ENDIF
IF EQUIP:ARG:剣 > 0 && CFLAG:ARG:悪魔変身 == 0
	RESULTS = ＳＷＯＲＤ
ELSEIF TALENT:ARG:喰奴 > 0 && CFLAG:ARG:悪魔変身 == 1
	RESULTS = ＨＵＮＴ
ELSEIF TALENT:ARG:小人体型 && (TALENT:ARG:オトコ == 0 || TALENT:ARG:男の娘)
	SELECTCASE ABL:ARG:攻撃相性
		CASE 2
			RESULTS = ぺしぺしする
		CASE 3
			RESULTS = かぷかぷする
		CASE 0
			RESULTS = つんつんする
		CASEELSE
			RESULTS = ＡＴＴＡＣＫ
	ENDSELECT
ELSEIF TALENT:ARG:小柄体型 && (TALENT:ARG:オトコ == 0 || TALENT:ARG:男の娘)
	SELECTCASE ABL:ARG:攻撃相性
		CASE 2
			RESULTS = ポカポカ叩く
		CASEELSE
			RESULTS = ＡＴＴＡＣＫ
	ENDSELECT
ELSE
	RESULTS = ＡＴＴＡＣＫ
ENDIF

;　命中
@SKILL_HITRATE_0, ARG = -1
LOCAL = 100
SIF ARG == -1
	RETURN 100
;IF ABL:ARG:種族 != 0 && NO:ARG != 3501 && NO:ARG != 3502
SELECTCASE ABL:ARG:射程
	CASE 2
		LOCAL -= 5
	CASE 3
		LOCAL -= 15
ENDSELECT
;ENDIF
RETURN LOCAL

;　威力
@SKILL_POWER_0, ARG = -1
LOCAL = 100
SIF ARG == -1
	RETURN 100
;IF ABL:ARG:種族 != 0 && NO:ARG != 3501 && NO:ARG != 3502
SELECTCASE ABL:ARG:攻撃範囲
	CASE 2
		LOCAL -= 15
	CASE 3
		LOCAL -= 30
ENDSELECT
;ENDIF
RETURN LOCAL

;　最低攻撃回数
@SKILL_MINATTACKNUMBER_0,ARG
RETURN ABL:ARG:最低攻撃回数

;　最大攻撃回数
@SKILL_MAXATTACKNUMBER_0,ARG
RETURN ABL:ARG:最大攻撃回数

;　ＨＰ2を支払うかＭＰ3を支払うか
@SKILL_COSTTYPE_0, ARG
RETURN 1
;消費なし＝1

;　コストの量
@SKILL_COST_0,ARG
RETURN 0

;　相性
@SKILL_TYPE_0,ARG
SIF ARG < 0
	RETURN 0
RETURN ABL:ARG:攻撃相性

;　1EXTRAか2MAGICか
@SKILL_DECIDE_TYPE_0
RETURN 0
;どちらでもない

;　1物理ダメージで計算するか2魔法ダメージで計算するか
@SKILL_DAMAGETYPE_0
RETURN 1


;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_0
RETURN 1

;　射程
@SKILL_RANGE_0,ARG
RETURN ABL:ARG:射程

;　範囲　1単体　21列　3全体　4　敵味方全体
@SKILL_SPHERE_0,ARG
RETURN ABL:ARG:攻撃範囲

;　 効果のタイプ　1攻撃、2回復、3状態変化、4特殊、5状態回復
@SKILL_EFECT_0
RETURN 1

;　 特殊処理の有無、及び処理内容
@SKILL_SPECIALEFECT_0,ARG
;ARG = 1だった場合は処理の有無を返します
SIF ARG == 1
	RETURN 0
;以下に特殊な処理を記述します

;継承タイプ
@SKILL_SUCCESSION_TYPE_0
RETURN 0
;継承不可

;速度補正
@SKILL_SPEED_0, ARG = -1
RETURN 0

;　口上の表示とそのタイプ
@SKILL_MESSAGE_0,ARG

;戦闘時以外に使用できるか
@SKILL_ACTIONABLE_FIELD_0
RETURN 0

;スキル変化？用ランク
@SKILL_RANK_0
RETURN 0
;変化不可

;テンプレート用、追加効果シリーズ
;追加効果ステータス
@SKILL_ADDTIONAL_STATE_0,ARG
RETURN ABL:ARG:追加効果

;追加効果相性
@SKILL_ADDTIONAL_TYPE_0,ARG
RETURN ABL:ARG:追加効果相性

;追加効果基本付与確率
@SKILL_ADDTIONAL_BASE_RATE_0,ARG
RETURN ABL:ARG:追加効果命中率

;追加効果付与確率上限
@SKILL_ADDTIONAL_MAX_RATE_0,ARG
RETURN ABL:ARG:追加効果最大命中率
;====================================================
;スキル及び行動の実行
;====================================================
@ACTION_0,ARG,ARG:1
LOCAL = ARG:1
IF ARG:1 == 0
	PRINTW 不正な場所を攻撃しようとしました
	RETURN 0
ENDIF	
IF ARG:1 < 17 
	LOCAL = POS(ARG:1)
	CALL ATTACK_SINGLE,ARG,LOCAL,0
ELSE
	CALL ATTACK_FIELD,ARG,ARG:1,0
ENDIF





;====================================================
;実行内容ここまで
;====================================================

;　 説明文の表示
@SKILL_EXPLAIN_0
PRINTL 武器や肉体などを駆使して攻撃する。

;使用条件
@SKILL_SPECIAL_ACTIONABLE_0,ARG
RETURN 1
;とくにない

