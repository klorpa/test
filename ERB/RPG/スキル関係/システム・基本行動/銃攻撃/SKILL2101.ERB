;通常攻撃
;スキルのテンプレートになっています

;スキル名
@SKILL_NAME_2101,ARG
RESULTS = ＧＵＮ

;　命中
@SKILL_HITRATE_2101, ARG
RETURN 100

;　威力
@SKILL_POWER_2101, ARG
RETURN 100

;　最低攻撃回数
@SKILL_MINATTACKNUMBER_2101,ARG
CALLFORM 最低攻撃回数_{EQUIP:ARG:銃}
RETURN RESULT

;　最大攻撃回数
@SKILL_MAXATTACKNUMBER_2101,ARG
CALLFORM 最大攻撃回数_{EQUIP:ARG:銃}
RETURN RESULT

;　ＨＰ2を支払うかＭＰ3を支払うか
@SKILL_COSTTYPE_2101, ARG
RETURN 1
;消費なし＝1

;　コストの量
@SKILL_COST_2101,ARG
RETURN 0

;　相性
@SKILL_TYPE_2101,ARG
SIF ARG >= 0	;スキル属性表示で落ちるのでガード
	CALLFORM 攻撃相性_{EQUIP:ARG:銃}
RETURN ARG >= 0 ? RESULT # 1
;Gunに依存
;RETURN 1
;Gun

;　1EXTRAか2MAGICか
@SKILL_DECIDE_TYPE_2101
RETURN 0
;どちらでもない

;　1物理ダメージで計算するか2魔法ダメージで計算するか
@SKILL_DAMAGETYPE_2101
RETURN 1


;　対象が1敵か2味方か3場全体か4特殊か
@SKILL_TARGET_2101
RETURN 1

;　射程
@SKILL_RANGE_2101,ARG
CALLFORM 射程_{EQUIP:ARG:銃}
RETURN RESULT

;　範囲　1単体　21列　3全体　4　敵味方全体
@SKILL_SPHERE_2101,ARG
CALLFORM 攻撃範囲_{EQUIP:ARG:銃}
RETURN RESULT

;　 効果のタイプ　1攻撃、2回復、3状態変化、4特殊、5状態回復
@SKILL_EFECT_2101
RETURN 1

;　 特殊処理の有無、及び処理内容
@SKILL_SPECIALEFECT_2101,ARG
;ARG = 1だった場合は処理の有無を返します
SIF ARG == 1
	RETURN 0
;以下に特殊な処理を記述します

;継承タイプ
@SKILL_SUCCESSION_TYPE_2101
RETURN 0
;継承不可

;速度補正
@SKILL_SPEED_2101, ARG = -1
SIF ARG == -1
	RETURN 0
TRYCCALLFORM 速度補正_{EQUIP:ARG:銃}
	RETURN RESULT
CATCH
	RETURN 0
ENDCATCH

;　口上の表示とそのタイプ
@SKILL_MESSAGE_2101,ARG

;戦闘時以外に使用できるか
@SKILL_ACTIONABLE_FIELD_2101
RETURN 0

;スキル変化？用ランク
@SKILL_RANK_2101
RETURN 0
;変化不可

;テンプレート用、追加効果シリーズ
;追加効果ステータス
@SKILL_ADDTIONAL_STATE_2101,ARG
TRYCCALLFORM 追加効果_{EQUIP:ARG:銃}
	RETURN RESULT
CATCH
	RETURN 0
ENDCATCH

;追加効果相性
@SKILL_ADDTIONAL_TYPE_2101,ARG
TRYCCALLFORM 追加効果相性_{EQUIP:ARG:銃}
	RETURN RESULT
CATCH
	RETURN 0
ENDCATCH

;追加効果基本付与確率
@SKILL_ADDTIONAL_BASE_RATE_2101,ARG
TRYCCALLFORM 追加効果命中率_{EQUIP:ARG:銃}
	RETURN RESULT
CATCH
	RETURN 0
ENDCATCH

;追加効果付与確率上限
@SKILL_ADDTIONAL_MAX_RATE_2101,ARG
TRYCCALLFORM 追加効果最大命中率_{EQUIP:ARG:銃}
	RETURN RESULT
CATCH
	RETURN 0
ENDCATCH

;====================================================
;スキル及び行動の実行
;====================================================
@ACTION_2101,ARG,ARG:1
CALL SKILL_SPHERE_2101,ARG
IF RESULT == 1
	LOCAL = POS(ARG:1)
	CALL GUN_SINGLE,ARG,LOCAL,2101
ELSE
	RESULT = 0
	TRYCALLFORM GUN_RANDOMTARGET_{EQUIP:ARG:銃}
	IF RESULT == 1
		CALL GUN_RAND,ARG,ARG:1,2101
	ELSE
		CALL GUN_FIELD,ARG,ARG:1,2101
	ENDIF
	
ENDIF





;====================================================
;実行内容ここまで
;====================================================

;　 説明文の表示
@SKILL_EXPLAIN_2101
PRINTL 装備しているGunで攻撃する。

;使用条件
@SKILL_SPECIAL_ACTIONABLE_2101,ARG
SIF EQUIP:ARG:銃 == 0 || CFLAG:ARG:悪魔変身
	RETURN 0
RETURN 1
;とくにない

