; EVENT_16_AI.ERB
; 全能のバランサーとマーガレットの戦闘AIを記述しています

; 全能のバランサーの思考パターン
; コンセントレイトと6種の単体属性魔法、メギドラオンを繰り返す。1moreはコンセントレイト固定
; 魔力が高いためHP3桁程度では弱点でなくても即死するくらいには痛い。運が低いのでよく外れるが……
; 基本的に単体魔法で弱点狙い。魔法反射がかかっている場合、33%でメギドラオン、HPが30%を切ると確実にメギドラオン
; HPが30%を切ると魔法反射がかかっていなくてもメギドラオンが混ざる
; クリティカルの出方次第で1ターンでも倒せるが下手に削って生き残ってしまうとコンセメギドがとんでくる
; ところでバランサーの攻撃時のぶるるるるって声なんか異様にかわいくないですかね？
@SET_ACTION_全能バランサ, ARG
; 初期化
VARSET LOCAL, 0
; 1moreかコンセントレイト未実行ならコンセントレイト
IF CFLAG:ARG:１moreフラグ == 2 || CFLAG:ARG:集中フラグ == 0
	CALL EVENT_16_SET_ACTION, ARG, [[スキル:コンセントレイト]]
	RETURN 0
; それ以外
ELSE
	; HPが5割を切ってるなら1/4でメギドラオンセット
	IF 現HP割合(ARG) < 50 && RAND:4 == 0
		CALL EVENT_16_SET_ACTION, ARG, [[スキル:メギドラオン]]
		RETURN 0
	ENDIF
	; キャラ走査
	FOR LOCAL:3, 1, 7
		LOCAL = POS(LOCAL:3)
		; 空ポジションかDYINGなら次
		SIF LOCAL < 0 || GET_STATE(CFLAG:LOCAL:ステート) == "DYING"
			CONTINUE
		; 魔法反射状態なら一定確率でメギドラオンセットしてキャラ走査終了
		; 気づいてしまったけど反射の持続1ターンだしもしかしてこれ意味がな……
		IF CFLAG:LOCAL:魔法反射フラグ > 0 && (現HP割合(ARG) < 40 || (現HP割合(ARG) < 80 && RAND:3 == 0))
			CALL EVENT_16_SET_ACTION, ARG, [[スキル:メギドラオン]]
			BREAK
		ENDIF
		; 既に行動が入力されている場合は1/3でキャラ走査終了
		SIF CFLAG:ARG:入力行動 >= 0 && RAND:3 == 0
			BREAK
		; 耐性走査
		FOR LOCAL:4, 40, 59
			SELECTCASE LOCAL:4
				; Fire
				CASE 44
					; 弱点でなければ次
					SIF CHECK_WEAKNESS(MAXBASE:LOCAL:(LOCAL:4)) < 0
						CONTINUE
					; 既に行動が入力されている場合は1/3で耐性走査終了
					SIF CFLAG:ARG:入力行動 > 0 && RAND:3 == 0
						BREAK
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:アギダイン]], LOCAL:3
				; Ice
				CASE 45
					; 弱点でなければ次
					SIF CHECK_WEAKNESS(MAXBASE:LOCAL:(LOCAL:4)) < 0
						CONTINUE
					; 既に行動が入力されている場合は1/3で耐性走査終了
					SIF CFLAG:ARG:入力行動 > 0 && RAND:3 == 0
						BREAK
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:ブフダイン]], LOCAL:3
				; Elect
				CASE 46
					; 弱点でなければ次
					SIF CHECK_WEAKNESS(MAXBASE:LOCAL:(LOCAL:4)) < 0
						CONTINUE
					; 既に行動が入力されている場合は1/3で耐性走査終了
					SIF CFLAG:ARG:入力行動 > 0 && RAND:3 == 0
						BREAK
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:ジオダイン]], LOCAL:3
				; Earth
				CASE 52
					; 弱点でなければ次
					SIF CHECK_WEAKNESS(MAXBASE:LOCAL:(LOCAL:4)) < 0
						CONTINUE
					; 既に行動が入力されている場合は1/3で耐性走査終了
					SIF CFLAG:ARG:入力行動 > 0 && RAND:3 == 0
						BREAK
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:マグダイン]], LOCAL:3
				; Water
				CASE 53
					; 弱点でなければ次
					SIF CHECK_WEAKNESS(MAXBASE:LOCAL:(LOCAL:4)) < 0
						CONTINUE
					; 既に行動が入力されている場合は1/3で耐性走査終了
					SIF CFLAG:ARG:入力行動 > 0 && RAND:3 == 0
						BREAK
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:アクアダイン]], LOCAL:3
				; Wind
				CASE 54
					; 弱点でなければ次
					SIF CHECK_WEAKNESS(MAXBASE:LOCAL:(LOCAL:4)) < 0
						CONTINUE
					; 既に行動が入力されている場合は1/3で耐性走査終了
					SIF CFLAG:ARG:入力行動 > 0 && RAND:3 == 0
						BREAK
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:ガルダイン]], LOCAL:3
			ENDSELECT
		NEXT
	NEXT
	; ここまで来て属性魔法がセットされていない場合はランダム対象で属性魔法を使う
	SIF CFLAG:ARG:入力行動 > 0
		RETURN 0
	SELECTCASE RAND:6
		CASE 0
			CALL EVENT_16_SET_ACTION, ARG, [[スキル:アギダイン]]
		CASE 1
			CALL EVENT_16_SET_ACTION, ARG, [[スキル:ブフダイン]]
		CASE 2
			CALL EVENT_16_SET_ACTION, ARG, [[スキル:ジオダイン]]
		CASE 3
			CALL EVENT_16_SET_ACTION, ARG, [[スキル:マグダイン]]
		CASE 4
			CALL EVENT_16_SET_ACTION, ARG, [[スキル:アクアダイン]]
		CASE 5
			CALL EVENT_16_SET_ACTION, ARG, [[スキル:ガルダイン]]
	ENDSELECT
ENDIF

; マーガレットの思考パターン
; 
; ・全段階で常時2回行動
; ・全段階で1moreを含む行動50回毎に9999固定メギドラオン
; ・全段階で一定条件においてメギドラオンモード(行動が全て9999固定ダメージメギドラオン化)
;     不屈の闘志が発動した場合、メギドラオンモードon
;     パーティーにレベルに対して能力が高すぎる(18+Lv*1.5+5の2倍以上)キャラが存在する場合、メギドラオンモードon
;     パーティーに主人以外のマハアナライズを持っているキャラが存在する場合、メギドラオンモードon
; 
; ・第一段階 HP90%まで
;   アタック*2
; ・第二段階 HP50%まで
;   Fire->Ice->Elect->Wind->コンセ->メギドラオンのローテーションで攻撃
; ・第三段階 初めてHP50%未満
;   明けの明星
; ・第四段階 明けの明星後
;   ディアラハン
; ・第五段階 第三段階以降HP90%まで
;   アタック*2
; ・第六段階 第三段階以降HP50%まで
;   Fire->Ice->Elect->Wind->コンセ->メギドラオンのローテーションで攻撃。攻撃相性が無効と吸収のみの場合は次の攻撃相性のガードブレイク
; ・第七段階 第三段階以降HP50%未満
;   Fire->Ice->Elect->Wind->気合->八艘飛びのローテーションで攻撃
;   この時点でスキルから不屈の闘志が削除される
@SET_ACTION_マーガレット, ARG
#LOCALSIZE 4
VARSET LOCAL, 0
; 全能の真珠モードチェック
IF !CFLAG:ARG:601 < 99
	; 速攻キルしたら死ぬがよい
	IF CFLAG:ARG:食いしばりフラグ
		DEBUGPRINTL メギドラオンモードon(食いしばりフラグ)
		CFLAG:ARG:601 = 99
	ELSE
		FOR LOCAL, 1, 7
			SIF POS(LOCAL) < 0
				CONTINUE
			; マハアナライズを持っていたら死ぬがよい(ただしMASTERは除く)
			IF POS(LOCAL) != MASTER && HAVE_SKILL(POS(LOCAL), [[スキル:マハアナライズ]])
				DEBUGPRINTL メギドラオンモードon(マハアナライズ)
				CFLAG:ARG:601 = 99
				BREAK
			ENDIF
			; 能力許容値(キャラクター作成時の基準の2倍)を超えているなら死ぬがよい
			; 例. Lv100なら合計346まで許容
			LOCAL:2 = 0
			FOR LOCAL:1 ,31 ,37
				LOCAL:2 += BASE:(POS(LOCAL)):(LOCAL:1)
			NEXT
			FOR LOCAL:1 ,1171 ,1177
				LOCAL:2 += CFLAG:(POS(LOCAL)):(LOCAL:1)
			NEXT
			IF LOCAL:2 > (23 + BASE:(POS(LOCAL)):LV * 3 / 2) * 3 / 2
				DEBUGPRINTL メギドラオンモードon(能力許容値オーバー)
				CFLAG:ARG:601 = 99
				BREAK
			ENDIF
		NEXT
	ENDIF
ELSE
	FOR LOCAL, 0, 6
		CFLAG:ARG:(GET_BATTLESTATUS(LOCAL) + "強化") = 32
	NEXT
ENDIF

; 50の倍数回行動するか全能の真珠モードで9999メギドラオン
IF (CFLAG:ARG:600 > 0 && (CFLAG:ARG:600 % 50) == 0) || CFLAG:ARG:601 == 99
	; 9999メギドラオン
	CALL EVENT_16_SET_ACTION, ARG, [[スキル:9999メギドラオン]]
	DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:9999メギドラオン
; 戦闘開始
ELSEIF CFLAG:ARG:601 == 0
	CALL EVENT_16_SET_ACTION, ARG, [[スキル:ＡＴＴＡＣＫ]]
	CFLAG:ARG:601 = 1
; 第一, 第五段階 アタックのみ
ELSEIF 現HP割合(ARG) > 90
	DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:アタック
	CALL EVENT_16_SET_ACTION, ARG, [[スキル:ＡＴＴＡＣＫ]]
ELSEIF 現HP割合(ARG) < 50 && CFLAG:ARG:601 < 5
	; 第三段階 明けの明星
	IF CFLAG:ARG:601 < 4
		CALL EVENT_16_SET_ACTION, ARG, [[スキル:明けの明星]]
		CFLAG:ARG:601 = 4
		DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:明けの明星
	; 第四段階 ディアラハン
	ELSE
		CALL EVENT_16_SET_ACTION, ARG, [[スキル:ディアラハン]]
		CFLAG:ARG:601 = 5
		DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ディアラハン
	ENDIF
; 第二, 第六, 第七段階 Fire->Ice->Elect->Wind->(コンセメギドラオン or チャージ八艘飛び) ローテーション攻撃
ELSE
	; 段階更新
	IF CFLAG:ARG:601 < 2
		CFLAG:ARG:601 = 2
		CFLAG:ARG:602 = 0
	ELSEIF CFLAG:ARG:601 == 5
		CFLAG:ARG:601 = 6
	ELSEIF 現HP割合(ARG) < 50 && CFLAG:ARG:601 == 6
		CFLAG:ARG:601 = 7
	ENDIF
	; ローテーション行動
	SELECTCASE CFLAG:ARG:602
		CASE 0
			; 基本的にメギドラオンで1moreは取らないと思うので、
			; 必然的に八艘跳びで取った際にここに来る
			IF CFLAG:ARG:１moreフラグ == 2
				; 弱体がかかっていればデクンダ。かかってなければヒートライザ
				FOR LOCAL, 0, 6
					IF CFLAG:ARG:(GET_BATTLESTATUS(LOCAL) + "強化") < 0
						; デクンダ
						CALL EVENT_16_SET_ACTION, ARG, [[スキル:デ・クンダ]]
						DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:1more_デクンダ
						BREAK
					ELSE
						; ヒートライザ
						CALL EVENT_16_SET_ACTION, ARG, [[スキル:ヒートライザ]]
						DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:1more_ヒートライザ
					ENDIF
				NEXT
			ELSE
				RESULT = EVENT_16_SEARCH_REGISTANCE(2, "火炎", 1, 1)
				DEBUGPRINTFORML 火炎耐性検索結果:{RESULT}
				; 弱点持ちがいる
				IF RESULT > 0
					; 火炎単体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:ラグナロク]], RESULT
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_火炎単体
				; 全員が無効か吸収
				ELSEIF (RESULT == -2 || (RESULT == -1 && RAND:2)) && CFLAG:ARG:601 >= 5
					; ガードキル
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:氷結ガードブレイク]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_火炎耐性対応
				; 弱点持ちがいないか反射持ちが混ざっている
				ELSE
					; 火炎全体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:プロミネンス]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_火炎全体
				ENDIF
			ENDIF
		CASE 1
			; 火炎で1more
			IF CFLAG:ARG:１moreフラグ == 2
				; 火炎全体
				CALL EVENT_16_SET_ACTION, ARG, [[スキル:プロミネンス]]
				DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:1more_火炎全体
			ELSE
				RESULT = EVENT_16_SEARCH_REGISTANCE(2, "氷結", 1, 1)
				DEBUGPRINTFORML 氷結耐性検索結果:{RESULT}
				; 弱点持ちがいる
				IF RESULT > 0
					; 氷結単体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:コキュートス]], RESULT
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_氷結単体
				; 全員が無効か吸収
				ELSEIF (RESULT == -2 || (RESULT == -1 && RAND:2)) && CFLAG:ARG:601 >= 5
					; ガードキル
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:電撃ガードブレイク]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_氷結耐性対応
				; 弱点持ちがいないか反射持ちが混ざっている
				ELSE
					; 氷結全体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:ニブルヘイム]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_氷結全体
				ENDIF
			ENDIF
		CASE 2
			; 氷結で1more
			IF CFLAG:ARG:１moreフラグ == 2
				; 氷結全体
				CALL EVENT_16_SET_ACTION, ARG, [[スキル:ニブルヘイム]]
				DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:1more_氷結全体
			ELSE
				RESULT = EVENT_16_SEARCH_REGISTANCE(2, "電撃", 1, 1)
				DEBUGPRINTFORML 電撃耐性検索結果:{RESULT}
				; 弱点持ちがいる
				IF RESULT > 0
					; 電撃単体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:真理の雷]], RESULT
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_電撃単体
				; 全員が無効か吸収
				ELSEIF (RESULT == -2 || (RESULT == -1 && RAND:2)) && CFLAG:ARG:601 >= 5
					; ガードキル
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:疾風ガードブレイク]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_電撃耐性対応
				; 弱点持ちがいないか反射持ちが混ざっている
				ELSE
					; 電撃全体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:エル・ジハード]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_電撃全体
				ENDIF
			ENDIF
		CASE 3
			; 電撃で1more
			IF CFLAG:ARG:１moreフラグ == 2
				; 電撃全体
				CALL EVENT_16_SET_ACTION, ARG, [[スキル:エル・ジハード]]
				DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:1more_電撃全体
			ELSE
				RESULT = EVENT_16_SEARCH_REGISTANCE(2, "疾風", 1, 1)
				DEBUGPRINTFORML 疾風耐性検索結果:{RESULT}
				; 弱点持ちがいる
				IF RESULT > 0
					; 疾風単体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:万物流転]], RESULT
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_疾風単体
				; 全員が無効か吸収
				ELSEIF (RESULT == -2 || (RESULT == -1 && RAND:2)) && CFLAG:ARG:601 >= 5
					; ガードキル
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:火炎ガードブレイク]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_疾風耐性対応
				; 弱点持ちがいないか反射持ちが混ざっている
				ELSE
					; 疾風全体
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:神空波]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_疾風全体
				ENDIF
			ENDIF
		CASE 4
			; 疾風で1more
			IF CFLAG:ARG:１moreフラグ == 2
				; 疾風全体
				CALL EVENT_16_SET_ACTION, ARG, [[スキル:神空波]]
				DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:1more_疾風全体
			ELSE
				IF CFLAG:ARG:601 == 7
					RESULT = EVENT_16_SEARCH_REGISTANCE(2, "剣撃", 1, 0)
					DEBUGPRINTFORML 剣撃耐性検索結果:{RESULT}
					; 剣撃が通らないならガードブレイク
					IF RESULT == -2 || (RESULT == -1 && RAND:2)
						CALL EVENT_16_SET_ACTION, ARG, [[スキル:剣撃ガードブレイク]]
						DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_剣撃耐性対応
					ELSE
						CALL EVENT_16_SET_ACTION, ARG, [[スキル:気合]]
						DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_チャージ
					ENDIF
				ELSE
					CALL EVENT_16_SET_ACTION, ARG, [[スキル:コンセントレイト]]
					DEBUGPRINTFORML 行動{CFLAG:ARG:600, 4}回目:ローテーション{CFLAG:ARG:602}_コンセ
				ENDIF
			ENDIF
		CASE 5
			IF CFLAG:ARG:601 == 7
				; 八艘飛び
				CALL EVENT_16_SET_ACTION, ARG, [[スキル:八艘飛び]]
				DEBUGPRINTFORML 行動{CFLAG:ARG:601,4}回目:ローテーション{CFLAG:ARG:602}_HASSO-BEAT
			ELSE
				; メギドラオン
				CALL EVENT_16_SET_ACTION, ARG, [[スキル:メギドラオン]]
				DEBUGPRINTFORML 行動{CFLAG:ARG:601,4}回目:ローテーション{CFLAG:ARG:602}_通常メギドラオン
			ENDIF
	ENDSELECT
ENDIF
CFLAG:ARG:600++
RETURN 0

; 相性変化
; ステータス変化はオミット
; ARG   変更対象キャラ
; ARG:1 変更ペルソナ番号
; ARG:2 現在のペルソナ番号
; ARG:3 変更を表示するかどうか
@EVENT_16_PCHANGE, ARG, CHANGE_P, ARG:1 = 0
#DIM CHANGE_P, 1
; 現況と変更先のペルソナ番号が同じならスキップ
SIF CHANGE_P == CFLAG:ARG:603
	RETURN 0
SELECTCASE CHANGE_P
	CASE 0
		; ペルソナ無し
		; 第一段階のみ
		LOCALS = P・チェンジ
		BASE:ARG:剣撃 = 100
		BASE:ARG:飛具 = 100
		BASE:ARG:打撃 = 100
		BASE:ARG:戦技 = 100
		BASE:ARG:火炎 = 100
		BASE:ARG:氷結 = 100
		BASE:ARG:電撃 = 100
		BASE:ARG:衝撃 = 100
		BASE:ARG:神経 = 100
		BASE:ARG:精神 = 100
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 100
		BASE:ARG:地変 = 100
		BASE:ARG:水撃 = 100
		BASE:ARG:疾風 = 100
		BASE:ARG:重力 = 100
		BASE:ARG:核熱 = 100
		BASE:ARG:万能 = 100
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
	CASE 1
		; 剛毅 ジークフリード (Fire)
		LOCALS = P・チェンジ(剛毅 ジークフリード)
		BASE:ARG:剣撃 = 100
		BASE:ARG:飛具 = 100
		BASE:ARG:打撃 = 100
		BASE:ARG:戦技 = 100
		BASE:ARG:火炎 = -200
		BASE:ARG:氷結 = 100
		BASE:ARG:電撃 = 100
		BASE:ARG:衝撃 = 100
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = 100
		BASE:ARG:水撃 = 100
		BASE:ARG:疾風 = 100
		BASE:ARG:重力 = 100
		BASE:ARG:核熱 = -50
		BASE:ARG:万能 = 25
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
	CASE 2
		; 愚者 ロキ (Ice)
		LOCALS = P・チェンジ(愚者 ロキ)
		BASE:ARG:剣撃 = 100
		BASE:ARG:飛具 = 100
		BASE:ARG:打撃 = 100
		BASE:ARG:戦技 = 100
		BASE:ARG:火炎 = 100
		BASE:ARG:氷結 = -200
		BASE:ARG:電撃 = 100
		BASE:ARG:衝撃 = 100
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = 100
		BASE:ARG:水撃 = -50
		BASE:ARG:疾風 = 100
		BASE:ARG:重力 = 100
		BASE:ARG:核熱 = 100
		BASE:ARG:万能 = 25
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
	CASE 3
		; 皇帝 オオクニヌシ (Elect)
		LOCALS = P・チェンジ(皇帝 オオクニヌシ)
		BASE:ARG:剣撃 = 100
		BASE:ARG:飛具 = 100
		BASE:ARG:打撃 = 100
		BASE:ARG:戦技 = 100
		BASE:ARG:火炎 = 100
		BASE:ARG:氷結 = 100
		BASE:ARG:電撃 = -200
		BASE:ARG:衝撃 = 100
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = -50
		BASE:ARG:水撃 = 100
		BASE:ARG:疾風 = 100
		BASE:ARG:重力 = 100
		BASE:ARG:核熱 = 100
		BASE:ARG:万能 = 25
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
	CASE 4
		; 塔 クー・フーリン (Wind)
		LOCALS = P・チェンジ(塔 クー・フーリン)
		BASE:ARG:剣撃 = 100
		BASE:ARG:飛具 = 100
		BASE:ARG:打撃 = 100
		BASE:ARG:戦技 = 100
		BASE:ARG:火炎 = 100
		BASE:ARG:氷結 = 100
		BASE:ARG:電撃 = 100
		BASE:ARG:衝撃 = -50
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = 100
		BASE:ARG:水撃 = 100
		BASE:ARG:疾風 = -200
		BASE:ARG:重力 = 100
		BASE:ARG:核熱 = 100
		BASE:ARG:万能 = 25
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
	CASE 5
		; 皇帝 オベロン (コンセントレイト, チャージ, 9999メギドラオン)
		; 万能以外無効。万能1/4
		; 食いしばりフラグが立っている場合は全無効
		; ガードキルで殴ることもできるが……
		LOCALS = P・チェンジ(皇帝 オベロン)
		BASE:ARG:剣撃 = 0
		BASE:ARG:飛具 = 0
		BASE:ARG:打撃 = 0
		BASE:ARG:戦技 = 0
		BASE:ARG:火炎 = 0
		BASE:ARG:氷結 = 0
		BASE:ARG:電撃 = 0
		BASE:ARG:衝撃 = 0
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = 0
		BASE:ARG:水撃 = 0
		BASE:ARG:疾風 = 0
		BASE:ARG:重力 = 0
		BASE:ARG:核熱 = 0
		BASE:ARG:万能 = 25
		SIF CFLAG:ARG:601 == 99
			BASE:ARG:万能 = 0
		BASE:ARG:サイコ = 0
		CALL SYNC_STATUS, ARG
	CASE 6
		; 審判 アルダー (ディアラハン, メギドラオン)
		; 全耐性。
		LOCALS = P・チェンジ(審判 アルダー)
		BASE:ARG:剣撃 = 25
		BASE:ARG:飛具 = 25
		BASE:ARG:打撃 = 25
		BASE:ARG:戦技 = 25
		BASE:ARG:火炎 = 25
		BASE:ARG:氷結 = 25
		BASE:ARG:電撃 = 25
		BASE:ARG:衝撃 = 25
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = 25
		BASE:ARG:水撃 = 25
		BASE:ARG:疾風 = 25
		BASE:ARG:重力 = 25
		BASE:ARG:核熱 = 25
		BASE:ARG:万能 = 25
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
	CASE 7
		; 星 ルシフェル (明けの明星)
		; 物理と万能、破魔呪殺以外を吸収
		; ただし行動パターンの関係上、これで攻撃を受けることはほぼない
		LOCALS = P・チェンジ(星 ルシフェル)
		BASE:ARG:剣撃 = 100
		BASE:ARG:飛具 = 100
		BASE:ARG:打撃 = 100
		BASE:ARG:戦技 = 100
		BASE:ARG:火炎 = -100
		BASE:ARG:氷結 = -100
		BASE:ARG:電撃 = -100
		BASE:ARG:衝撃 = -100
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = -100
		BASE:ARG:水撃 = -100
		BASE:ARG:疾風 = -100
		BASE:ARG:重力 = -100
		BASE:ARG:核熱 = -100
		BASE:ARG:万能 = 25
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
	CASE 8
		; 塔 ヨシツネ (八艘飛び)
		; 物理吸収
		LOCALS = P・チェンジ(塔 ヨシツネ)
		BASE:ARG:剣撃 = -200
		BASE:ARG:飛具 = -100
		BASE:ARG:打撃 = -100
		BASE:ARG:戦技 = -100
		BASE:ARG:火炎 = 100
		BASE:ARG:氷結 = 100
		BASE:ARG:電撃 = 100
		BASE:ARG:衝撃 = 100
		BASE:ARG:神経 = 0
		BASE:ARG:精神 = 0
		BASE:ARG:破魔 = 0
		BASE:ARG:呪殺 = 0
		BASE:ARG:地変 = 100
		BASE:ARG:水撃 = 100
		BASE:ARG:疾風 = 100
		BASE:ARG:重力 = 100
		BASE:ARG:核熱 = 100
		BASE:ARG:万能 = 25
		BASE:ARG:サイコ = 100
		CALL SYNC_STATUS, ARG
ENDSELECT
; 変更ペルソナの表示
IF ARG:1 == 0
	CUSTOMDRAWLINE =
	SETCOLOR 0xff0033
	PRINTFORML 　                            　┏>ACT━━━━━━━━━━━━━━━━━━┓
	PRINTFORML 　                            　┃%LOCALS + "", 40, LEFT%┃
	PRINTFORML 　                            　┗━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
ENDIF
CFLAG:ARG:603 = CHANGE_P
RETURN 0

; 行動セット
@EVENT_16_SET_ACTION, ARG, ARG:1, ARG:2 = -1
; MP全快
BASE:ARG:ＭＰ = MAXBASE:ARG:ＭＰ
CFLAG:ARG:入力行動 = ARG:1
IF ARG:2 == -1
	CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動
ELSE
	CFLAG:ARG:ターゲット = ARG:2
ENDIF
CALL CHECK_ACTIONABLE, ARG, CFLAG:ARG:入力行動
IF RESULT != 1
	CFLAG:ARG:入力行動 = [[スキル:ＡＴＴＡＣＫ]]
	CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動
ENDIF
RETURN 0

; ほんまベルベットルームの従者はドＳやでぇ……
@ENEMY_BATTLE_MESSAGE_K4574, ARG
IF CFLAG:ARG:入力行動 == [[スキル:9999メギドラオン]]
	CALL EVENT_16_PCHANGE, ARG, 5
; メッセージ出力
ELSEIF CFLAG:ARG:601 == 1 || CFLAG:ARG:601 == 5
	; 初回
	IF CFLAG:ARG:601 == 1
		IF CFLAG:ARG:604 < 1
			CALL EVENT_16_PCHANGE, ARG, 0, 1
			SETCOLOR 0xff0033
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃戦いの熱と緊張感が、体を満たしていく……　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
			PRINTL 
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃さあ、始めましょう。　　　　　　　　　　　　┃
			PRINTL ┃私を失望させないでちょうだい！　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
			RESETCOLOR
			PRINTL 
			CUSTOMDRAWLINE =
			PRINTL 
			CFLAG:ARG:604 = 1
		ENDIF
	; ディアラハン後
	ELSE
		CALL EVENT_16_PCHANGE, ARG, 6
	ENDIF
ELSEIF CFLAG:ARG:601 == 2 && CFLAG:ARG:604 < 2
	CALL ENEMY_BATTLE_MESSAGE_K4574_LOTATION, ARG
	SETCOLOR 0xff0033
	PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTL ┃今のうちに言っておくわ。　　　　　　　　　　┃
	PRINTL ┃私を気遣う心は捨てなさい。　　　　　　　　　┃
	PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
	PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
	PRINTL 
	PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTL ┃殺す気で挑むことこそ敬意。　　　　　　　　　┃
	PRINTL ┃手加減は侮辱よ。　　　　　　　　　　　　　　┃
	PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
	PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	PRINTL 
	CUSTOMDRAWLINE =
	PRINTL 
	CFLAG:ARG:604 = 2
ELSEIF CFLAG:ARG:601 == 4 && CFLAG:ARG:604 < 3
	CALL EVENT_16_PCHANGE, ARG, 7
	SETCOLOR 0xff0033
	PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTL ┃フゥ……この高揚感……　　　　　　　　　　　┃
	PRINTL ┃いつ以来かしら。　　　　　　　　　　　　　　┃
	PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
	PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
	PRINTL 
	PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
	PRINTL ┃悪く思わないで……　　　　　　　　　　　　　┃
	PRINTL ┃ますます貴方を倒したくなってきたわ！　　　　┃
	PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
	PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
	RESETCOLOR
	PRINTL 
	CUSTOMDRAWLINE =
	PRINTL 
	CFLAG:ARG:604 = 3
ELSEIF CFLAG:ARG:601 == 5
	CALL EVENT_16_PCHANGE, ARG, 6
ELSE
	CALL ENEMY_BATTLE_MESSAGE_K4574_LOTATION, ARG
	IF CFLAG:ARG:601 == 7 && CFLAG:ARG:604 < 4
		SETCOLOR 0xff0033
		PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
		PRINTL ┃ハァ。ハァ……最高よ。エリザベスでも　　　　┃
		PRINTL ┃ここまで私を追い詰めたことはないわ。　　　　┃
		PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
		PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
		PRINTL 
		PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
		PRINTL ┃さあ、今こそ見せてちょうだい……　　　　　　┃
		PRINTL ┃戦いの、その先にあるものを……！　　　　　　┃
		PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
		PRINTW ┗━━━━━━━━━━━━━━━━━━━━━━┛
		RESETCOLOR
		PRINTL 
		CUSTOMDRAWLINE =
		PRINTL 
		CFLAG:ARG:604 = 4
		; 不屈の闘志を外す
		ABL:ARG:スキル8 = 0
	ELSE
		SETCOLOR 0xff0033
		IF RAND:3 == 0
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃倒れちゃダメよ？　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┗━━━━━━━━━━━━━━━━━━━━━━┛
		ELSEIF RAND:2 == 0
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃受けなさい！　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┗━━━━━━━━━━━━━━━━━━━━━━┛
		ELSE
			PRINTL ┏>MESSAGE━━━━━━━━━━━━━━━━━━┓
			PRINTL ┃見てみなさい！　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┃　　　　　　　　　　　　　　　　　　　　　　┃
			PRINTL ┗━━━━━━━━━━━━━━━━━━━━━━┛
		ENDIF
		PRINTL 
		CUSTOMDRAWLINE =
		PRINTL 
		RESETCOLOR
	ENDIF
ENDIF
RETURN 0

@ENEMY_BATTLE_MESSAGE_K4574_LOTATION, ARG
SELECTCASE CFLAG:ARG:602
	CASE 0
		SIF !CFLAG:ARG:１moreフラグ
			CALL EVENT_16_PCHANGE, ARG, 1
	CASE 1
		SIF !CFLAG:ARG:１moreフラグ
			CALL EVENT_16_PCHANGE, ARG, 2
	CASE 2
		SIF !CFLAG:ARG:１moreフラグ
			CALL EVENT_16_PCHANGE, ARG, 3
	CASE 3
		SIF !CFLAG:ARG:１moreフラグ
			CALL EVENT_16_PCHANGE, ARG, 4
	CASE 4
		SIF !CFLAG:ARG:１moreフラグ
			CALL EVENT_16_PCHANGE, ARG, 5
	CASE 5
		IF !CFLAG:ARG:１moreフラグ
			IF CFLAG:ARG:601 < 7
				CALL EVENT_16_PCHANGE, ARG, 6
			ELSE
				CALL EVENT_16_PCHANGE, ARG, 8
			ENDIF
		ENDIF
ENDSELECT
CFLAG:ARG:602 = (CFLAG:ARG:602 + 1) % 6
RETURN 0

;-----------------------------------------------------------
; 相性走査関数　@EVENT_16_SEARCH_REGISTANCE(ARG, ARGS, ARG:1 = 0, ARG:2 = 0)
; 
; 引数
;  ARG   攻撃範囲。0 == 前列(1〜3), 1 == 後列(4〜6), 2 == 全体(1〜6)
;  ARGS  相性名
;  ARG:1 カーン、壁、ガードキルを考慮するか？(1 == する)
;  ARG:2 カーンを考慮する場合の攻撃種別 (0 == 物理, 1 == 魔法)
;
; 戻り値
;  -999 引数の設定ミスによる処理中断
;  -3   反射持ちキャラがいる
;  -2   吸収のみ
;  -1   無効のみ
;  0    弱点持ちキャラがいない
;  >= 1 弱点持ちキャラのポジション
;-----------------------------------------------------------
@EVENT_16_SEARCH_REGISTANCE(ARG, ARGS, ARG:1 = 0, ARG:2 = 0)
#FUNCTION
#DIM COM, 7
#LOCALSIZE 6
; 変数初期化
VARSET LOCAL, 0
VARSET COM, -999
; 走査範囲設定
SELECTCASE ARG
	CASE 0
		LOCAL:2 = 1
		LOCAL:3 = 4
	CASE 1
		LOCAL:2 = 4
		LOCAL:3 = 7
	CASE 2
		LOCAL:2 = 1
		LOCAL:3 = 7
ENDSELECT
; 範囲チェック
SIF !LOCAL:2 && !LOCAL:3
	RETURNF -999
; 走査処理
FOR LOCAL, LOCAL:2, LOCAL:3
	; 存在しないか死んでいるならスキップ
	SIF POS(LOCAL) < 0 || GET_STATE(CFLAG:(POS(LOCAL)):ステート) == "DYING"
		CONTINUE
	LOCAL:1 = GETNUM(BASE, ARGS)
	; カーン
	; 反射扱いにする
	IF ARG:1 && ARGS != "万能" && (ARG:2 == 0 && CFLAG:(POS(LOCAL)):物理反射フラグ) || (ARG:2 == 1 && CFLAG:(POS(LOCAL)):魔法反射フラグ)
		COM:(LOCAL - 1) = 999
		CONTINUE
	; 壁
	; 無効未満なら無効扱いにする
	ELSEIF ARG:1 && CFLAG:(POS(LOCAL)):(1130 + GET_TYPE_NUM(ARGS)) > 0 && MAXBASE:(POS(LOCAL)):(LOCAL:1) > 0
		COM:(LOCAL - 1) = 0
		CONTINUE
	; ガードキル
	; 耐性以上なら等倍扱いにする
	ELSEIF ARG:1 && CFLAG:(POS(LOCAL)):(1270 + GET_TYPE_NUM(ARGS)) > 0 && MAXBASE:(POS(LOCAL)):(LOCAL:1) < 100
		COM:(LOCAL - 1) = 100
		CONTINUE
	; 防御
	; 1moreが取れないので等倍扱いにする
	ELSEIF ARG:1 && CFLAG:(POS(LOCAL)):防御フラグ && MAXBASE:(POS(LOCAL)):(LOCAL:1) > 100
		COM:(LOCAL - 1) = 100
		CONTINUE
	ENDIF
	COM:(LOCAL - 1) = MAXBASE:(POS(LOCAL)):(LOCAL:1)
NEXT
; 変数セット
LOCAL:4 = -999
FOR LOCAL, 0, 6
	IF LOCAL:4 < COM:LOCAL
		LOCAL:4 = COM:LOCAL
		LOCAL:5 = LOCAL
	ENDIF
NEXT
SIF LOCAL:4 == 999
	RETURNF -3
SIF LOCAL:4 < 0
	RETURNF -2
SIF LOCAL:4 == 0
	RETURNF -1
SIF LOCAL:4 > 100
	RETURNF LOCAL:5 + 1
RETURNF 0


