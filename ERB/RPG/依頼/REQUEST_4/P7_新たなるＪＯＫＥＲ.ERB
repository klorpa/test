;====================
;関連フラグリスト
;====================
;====================
;条件
;====================
;====================
;説明
;====================
@EXPLAIN_REQUEST_4_P7
PRINTL 
PRINTFORML ペルソナ使い 芹沢 うららから電話で依頼があった
PRINTFORML 彼女を止めて欲しいという
PRINTFORML どういうことかよくわからないが、
PRINTFORML とにかく指示されたフィットネスクラブへ向かおう
PRINTL 
;====================
;内容
;====================
@REQUEST_4_P7
#LOCALSIZE 2
FLAG:逃走不可フラグ = 1
FLAG:会話不能フラグ = 1
FLAG:先制不意打ちキャンセル = 1
CUSTOMDRAWLINE =
PRINTW ＞依頼者であるうららがいるフィットネスクラブＧＯＬＤへとやってきた
PRINTW ＞珠閒瑠市夢崎区にあるこのフィットネスクラブはなかなか設備も整っており、
PRINTW ＞なかなか繁盛しているらしい……
PRINTW ＞が、中は騒然としている……客の一人がＪＯＫＥＲとなって暴れているというのだ
PRINTL 
PRINTL インストラクター:
PRINTL 「えっと、今は危ないですよ
PRINTW 　そこがジムになってるんですが、ＪＯＫＥＲが暴れてて……」
PRINTL 「うちの職員も何人かぼこぼこにされちゃって…
PRINTL 　しかし、ワンロン千鶴の言ったとおり、ＪＯＫＥＲ呪いをした人って
PRINTW 　ほんとにあんなんになっちゃうんですね……」
PRINTL 「え、あなたはサマナーなんですか？
PRINTL 　よかった。それじゃあ、どうにかしてください
PRINTW 　今ドアを開けますね」
PRINTL 
PRINTFORMW ＞%CALLNAME:MASTER%は扉の中に足を踏み入れた
PRINTFORMW ＞中にいるのは、間違いなく芹沢 うららだ
PRINTFORML ＞しかし、その顔は七姉妹学園で出会った少女と同じく白塗りになっており 
PRINTFORMW ＞彼女の背後には、以前見たものより大きく成長した異形のペルソナ『ＪＯＫＥＲ』が見える…
PRINTFORMW ＞
PRINTL 
PRINTL ＪＯＫＥＲうらら:
PRINTW 「あら、サマナーさん来てくれたの」
PRINTW 「でも、私忙しいのよぉ」
PRINT 「そう、マーヤを殺しにいかなきゃいけないの
CALL HEARTB
PRINTW 　あははははははは！」
CALL SET_ENEMY,9, [[キャラ:ＪＯＫＥＲうらら]], 32, 0 , 1
LOCAL = CHARANUM - 1
LOCAL:1 = FLAG:戦闘難易度 - 3 > 0 ? 120 + (FLAG:戦闘難易度 - 3) * 20 # 100
CALL SET_BATTLE_STATUS(LOCAL, 40 * LOCAL:1, "ＨＰ")
CALL SET_BATTLE_STATUS(LOCAL, 20 * LOCAL:1, "ＭＰ")
CSTR:LOCAL:思考パターン = ＪＯＫＥＲうらら
CFLAG:LOCAL:行動回数 = 2
CFLAG:LOCAL:速度補正 = 15
CALL SYNC_STATUS, (LOCAL)
CALL HEALTH_CHARA, (LOCAL)
CALL BATTLE_START
CUSTOMDRAWLINE =
WAIT
PRINTW ＞ＪＯＫＥＲうららは力尽きた…
PRINTFORMW ＞%CALLNAME:MASTER%は倒れたうららに駆け寄り、抱き起こした
PRINTW ＞その瞬間、以前七姉妹学園であった仮面の男フィレモンの声が脳裏に響く…
PRINTL 
PRINTL フィレモン:
PRINTL 「天野 舞耶の友人か……
PRINTW 　君はやはり、奴が張り巡らせた悪しき運命に自ら飛び込んでいってる」
PRINTL 「このＪＯＫＥＲの増殖も奴の罠の一部に過ぎない
PRINTW 　私の力で、今彼女を助けるがしかし、奴の真の狙いは別のところにある」
PRINTL 「そうこの世界における奴の力の鍵となる特異点は別にあるのだ
PRINTW 　奴と戦うのなら、その特異点に気をつけねばならない」
PRINTW 「そして、その特異点のひとつが天野 舞耶なのだ……」
PRINTW 「……ぐ……今はこれ以上、君の運命に干渉はできないか」
PRINTL 「しかし、君の勝利により、奴のつむぐ運命は綻びはじめている…
PRINTW 　いずれ、奴の真の狙いを君に伝えられる日も来るだろう……」
PRINTL 
PRINTW ＞フィレモンの声は聞こえなくなる……
PRINTL 
PRINTL うらら:
PRINTW 「うぅ……」
PRINTW 「あ、サマナーさん……」
PRINTL 「……どうやら助けてもらったみたいね
PRINTL 　やっぱり、助けを求めて正解だったみたい」
PRINTW 　ありがとう」
PRINTL 
PRINTW ＞うららは立ち上がると、礼を言った
PRINTW ＞その顔は真っ青だ…
PRINTL 
PRINTL うらら:
PRINTW 「……ＪＯＫＥＲ占いなんてして、軽蔑したよね」
CALL INPUT_YN("………", "何故天野 舞耶を？")
IF RESULT == 0
	PRINTL うらら:
	PRINTL 「はは、ほんと自分が嫌になっちゃう
	PRINTW 　お礼はしっかりと振り込ませてもらうから…それじゃあ…」
	CALL INPUT_YN("黙って見送る", "捕まえて奴隷にする")
	IF RESULT == 0
		PRINTW ＞うららは去っていった
		PRINTW ＞\50000が振り込まれた
		MONEY += 50000
	ELSE
		PRINTL うらら:
		PRINTW 「え、ちょっと、何を…！」
		PRINTL 
		PRINTW ＞芹沢 うららを手に入れた
		PRINTW ＞また、ＧＯＬＤから迷惑を解決した礼金として\25000が振り込まれた
		CALL ADD_NEW_COMPANION, [[キャラ:うらら]],  200, 1
	ENDIF
ELSE
	PRINTL うらら:
	PRINTL 「私が誰を殺して欲しかったかも知られちゃってたのか…
	PRINTW 　友達にＪＯＫＥＲ呪いするなんてほんと最低よね」
	PRINTL 「まさか…本当だなんて…思ってなかったら…
	PRINTW 　酔った勢いで、マーヤ相手に呪いしちゃったの…」
	PRINTL 「私さ…何やっても…上手くいかなくて…
	PRINTL 　習い事したり…資格取ったりしてみても…
	PRINTW 　結局…やりたいこと…見つからなくて…」
	PRINTL 「マーヤはあの通り雑誌記者で、やりたい仕事を頑張ってて…
	PRINTL 　私に無いもの…全部もってるようで…
	PRINTW 　私…大バカだ…」
	PRINTL 「……そうだ。サマナーさん
	PRINTW 　私にサマナーさんのお仕事手伝わせて…」
	PRINTL 「サマナーさんはＪＯＫＥＲを追ってるんでしょ？
	PRINTW 　マーヤもまだＪＯＫＥＲを追っかける気まんまんだったから」
	PRINTL 「きっと、サマナーさんを手伝えば、マーヤを助けることになる
	PRINTL 　こんなバカなことしちゃって、なんにもない私だけど
	PRINTW 　せめて、そのくらいの罪滅ぼしはしたい…」
	PRINTL [0] 手伝うことを許可する
	PRINTL [1] 手伝うことを許可しない
	PRINTL [2] 直接天野 舞耶の力になることを薦める
	CALL INPUTINT(0,1,2)
	IF RESULT == 0
		PRINTL うらら:
		PRINTW 「ありがとう…こんな私だけど、これからよろしく」
		PRINTL 
		PRINTW ＞芹沢 うららが仲間になった
		CALL ADD_NEW_COMPANION, [[キャラ:うらら]],  500
		SIF RESULT
			CFLAG:(RESULT:1):戦闘参加不可能 = -1 
	ELSEIF RESULT == 1
		PRINTL うらら:
		PRINTL 「そうよね。あはは、ほんとバカな事頼んで…
		PRINTW 　迷惑ばっかりかけてごめんね…」
		PRINTL 
		PRINTW ＞うららは悄然として去っていった…
	ELSE
		PRINTL うらら:
		PRINTW 「そんなことできるわけが……」
		PRINTL 「ううん、これはマーヤと私の問題だものね…
		PRINTW 　ちゃんと私達の間で決着つけないとね…」
		PRINTL 「私にとって、マーヤは大切な友達だと思う…
		PRINTL 　でも、マーヤを妬んでいたのも本当
		PRINTW 　私は今回のことで、そのことを思い知った…」
		PRINTL 「とにかく、そのことをマーヤにも打ち明けてみる
		PRINTW 　マーヤが自分を殺されることを望むような友達をどう思うかはわからないけど…」
		PRINTW 「それでも、ここでこのことを隠して置いたまま、別れたらきっと後悔するだろうから」
		PRINTW 「ありがとう。サマナーさん。おかげで助かった…と思う」
		PRINTL 
		PRINTW >うららは礼をいうと去っていった…
		IF STRFLAG_NUM_EV("フィレモン進行", , , , [[イベント:ペルソナ２]]) == 5
			CALLF STRFLAG_EV("うらら激励", 1, [[イベント:ペルソナ２]])
			CALLF STRFLAG_NUM_EV("フィレモン進行", "=", , 6, [[イベント:ペルソナ２]])
		ENDIF
	ENDIF
	PRINTW ＞報酬として\50000が振り込まれた
	MONEY+=50000
ENDIF

;-------------------------------
;ＪＯＫＥＲうらら
;ダークネビュラつくろうかと思ったけど
;ここで特大いれるとさすがに威力あれだし、ストームナイトメアで妥協
;全体８割死亡とか、壮絶だけど、まぁTetrajammerで防げるし、一応使用ターンに法則持たせて回避可能に
;-------------------------------
@SET_ACTION_ＪＯＫＥＲうらら, ARG
;HP減少による確率補正を作成
LOCAL = (100 - 現HP割合(ARG)) / 5
;入力行動を-1とターゲットを-1にしておく
CFLAG:ARG:入力行動 = -1
CFLAG:ARG:ターゲット = -1
IF CFLAG:ARG:１moreフラグ == 2
	SELECTCASE RAND:100
		CASE 0 TO LOCAL * 2
			CFLAG:ARG:入力行動 = [[スキル:マハ・ガルーラ]]
		CASE LOCAL * 2 TO LOCAL * 4
			CFLAG:ARG:入力行動 = [[スキル:竜巻]]
		CASE LOCAL * 4 TO LOCAL * 6
			CFLAG:ARG:入力行動 = [[スキル:メテオフィスト]]
	ENDSELECT
ELSEIF CFLAG:ARG:行動した回数 == 0
	CALL OLDMADE, ARG
	SELECTCASE RAND:100
		CASE IS < 30
			CFLAG:ARG:入力行動 = [[スキル:パニックボイス]]
		CASE IS < 50 + LOCAL
			TSTR = オールドメイド
			;オールドメイド
			CFLAG:ARG:入力行動 = [[スキル:ＤＵＭＭＹ]]
	ENDSELECT
ELSE
	IF FLAG:経過ターン数 % 4 == 3
		CFLAG:ARG:入力行動 = [[スキル:ストームナイトメア]]
	ELSE
		SELECTCASE RAND:100
			CASE IS < 20
				CFLAG:ARG:入力行動 = [[スキル:マハ・ガルーラ]]
			CASE IS < 35
				CFLAG:ARG:入力行動 = [[スキル:竜巻]]
			CASE IS < 50
				CFLAG:ARG:入力行動 = [[スキル:メテオフィスト]]
		ENDSELECT
	ENDIF
ENDIF
;ここまで入力行動が決まってなかったら、汎用3種に行く
;GUNは特殊弾でも撃って来やすいので減らしとく
IF CFLAG:ARG:入力行動 == -1
	SELECTCASE RAND:100
		CASE IS < 25
			CFLAG:ARG:入力行動 = [[スキル:ガルダイン]]
		CASE IS < 50
			CFLAG:ARG:入力行動 = [[スキル:怒りの鉄拳]]
		CASEELSE
			CFLAG:ARG:入力行動 = 0
	ENDSELECT
ENDIF
;アクション実行不能なら自動的に通常攻撃にする
CALL CHECK_ACTIONABLE, ARG, CFLAG:ARG:入力行動
IF RESULT == 0
	CFLAG:ARG:入力行動 = 0
	CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動
ENDIF
;ここまでターゲットが決まってなかったら、ランダムターゲットをよんで決定
SIF CFLAG:ARG:ターゲット == -1
	CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動
RETURN 1
;力技で、JOKERを最速行動させて、そこで処理を行う
@EVENT_BATTLE_TURNTOP_REQUEST4
LOCAL = GETCHARA([[キャラ:ＪＯＫＥＲうらら]])
SIF LOCAL == -1
	RETURN 0
SIF ABL:LOCAL:初期ペルソナ == -1
	RETURN 0
CFLAG:LOCAL:速度補正 += 50000

@ENEMY_BATTLE_MESSAGE_K4044, ARG
SKIPDISP 0
IF CFLAG:ARG:入力行動 == [[スキル:ＤＵＭＭＹ]]
	PRINTFORMW 味方の誰かにＪＯＫＥＲが憑依した！
	WHILE 1
		LOCAL = RAND:6 + 1
		SIF POS(LOCAL) >= 0 && GET_STATE(CFLAG:POS(LOCAL):ステート) != "DYING"
			BREAK
		LOCAL = 0
	WEND
	ABL:ARG:初期ペルソナ = POS(LOCAL)
ENDIF

