;===================================
;オルギア時の行動決定
;===================================
@SET_ACT_ORGY,ARG
$DECIDE_ACTION
VARSET LOCAL , 0
;ターゲット数確認
FOR LOCAL:50,0,10
	IF POS(7+LOCAL:50) > -1 && CFLAG:(POS(7+LOCAL:50)):ステート != GET_STATE_NUM("DYING")
		FOR LOCAL:51,0,4
			SELECTCASE MAXBASE:(POS(7+LOCAL:50)):(GET_TYPE(LOCAL:51))
				CASE IS < 0, 999
					SIF LOCAL:50 < 5
						LOCAL:(LOCAL:51 + 4) -= 1
					LOCAL:(LOCAL:51) -= 1
				CASE IS > 100
					IF LOCAL:50 < 5
						LOCAL:(LOCAL:51 + 4) += 3
						LOCAL:(LOCAL:51 + 75) = 1
					ENDIF
					LOCAL:(LOCAL:51) += 3
				CASE 0
				CASEELSE
					SIF LOCAL:50 < 5
						LOCAL:(LOCAL:51 + 4) += 1
					LOCAL:(LOCAL:51) += 1
			ENDSELECT
		NEXT
		SIF LOCAL:50 < 5
			LOCAL:53 += 1
		LOCAL:52 += 1
	ENDIF
NEXT
;PRINTFORML 敵前衛{LOCAL:53}、敵全体で{LOCAL:52}であります！
;PRINTFORMW {LOCAL:0} {LOCAL:1} {LOCAL:2} {LOCAL:3} {LOCAL:4} {LOCAL:5} {LOCAL:6} {LOCAL:7}
;スキル評価
;LOCAL:60-70

CALL CHECK_ACTIONABLE,ARG,0
IF RESULT == 1
	CALL SKILL_MINATTACKNUMBER_0,ARG
	LOCAL:54 = RESULT
	CALL SKILL_MAXATTACKNUMBER_0,ARG
	LOCAL:55 = RESULT
	CALL SKILL_SPHERE_0,ARG
	SELECTCASE RESULT
		CASE 1
			IF LOCAL:75 > 0
				LOCAL:56 = 3
			ELSEIF LOCAL:4 > 0
				LOCAL:56 = 1
			ELSE
				LOCAL:56 = 0
			ENDIF
		CASE 2
			LOCAL:56 = LOCAL:4
		CASE 3
			LOCAL:56 = LOCAL:0
		CASEELSE
			LOCAL:56 = 0
	ENDSELECT
	LOCAL:60 = LOCAL:56 * 100 * MAXBASE:ARG:(GET_BATTLESTATUS(0)) * (LOCAL:54 + LOCAL:55) / 2
ELSE
	LOCAL:60 = 0
ENDIF

CALL CHECK_ACTIONABLE,ARG,2101
IF RESULT == 1
	CALL SKILL_MINATTACKNUMBER_2101,ARG
	LOCAL:54 = RESULT
	CALL SKILL_MAXATTACKNUMBER_2101,ARG
	LOCAL:55 = RESULT
	CALL SKILL_SPHERE_2101,ARG
	SELECTCASE RESULT
		CASE 1
			IF LOCAL:76 > 0
				LOCAL:56 = 3
			ELSEIF LOCAL:5 > 0
				LOCAL:56 = 1
			ELSE
				LOCAL:56 = 0
			ENDIF
		CASE 2
		IF LOCAL:55 >= 3
			LOCAL:56 = LOCAL:5 / LOCAL:53
		ELSE
			LOCAL:56 = LOCAL:5
		ENDIF
		CASE 3
		IF LOCAL:55 >= 3
			LOCAL:56 = LOCAL:5 / LOCAL:52
		ELSE
			LOCAL:56 = LOCAL:1
		ENDIF
		CASEELSE
			LOCAL:56 = 0
	ENDSELECT
	LOCAL:61 = LOCAL:56 * 100 * MAXBASE:ARG:銃攻撃 * (LOCAL:54 + LOCAL:55) / 2
ELSE
	LOCAL:61 = 0
ENDIF

FOR LOCAL:50,0,FLAG:スキル数
	LOCALS = スキル{LOCAL:50+1}
	SIF ABL:ARG:LOCALS == 0
		BREAK
	RESULT = 1
	TRYCALLFORM SKILL_ACTIONABLE_BATTLE_{ABL:ARG:LOCALS}
	SIF RESULT == 0
		CONTINUE
	TRYCALLFORM SKILL_TARGET_{ABL:ARG:LOCALS}
	SIF RESULT != 1
		CONTINUE
	TRYCALLFORM SKILL_EFECT_{ABL:ARG:LOCALS}
	SIF RESULT != 1
		CONTINUE
	CALL CHECK_ACTIONABLE,ARG,ABL:ARG:LOCALS
	IF RESULT == 1
		CALLFORM SKILL_TYPE_{ABL:ARG:LOCALS},ARG
		LOCAL:71 = RESULT
		SIF LOCAL:71 >= 4
			CONTINUE
		CALLFORM SKILL_MINATTACKNUMBER_{ABL:ARG:LOCALS},ARG
		LOCAL:54 = RESULT
		CALLFORM SKILL_MAXATTACKNUMBER_{ABL:ARG:LOCALS},ARG
		LOCAL:55 = RESULT
		CALLFORM SKILL_SPHERE_{ABL:ARG:LOCALS},ARG
		SELECTCASE RESULT
			CASE 1
				IF LOCAL:(LOCAL:71 + 75) > 0
					LOCAL:56 = 3
				ELSEIF LOCAL:(LOCAL:71) > 0
					LOCAL:56 = 1
				ELSE
					LOCAL:56 = 0
				ENDIF
			CASE 2
				IF LOCAL:55 >= 3
					LOCAL:56 = LOCAL:(LOCAL:71 + 4) / LOCAL:53
				ELSE
					LOCAL:56 = LOCAL:(LOCAL:71 + 4)
				ENDIF
			CASE 3
				IF LOCAL:55 >= 3
					LOCAL:56 = LOCAL:(LOCAL:71) / LOCAL:52
				ELSE
					LOCAL:56 = LOCAL:(LOCAL:71)
				ENDIF
			CASEELSE
				LOCAL:56 = 0
		ENDSELECT
		CALLFORM SKILL_POWER_{ABL:ARG:LOCALS}, ARG
		LOCAL:57 = RESULT
		LOCAL:(LOCAL:50+62) = LOCAL:56 * LOCAL:57 * MAXBASE:ARG:(GET_BATTLESTATUS(0)) * (LOCAL:54 + LOCAL:55) / 2
	ENDIF
NEXT
;PRINTFORMW スキル評価 1.{LOCAL:60} 2.{LOCAL:61} 3.{LOCAL:62} 4.{LOCAL:63}  5.{LOCAL:64}
;PRINTFORMW 　　　　　 6.{LOCAL:65} 7.{LOCAL:66} 8.{LOCAL:67} 9.{LOCAL:68} 10.{LOCAL:69}
FOR LOCAL:50,0,10
	IF LOCAL:58 < LOCAL:(LOCAL:50+60)
		LOCAL:58 = LOCAL:(LOCAL:50+60)
		LOCAL:59 = LOCAL:50
	ENDIF
NEXT
SELECTCASE LOCAL:59
	CASE 0
		LOCAL:99 = 0
	CASE 1
		LOCAL:99 = 2101
	CASEELSE
		LOCALS = スキル{LOCAL:59-1}
		LOCAL:99 = ABL:ARG:LOCALS
ENDSELECT
		
IF LOCAL:58 == 0
	CFLAG:ARG:入力行動 = -1
	CFLAG:ARG:防御フラグ = 1
	RETURN 0
ENDIF

CFLAG:ARG:ターゲット = 0
CALLFORM SKILL_SPHERE_{LOCAL:99},ARG
IF RESULT == 1 && LOCAL:75+LOCAL:76+LOCAL:77+LOCAL:78
	CALLFORM SKILL_TYPE_{LOCAL:99},ARG
	FOR LOCAL:50,0,5
	IF POS(7+LOCAL:50) > -1 && CFLAG:(POS(7+LOCAL:50)):ステート != GET_STATE_NUM("DYING")
		IF MAXBASE:(POS(7+LOCAL:50)):(GET_TYPE(RESULT)) > 100
			CFLAG:ARG:ターゲット = 7+LOCAL:50
		ENDIF
	ENDIF
	NEXT	
ENDIF
SIF CFLAG:ARG:ターゲット <= 0
	CALL RANDOM_TARGET,ARG,LOCAL:99
CFLAG:ARG:入力行動 = LOCAL:99

