
;=======================================================================
;皇居
;ボス・イザナミ
;見るたびに陛下どんな所に住んでいらっしゃったんすかｗｗｗｗと突っ込みたくなる。
;ダンジョンナンバー6　ＭＡＰ0~2
;ダンジョンフラグ:(FLAG:現ダンジョン):0 イベント進行状況 1 イザナミ撃破
;ダンジョンフラグ:(FLAG:現ダンジョン):11〜　宝箱
;=======================================================================




;=======================================================================
;攻略条件
;=======================================================================
;四天王の試練クリア済み
@PREREQUISITE_DUNGEON_25
SIF 闘技場フラグ:1:0 == 0
	RETURN 0
RETURN 1



;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_25
RESULTS = Former Imperial Palace
;旧皇居


;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_25
PRINT Former Imperial Palace
;旧皇居　
SELECTCASE FLAG:現M
	CASE 0
		PRINT Ｂ１Ｆ
	CASE 1
		PRINT Ｂ２Ｆ
	CASE 2
		PRINT Ｂ３Ｆ
	CASE 3
		PRINT Ｂ４Ｆ
	CASE 4
		PRINT Ｂ５Ｆ
ENDSELECT


;=======================================================================
;特殊タイル表示
;=======================================================================
@TILE_MARK_25_25_25_25
RESULTS = Ｔ


;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_25,ARG
FLAG:現M = 0
FLAG:現X = 7
FLAG:現Y = 7
;CALL AUTOMAP

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_25



;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_25,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT
;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_25
SELECTCASE FLAG:現M
	CASE 0
		FLAG:最大X = 9
		FLAG:最大Y = 9
		CALL MAKE_FLOOR_LINE, 0,"000000000"
		CALL MAKE_FLOOR_LINE, 1,"011115110"
		CALL MAKE_FLOOR_LINE, 2,"01     10"
		CALL MAKE_FLOOR_LINE, 3,"0521 1250"
		CALL MAKE_FLOOR_LINE, 4,"01     10"
		CALL MAKE_FLOOR_LINE, 5,"0121 1210"
		CALL MAKE_FLOOR_LINE, 6,"01     20"
		CALL MAKE_FLOOR_LINE, 7,"0127   80"
		CALL MAKE_FLOOR_LINE, 8,"000000000"

	CASE 1
		FLAG:最大X = 9
		FLAG:最大Y = 9
		CALL MAKE_FLOOR_LINE, 0,"000000000"
		CALL MAKE_FLOOR_LINE, 1,"011511150"
		CALL MAKE_FLOOR_LINE, 2,"02  2  10"
		CALL MAKE_FLOOR_LINE, 3,"07  1  10"
		CALL MAKE_FLOOR_LINE, 4,"0 1121210"
		CALL MAKE_FLOOR_LINE, 5,"0 1 1  50"
		CALL MAKE_FLOOR_LINE, 6,"0      10"
		CALL MAKE_FLOOR_LINE, 7,"0  621110"
		CALL MAKE_FLOOR_LINE, 8,"000000000"
	
	CASE 2
		FLAG:最大X = 9
		FLAG:最大Y = 9
		CALL MAKE_FLOOR_LINE, 0,"000000000"
		CALL MAKE_FLOOR_LINE, 1,"0    7210"
		CALL MAKE_FLOOR_LINE, 2,"0   1  10"
		CALL MAKE_FLOOR_LINE, 3,"06 111250"
		CALL MAKE_FLOOR_LINE, 4,"01  1  10"
		CALL MAKE_FLOOR_LINE, 5,"01  7  10"
		CALL MAKE_FLOOR_LINE, 6,"01     10"
		CALL MAKE_FLOOR_LINE, 7,"011511150"
		CALL MAKE_FLOOR_LINE, 8,"000000000"
	
	CASE 3
		FLAG:最大X = 9
		FLAG:最大Y = 9
		CALL MAKE_FLOOR_LINE, 0,"000000000"
		CALL MAKE_FLOOR_LINE, 1,"011126  0"
		CALL MAKE_FLOOR_LINE, 2,"01      0"
		CALL MAKE_FLOOR_LINE, 3,"05 111  0"
		CALL MAKE_FLOOR_LINE, 4,"01 111  0"
		CALL MAKE_FLOOR_LINE, 5,"01 161 70"
		CALL MAKE_FLOOR_LINE, 6,"01     20"
		CALL MAKE_FLOOR_LINE, 7,"051115110"
		CALL MAKE_FLOOR_LINE, 8,"000000000"
	
	
	CASE 4
		FLAG:最大X = 17
		FLAG:最大Y = 20
		CALL MAKE_FLOOR_LINE, 0,"00000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0        11111110"
		CALL MAKE_FLOOR_LINE, 2,"0        1     20"
		CALL MAKE_FLOOR_LINE, 3,"0        111   60"
		CALL MAKE_FLOOR_LINE, 4,"0          2    0"
		CALL MAKE_FLOOR_LINE, 5,"01111111111111110"
		CALL MAKE_FLOOR_LINE, 6,"01             10"
		CALL MAKE_FLOOR_LINE, 7,"01 11111 11111 10"
		CALL MAKE_FLOOR_LINE, 8,"01 1 2       1 10"
		CALL MAKE_FLOOR_LINE, 9,"01 1 11111 1 1 10"
		CALL MAKE_FLOOR_LINE,10,"01 1 1   2 1 2 10"
		CALL MAKE_FLOOR_LINE,11,"01 1 1 1 1 1 1 10"
		CALL MAKE_FLOOR_LINE,12,"02 1 1 1 1 1 1 20"
		CALL MAKE_FLOOR_LINE,13,"01 1 1 2 2 1 1 10"
		CALL MAKE_FLOOR_LINE,14,"01 1 111 111 1 10"
		CALL MAKE_FLOOR_LINE,15,"01 1         1 10"
		CALL MAKE_FLOOR_LINE,16,"01 11111111111 10"
		CALL MAKE_FLOOR_LINE,17,"01       2     10"
		CALL MAKE_FLOOR_LINE,18,"01111121 11111110"
		CALL MAKE_FLOOR_LINE,19,"00000000000000000"
		
		
		;イベントの設置
		CALL SET_TILE,7,11,ダンジョンフラグ:(FLAG:現ダンジョン):0,0,4
		



	CASEELSE





ENDSELECT



;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_25

;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 3
	CALL TREASURE_25
	RETURN RESULT
	
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 6
	CALL UPSTAIRS_25
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 7
	CALL DOWNSTAIRS_25
	RETURN RESULT


ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
	CALL DUNGEON_TERMINAL
	RETURN 1
ELSE
	CALL MESSAGE_WINDOW_D, "", "＞There seems to be nothing here."
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_25,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 3
	CALL TREASURE_25
	RETURN RESULT

;========
;罠の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 5
	CALL TRAP_25
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 6
	CALL UPSTAIRS_25
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 7
	CALL DOWNSTAIRS_25
	RETURN RESULT


;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1


;========================
;イベント
;========================
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 4
	SELECTCASE FLAG:現M
		CASE 4
			IF FLAG:現X == 7 && FLAG:現Y == 11
			FLAG:逃走不可フラグ = 1
			FLAG:会話不能フラグ = 1
			FLAG:先制不意打ちキャンセル = 1
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "", "＞This looks like a bedroom./＞You can see a female appearance standing next to a shrine..."
			;寝室のようだ/＞御簾ごしに女性の姿が見える……
			CALL MESSAGE_WINDOW_D, "Mother Izanami", "Leave, children./This place is reserved to those who inherit our blood./You are also my children,/do not waste your life here."
			;地母神 イザナミ	子供達よ、立ち去りなさい/此処は私達の血を継ぐ者のみ通る事が叶う場/貴方達も私の産んだ子らとはいえ、/みだりに入ってはなりません
			CALL MESSAGE_WINDOW_D, "Mother Izanami", "....what, you received permission from Bishamonten?/Well, then.../The earthly children who where ordered to protect this place.../Why should the word of newcomers have weight?"
			;……毘沙門天達の 許可を 受けたと？/それでは、貴方は……/此処を守れと命じた地上の子らは/私達の言葉よりもあの新参者共の言葉を優先したと
			CALL MESSAGE_WINDOW_D, "Mother Izanami", "...how unpleasant./Very unpleasant!/My anger is great!"
			;……不愉快じゃ/実に不愉快じゃ！/余程 妾の怒りを招きたいと見える！
			CALL MESSAGE_WINDOW_D, "Mother Izanami", "Well, I'll give you what you want./Sleep in the arms of your mother, annoying children!"
			;よかろう、汝の望むままにしてやろうではないか/母の腕に抱かれて眠れ、忌々しき子供達よ！
			CALL MESSAGE_WINDOW_D, "", "＞Mother Izanami appeared!"
			;地母神 イザナミが一体出た！
			CALL SET_ENEMY,14,7,50,1,1
			CFLAG:(CHARANUM-1):ＨＰ補正 = 2000
			CFLAG:(CHARANUM-1):ＭＰ補正 = 4000
			CFLAG:(CHARANUM-1):速度補正 = 15
			CSTR:(CHARANUM-1):思考パターン = 7
			CFLAG:(CHARANUM-1):行動回数 = 5
			ABL:(CHARANUM-1):スキル3 = 2001
			ABL:(CHARANUM-1):スキル4 = 1223
			ABL:(CHARANUM-1):スキル5 = 1225
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			
			FLAG:21091 += 1
			FLAG:21092 += 1
			
			;ゾンビーサムライを前衛
			CALL SET_ENEMY,8,1091,40,1
			CSTR:(CHARANUM-1):思考パターン = 1091
			CFLAG:(CHARANUM-1):ＨＰ補正 = 200
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			
			CALL SET_ENEMY,9,1091,40,1
			CSTR:(CHARANUM-1):思考パターン = 1091
			CFLAG:(CHARANUM-1):ＨＰ補正 = 200
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			CALL SET_ENEMY,10,1091,40,1
			CSTR:(CHARANUM-1):思考パターン = 1091
			CFLAG:(CHARANUM-1):ＨＰ補正 = 200
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			
			;ゾンビーケンペイ自爆なし
			CALL SET_ENEMY,13,1092,40,1
			CSTR:(CHARANUM-1):思考パターン = 1092
			CFLAG:(CHARANUM-1):ＨＰ補正 = 100
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			ABL:(CHARANUM-1):スキル3 = 0
			CALL SET_ENEMY,15,1092,40,1
			ABL:(CHARANUM-1):スキル3 = 0
			CSTR:(CHARANUM-1):思考パターン = 1092
			CFLAG:(CHARANUM-1):ＨＰ補正 = 100
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			
			CALL BATTLE_START

			CALL MESSAGE_WINDOW_D, "Mother Izanami", "Ah..."
			;ああっ……
			CALL MESSAGE_WINDOW_D, "", @"＞Izanami collapsed on the spot./＞The skin exposed by her messy kimono is somewhat arousing %CALLNAME:MASTER%./＞Even Izanami is just a woman after all."
			;イザナミはその場に崩れ落ちた/＞肌蹴た着物から覗く肌は%CALLNAME:MASTER%の情欲をそそるには十分だ/＞伝説の国母もこうなっては只の女にしか過ぎない"
			CALL MESSAGE_WINDOW_D, "", "＞You received Mother Izanami."
			;地母神 イザナミを手に入れた
			FLAG:10007 = 1
			CALL ADD_NEW_COMPANION,7,0,1
			SIF FLAG:20007 < 100
				FLAG:20007 = 100
			IF RESULT == 1
				CFLAG:(CHARANUM-1):戦闘参加不可能 = 1
				BASE:(CHARANUM-1):LV -= 28
				BASE:(CHARANUM-1):力 -= 7
				BASE:(CHARANUM-1):知恵 -= 7
				BASE:(CHARANUM-1):魔力 -= 7
				BASE:(CHARANUM-1):耐力 -= 7
				BASE:(CHARANUM-1):速さ -= 7
				BASE:(CHARANUM-1):運 -= 7
				BASE:(CHARANUM-1):ＥＸＰ = 5*(31)*(32)*(33)/3
			ENDIF
			FLAG:逃走不可フラグ = 0
			FLAG:会話不能フラグ = 0
			FLAG:先制不意打ちキャンセル = 0
			ダンジョンフラグ:(FLAG:現ダンジョン):0 = 1
			DA:7:11 = 1
			
		
		RETURN 1
				
				
			ENDIF
	
	ENDSELECT
	
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
ENDIF
RETURN 0


;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_25
SELECTCASE FLAG:現M
	CASE 0
ENDSELECT
RETURN 1


;========
;罠置き場
;========
@TRAP_25
SELECTCASE FLAG:現M
	CASE 0
		IF FLAG:現X == 5 && FLAG:現Y == 1
			CALL DUNGEON_TRAP_MP,10
		ELSE
			CALL DUNGEON_TRAP_HP,10
		ENDIF
	CASE 1
		IF FLAG:現X == 7 && FLAG:現Y == 1
			CALL DUNGEON_TRAP_HP,10
		ELSE
			CALL DUNGEON_TRAP_MP,10
		ENDIF
	CASE 2
		IF FLAG:現X == 7 && FLAG:現Y == 7
			CALL DUNGEON_TRAP_MP,10
		ELSE
			CALL DUNGEON_TRAP_HP,10
		ENDIF
	CASE 3
		IF FLAG:現X == 1 && FLAG:現Y == 3
			CALL DUNGEON_TRAP_MP,10
		ELSEIF FLAG:現X == 1 && FLAG:現Y == 7
			CALL DUNGEON_TRAP_POISON,40
		ELSE
			CALL DUNGEON_TRAP_HP,10
		ENDIF
ENDSELECT
RETURN 0


;=======================================================================
;階段置き場
;=======================================================================
@UPSTAIRS_25
SELECTCASE FLAG:現M
	CASE IS < 4
			CALL DUNGEON_DOWNSTAIRS,FLAG:現M - 1,FLAG:現X,FLAG:現Y
	CASE 4
		CALL DUNGEON_UPSTAIRS,3,7,5
	CASEELSE
ENDSELECT
RETURN 1

@DOWNSTAIRS_25
SELECTCASE FLAG:現M
	CASE IS < 3
			CALL DUNGEON_DOWNSTAIRS,FLAG:現M + 1,FLAG:現X,FLAG:現Y
	CASE 3
			CALL DUNGEON_DOWNSTAIRS,4,15,3
	CASEELSE
ENDSELECT
RETURN 1

;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_25

SELECTCASE FLAG:現M
	CASE 0
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5 + 5
	CASEELSE
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5 + 5
ENDSELECT
SIF FLAG:未遭遇歩数 < 5
	FLAG:エンカウント率 = 0
;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_25
RETURN 33, 30, 39, 50
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_25
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_25
RETURN CLASS_NUM_COMMON("上級")
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;=======================================================================
@ENEMY_NUM_25, ARG
RETURN ENEMY_NUM_COMMON("上級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_25, ARG
SELECTCASE FLAG:現M
	CASE IS < 4
		SELECTCASE RAND:100
			CASE IS < 30
				;30%でサラシナヒメ
				RETURN 605, 30+RAND:(1+(FLAG:現M))
			CASE IS < 55
				;25%でフジムスメ
				RETURN 507, 31+RAND:(1+(FLAG:現M))
			CASE IS < 70
				;15%でセンリ
				RETURN 806, 33+RAND:(1+(FLAG:現M))
			CASE IS < 85
				;15%でハクジョウシ
				RETURN 705, 34+RAND:(1+(FLAG:現M))
			CASE IS < 100
				;10%でおしち
				RETURN 1008, 35+RAND:(1+(FLAG:現M))
		ENDSELECT
	CASE 4
		SELECTCASE RAND:100
			CASE IS < 30
				;30%でサラシナヒメ
				RETURN 605, 34
			CASE IS < 55
				;25%でフジムスメ
				RETURN 507, 35
			CASE IS < 70
				;15%でセンリ
				RETURN 806, 37
			CASE IS < 85
				;15%でハクジョウシ
				RETURN 705, 38
			CASE IS < 100
				;10%でおしち
				RETURN 1008, 39
		ENDSELECT
ENDSELECT
;=======================================================================
;特殊調整
;=======================================================================
@SET_ENEMY_STATUS_25, ARG
SIF NO:ARG == [[キャラ:フジムスメ]]
	ABL:ARG:スキル4 = 106
;=================================
;思考パターン
;=================================
;ゾンビーサムライの思考パターン
@SET_ACTION_1091,ARG
;後衛に居るなら前衛に出る
IF CPOS(ARG) > 11
	CFLAG:ARG:入力行動 = 2301
	CFLAG:ARG:入力行動 = CPOS(ARG)-5
;それ以外は通常
ELSE
		$DECIDE_ACTION
		CALL SEARCH_ACT,ARG
		IF RESULT == 0
			CFLAG:ARG:入力行動 = -1
			CFLAG:ARG:防御フラグ = 1
			RETURN 0
		ENDIF
;		PRINTFORML [{CFLAG:ARG:5}]のスキル数 {RESULT}
		LOCAL = RAND:RESULT
		;スキル番号決定
		LOCAL:1 = S:(10+LOCAL)
;		PRINTFORML [{CFLAG:ARG:5}]のスキル {LOCAL:1}
		$TARGET_LOOP
		
		CALL RANDOM_TARGET,ARG,LOCAL:1
		CALLFORM SKILL_TARGET_{LOCAL:1}
		;ここでスキル番号を行動内容に代入
		SIF CFLAG:ARG:ターゲット == -1
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = LOCAL:1
ENDIF

;ゾンビーケンペイの思考パターン
@SET_ACTION_1092,ARG
;前衛に居るなら自爆
IF CPOS(ARG) < 12
	CFLAG:ARG:入力行動 = 2005
	CALL CHECK_ACTIONABLE_AND_SET , ARG , CFLAG:ARG:入力行動
;それ以外は通常
ELSE
		$DECIDE_ACTION
		CALL SEARCH_ACT,ARG
		IF RESULT == 0
			CFLAG:ARG:入力行動 = -1
			CFLAG:ARG:防御フラグ = 1
			RETURN 0
		ENDIF
;		PRINTFORML [{CFLAG:ARG:5}]のスキル数 {RESULT}
		LOCAL = RAND:RESULT
		;スキル番号決定
		LOCAL:1 = S:(10+LOCAL)
;		PRINTFORML [{CFLAG:ARG:5}]のスキル {LOCAL:1}
		$TARGET_LOOP
		
		CALL RANDOM_TARGET,ARG,LOCAL:1
		CALLFORM SKILL_TARGET_{LOCAL:1}
		;ここでスキル番号を行動内容に代入
		SIF CFLAG:ARG:ターゲット == -1
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = LOCAL:1
ENDIF

;イザナミの思考パターン
@SET_ACTION_7,ARG
IF CFLAG:ARG:行動した回数 == 4
	;５回目の行動は闇の審判
	CALL CHECK_ACTIONABLE,ARG,1406
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1406
	CALL RANDOM_TARGET,ARG,1406
ELSEIF CFLAG:ARG:行動した回数 < 4
	;ポジション9のゾンビかポジション14のゾンビが死んでたら優先サマリカーム
	;それ以外に死んでるキャラが居たらサマリカーム
	;居なかったらコンセントレイト(既に貯まってたらメギド)or子守唄（一人でも眠ってたら誘い）
	IF CFLAG:POS(9):ステート == GET_STATE_NUM("DYING")
		CALL CHECK_ACTIONABLE,ARG,418
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 418
		CFLAG:ARG:ターゲット = POS(9)
		RETURN 1
	ELSEIF CFLAG:POS(14):ステート == GET_STATE_NUM("DYING")
		CALL CHECK_ACTIONABLE,ARG,418
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 418
		CFLAG:ARG:ターゲット = POS(14)
		RETURN 1
	ENDIF
	;前衛に居るならスウェイバック
	IF CPOS(ARG) == 9
		CFLAG:ARG:入力行動 = 2301
		CFLAG:ARG:ターゲット = 14
		RETURN 1
	ENDIF
	
	LOCAL:10 = 0

	FOR LOCAL,16,6,-1
		IF POS(LOCAL) > -1 && CFLAG:(POS(LOCAL)):ステート == GET_STATE_NUM("DYING")
			CALL CHECK_ACTIONABLE,ARG,418
			SIF RESULT == 0
			GOTO DECIDE_ACTION
			CFLAG:ARG:入力行動 = 418
			CFLAG:ARG:ターゲット = POS(LOCAL)
			RETURN 1
		ENDIF
	NEXT
	
	IF RAND:2 == 0
	
		IF CFLAG:ARG:集中フラグ
			CALL CHECK_ACTIONABLE,ARG,2001
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:入力行動 = 2001
			CALL RANDOM_TARGET,ARG,2001
			RETURN 1
		ELSE
			CALL CHECK_ACTIONABLE,ARG,512
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:入力行動 = 512
			CALL RANDOM_TARGET,ARG,512
			RETURN 1
		ENDIF
	ELSE
		FOR LOCAL,1,7
			IF POS(LOCAL) > -1 && CFLAG:(POS(LOCAL)):ステート == GET_STATE_NUM("SLEEP")
				CALL CHECK_ACTIONABLE,ARG,1225
				SIF RESULT == 0
					GOTO DECIDE_ACTION
				CFLAG:ARG:入力行動 = 1225
				CALL RANDOM_TARGET,ARG,1225
				RETURN 1
			ENDIF
		NEXT
		CALL CHECK_ACTIONABLE,ARG,1223
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 1223
		CALL RANDOM_TARGET,ARG,1223
		RETURN 1
	ENDIF
	
ELSE
		$DECIDE_ACTION
		CALL SEARCH_ACT,ARG
		IF RESULT == 0
			CFLAG:ARG:入力行動 = -1
			CFLAG:ARG:防御フラグ = 1
			RETURN 0
		ENDIF
;		PRINTFORML [{CFLAG:ARG:5}]のスキル数 {RESULT}
		LOCAL = RAND:RESULT
		;スキル番号決定
		LOCAL:1 = S:(10+LOCAL)
;		PRINTFORML [{CFLAG:ARG:5}]のスキル {LOCAL:1}
		$TARGET_LOOP
		
		CALL RANDOM_TARGET,ARG,LOCAL:1
		CALLFORM SKILL_TARGET_{LOCAL:1}
		;ここでスキル番号を行動内容に代入
		SIF CFLAG:ARG:ターゲット == -1
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = LOCAL:1


ENDIF

