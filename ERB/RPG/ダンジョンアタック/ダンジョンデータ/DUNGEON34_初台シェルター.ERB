
;=======================================================================
;初台シェルター
;
;偽典女神転生、主人公の生まれの地であり始まりの地。
;大破壊後の東京で生き残っている有数のシェルター『だった』が、
;魔王バール一味を頭とするムールムールに侵入され壊滅する。
;eraMegatenでは舞台背景が大破壊前である可能性も考慮し、
;大破壊前から、実用性があるかテストも兼ねて、すでに稼働していたことにする。
;ダンジョンフラグ:34:1-6 = 1.宝箱
;ダンジョンフラグ:34:7 = 1.中ボス扉警告
;ダンジョンフラグ:34:8 = 1.ボス撃破＆クリアフラグ
;ダンジョンフラグ:34:9 = 1.中ボスゾンビの群れ
;ダンジョンフラグ:34:10 = 1.ライドウ参戦
;ダンジョンフラグ:34:11 = 1.COMPチュートリアル
;ダンジョンフラグ:34:12 = 1.戦闘チュートリアル
;ダンジョンフラグ:34:13 = 1.チュートリアル途中で帰った
;ダンジョンフラグ:34:14 = 1.中ボスマシン
;ダンジョンフラグ:34:15 = 1.宝
;ダンジョンフラグ:34:16 = 1.特殊弾チュートリアル
;ダンジョンフラグ:34:17 = 1.スキルカードチュートリアル
;ダンジョンフラグ:34:18 = ゾンビ戦中断用
;ダンジョンフラグ:34:20 = 1.初期奴隷ドリー
;ダンジョンフラグ:34:21 = 1.初期奴隷マリ
;ダンジョンフラグ:34:30 = 1,チュートリアルを受けた 2,メッセージを見た(一度出ると2になってるため、ライドウ加入をはじめとしたイベントがおこらなく)
;ダンジョンフラグ:34:31 = キクリヒメ撃破
;ダンジョンフラグ:34:32 = チュートリアル中（ダンタリオン撃破or途中帰還で0に）
;ダンジョンナンバー34
;=======================================================================




;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_34
;特になし
RETURN 1



;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_34
RESULTS = Hatsudai Shelter


;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_34
PRINT Hatsudai Shelter
SELECTCASE FLAG:現M
	CASE 0
		PRINTL   １Ｆ
	CASE 1
		PRINTL   Ｂ１Ｆ
	CASE 2
		PRINTL   Ｂ２Ｆ
	CASE 3
		PRINTL   Ｂ３Ｆ
	CASE 4
		PRINTL   Ｂ４Ｆ
	CASE 5
		PRINTL   Ｂ５Ｆ
	CASE 6
		PRINTL   Ｂ６Ｆ
	CASE 7
		PRINTL   Ｂ７Ｆ
	CASE 8
		PRINTL   Ｂ８Ｆ
	CASE 9
		PRINTL   Ｂ９Ｆ
	CASE 10
		PRINTL   Ｂ１０Ｆ
	CASEELSE
		PRINTL
ENDSELECT


;=======================================================================
;特殊タイル表示
;=======================================================================
@TILE_MARK_34_9_8_6
RESULTS = Ｔ
@TILE_MARK_34_0_6_9
RESULTS = Ｅ

@TILE_MARK_34_5_3_3
RESULTS = Ｅ
@TILE_MARK_34_5_9_3
RESULTS = Ｅ

@TILE_MARK_34_6_5_1
RESULTS = Ｅ
@TILE_MARK_34_6_10_1
RESULTS = Ｅ

@TILE_MARK_34_7_12_1
RESULTS = Ｅ

@TILE_MARK_34_8_12_1
RESULTS = Ｅ
@TILE_MARK_34_8_4_9
RESULTS = □

@TILE_MARK_34_9_6_1
RESULTS = Ｅ

@TILE_MARK_34_10_9_1
RESULTS = Ｅ

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_34,ARG
FLAG:現M = 0
FLAG:現X = 4
FLAG:現Y = 1
;CALL AUTOMAP

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_34
;チュートリアル
;IF FLAG:戦闘難易度 >= 3 || FLAG:周回回数 >= 1 || ダンジョンフラグ:1:1 == 1 || ダンジョンフラグ:34:8 == 1 || BASE:MASTER:LV >= 11
IF ダンジョンフラグ:34:30 == 1
	ダンジョンフラグ:34:30 = 2
;	REPEAT CHARANUM
;		SIF NO:COUNT == 4505
;			ダンジョンフラグ:34:20 = 1
;	REND
;	REPEAT CHARANUM
;		SIF NO:COUNT == 4506
;			ダンジョンフラグ:34:20 = 2
;	REND
;	PRINTFORMW Do the tutorial?
;	CALL INPUT_YN
;	IF RESULT == 0
	CALL MESSAGE_WINDOW_D, "", @"＞When trying to enter Hatsudai Shelter、/＞%CALLNAME:MASTER%'s cellphone began to ring"
	CALL MESSAGE_WINDOW_D, "Voice", "Fufu…Are you working？"
	CALL MESSAGE_WINDOW_D, "", "＞It's Mayone's voice"
	IF GETCHARA([[キャラ:マリ]]) == -1
		IF RAND:2 == 0
			CALL MESSAGE_WINDOW_D, "", "＞Is that the sound of a slave being used？/＞Ocassionally、the indecent sounds of sex are overheard…"
		ELSE
			CALL MESSAGE_WINDOW_D, "", "＞Is that the sound of a slave being used？/＞The weak gasps of a woman soaking in passion are overheard…"
		ENDIF
	ENDIF
	CALL MESSAGE_WINDOW_D, "Mayone", "I heard in this shelter、/information on an enemy of the Phantom Society/called Kuzunoha can be found"
	CALL MESSAGE_WINDOW_D, "", "＞Mayone speaks of danger with a happy voice…"
	CALL MESSAGE_WINDOW_D, "Mayone", "If he comes、a newcomer like you/may not be much of a match for him/so be careful"
	IF GETCHARA([[キャラ:ドリー]]) == -1
		CALL MESSAGE_WINDOW_D, "", @"＞Alongside Mayone's advice/＞sounded the crack of a whip"
		CALL MESSAGE_WINDOW_D, "Girl's voice", @"%\@RAND:2?Forgive me、master…#\heee、help…\@%"
		CALL MESSAGE_WINDOW_D, "", @"＞Hearing the %\@RAND:2?screaming#sobbing\@% you ended the call…"
	ELSE
		CALL MESSAGE_WINDOW_D, "", "＞Heard the warning and ended the call"
	ENDIF
ELSE
	ダンジョンフラグ:34:10 = 1
	ダンジョンフラグ:34:11 = 1
	ダンジョンフラグ:34:12 = 1
	ダンジョンフラグ:34:13 = 2
	ダンジョンフラグ:34:16 = 1
	ダンジョンフラグ:34:17 = 1
	CALL MAKE_FLOOR, FLAG:現ダンジョン
ENDIF


;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_34,ARG,ARG:1
IF FLAG:現M == 5 && ARG ==1 && ARG:1 == 9 && ダンジョンフラグ:34:9 == 0
	CALL MESSAGE_WINDOW_D, "", "＞The stench of death fills the air/＞Do you open the door？"
	CALL INPUT_YN_D
	SIF RESULT
		RETURN 0
;ELSEIF FLAG:現M == 5 && ARG == 1 && ARG:1 == 9 && ダンジョンフラグ:34:7 == 0
;	CALL MESSAGE_WINDOW_D, "", "＞The door is wet with blood/＞Do you open the door？"
;	CALL INPUT_YN_D
;	SIF RESULT
;		RETURN 0
;	ダンジョンフラグ:34:7 = 1
ELSEIF FLAG:現M == 9
	IF ARG == 7 && ARG:1 == 9 && !ダンジョンフラグ:34:8
		CALL MESSAGE_WINDOW_D, "", "＞From the other side of the door、the sound of a keyboard rings out/＞Seems someone's there…/＞Open the door？"
		CALL INPUT_YN_D
		SIF RESULT
			RETURN 0
	ENDIF
ENDIF
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT

;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_34
SELECTCASE FLAG:現M
	CASE 0
	;１階
		FLAG:最大X = 9
		FLAG:最大Y = 12
							    ;012345678
		CALL MAKE_FLOOR_LINE, 0,"    8    "
		CALL MAKE_FLOOR_LINE, 1,"   111   "
		CALL MAKE_FLOOR_LINE, 2,"    2    "
		CALL MAKE_FLOOR_LINE, 3,"    1    "
		CALL MAKE_FLOOR_LINE, 4,"  11111  "
		CALL MAKE_FLOOR_LINE, 5,"  11111  "
		CALL MAKE_FLOOR_LINE, 6,"  2 1    "
		CALL MAKE_FLOOR_LINE, 7,"  1 7    "
		CALL MAKE_FLOOR_LINE, 8,"  1      "
		CALL MAKE_FLOOR_LINE, 9,"  11119  "
		CALL MAKE_FLOOR_LINE,10,"         "
							    ;012345678

		CALL SET_TILE,4,4,ダンジョンフラグ:34:10,0,4
		CALL SET_TILE,4,4,ダンジョンフラグ:34:13,1,4

		CALL SET_TILE,4,5,ダンジョンフラグ:34:11,0,4
		CALL SET_TILE,4,6,ダンジョンフラグ:34:12,0,4

		;CALL SET_TILE,6,9,ダンジョンフラグ:34:14,0,4

	CASE 1
	;地下１階
		FLAG:最大X = 7
		FLAG:最大Y = 7
							   ;0123456
		CALL MAKE_FLOOR_LINE,0,"       "
		CALL MAKE_FLOOR_LINE,1," 11111 "
		CALL MAKE_FLOOR_LINE,2," 11111 "
		CALL MAKE_FLOOR_LINE,3," 11111 "
		CALL MAKE_FLOOR_LINE,4," 1   1 "
		CALL MAKE_FLOOR_LINE,5," 7   6 "
		CALL MAKE_FLOOR_LINE,6,"       "

	CASE 2
	;地下２階
		FLAG:最大X = 7
		FLAG:最大Y = 7
		CALL MAKE_FLOOR_LINE,0,"       "
		CALL MAKE_FLOOR_LINE,1," 11111 "
		CALL MAKE_FLOOR_LINE,2," 11111 "
		CALL MAKE_FLOOR_LINE,3," 11111 "
		CALL MAKE_FLOOR_LINE,4," 1   1 "
		CALL MAKE_FLOOR_LINE,5," 7   6 "
		CALL MAKE_FLOOR_LINE,6,"       "

	CASE 3
	;地下３階
		FLAG:最大X = 7
		FLAG:最大Y = 7
							   ;0123456
		CALL MAKE_FLOOR_LINE,0,"       "
		CALL MAKE_FLOOR_LINE,1," 11111 "
		CALL MAKE_FLOOR_LINE,2," 11111 "
		CALL MAKE_FLOOR_LINE,3," 11111 "
		CALL MAKE_FLOOR_LINE,4," 1   1 "
		CALL MAKE_FLOOR_LINE,5," 7   6 "
		CALL MAKE_FLOOR_LINE,6,"       "

		;CALL SET_TILE,5,3,ダンジョンフラグ:34:17,0,4

	CASE 4
	;地下４階
		FLAG:最大X = 7
		FLAG:最大Y = 7
							   ;0123456
		CALL MAKE_FLOOR_LINE,0,"       "
		CALL MAKE_FLOOR_LINE,1," 11121 "
		CALL MAKE_FLOOR_LINE,2," 111 1 "
		CALL MAKE_FLOOR_LINE,3," 111 1 "
		CALL MAKE_FLOOR_LINE,4," 1   1 "
		CALL MAKE_FLOOR_LINE,5," 7   6 "
		CALL MAKE_FLOOR_LINE,6,"       "

		CALL SET_TILE,5,3,ダンジョンフラグ:34:16,0,4

	CASE 5
	;地下５階
		FLAG:最大X = 11
		FLAG:最大Y = 13
							    ;01234567890123
		CALL MAKE_FLOOR_LINE, 0,"           "
		CALL MAKE_FLOOR_LINE, 1," 111112116 "
		CALL MAKE_FLOOR_LINE, 2," 1      1  "
		CALL MAKE_FLOOR_LINE, 3," 1 9    19 "
		CALL MAKE_FLOOR_LINE, 4," 1 2 11    "
		CALL MAKE_FLOOR_LINE, 5," 1 1211    "
		CALL MAKE_FLOOR_LINE, 6," 1 1       "
		CALL MAKE_FLOOR_LINE, 7," 1 1211    "
		CALL MAKE_FLOOR_LINE, 8," 1 1 11    "
		CALL MAKE_FLOOR_LINE, 9," 2 2       "
		CALL MAKE_FLOOR_LINE,10," 11111     "
		CALL MAKE_FLOOR_LINE,11," 1111127   "
		CALL MAKE_FLOOR_LINE,12,"           "
		
		;宝箱の設置
		CALL SET_TILE,6,4,ダンジョンフラグ:34:1,0,3
		;中ボス　警告
		;CALL SET_TILE,1,8,ダンジョンフラグ:34:7,0,4
		;中ボス　ゾンビの設置
		CALL SET_TILE,1,10,ダンジョンフラグ:34:9,0,4

	CASE 6
	;地下６階
		FLAG:最大X = 14
		FLAG:最大Y = 10
							    ;01234567890123
		CALL MAKE_FLOOR_LINE, 0,"              "
		CALL MAKE_FLOOR_LINE, 1,"     9 11 9   "
		CALL MAKE_FLOOR_LINE, 2,"     2 11 2   "
		CALL MAKE_FLOOR_LINE, 3,"     1  2 121 "
		CALL MAKE_FLOOR_LINE, 4," 111 1 1111   "
		CALL MAKE_FLOOR_LINE, 5," 111212111121 "
		CALL MAKE_FLOOR_LINE, 6," 111 1 1111   "
		CALL MAKE_FLOOR_LINE, 7,"     1 2  1   "
		CALL MAKE_FLOOR_LINE, 8," 12111 1 617  "
		CALL MAKE_FLOOR_LINE, 9,"              "

		;宝箱の設置
		CALL SET_TILE,1,8,ダンジョンフラグ:34:15,0,3
		CALL SET_TILE,12,3,ダンジョンフラグ:34:2,0,3

	CASE 7
	;地下７階
		FLAG:最大X = 14
		FLAG:最大Y = 11
							    ;01234567890123
		CALL MAKE_FLOOR_LINE, 0,"              "
		CALL MAKE_FLOOR_LINE, 1," 1 1 1 1 1  9 "
		CALL MAKE_FLOOR_LINE, 2," 2 2 2 2 2  2 "
		CALL MAKE_FLOOR_LINE, 3," 111111111411 "
		CALL MAKE_FLOOR_LINE, 4,"  1  1  2 1 1 "
		CALL MAKE_FLOOR_LINE, 5,"121111 11 1 1 "
		CALL MAKE_FLOOR_LINE, 6,"  11   11 6 7 "
		CALL MAKE_FLOOR_LINE, 7,"121  1 2      "
		CALL MAKE_FLOOR_LINE, 8,"  2 111111111 "
		CALL MAKE_FLOOR_LINE, 9,"  1 1 1 1 1 1 "
		CALL MAKE_FLOOR_LINE,10,"              "

		;宝箱の設置
		CALL SET_TILE,5,7,ダンジョンフラグ:34:3,0,3
		;中ボス、キクリヒメ
		DA:10:3 = !ダンジョンフラグ:34:30 || ダンジョンフラグ:34:31 ? 1 # 4

	CASE 8
	;地下８階
		FLAG:最大X = 14
		FLAG:最大Y = 13
							    ;01234567890123
		CALL MAKE_FLOOR_LINE, 0,"              "
		CALL MAKE_FLOOR_LINE, 1," 1 1 1 1 1  9 "
		CALL MAKE_FLOOR_LINE, 2," 2 2 2 2 2  2 "
		CALL MAKE_FLOOR_LINE, 3," 111111111111 "
		CALL MAKE_FLOOR_LINE, 4,"  1  1  2 1 1 "
		CALL MAKE_FLOOR_LINE, 5,"121111 11 1 4 "
		CALL MAKE_FLOOR_LINE, 6,"  1    11 6 7 "
		CALL MAKE_FLOOR_LINE, 7,"121 1  2      "
		CALL MAKE_FLOOR_LINE, 8,"  1 2  1    1 "
		CALL MAKE_FLOOR_LINE, 9,"  1 1  1    2 "
		CALL MAKE_FLOOR_LINE,10,"  2 1 1111111 "
		CALL MAKE_FLOOR_LINE,11,"  111 1 1 1 1 "
		CALL MAKE_FLOOR_LINE,12,"              "

		;宝箱の設置
		CALL SET_TILE,4,7,ダンジョンフラグ:34:5,0,3
		CALL SET_TILE,12,8,ダンジョンフラグ:34:4,0,3
		;中ボスの設置
		DA:12:5 = ダンジョンフラグ:34:14 ? 1 # 4

	CASE 9
	;地下９階
		FLAG:最大X = 13
		FLAG:最大Y = 13
							    ;01234567890123
		CALL MAKE_FLOOR_LINE, 0,"             "
		CALL MAKE_FLOOR_LINE, 1,"      9      "
		CALL MAKE_FLOOR_LINE, 2,"      2      "
		CALL MAKE_FLOOR_LINE, 3,"   7111      "
		CALL MAKE_FLOOR_LINE, 4,"      121    "
		CALL MAKE_FLOOR_LINE, 5," 121111 2    "
		CALL MAKE_FLOOR_LINE, 6,"      1 9    "
		CALL MAKE_FLOOR_LINE, 7,"    611   11 "
		CALL MAKE_FLOOR_LINE, 8," 11   1  111 "
		CALL MAKE_FLOOR_LINE, 9," 11 12121111 "
		CALL MAKE_FLOOR_LINE,10,"  2   1  111 "
		CALL MAKE_FLOOR_LINE,11," 111111   11 "
		CALL MAKE_FLOOR_LINE,12,"             "

		;宝箱の設置
		CALL SET_TILE,1,8,ダンジョンフラグ:34:6,0,3
		;ボスの設置
		CALL SET_TILE,8,9,ダンジョンフラグ:34:8,0,4

	CASE 10
	;地下１０階、何も無い
		FLAG:最大X = 13
		FLAG:最大Y = 13
		
		;座標X7/Y5が7=降り階段指定になっていた
		;9階に対する昇り階段なので6が正しい
		
								;0123456789012
		CALL MAKE_FLOOR_LINE, 0,"             "
		CALL MAKE_FLOOR_LINE, 1," 111 11  9   "
		CALL MAKE_FLOOR_LINE, 2," 111 11  2   "
		CALL MAKE_FLOOR_LINE, 3,"  2  2   1   "
		CALL MAKE_FLOOR_LINE, 4," 11111   121 "
		CALL MAKE_FLOOR_LINE, 5,"   11126 1   "
		CALL MAKE_FLOOR_LINE, 6," 12111   121 "
		CALL MAKE_FLOOR_LINE, 7,"   1     1   "
		CALL MAKE_FLOOR_LINE, 8,"   111111121 "
		CALL MAKE_FLOOR_LINE, 9," 121  2  2   "
		CALL MAKE_FLOOR_LINE,10,"   1 11 11   "
		CALL MAKE_FLOOR_LINE,11," 121 11 1121 "
		CALL MAKE_FLOOR_LINE,12,"             "
	CASEELSE

ENDSELECT



;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_34

;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 3
	CALL TRESURING_CALLING_34
	RETURN 1
;==========
;イベントの処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 4
	CALL EVENTING_CALLING_34
	RETURN 1
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 6
	SELECTCASE FLAG:現M
		CASE 0
		CASE 1
			CALL DUNGEON_UPSTAIRS,0,4,4
		CASE 2
			CALL DUNGEON_UPSTAIRS,1,1,4
		CASE 3
			CALL DUNGEON_UPSTAIRS,2,1,4
		CASE 4
			CALL DUNGEON_UPSTAIRS,3,1,4
		CASE 5
			CALL DUNGEON_UPSTAIRS,4,1,4
		CASE 6
			CALL DUNGEON_UPSTAIRS,5,7,11
		CASE 7
			CALL DUNGEON_UPSTAIRS,6,10,8
		CASE 8
			CALL DUNGEON_UPSTAIRS,7,12,5
		CASE 9
			CALL DUNGEON_UPSTAIRS,8,10,6
		CASE 10
			CALL DUNGEON_UPSTAIRS,9,4,3
		CASEELSE
	ENDSELECT
	
	RETURN 1

ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 7
	SELECTCASE FLAG:現M
		CASE 0
			CALL DUNGEON_DOWNSTAIRS,1,5,4
		CASE 1
			CALL DUNGEON_DOWNSTAIRS,2,5,4
		CASE 2
			CALL DUNGEON_DOWNSTAIRS,3,5,4
		CASE 3
			CALL DUNGEON_DOWNSTAIRS,4,5,4
		CASE 4
			CALL DUNGEON_DOWNSTAIRS,5,9,1
		CASE 5
			CALL DUNGEON_DOWNSTAIRS,6,10,8
		CASE 6
			CALL DUNGEON_DOWNSTAIRS,7,10,5
		CASE 7
			CALL DUNGEON_DOWNSTAIRS,8,10,8
		CASE 8
			CALL DUNGEON_DOWNSTAIRS,9,5,6
		CASE 9
			CALL DUNGEON_DOWNSTAIRS,10,6,5
		CASEELSE
	ENDSELECT
	RETURN 1


;==========
;ターミナル
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
	SELECTCASE FLAG:現M
		CASE 0
			SIF FLAG:現X == 6 && FLAG:現Y == 9
				CALL SYODAI_E
		CASE 5
			SIF (FLAG:現X == 3 || FLAG:現X == 9) && FLAG:現Y == 3
				CALL SYODAI_E
		CASE 6
			SIF (FLAG:現X == 5 || FLAG:現X == 10) && FLAG:現Y == 1
				CALL SYODAI_E
		CASE 7
			SIF FLAG:現X == 12 && FLAG:現Y == 1
				CALL SYODAI_E
		CASE 8
			SIF FLAG:現X == 12 && FLAG:現Y == 1
				CALL SYODAI_E
		CASE 9
			SIF FLAG:現X == 6 && FLAG:現Y == 1 
				CALL SYODAI_E
			SIF FLAG:現X == 8 && FLAG:現Y == 6 
				CALL RAIDOU_TERMINAL
		CASE 10
			SIF FLAG:現X == 9 && FLAG:現Y == 1
				CALL SYODAI_E
		CASEELSE
	ENDSELECT
	RETURN 1
ELSE
	CALL MESSAGE_WINDOW_D, "", "＞Nothing seems to be here." 
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_34,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	IF FLAG:現M == 0 && FLAG:現X ==4 && FLAG:現Y == 0
		;さらばライドウ
		LOCAL = GETCHARA([[キャラ:ライドウ]])
		IF LOCAL >= 0 && !CFLAG:LOCAL:引継ぎ回数
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "Gouto", "Are you heading back？/Today is the only day/when I can properly instruct you"
			CALL INPUT_YN_D
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D, "Gouto", "Then、farewell/I would appreciate it if you examined the area"
				CALL MESSAGE_WINDOW_D, "", "＞Gouto and Raidou left"
				ダンジョンフラグ:34:13 = 1
				ダンジョンフラグ:34:32 = 0
				CALL DEL_GUEST, LOCAL
				FLAG:脱出スキル禁止 = 0
				CALL DUNGEON_EXIT
			ELSE
				CALL MESSAGE_WINDOW_D, "Gouto", "then、let's continue searching"
				CALL MESSAGE_WINDOW_D, "", "＞Gouto prompted you to advance into the dungeon"
				FLAG:現Y = 1
			ENDIF
		ELSE
			CALL DUNGEON_EXIT
		ENDIF
	ENDIF
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 3
	CALL TRESURING_CALLING_34
	RETURN 1
;==========
;イベントの処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 4
	CALL EVENTING_CALLING_34
	RETURN 1
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 6
SELECTCASE FLAG:現M
	CASE 0
	CASE 1
		CALL DUNGEON_UPSTAIRS,0,4,7
	CASE 2
		CALL DUNGEON_UPSTAIRS,1,1,4
	CASE 3
		CALL DUNGEON_UPSTAIRS,2,1,4
	CASE 4
		CALL DUNGEON_UPSTAIRS,3,1,4
	CASE 5
		CALL DUNGEON_UPSTAIRS,4,1,4
	CASE 6
		CALL DUNGEON_UPSTAIRS,5,7,11
	CASE 7
		CALL DUNGEON_UPSTAIRS,6,10,8
	CASE 8
		CALL DUNGEON_UPSTAIRS,7,12,5
	CASE 9
		CALL DUNGEON_UPSTAIRS,8,12,6
	CASE 10
		CALL DUNGEON_UPSTAIRS,9,4,3
	CASEELSE
ENDSELECT

RETURN 1

ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 7
SELECTCASE FLAG:現M
	CASE 0
		CALL DUNGEON_DOWNSTAIRS,1,5,4
	CASE 1
		CALL DUNGEON_DOWNSTAIRS,2,5,4
	CASE 2
		CALL DUNGEON_DOWNSTAIRS,3,5,4
	CASE 3
		CALL DUNGEON_DOWNSTAIRS,4,5,4
	CASE 4
		CALL DUNGEON_DOWNSTAIRS,5,9,1
	CASE 5
		CALL DUNGEON_DOWNSTAIRS,6,10,8
	CASE 6
		CALL DUNGEON_DOWNSTAIRS,7,10,5
	CASE 7
		CALL DUNGEON_DOWNSTAIRS,8,10,5
	CASE 8
		CALL DUNGEON_DOWNSTAIRS,9,5,7
	CASE 9
		CALL DUNGEON_DOWNSTAIRS,10,6,5
	CASEELSE
ENDSELECT
RETURN 1
;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
;==========
;ターミナル
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
	SELECTCASE FLAG:現M
		CASE 0
			SIF FLAG:現X == 6 && FLAG:現Y == 9
				CALL SYODAI_E
		CASE 5
			SIF (FLAG:現X == 3 || FLAG:現X == 9) && FLAG:現Y == 3
				CALL SYODAI_E
		CASE 6
			SIF (FLAG:現X == 5 || FLAG:現X == 10) && FLAG:現Y == 1
				CALL SYODAI_E
		CASE 7
			SIF FLAG:現X == 12 && FLAG:現Y == 1
				CALL SYODAI_E
		CASE 8
			SIF FLAG:現X == 12 && FLAG:現Y == 1
				CALL SYODAI_E
		CASE 9
			SIF FLAG:現X == 6 && FLAG:現Y == 1 
				CALL SYODAI_E
			SIF FLAG:現X == 8 && FLAG:現Y == 6 
				CALL RAIDOU_TERMINAL
		CASE 10
			SIF FLAG:現X == 9 && FLAG:現Y == 1
				CALL SYODAI_E
		CASEELSE
	ENDSELECT
	RETURN 1
ENDIF
RETURN 0

;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_34
RETURN 5, 3, 13, 15
;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_34
IF FLAG:現M < 8
	FLAG:エンカウント率 = FLAG:未遭遇歩数 > 5 ? FLAG:未遭遇歩数*5 # 0
ELSEIF FLAG:現M == 9
	FLAG:エンカウント率 = FLAG:未遭遇歩数 > 5 ? FLAG:未遭遇歩数*5 + 10 # 0
ELSE
	FLAG:エンカウント率 = FLAG:未遭遇歩数 > 5 ? FLAG:未遭遇歩数*5 + 20 # 0
ENDIF
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_34
IF RAND:100 < FLAG:エンカウント率
	IF ダンジョンフラグ:34:32
		FLAG:先制不意打ちキャンセル = 1
		ダンジョンフラグ:34:18 = 0
	ENDIF
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	ダンジョンフラグ:34:18 = 0
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_34
;チュートリアル
IF ダンジョンフラグ:34:30
	SELECTCASE RAND:100
		CASE IS < 50
			RETURN 1
		CASEELSE
			RETURN 2
	ENDSELECT
ELSE
	SELECTCASE RAND:100
		CASE IS < 40
			RETURN 1
		CASE IS < 95
			RETURN 2
		CASEELSE
			RETURN 3
	ENDSELECT
ENDIF
;=======================================================================
;敵の総数
;=======================================================================
@ENEMY_NUM_34, ARG
;チュートリアル
IF ダンジョンフラグ:34:30
	SELECTCASE ARG
		CASE 1
			RETURN RAND(1, 4)
		CASE 2
			RETURN RAND(2, 5)
	ENDSELECT
;通常
ELSE
	SELECTCASE ARG
		CASE 1
			RETURN RAND(1, 3)
		CASE 2
			RETURN RAND(2, 5)
		CASE 3
			RETURN RAND(3, 6)
	ENDSELECT
ENDIF
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_34, ARG
;チュートリアルを受けた場合は専用テーブルしか出てこない
;難度低下と見せかけて、実際のところ、獲得できる仲魔の種類がかなり減るし、レベルも上げにくくなるので
;楽になる反面、攻略上の旨みは減ると思います
IF ダンジョンフラグ:34:30
	SELECTCASE RAND:100
		CASE IS < 10
			;10%でラタトスクLv5
			RETURN [[キャラ:ラタトスク]], 5
		CASE IS < 25
			;15%でピクシーLv3
			RETURN [[キャラ:ピクシー]], 3
		CASE IS < 40
			;15%でアプサラスLv5
			RETURN [[キャラ:アプサラス]], 5
		CASE IS < 60
			;20%でゾンビちゃんLv4
			RETURN [[キャラ:ゾンビちゃん]], 4
		CASE IS < 70
			;10%でゾンビくんLv5
			RETURN [[キャラ:ゾンビくん]], 5
		CASE IS < 80
			;10%でゾンビドッグ
			RETURN [[キャラ:ゾンビドッグ]], 1
		CASE IS < 85
			;5%でコボルトLv5
			RETURN [[キャラ:コボルト]], 5
		CASE IS < 95
			;5%でノッカーLv4
			RETURN [[キャラ:ノッカー]], 4
		CASE IS < 100
			;10%でガキLv4
			RETURN [[キャラ:ガキ]], 4
	ENDSELECT
ELSE
	SELECTCASE FLAG:現M
		;五階まで
		CASE IS < 6
			SELECTCASE RAND:100
				CASE IS < 15
					;15%でピクシーLv3
					RETURN [[キャラ:ピクシー]], 3
				CASE IS < 30
					;15%でアプサラスLv5
					RETURN [[キャラ:アプサラス]], 5
				CASE IS < 50
					;20%でゾンビちゃんLv4
					RETURN [[キャラ:ゾンビちゃん]], 4
				CASE IS < 60
					;15%でゾンビくんLv7
					RETURN [[キャラ:ゾンビくん]], 7
				CASE IS < 70
					;10%でゾンビドッグ
					RETURN [[キャラ:ゾンビドッグ]], 3
				CASE IS < 80
					;15%でコボルトLv5
					RETURN [[キャラ:コボルト]], 5
				CASE IS < 90
					;10%でノッカーLv4
					RETURN [[キャラ:ノッカー]], 4
				CASE IS < 100
					;10%でガキLv4
					RETURN [[キャラ:ガキ]], 4
			ENDSELECT
		;六階-七階
	CASE IS < 8
		SELECTCASE RAND:100
			CASE IS < 15
				;15%でアプサラス
				RETURN [[キャラ:アプサラス]], 5
			CASE IS < 30
				;15%でラタトスクLv5
				RETURN [[キャラ:ラタトスク]], 5
			CASE IS < 45
				;15%でゾンビちゃん
				RETURN [[キャラ:ゾンビちゃん]], 7
			CASE IS < 50
				;5%でゾンビナース
				RETURN [[キャラ:ゾンビナース]], 8
			CASE IS < 55
				;5%でモコイLv6
				RETURN [[キャラ:モコイ]], 6
			CASE IS < 65
				;10%でゴブリンLv7
				RETURN [[キャラ:ゴブリン]], 7
			CASE IS < 75
				;10%でカーシーLv6
				RETURN [[キャラ:カーシー]], 6
			CASE IS < 90
				;15%でガキ
				RETURN [[キャラ:ガキ]], 7
			CASE IS < 100
				;10%でゾンビくん
				RETURN [[キャラ:ゾンビくん]], 7
		ENDSELECT
	;七階-九階
	CASE IS < 10
		SELECTCASE RAND:100
			CASE IS < 5
				;5%でリリムLv9
				RETURN [[キャラ:リリム]], 9
			CASE IS < 15
				;10%でシルキーLV7
				RETURN [[キャラ:シルキー]], 7
			CASE IS < 30
				;15%でラタトスク
				RETURN [[キャラ:ラタトスク]], 8
			CASE IS < 45
				;15%でゾンビちゃん　ランク1
				RETURN [[キャラ:ゾンビちゃん]], 7
			CASE IS < 50
				;5%でゾンビナース
				RETURN [[キャラ:ゾンビナース]], 8
			CASE IS < 55
				;5%でモコイランク1
				RETURN [[キャラ:モコイ]], 9
			CASE IS < 65
				;10%でゴブリン
				RETURN [[キャラ:ゴブリン]], 7
			CASE IS < 75
				;10%でカーシー
				RETURN [[キャラ:カーシー]], 9
			CASE IS < 85
				;10%でアンドラス
				RETURN [[キャラ:アンドラス]], 8
			CASE IS < 100
				;15%でゾンビくん
				RETURN [[キャラ:ゾンビくん]], 7
		ENDSELECT
	CASEELSE
		SELECTCASE RAND:100
			CASE IS < 15
				;15%でリリム　ランク1
				RETURN [[キャラ:リリム]], 12
			CASE IS < 25
				;10%でシルキー　ランク1
				RETURN [[キャラ:シルキー]], 10
			CASE IS < 40
				;15%でゾンビちゃん　ランク2
				RETURN [[キャラ:ゾンビちゃん]], 10
			CASE IS < 50
				;10%でゾンビナース
				RETURN [[キャラ:ゾンビナース]], 11
			CASE IS < 60
				;10%でブラウニー
				RETURN [[キャラ:ブラウニー]], 11
			CASE IS < 75
				;15%でイヌガミ
				RETURN [[キャラ:イヌガミ]], 13
			CASE IS < 90
				;15%でゾンビくん　ランク1
				RETURN [[キャラ:ゾンビくん]], 10
			CASE IS < 100
				;10%でアンドラス　ランク1
				RETURN [[キャラ:アンドラス]], 11
		ENDSELECT
	ENDSELECT
ENDIF

;=======================================================================
;イベントまとめ
;=======================================================================
@EVENTING_CALLING_34
SELECTCASE FLAG:現M
	CASE 0
		IF FLAG:現X == 4 && FLAG:現Y == 4 && ダンジョンフラグ:34:10 == 0
			CALL SHOW_PICTURE, "D"
			;空の博物館　舞耶姉システムから拝借
			CALL MESSAGE_WINDOW_D, "", "＞There is a young man wearing a black overcoat accompanied by a cat"
;			IF ダンジョンフラグ:34:10 == 0
				CALL MESSAGE_WINDOW_D, "Black cat", "Is that a summoner from the Phantom Society？"
				;アマラ宇宙に迷い込んだライドウとGouto　とある閣下の依頼で色々特別なのでGoutoが喋ります
				CALL MESSAGE_WINDOW_D, "", "＞The black cat spoke…"
				CALL MESSAGE_WINDOW_D, "Black cat", "This man is Raidou Kuzunoha、and I am Gouto/Although usually we have a hostile relationship/this time we seem to be responding to the same request"
;				ダンジョンフラグ:34:10 = 1
				ダンジョンフラグ:34:32 = 1
;			ELSE
;				PRINTFORML I've been waiting」
;				PRINTFORMW 「Let us aim for the deepest parts together」
;			ENDIF
			FOR LOCAL, 1, 7
				LOCALS = ポジション{LOCAL}
				SIF FLAG:LOCALS < 0
					BREAK
				SIF ABL:(FLAG:LOCALS):種族 > 0 && NO:(FLAG:LOCALS) != 4509
					CONTINUE
				SIF LOCAL == 6
					GOTO KUZUNOHA01
			NEXT
			;
			IF FLAG:戦闘難易度 < 3 || FLAG:DEBUG
				CALL MESSAGE_WINDOW_D, "Gouto", "I request/that we work together because/we cannot leave、escape skills have been sealed"
				CALL SET_GUEST_D34
				ダンジョンフラグ:34:10 = 1
				ダンジョンフラグ:34:13 = 0
				FLAG:脱出スキル禁止 = 1
			ELSE
				$KUZUNOHA01
				CALL MESSAGE_WINDOW_D, "Gouto", "Hmm…/your party seems to be full"
			ENDIF
			CALL CLEAR_TILE
		ELSEIF FLAG:現X == 4 && FLAG:現Y == 5 && ダンジョンフラグ:34:11 == 0 && FLAG:周回回数 < 1
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "", "＞The black cat with Raidou、Gouto、began to talk"
			CALL MESSAGE_WINDOW_D, "Gouto", "Summoners can negotiate using TALK、/allowing you to gain more demons/who are stored in the ＣＯＭＰ、a type of computer？"
			CALL MESSAGE_WINDOW_D, "Gouto", "Be aware that the COMP cannot hold more demons if it is full/so be careful to have free space"
			CALL MESSAGE_WINDOW_D, "Gouto", "To start, it's probably a good idea to gather all the demons on this floor/Among the elite, I have heard good things about you, but…/you are still new and/have much to grow、it would be better to work together"
			SIF GETCHARA([[キャラ:ピクシー]]) >= 0 && CPOS(GETCHARA([[キャラ:ピクシー]])) == 0
				CALL MESSAGE_WINDOW_D, "Gouto", "By the way, haven't you formed any contracts with demons？/If so、they should be properly summoned/Do it now、for when battles begin、summoning demons from your ＣＯＭＰ takes time/Summoning can be done from the Setup menu"
			CALL MESSAGE_WINDOW_D, "Gouto", "Also, if there is nobody in the party who can use a ＣＯＭＰ/Negotiating with or summoning demons is impossible/So beware of status effects such as sleep or paralysis"
			ダンジョンフラグ:34:11 = 1
			LOCAL = 0
			CALL CLEAR_TILE
		ELSEIF FLAG:現M == 0 && FLAG:現X == 4 && FLAG:現Y == 6 && ダンジョンフラグ:34:12 == 0 && FLAG:周回回数 < 1
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "", "＞A group of zombies stands ahead"
			CALL MESSAGE_WINDOW_D, "Gouto", "Rookie…/Are you aware that some demons have weaknesses？"
			CALL MESSAGE_WINDOW_D, "Gouto", "For example, against these guys, zombies, guns are ineffective/However, they are quite weak to swords and Fire/Many flying demons are weak to force and Wind"
			CALL MESSAGE_WINDOW_D, "Gouto", "Aim for weaknesses and you can fight with ease/Although fighting with courage can be good too"
			FLAG:逃走不可フラグ = 1
			FLAG:会話不能フラグ = 1
			FLAG:先制不意打ちキャンセル = 1
			;戦闘後の経験値処理をスキップする
			FLAG:RESULTスキップ = 1
			CALL SET_ENEMY,7,[[キャラ:ゾンビくん]],1,0,0
			CALL SET_ENEMY,8,[[キャラ:ゾンビちゃん]],1,0,0
			CALL SET_ENEMY,9,[[キャラ:ゾンビくん]],1,0,0
			CALL BATTLE_START
			FLAG:逃走不可フラグ = 0
			FLAG:会話不能フラグ = 0
			FLAG:先制不意打ちキャンセル = 0
			;戦闘後の経験値処理をスキップする
			FLAG:RESULTスキップ = 0
			ダンジョンフラグ:34:12 = 1
			CALL CLEAR_TILE
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "Gouto", "Those ones were small fry、so i wasn't particularly worried…/However、don't be negligent with wounds/Even if it's just a little scratch/It could lead to a tough situation"
			CALL MESSAGE_WINDOW_D, "Gouto", "Oi、Raidou、divide up the recovery items"
			CALL MESSAGE_WINDOW_D, "", @"＞Raidou gave %CALLNAME:MASTER% some items"
			CALL GET_ITEMS, "Medicine", 5
			CALL GET_ITEMS, "ValueMedicine", 3
			CALL GET_ITEMS, "RevivalBead", 1
			CALL GET_ITEMS, "Dis-Poison", 3
			CALL GET_ITEMS, "OxBezoar", 1
			CALL MESSAGE_WINDOW_D, "", "＞Received Medicine/＞Received ValueMedicine/＞Received RevivalBead"
			CALL MESSAGE_WINDOW_D, "", "＞Received Dis-Poison/＞Received OxBezoar"
			CALL MESSAGE_WINDOW_D, "Gouto", "Recovery items can be used、along with healing wounds、/for reviving fallen demons、/and curing poison"
			CALL MESSAGE_WINDOW_D, "Gouto", "金がかかることと、/持てる数にそれぞれ限度があることは厄介ではあるが/それでも、効果は絶大だ"
			CALL MESSAGE_WINDOW_D, "Gouto", "お主自身の身を守るのに使うのはもちろん、/お主を信じて戦う仲魔を助けるためにも、/回復アイテムの準備を怠ってはならぬぞ"
		ENDIF
	CASE 3
		;出口でのイベントとかぶるのと
		;脱出後の話は脱出後にだけまとめたほうがいいだろうということでカード入手自体も脱出時にまわす
		;IF FLAG:現X == 5 && FLAG:現Y == 3 && ダンジョンフラグ:34:17 == 0 && FLAG:周回回数 < 1
		;	PRINTFORML ＞Raidou held out a card
		;	PRINTFORMW ＞Gouto began to explain about the card
		;	PRINTFORML 「This is a skill card」
		;	PRINTFORMW 「In the city［122］ they can be used to transfer skills between demons」
		;	PRINTFORML 「At Jakyou Manor［120］ skill cards can be created」
		;	PRINTFORMW 「I have also heard they can be extracted from demons who trust you」
		;	PRINTFORMW 「You should try out several different ways」
		;	PRINTFORMW ＞Skill card 【ガル】 was created
		;	ITEM:11701 += 1
		;	ダンジョンフラグ:34:17 = 1
		;ENDIF
	CASE 4
		IF FLAG:現X == 5 && FLAG:現Y == 3 && ダンジョンフラグ:34:16 == 0 && FLAG:周回回数 < 1
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "", "＞Gouto began to speak"
			CALL MESSAGE_WINDOW_D, "Gouto", "Rookie、you've got the makings of a gunslinger./Do you know about special bullets？/For example、Undead types like Zombie are resistant to normal bullets/But Fire and Light type bullets are quite effective"
			CALL MESSAGE_WINDOW_D, "Gouto", "This time… No、it's faster to just show you /Raidou、give this guy a few bullets"
			CALL MESSAGE_WINDOW_D, "", "＞Received Holy Shell/＞Received Corona Shell/＞Received Frost Shell/＞Received Lantern Shell"
			CALL MESSAGE_WINDOW_D, "Gouto", "Careful、if bullets are not equipped they are useless/This can be done in the ＥＱＵＩＰ menu、in the ［特殊弾変更］ category/Then during battles、you can pick which to use with the ＢＵＬＬＥＴ command/If you have any, you can then attack with them"
			CALL GET_ITEMS, "Holy Shell", 10
			CALL GET_ITEMS, "Corona Shell", 5
			CALL GET_ITEMS, "Frost Shell", 5
			CALL GET_ITEMS, "Lantern Shell", 5
			CALL CLEAR_TILE
			ダンジョンフラグ:34:16 = 1
		ENDIF
	CASE 5
		IF FLAG:現X == 1 && FLAG:現Y == 10 && ダンジョンフラグ:34:9 == 0
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "", @"＞When the door was opened、the stench of death suddenly pressed at %CALLNAME:MASTER%！/＞It seems a large group of zombies were on the other side of the door！"
			;ライドウが生きてる時のみ
			IF FINDCHARA_B(4542,0) == 2
				IF ダンジョンフラグ:34:19++ == 0
					CALL MESSAGE_WINDOW_D, "Gouto", "Mrgh、I thought something was on the other side of that door……/Raidou, get out the supplies"
					CALL MESSAGE_WINDOW_D, "", @"＞Raidou handed supplies to %CALLNAME:MASTER%/＞5 NapalmBomb obtained/＞5 RiotBomb obtained/＞ 5 NitrogenCylinder obtained"
					CALL GET_ITEMS,"NapalmBomb", 5
					CALL GET_ITEMS,"RiotBomb", 5
					CALL GET_ITEMS,"NitrogenCylinder", 5
					CALL MESSAGE_WINDOW_D, "Gouto", "You probably already understand/The importance of exploiting weaknesses、but/your magical power has a limit/so use items to compensate"
					CALL MESSAGE_WINDOW_D, "Gouto", "Among the items Raidou gave you、/There are some useful for this situation/When the battle begins、try using what you think will work/Now then、let's go！"
				ELSE
					CALL MESSAGE_WINDOW_D, "Gouto", "Are you ready this time？/Use items、and work together"
					IF ITEM:NapalmBomb == 0
						CALL MESSAGE_WINDOW_D, "Gouto", "Oi、you don't have any NapalmBomb？…/Can't be helped then。Raidou"
						CALL GET_ITEMS,"NapalmBomb", 1
						CALL MESSAGE_WINDOW_D, "", "＞Raidou passed an item to %CALLNAME:MASTER%/＞Received 1 NapalmBomb"
					ENDIF
					CALL MESSAGE_WINDOW_D, "Gouto", "Keep gathering demons after this and you should excel/Now then、let's go！"
				ENDIF
				ダンジョンフラグ:34:18 = 1
			ENDIF
			FLAG:逃走不可フラグ = 1
			FLAG:会話不能フラグ = 1
			FLAG:先制不意打ちキャンセル = 1
			IF FINDCHARA_B(4542,0) == 2
				FLAG:RESULTスキップ = 1
				FOR LOCAL, 7, 12
					CALL SET_ENEMY,LOCAL,[[キャラ:ゾンビくん]],9,0,1
					CSTR:(CHARANUM-1):思考パターン = チュートリアルゾンビ
					BASE:(CHARANUM-1):疾風 = 25
					BASE:(CHARANUM-1):火炎 = 400
					MAXBASE:(CHARANUM-1):疾風 = 25
					MAXBASE:(CHARANUM-1):火炎 = 400
					CFLAG:(CHARANUM-1):速度補正 = -100
					CFLAG:(CHARANUM-1):回避補正 = -100
					CALL SET_BATTLE_STATUS(CHARANUM-1, 80, "ＨＰ")
					CALL SYNC_STATUS, CHARANUM- 1
					CALL HEALTH_CHARA, CHARANUM-1
				NEXT
			ELSE
				CALL SET_ENEMY,7,[[キャラ:ゾンビくん]],13,0,0
				CALL SET_ENEMY,8,[[キャラ:ゾンビちゃん]],10,0,0
				CALL SET_ENEMY,9,[[キャラ:ゾンビくん]],13,0,0
				CALL SET_ENEMY,10,[[キャラ:ゾンビナース]],14,0,0
				CALL SET_ENEMY,11,[[キャラ:ゾンビちゃん]],10,0,0
			ENDIF
			CALL BATTLE_START
			FLAG:逃走不可フラグ = 0
			FLAG:会話不能フラグ = 0
			FLAG:先制不意打ちキャンセル = 0
			FLAG:RESULTスキップ = 0
			IF ダンジョンフラグ:34:18 == 2
				FLAG:現Y -= 2
				CALL SHOW_PICTURE, "D"
				CALL MESSAGE_WINDOW_D, "Gouto", "Rookie、you alright？/So many of them/was quite the challenge"
				CALL MESSAGE_WINDOW_D, "Gouto", "even so、if by any chance/your power isn't sufficient、you should collect more demons/COOP attacks can be performed by allies next to each other/The more allies that take part, the more the damage increases"
				ダンジョンフラグ:34:18 = 0
				RETURN 0
			ENDIF
			ダンジョンフラグ:34:18 = 0
			ダンジョンフラグ:34:9 = 1
			CALL CLEAR_TILE
			;ライドウ
			IF (FINDCHARA_B(4542,1) == 2 || FINDCHARA_B(4542,0) == 2) && FLAG:周回回数 < 1
				CALL SHOW_PICTURE, "D"
				CALL MESSAGE_WINDOW_D, "Gouto", "Rookie, are you alright？"
				CALL INPUT_YN_D
				IF RESULT == 0
					CALL MESSAGE_WINDOW_D, "Gouto", "If it's not serious, that's good"
				ELSE
					CALL MESSAGE_WINDOW_D, "Gouto", "If you can still reply, you must be alright/However, if you feel you're in danger, head back"
				ENDIF
				CALL MESSAGE_WINDOW_D, "Gouto", "Especially on the other side of this door/many strong enemies are waiting/so you'll want to be in perfect shape before opening it"
			ENDIF
		ENDIF
	CASE 7
		CALL SHOW_PICTURE, "D"
		IF ダンジョンフラグ:34:31 == 0
			CALL MESSAGE_WINDOW_D, "Gouto", "That's Kikuri-hime…/Why is it in such a place…？/Rookie, that demon has the Curse skill [Mudo]"
			CALL MESSAGE_WINDOW_D, "Gouto", "When a skill like that hits、no matter how strong you are、you'll die in a single blow/That story probably left you shaking in your boots、huh？/However、there are countermeasures"
		ENDIF
		CALL MESSAGE_WINDOW_D, "Gouto", "You should use GUARD/While using GUARD、instant kill spells have no effect/During that time Raidou will take care of them for you"
		CALL SET_ENEMY, 9,[[キャラ:キクリヒメ]],9,0,1
		CSTR:(CHARANUM-1):思考パターン = チュートリアルキクリヒメ
		CFLAG:(CHARANUM-1):速度補正 = 100
		CFLAG:(CHARANUM-1):ＭＰ補正 = 1000
		CALL SYNC_STATUS, CHARANUM-1
		CALL HEALTH_CHARA, CHARANUM-1
		FLAG:逃走不可フラグ = 1
		FLAG:会話不能フラグ = 1
		FLAG:先制不意打ちキャンセル = 1
		FLAG:RESULTスキップ = 1
		ダンジョンフラグ:34:18 = 3
		CALL BATTLE_START
		FLAG:逃走不可フラグ = 0
		FLAG:会話不能フラグ = 0
		FLAG:先制不意打ちキャンセル = 0
		FLAG:RESULTスキップ = 0
		IF ダンジョンフラグ:34:18 == 2
			FLAG:現Y += 2
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "Gouto", "Curse skills sure are scary/You understand now, right？/This time things went well because Raidou was here to help、/but you should essentially understand what to do now"
			CALL MESSAGE_WINDOW_D, "Gouto", "Nonetheless, no matter how strong you are, you still have a weakness/Kikuri-hime reminded you of that/Well then, take care as we go"
			ダンジョンフラグ:34:31 = -1
		ELSE
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D, "Gouto", "Were you able to make it out safely？/GUARD not only simply reduces the damage of attacks、/but it can be used this way too"
			CALL MESSAGE_WINDOW_D, "Gouto", "Also、GUARD not only stops Curse、/but other bad statuses like Poison or Charm as well/And, as long as you are using GUARD、/enemies hitting your weakness will not cause １ＭＯＲＥ to occur"
			CALL MESSAGE_WINDOW_D, "Gouto", "Do you understand the importance of this？/This means you can overcome the weaknesses/you and your demons each have"
			CALL MESSAGE_WINDOW_D, "Gouto", "Of course, a demon in battle will not tell you their weakness, but/if you instruct to GUARD、/you can protect your demons/Remember to give accurate instructions"
			CALL CLEAR_TILE
			ダンジョンフラグ:34:31 = 1
		ENDIF
		ダンジョンフラグ:34:18 = 0
	CASE 8
		IF FLAG:現X == 12 && FLAG:現Y == 5 && ダンジョンフラグ:34:14 <= 0
			CALL SHOW_PICTURE, "D"
			FLAG:逃走不可フラグ = 1
			FLAG:会話不能フラグ = 1
			FLAG:先制不意打ちキャンセル = 1
			;ライドウ
			IF (FINDCHARA_B(4542,1) == 2 || FINDCHARA_B(4542,0) == 2) && GETCHARA([[キャラ:モー・ショボー]]) == -1
				CALL MESSAGE_WINDOW_D, "", "＞A large quantity of machines are in the passage！"
				FLAG:RESULTスキップ = 1
				IF ダンジョンフラグ:34:14 == 0
					CALL MESSAGE_WINDOW_D, "Gouto", "Machinery、huh……/Machines are weak to Elect shocks/Use electricity and they'll be brought down quickly"
					CALL MESSAGE_WINDOW_D, "Gouto", "That said, this many is still dangerous…/Raidou, have this guy do a fusion spell/Pass him a Gun to use"
					CALL MESSAGE_WINDOW_D, "", @"＞When Gouto winked、Raidou nodded/＞He handed the Pipe Shotgun to %CALLNAME:MASTER%/＞%CALLNAME:MASTER% equipped it"
					EQUIP:MASTER:特殊弾1 = GETNUM(ITEM, "Corona Shell")
					EQUIP:MASTER:特殊弾2 = 0
					EQUIP:MASTER:特殊弾3 = 0
					CALL GET_ITEMS,"Corona Shell",5
					CALL GET_ITEMS,"Pipe Shotgun",1
					CALL 装備変更, MASTER, GET_EQUIPNUM("銃"), GETNUM(ITEM, "Pipe Shotgun")
					DA:34:18 = 1
					CALL MESSAGE_WINDOW_D, "Gouto", "That's a shotgun/It can hit an entire row of enemies at the same time"
					CALL MESSAGE_WINDOW_D, "Gouto", "If you use the Corona Shell、/It will hit the entire first row of enemies/But in this case that won't be enough。 Raidou！"
				ENDIF
				CALL MESSAGE_WINDOW_D, "", "＞Raidou summoned Raptor Moh Shuvuu/＞Moh Shuvuu will temporarily join the party"
				CALL ADD_GUEST_COMPANION, [[キャラ:モー・ショボー]], 200, 1
				ABL:GETCHARA([[キャラ:モー・ショボー]]):スキル2 = [[スキル:マハ・ガル]]
				SIF ITEM:"Corona Shell" == 0
					CALL GET_ITEMS,"Corona Shell",3
				CALL MESSAGE_WINDOW_D, "Gouto", "Corona Shell plus skills like Garu or Magaru/can combine to create an Elect-charged magical bullet technique that is used、/which can turn a weak attack into a strong one"
				CALL MESSAGE_WINDOW_D, "Gouto", "If you attack with a handgun, the fusion spell will hit a row、/and if you use a shotgun it will hit all enemies/When the battle begins, pick Fusion spell"
				CALL MESSAGE_WINDOW_D, "", "＞10 Bit Balls appeared！"
				FOR LOCAL, 7 ,17
					CALL SET_ENEMY,LOCAL,[[キャラ:ビットボール]],9,0,1
					CSTR:(CHARANUM-1):思考パターン = チュートリアルビットボール
					BASE:(CHARANUM-1):疾風 = 25
					BASE:(CHARANUM-1):電撃 = 300
					MAXBASE:(CHARANUM-1):疾風 = 25
					MAXBASE:(CHARANUM-1):電撃 = 400
					CFLAG:(CHARANUM-1):魔法被ダメージ補正 = -50
					CFLAG:(CHARANUM-1):COOP被ダメージ補正 = 250
					CFLAG:(CHARANUM-1):速度補正 = -100
					ダンジョンフラグ:34:18 = 1
					CALL SET_BATTLE_STATUS(CHARANUM-1, 120, "ＨＰ")
					CALL SYNC_STATUS, CHARANUM- 1
					CALL HEALTH_CHARA, CHARANUM-1
				NEXT
			ELSE
				ダンジョンフラグ:34:18 = 0
				CALL MESSAGE_WINDOW_D, "", "＞A machine blocked the way"
				CALL MESSAGE_WINDOW_D, "Machine", "Beginning intruder elimination"
				CALL MESSAGE_WINDOW_D, "", "＞3 Bit Balls appeared！"
				CALL SET_ENEMY,8,[[キャラ:ビットボール]],13,0,0
				CALL SET_ENEMY,9,[[キャラ:ビットボール]],13,0,0
				CALL SET_ENEMY,10,[[キャラ:ビットボール]],13,0,0
			ENDIF
			CALL BATTLE_START
			FLAG:逃走不可フラグ = 0
			FLAG:会話不能フラグ = 0
			FLAG:先制不意打ちキャンセル = 0
			FLAG:RESULTスキップ = 0
			IF ダンジョンフラグ:34:32
				CALL キャラ削除(GETCHARA([[キャラ:モー・ショボー]]))
				IF ダンジョンフラグ:34:18 == 2
					FLAG:現Y -= 2
					CALL SHOW_PICTURE, "D"
					CALL MESSAGE_WINDOW_D, "Gouto", "ふぅ、さすがに冷や汗がでたぞ/新米よ。さすがにここでミスするのは洒落にならん/また、Moh Shuvuuを呼び出すから次はしっかり頼むぞ"
					ダンジョンフラグ:34:18 = 0
					ダンジョンフラグ:34:14 = -1
					RETURN 0
				ENDIF
				ダンジョンフラグ:34:18 = 0
				CALL SHOW_PICTURE, "D"
				CALL MESSAGE_WINDOW_D, "Raptor Moh Shuvuu", "I'm tired~/Hey, can I go back already?？"
				CALL MESSAGE_WINDOW_D, "", "＞Raidou nodded and returned Moh Shuvuu/＞Moh Shuvuu was detached from the party"
				CALL MESSAGE_WINDOW_D, "Gouto", "How was it？/Fusion spells are strong, you understand now don't you？/Not only you with allies, but also allies with allies can perform Fusion spells"
				CALL MESSAGE_WINDOW_D, "Gouto", "このように力をあわせることができるように/仲魔を集めていくのも一つのやり方だな/まぁそのためにはスキルの組み合わせ方を知らねばならぬが…"
				CALL MESSAGE_WINDOW_D, "Gouto", "とりあえず、そのＣＯＭＰにも/いくつかレシピが登録されてるのはではないか？/戦闘中にリンケージを選び、/そこからアナライズをして確認しておくことだ"
				CALL MESSAGE_WINDOW_D, "Gouto", "また、これから新たに発見したレシピも/登録され、確認できるようになるぞ/まぁなんにしろとにかく、/色々試してみれば、そのうちわかってくるだろう"
			ENDIF
			ダンジョンフラグ:34:14 = 1
			CALL CLEAR_TILE
		ENDIF
	CASE 9
		IF FLAG:現X == 8 && FLAG:現Y == 9 && ダンジョンフラグ:34:8 == 0
			;部屋をアナライズし表示更新
			CALL SET_FLOOR_ANALYZE(2, FLAG:現ダンジョン, FLAG:現M, 8, 11)
			FOR LOCAL:1, 6, 13
				FOR LOCAL, 9, 13
					CALL SET_FLOOR_ANALYZE(2, FLAG:現ダンジョン, FLAG:現M, LOCAL, LOCAL:1)
				NEXT
			NEXT
			CALL MESSAGE_WINDOW_D, "", "＞In a room lined with many computers and monitors、/＞one demon was facing a monitor"
			CALL MESSAGE_WINDOW_D, "Fallen Dantalion", "Who are you！/……No、it doesn't matter who you are/I've been starving for MAG/I'll squeeze it out of you！"
			CALL MESSAGE_WINDOW_D, "", "＞1 Fallen Dantalion appeared！！"
			FLAG:逃走不可フラグ = 1
			FLAG:会話不能フラグ = 1
			FLAG:先制不意打ちキャンセル = 1
			;チュートリアルだとLVが減る
			IF ダンジョンフラグ:34:30
				CALL SET_ENEMY,9, [[キャラ:ダンタリオン]],8,0,1
			ELSE
				CALL SET_ENEMY,9, [[キャラ:ダンタリオン]],15,0,1
			ENDIF
			CFLAG:(CHARANUM-1):行動回数 = 2
			CFLAG:(CHARANUM-1):ＨＰ補正 += 400
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			CALL BATTLE_START
			FLAG:逃走不可フラグ = 0
			FLAG:会話不能フラグ = 0
			FLAG:先制不意打ちキャンセル = 0
			CALL MESSAGE_WINDOW_D, "", "＞Fallen Dantalion ran out of energy…"
			CALL MESSAGE_WINDOW_D, "", @"%CALLNAME:MASTER%はダンタリオンが操作していたコンピュータの画面を覗き込んだ…/画面にはスクリプトが踊っている…/細かい内容はわからないが、/どうもインストールソフトのようだ"
			CALL MESSAGE_WINDOW_D, "", "＞適当に操作を続けると、ソフトの説明書が見つかった/＞『新たな儀式に適応せし、新たなソロモンの鍵の作成……』/＞『天使を放逐し、我らが道を開くためのプログラム……』"
			CALL MESSAGE_WINDOW_D, "", @"＞どうやら、これはコンバータのようだ/＞しかも、すでに完成済みであるらしい……/＞%CALLNAME:MASTER%はソフトをダウンロードすることにした…/＞インストールソフト【Goetia】を手に入れた…/＞インストールソフト【ラビスシジル】を手に入れた…"
			;ライドウ
			LOCAL = GETCHARA([[キャラ:ライドウ]])
			IF LOCAL >= 0 && !CFLAG:LOCAL:引継ぎ回数
				CALL MESSAGE_WINDOW_D, "Gouto", "どうやらここは俺達の探している場所ではないようだな/ファントムのサマナーよ/共に歩むのはココまでだ"
				CALL MESSAGE_WINDOW_D, "Gouto", "今日教えたことを忘れずに/己を磨き、強くなることだ"
				CALL MESSAGE_WINDOW_D, "Gouto", "そうだな……/帰ったら、まずは[120] 邪教の館にいくといい/そこで仲魔同士を合体させ、より強力な仲魔を得ることができる"
				CALL MESSAGE_WINDOW_D, "Gouto", "サマナーにとって強くなるということは/要するにより強力な仲魔を作ることだ/ＴＡＬＫで仲魔を誘うのも重要だが、合体も疎かには出来ぬ"
				CALL MESSAGE_WINDOW_D, "Gouto", "とはいえ、お主自身もまた強くならねば/強力な仲魔を従えることはできないが……/お主が強くなれば、それだけ強い仲魔を得られるのを/しっかりと覚えておくが良い"
				CALL MESSAGE_WINDOW_D, "Gouto", "あとはそうだな/[122] 仲魔強化も試してみるべきだな/今いるお主の仲魔を育て、強化することができる"
				CALL MESSAGE_WINDOW_D, "Gouto", "ＭＡＧさえあれば、力や魔力といったステータスを伸ばす他に/相性や攻撃範囲など様々な能力を変更できる/まぁ、そのＭＡＧを用意するのが難しいのだがな…"
				CALL MESSAGE_WINDOW_D, "", "＞ライドウはGoutoに耳打ちすると/＞一枚のカードを差し出してきた/＞You got one Skillcard【Garu】"
				CALL GET_ITEMS,"スキルカード【ガル】", 1
				CALL MESSAGE_WINDOW_D, "Gouto", "済まない/大切な事を言い忘れるところだった/そのカードはスキルカードといい、/魔法や特技の力が籠められているのだ"
				CALL MESSAGE_WINDOW_D, "Gouto", "[122] 仲魔強化ではそのカードとＭＡＧを使い、/仲魔にスキルを覚えさせることができる/そのカードなら当然ガルだ"
				CALL MESSAGE_WINDOW_D, "Gouto", "先程はモー・ショボーと共にリンケージをしたが/そのカードを使えばお主の仲魔とリンケージすることもできるな/どうだ？　便利だろう？"
				CALL MESSAGE_WINDOW_D, "Gouto", "そしてカードは拾ったり、貰ったりもできるが……/その他に邪教の館で仲魔の力を/カード化してもらうことで集めることも出来るぞ"
				CALL MESSAGE_WINDOW_D, "Gouto", "まぁ……そうするためには仲魔に信頼されてなければならなく/カード化してしまえばその仲魔からスキルは失われるが……/ここらへんについてはよく考える事だな"
				CALL MESSAGE_WINDOW_D, "Gouto", "おっと、[115]ジャンクショップや[114]魔貨交換も忘れるなよ/金や魔貨をつかうことで様々な装備やアイテム、それにインストールソフトなども買えるぞ"
				CALL MESSAGE_WINDOW_D, "Gouto", "アイテムの有用性は今日の戦いでよくわかったと思うが/装備の大切さも忘れてはならない/当然のことだが人間は悪魔に比べて非力だ"
				CALL MESSAGE_WINDOW_D, "Gouto", "防具がなければ悪魔の攻撃には耐えられないし/武器がなければダメージを与えることも難しい/いや、それどころか武器が悪ければ攻撃も当たらなくなる"
				CALL MESSAGE_WINDOW_D, "Gouto", "魔法を使う人間でも武器に宿る魔力や/防具に宿る魔法効果を利用しなければ/魔法を有効につかうこともできない"
				CALL MESSAGE_WINDOW_D, "Gouto", "要するに人間が悪魔と渡り合うには金が必要ということだ/装備にしろ、アイテムにしろ、/インストールソフトにしろ、何が必要になるかわからない"
				CALL MESSAGE_WINDOW_D, "Gouto", "以前に何気なく買っておいたものが/意外に役に立つということもある/無駄遣いをしろというわけではないが、/あまりけちけちするべきでもないな"
				CALL MESSAGE_WINDOW_D, "Gouto", "まぁ、とにかく、強くなるための努力を惜しまないことだ/でなければ、生き残れない世界だ/では、縁があればまた会う事もあるだろう"
				CALL MESSAGE_WINDOW_D, "", "＞GoutoとライドウがＰＴを去った"
				CALL DEL_GUEST, LOCAL
			ENDIF
			CALL MESSAGE_WINDOW_D, "", "＞Hatsudai Shelter was secured"
			IF ダンジョンフラグ:34:30
				CALL MESSAGE_WINDOW_D, "", "＞￥10000 was obtained as a reward"
				MONEY += 10000
			ELSE
				CALL MESSAGE_WINDOW_D, "", "＞35000 was obtained as a reward"
				MONEY += 35000
			ENDIF
			ITEM:Goetia += 1
			ITEM:ラビスシジル += 1
			ダンジョンフラグ:34:8 = 1
			ダンジョンフラグ:34:10 = 1
			ダンジョンフラグ:34:13 = 2
			ダンジョンフラグ:34:32 = 0
			CALL CLEAR_TILE
			FLAG:脱出スキル禁止 = 0
			FLAG:脱出 = 1
			RETURN 1
		ENDIF
	CASEELSE
	ENDSELECT
	RETURN 1
RETURN 0
;=======================================================================
;宝箱まとめ
;=======================================================================
@TRESURING_CALLING_34
SELECTCASE FLAG:現M
	CASE 5
		CALL GET_TREASURE,1017,2,34,1
	CASE 6
		IF FLAG:現X == 1 && FLAG:現Y == 8
			CALL GET_TREASURE,1001,3,34,15
		ELSEIF FLAG:現X == 12 && FLAG:現Y == 3
			CALL GET_TREASURE,1002,2,34,2
		ENDIF
	CASE 7
		CALL GET_TREASURE,1005,1,34,3
	CASE 8
		IF FLAG:現X == 12 && FLAG:現Y == 8
			CALL GET_TREASURE,2005,1,34,4
		ELSEIF FLAG:現X == 4 && FLAG:現Y == 7
			;チュートリアルを受けた場合はディスポイズン
			IF ダンジョンフラグ:34:30
				CALL GET_TREASURE,GETNUM(ITEM, "Dis-Poison"),3,34,5
			ELSE
				CALL GET_TREASURE,GETNUM(ITEM, "Pipe Shotgun"),1,34,5
			ENDIF
		ENDIF
	CASE 9
		CALL GET_TREASURE,1034,2,34,6
ENDSELECT
RETURN 0


@SYODAI_E
CALL MESSAGE_WINDOW_D, "", "＞Seems to be an elevator……But、it was broken by demons"

@SET_GUEST_D34
#LOCALSIZE 1
CALL ADD_GUEST_COMPANION(4542, 0, 1)
CALL MESSAGE_WINDOW_D, "", @"＞Raidou Kuzunoha joined %CALLNAME:MASTER%'s party%\@RESULT:1 >= 0 ?/＞Please note that %CALLNAME:(RESULT:1)% was returned to the comp#\@%"
LOCAL = GETCHARA(4542)
BASE:LOCAL:LV += 10
BASE:LOCAL:ＭＡＧ = 0
BASE:LOCAL:力 += 6
BASE:LOCAL:知恵 += 5
BASE:LOCAL:魔力 += 5
BASE:LOCAL:耐力 += 5
BASE:LOCAL:速さ += 6
BASE:LOCAL:運 += 5
ABL:(CHARANUM-1):スキル2 = 308
ABL:(CHARANUM-1):スキル3 = 813
ABL:(CHARANUM-1):スキル4 = 914
ABL:(CHARANUM-1):スキル5 = 1710
ABL:(CHARANUM-1):スキル7 = 515
ABL:(CHARANUM-1):スキル8 = 2430
CALL SYNC_STATUS, LOCAL
CALL HEALTH_CHARA, LOCAL


;=====================================================================
;ターミナル
;=====================================================================
@RAIDOU_TERMINAL
CALL SHOW_PICTURE, "D"
CALL MESSAGE_WINDOW_D, "", "＞There is a terminal"
CALL MESSAGE_WINDOW_D, "Terminal", "This is the Hatsudai Shelter terminal/Please state your business"
$START
CALL INPUT_SELECT_D, "[1] Save/[2] Change installed software/[3] Return to base/[0] Do nothing"
IF RESULT == 0
	RETURN 1
ELSEIF RESULT == 1
	CALL TITLE_SAVEGAME
ELSEIF RESULT == 2
	CALL SETUP_SOFT
ELSEIF RESULT == 3
	LOCAL = GETCHARA([[キャラ:ライドウ]])
	IF LOCAL >= 0 && !CFLAG:LOCAL:引継ぎ回数
		CALL MESSAGE_WINDOW_D, "Gouto", "Are you going back？/Today is the only day/that I can instruct you properly"
		CALL INPUT_YN_D
		IF RESULT == 0
			CALL MESSAGE_WINDOW_D, "Gouto", "Then、farewell/I would appreciate it if you examined the area"
			CALL MESSAGE_WINDOW_D, "", "＞Gouto and Raidou left ＰＴ"
			ダンジョンフラグ:34:13 = 1
			ダンジョンフラグ:34:32 = 0
			CALL DEL_GUEST, LOCAL
			FLAG:脱出スキル禁止 = 0
			CALL MESSAGE_WINDOW_D, "Terminal", "Beginning transfer"
			CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER% returned to base"
			FLAG:脱出 = 1
		ELSE
			CALL MESSAGE_WINDOW_D, "Gouto", "Then、let's continue searching"
			CALL MESSAGE_WINDOW_D, "", "＞Gouto prompted you to advance into the dungeon"
			FLAG:脱出 = 0
		ENDIF
	ELSE
		FLAG:脱出 = 1
		FLAG:脱出スキル禁止 = 0
	ENDIF
	RETURN 1
ENDIF
GOTO START

@EVENT_BATTLE_ACTEND_DUNGEON34
SIF ダンジョンフラグ:34:32 == 0
	RETURN 0
CALL SYNC_STATUS, MASTER
SIF BASE:MASTER:ＨＰ > 0
	RETURN 0
CALL MESSAGE_B(2, "MMmmrg、this is bad！", "Raidou！　Our ally is going down！")
PRINTL 
PRINTFORMW ＞Raidou ran over and caught %CALLNAME:MASTER% with an arm
PRINTL 
BASE:MASTER:ＨＰ = 1
CFLAG:MASTER:ステート = GET_STATE_NUM("GOOD")
FLAG:勝利フラグ = 1
FLAG:敗北フラグ = 0
ダンジョンフラグ:34:18 = 2
RETURN -1
@EVENT_BATTLE_TURNEND_DUNGEON34
SIF ダンジョンフラグ:34:18 != 1
	RETURN 0
CALL MESSAGE_B(2, "Mmmmrgh、did you fail", "Raidou！　Our ally is going down！")
FLAG:勝利フラグ = 1
ダンジョンフラグ:34:18 = 2
PRINTL 
PRINTFORMW ＞Raidou ran over and caught %CALLNAME:MASTER% with an arm
PRINTL 
RETURN -1
@SET_ACTION_チュートリアルゾンビ, ARG
IF CPOS(MASTER) > 3
	CFLAG:ARG:入力行動 = -1
ELSE
	CFLAG:ARG:入力行動 = [[スキル:ひっかき]]
	CFLAG:ARG:ターゲット = CPOS(MASTER)
ENDIF
@SET_ACTION_チュートリアルビットボール, ARG
CFLAG:ARG:入力行動 = [[スキル:電撃]]
CFLAG:ARG:ターゲット = CPOS(MASTER)


@SET_ACTION_チュートリアルキクリヒメ, ARG
CFLAG:ARG:入力行動 = [[スキル:ムド]]
MAXBASE:MASTER:呪殺 = 3000
CFLAG:ARG:ターゲット = CPOS(MASTER)


;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_34
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:4
	CASE 0
		RETURN [[キャラ:ノッカー]] , 6 + RAND:15 , CSVBASE([[キャラ:ノッカー]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:コボルト]] , 3 + RAND:10 , CSVBASE([[キャラ:コボルト]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:ガキ]] , 3 + RAND:10 , CSVBASE([[キャラ:ガキ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 3
		RETURN [[キャラ:ゾンビくん]] , 3 + RAND:10 , CSVBASE([[キャラ:ゾンビくん]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT


