;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_51
SELECTCASE FLAG:現M
	;１Fは敵がでない
	CASE 0
		FLAG:エンカウント率 = 0
	;１F奥から敵出現開始,悪魔出現フラグがたってない場合、エンカウントが少ない
	CASEELSE
		FLAG:エンカウント率 = FLAG:未遭遇歩数 * 5
ENDSELECT
SIF FLAG:未遭遇歩数 < 5
	FLAG:エンカウント率 = 0
;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_51
RETURN 19, 16, 20, 24
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_51
#LOCALSIZE 10
IF RAND:100 < FLAG:エンカウント率
	IF RAND:2
		;一色目
		LOCAL:4 = RAND:4 + [[キャラ:牡牛カメーン]]
		$CHANGE_KAMEN
		;ニ色目
		LOCAL:5 = RAND:4 + [[キャラ:牡牛カメーン]]
		SIF LOCAL:4 == LOCAL:5
			GOTO CHANGE_KAMEN
		LOCAL:6 = [[キャラ:牡牛カメーン]] + RAND:2
		;後衛タイプ
		LOCAL:7 = RAND:2 + [[キャラ:蠍カメーン]]
		SELECTCASE FLAG:現M
			CASE 1
				SELECTCASE RAND:100
					;同色1列 30%
					CASE IS < 30
						CALL SET_ENEMY,8,LOCAL:4, 18,0
						CALL SET_ENEMY,9,LOCAL:4, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
					;2色1列 30%
					CASE IS < 60
						CALL SET_ENEMY,8,LOCAL:4, 18,0
						CALL SET_ENEMY,9,LOCAL:5, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
					;同色2列 20%
					CASE IS < 80
						CALL SET_ENEMY,8,LOCAL:4, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
						CALL SET_ENEMY,13,LOCAL:4, 18,0
						CALL SET_ENEMY,15,LOCAL:4, 18,0
					;2色2列 20%
					CASE IS < 100
						CALL SET_ENEMY,8,LOCAL:6, 18,0
						CALL SET_ENEMY,10,LOCAL:6, 18,0
						CALL SET_ENEMY,13,LOCAL:7, 18,0
						CALL SET_ENEMY,15,LOCAL:7, 18,0
				ENDSELECT
			CASE 2, 3
				SELECTCASE RAND:100
					;同色1列 20%
					CASE IS < 20
						CALL SET_ENEMY,7,LOCAL:4, 18,0
						CALL SET_ENEMY,8,LOCAL:4, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
						CALL SET_ENEMY,11,LOCAL:4, 18,0
					;2色1列 20%
					CASE IS < 40
						CALL SET_ENEMY,7,LOCAL:4, 18,0
						CALL SET_ENEMY,8,LOCAL:5, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
						CALL SET_ENEMY,11,LOCAL:5, 18,0
					;同色2列 20%
					CASE IS < 60
						CALL SET_ENEMY,8,LOCAL:4, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
						CALL SET_ENEMY,13,LOCAL:4, 18,0
						CALL SET_ENEMY,15,LOCAL:4, 18,0
					;2色2列 20%
					CASE IS < 80
						CALL SET_ENEMY,8,LOCAL:6, 18,0
						CALL SET_ENEMY,10,LOCAL:6, 18,0
						CALL SET_ENEMY,13,LOCAL:7, 18,0
						CALL SET_ENEMY,15,LOCAL:7, 18,0
					;4色1列 10%
					CASE IS < 90
						CALL SET_ENEMY,7, [[キャラ:牡牛カメーン]], 18,0
						CALL SET_ENEMY,8, [[キャラ:獅子カメーン]], 18,0
						CALL SET_ENEMY,10, [[キャラ:蠍カメーン]], 18,0
						CALL SET_ENEMY,11, [[キャラ:水瓶カメーン]], 18,0
					;4色2列 10%
					CASE IS < 100
						CALL SET_ENEMY,8, [[キャラ:牡牛カメーン]], 18,0
						CALL SET_ENEMY,10, [[キャラ:獅子カメーン]], 18,0
						CALL SET_ENEMY,13, [[キャラ:蠍カメーン]], 18,0
						CALL SET_ENEMY,15, [[キャラ:水瓶カメーン]], 18,0
				ENDSELECT
			CASE 4
				SELECTCASE RAND:100
					;同色1列 15%
					CASE IS < 15
						CALL SET_ENEMY,7,LOCAL:4, 18,0
						CALL SET_ENEMY,8,LOCAL:4, 18,0
						CALL SET_ENEMY,9,LOCAL:4, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
						CALL SET_ENEMY,11,LOCAL:4, 18,0
					;2色1列 20%
					CASE IS < 35
						CALL SET_ENEMY,7,LOCAL:4, 18,0
						CALL SET_ENEMY,8,LOCAL:5, 18,0
						CALL SET_ENEMY,9,LOCAL:4, 18,0
						CALL SET_ENEMY,10,LOCAL:5, 18,0
						CALL SET_ENEMY,11,LOCAL:4, 18,0
					;同色2列 20%
					CASE IS < 55
						CALL SET_ENEMY,8,LOCAL:4, 18,0
						CALL SET_ENEMY,9,LOCAL:4, 18,0
						CALL SET_ENEMY,10,LOCAL:4, 18,0
						CALL SET_ENEMY,13,LOCAL:4, 18,0
						CALL SET_ENEMY,14,LOCAL:4, 18,0
						CALL SET_ENEMY,15,LOCAL:4, 18,0
					;2色2列 20%
					CASE IS < 75
						CALL SET_ENEMY,8,LOCAL:6, 18,0
						CALL SET_ENEMY,9,LOCAL:6, 18,0
						CALL SET_ENEMY,10,LOCAL:6, 18,0
						CALL SET_ENEMY,13,LOCAL:7, 18,0
						CALL SET_ENEMY,14,LOCAL:7, 18,0
						CALL SET_ENEMY,15,LOCAL:7, 18,0
					;4色1列 10%
					CASE IS < 85
						CALL SET_ENEMY,7,[[キャラ:牡牛カメーン]], 18,0
						CALL SET_ENEMY,8,[[キャラ:獅子カメーン]], 18,0
						CALL SET_ENEMY,10,[[キャラ:蠍カメーン]], 18,0
						CALL SET_ENEMY,11,[[キャラ:水瓶カメーン]], 18,0
					;4色2列 10%
					CASE IS < 95
						CALL SET_ENEMY,8, [[キャラ:牡牛カメーン]], 18,0
						CALL SET_ENEMY,10, [[キャラ:獅子カメーン]], 18,0
						CALL SET_ENEMY,13, [[キャラ:蠍カメーン]], 18,0
						CALL SET_ENEMY,15, [[キャラ:水瓶カメーン]], 18,0
					;4色2列B 5%
					CASE IS < 100
						CALL SET_ENEMY,7,[[キャラ:牡牛カメーン]], 18,0
						CALL SET_ENEMY,8,[[キャラ:牡牛カメーン]], 18,0
						CALL SET_ENEMY,10,[[キャラ:獅子カメーン]], 18,0
						CALL SET_ENEMY,11,[[キャラ:獅子カメーン]], 18,0
						CALL SET_ENEMY,12,[[キャラ:蠍カメーン]], 18,0
						CALL SET_ENEMY,13,[[キャラ:蠍カメーン]], 18,0
						CALL SET_ENEMY,15,[[キャラ:水瓶カメーン]], 18,0
						CALL SET_ENEMY,16,[[キャラ:水瓶カメーン]], 18,0
				ENDSELECT
		ENDSELECT
	ELSE
		CALL ENEMY_TABLE
	ENDIF
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_51
RETURN CLASS_NUM_COMMON("中級")
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;=======================================================================
@ENEMY_NUM_51, ARG
RETURN ENEMY_NUM_COMMON("中級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_51, ARG
SELECTCASE FLAG:現M
	CASE 1
	;１Ｆ奥での遭遇
		SELECTCASE RAND:100
			CASE IS < 20
				RETURN [[キャラ:イーリス]],16
			CASE IS < 40
				RETURN [[キャラ:ニケー]],16
			CASE IS < 60
				RETURN [[キャラ:はなこ]],16
			CASE IS < 80
				RETURN [[キャラ:ジャックフロスト]],16
			CASE IS < 100
				RETURN [[キャラ:ガギソン]],16
		ENDSELECT
	CASE 2, 3
		SELECTCASE RAND:100
			CASE IS < 20
				RETURN [[キャラ:キヨヒメ]], 18
			CASE IS < 40
				RETURN [[キャラ:カシマレイコ]], 18
			CASE IS < 60
				RETURN [[キャラ:イーリス]], 18
			CASE IS < 80
				RETURN [[キャラ:ジャックフロスト]], 18
			CASE IS < 100
				RETURN [[キャラ:ガギソン]], 18
		ENDSELECT
	CASE 4
	;3Fでの遭遇
		SELECTCASE RAND:100
			CASE IS < 20
				RETURN [[キャラ:ルサールカ]],  20, 0
			CASE IS < 40
				RETURN [[キャラ:カシマレイコ]], 20
			CASE IS < 60
				RETURN [[キャラ:リャナンシー]], 20
			CASE IS < 80
				RETURN [[キャラ:ノズチ]], 20
			CASE IS < 100
				RETURN [[キャラ:コッパテング]], 20
		ENDSELECT
ENDSELECT
;仮面党員の思考パターン
;ターン経過数に応じて、一定人数が魔法攻撃をしてくる
;はじめは二人だけど、処理が遅れると魔法連発されるのではやめに数を減らそう
;もしくはイシュキック撃破で戦うと防御しかしなくなるので、イシュキック速攻撃破で無力化を狙おう
;明らかにボスのイシュより、取り巻きのカメーンのほうがつよいともいう
@SET_ACTION_牡牛カメーン,ARG
CALL SET_ACTION_KAMEN,ARG
IF RESULT == 1
	CALL CHECK_ACTIONABLE,ARG,1502
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1502
	CALL RANDOM_TARGET,ARG,1502
ELSEIF RESULT == 2
	CALL CHECK_ACTIONABLE,ARG,1505
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1505
	CALL RANDOM_TARGET,ARG,1505
ELSEIF RESULT == 3
	RETURN 0
ELSE
	$DECIDE_ACTION
	IF  CFLAG:ARG:ポジション >= 12
		CFLAG:ARG:入力行動 = -1
		CFLAG:ARG:防御フラグ = 1
	ELSE
		IF RAND:70 < 25
			CALL CHECK_ACTIONABLE,ARG,1
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:入力行動 = 1
			CALL RANDOM_TARGET,ARG,1
		ELSE
			CFLAG:ARG:入力行動 = 0
			CALL RANDOM_TARGET,ARG,0
		ENDIF
	ENDIF
ENDIF
	
@SET_ACTION_獅子カメーン,ARG
CALL SET_ACTION_KAMEN,ARG
IF RESULT == 1
	CALL CHECK_ACTIONABLE,ARG,702
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 702
	CALL RANDOM_TARGET,ARG,702
ELSEIF RESULT == 2

	CALL CHECK_ACTIONABLE,ARG,705
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 705
	CALL RANDOM_TARGET,ARG,705
ELSEIF RESULT == 3
	RETURN 0
ELSE
	$DECIDE_ACTION
	IF CFLAG:ARG:ポジション >= 12
		CFLAG:ARG:入力行動 = -1
		CFLAG:ARG:防御フラグ = 1
	ELSE
		IF RAND:70 < 25
			CALL CHECK_ACTIONABLE,ARG,3
			SIF RESULT == 0
				GOTO DECIDE_ACTION
			CFLAG:ARG:入力行動 = 3
			CALL RANDOM_TARGET,ARG,3
		ELSE
			CFLAG:ARG:入力行動 = 0
			CALL RANDOM_TARGET,ARG,0
		ENDIF
	ENDIF
ENDIF
@SET_ACTION_蠍カメーン,ARG
CALL SET_ACTION_KAMEN,ARG
IF RESULT == 1
	CALL CHECK_ACTIONABLE,ARG,1602
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1602
	CALL RANDOM_TARGET,ARG,1602
ELSEIF RESULT == 2
	CALL CHECK_ACTIONABLE,ARG,1605
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1605
	CALL RANDOM_TARGET,ARG,1605
ELSEIF RESULT == 3
	RETURN 0
ELSE
	CALL CHECK_ACTIONABLE,ARG,405
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 405
	CALL RANDOM_TARGET,ARG,405
	RETURN 0
	$DECIDE_ACTION
	IF  CFLAG:ARG:ポジション >= 12
		CFLAG:ARG:入力行動 = -1
		CFLAG:ARG:防御フラグ = 1
	ELSE
		CFLAG:ARG:入力行動 = 0
		CALL RANDOM_TARGET,ARG,0
	ENDIF
ENDIF
@SET_ACTION_水瓶カメーン,ARG
CALL SET_ACTION_KAMEN,ARG
IF RESULT == 1
	CALL CHECK_ACTIONABLE,ARG,1702
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1702
	CALL RANDOM_TARGET,ARG,1702
ELSEIF RESULT == 2
	CALL CHECK_ACTIONABLE,ARG,1705
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1705
	CALL RANDOM_TARGET,ARG,1705
ELSEIF RESULT == 3
	RETURN 0
ELSE
	IF RAND:2
		CALL CHECK_ACTIONABLE,ARG,1111
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 1111
		CALL RANDOM_TARGET,ARG,1111
	ELSE
		CALL CHECK_ACTIONABLE,ARG,1101
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 1101
		CALL RANDOM_TARGET,ARG,1101
	ENDIF
	RETURN 0
	$DECIDE_ACTION
	IF  CFLAG:ARG:ポジション >= 12
		CFLAG:ARG:入力行動 = -1
		CFLAG:ARG:防御フラグ = 1
	ELSE
		CFLAG:ARG:入力行動 = 0
		CALL RANDOM_TARGET,ARG,0
	ENDIF
ENDIF
;仮面党員共通思考パターン
@SET_ACTION_KAMEN(ARG)
;イシュキック不在
IF POS(9) == -1
	IF  CFLAG:ARG:ポジション >= 12
		CFLAG:ARG:入力行動 = -1
		CFLAG:ARG:防御フラグ = 1
	ELSE
		CFLAG:ARG:入力行動 = 0
		CALL RANDOM_TARGET,ARG,0
	ENDIF
	RETURN 3
ENDIF

;ターンに1回のみ
IF LOCAL:99 == FLAG:経過ターン数
	LOCAL:99 += 1
	;健在な仮面党員をチェックしていく
	LOCAL = 0
	FOR COUNT,77,87
		;イシュキックは除外
		SIF COUNT == 79
			CONTINUE
		SIF FLAG:COUNT == -1
			CONTINUE
		LOCAL += 1
		LOCAL:(LOCAL) = FLAG:COUNT
	NEXT
	;健在な仮面党員をシャッフルしつつ、余った枠を整理
	FOR COUNT,1,10
		IF COUNT <= LOCAL
			SWAP LOCAL:COUNT,LOCAL:(RAND:LOCAL+1)
		ELSE
			LOCAL:COUNT = 0
		ENDIF
	NEXT
ENDIF
;経過ターン/2＋2人が魔法攻撃をしてくる
LOCAL:20 = (FLAG:経過ターン数)/2 + 2
FOR COUNT,1,LOCAL
	IF LOCAL:COUNT == ARG
		IF COUNT <= LOCAL:20
			;単体魔法
			IF COUNT % 2
				RETURN 1
			;範囲魔法
			ELSE
				RETURN 2
			ENDIF
		ELSE
			BREAK
		ENDIF
	ENDIF
NEXT
RETURN 0

;レイディの思考パターン
;アクエスを撃った場合、対象の水撃の相性を学習する。
;学習後は相性悪い相手にアクエスをうたなくなり、ターゲットを振り変える
;相性がいい相手が発覚してる場合に、アクエスが選択された場合はその相手にタゲ確定
;弱点もちは出してるとあっさり死亡まで持って行かれる…のはまぁそんなやらないとしても
;耐性持ちを増やすとその分、耐性持ってないキャラにアクエスが多くいくので注意か
;とくにHP600未満からは、マハアクエスはあんまり連打しないように調整されてるマハアクエスが行動回数に関わらず出てくるので注意
;
;デバッグの感触だと、火弱点は割と致命的というか、COOP連打しやすい部類にはいるんで、脆さが目立つ
;Lv20強から、エルフ・ユキジョロウ・ウンディーネが作れるんでそこまでいけば割と楽勝感
@SET_ACTION_レイディ, ARG
#LOCALSIZE 300
;フラグ初期化
IF LOCAL:9 == 0
	VARSET LOCAL,-1
	LOCAL:1 = 0
	LOCAL:2 = 0
ENDIF
LOCAL = RAND:100
;ターン限定攻撃、というか、マハアクエスとディアラマの回数制御
IF CFLAG:ARG:行動した回数 == 0
	IF BASE:ARG:ＨＰ <= 1500 && LOCAL < 50
		;ＨＰ1500を割ってからは初回行動でディアラマを使い出す
		CALL CHECK_ACTIONABLE,ARG,402
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 402
		CALL RANDOM_TARGET,ARG,402
		RETURN 0
	ENDIF
ELSEIF CFLAG:ARG:行動した回数 == 2
	;20%でマハアクエス,ＨＰ1200を割った段階で30％に
	IF LOCAL < 20 || (BASE:ARG:ＨＰ <= 1200 && LOCAL < 30)
		CALL CHECK_ACTIONABLE,ARG,1605
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 1605
		CALL RANDOM_TARGET,ARG,1605
		RETURN 0
	;10%で暗黒マハアクエス、HP1200を割った段階で15％
	ELSEIF LOCAL < 30 || (BASE:ARG:ＨＰ <= 1200 && LOCAL < 50)
		CALL CHECK_ACTIONABLE, ARG,
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = [[スキル:暗黒マハ・アクエス]]
		CALL RANDOM_TARGET,ARG, [[スキル:暗黒マハ・アクエス]]
		RETURN 0
	ENDIF
ENDIF
;アクエスのチェンジ回数を初期化
LOCAL:6 = -1
;以下単体攻撃
$DECIDE_ACTION
LOCAL = RAND:100
;攻撃対象のNOを先にとっておく
;1/4の確率で最もHP低い敵を狙いに行く
IF RAND:4 == 0
	LOCAL:7 = -1
	LOCAL:4 = 1
	FOR COUNT,1,7
		IF POS(COUNT) >= 0 && GET_STATE(CFLAG:(POS(COUNT)):ステート) != "DYING"
			IF LOCAL:7 == -1
				LOCAL:7 = BASE:(POS(COUNT)):ＨＰ
				LOCAL:4 = COUNT
			ENDIF
			IF BASE:(POS(COUNT)):ＨＰ <= LOCAL:7
				 LOCAL:7 = BASE:(POS(COUNT)):ＨＰ
				 LOCAL:4 = COUNT
			ENDIF
		ENDIF
	NEXT
	CFLAG:ARG:ターゲット = LOCAL:4
ELSE
	CALL RANDOM_TARGET,ARG
ENDIF
;対象のキャラ番号をLOCAL:5にいれておく
LOCAL:5 = FLAG:(CFLAG:ARG:ターゲット+70)
;20％の確率でマリンカリン
IF LOCAL < 20
	;対象が魅了状態の場合、マリンカリンをやめる
	SIF GET_STATE(CFLAG:(LOCAL:5):ステート) == "CHARM"	
		GOTO DECIDE_ACTION
	CALL CHECK_ACTIONABLE,ARG,1211
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1211
;40％の確率でアクエス
ELSEIF ((LOCAL < 60 && BASE:ARG:ＨＰ >= 600) || (LOCAL < 50 && BASE:ARG:ＨＰ <= 600)) && LOCAL:6 < 10
	;水撃弱点の相手を覚えている場合、アクエスはその相手を狙う。
	LOCAL:8 = 0
	FOR COUNT,0,LOCAL:2
		IF CFLAG:(LOCAL:(COUNT+30)):ポジション && GET_STATE(CFLAG:(LOCAL:(COUNT+30)):ステート) != "DYING"
			CFLAG:ARG:ターゲット = CFLAG:(LOCAL:(COUNT+30)):ポジション
			LOCAL:5 = FLAG:(CFLAG:ARG:ターゲット+70)
			LOCAL:8 = 1
		ENDIF
	NEXT
	$CHANGE_TARGET
	;チェンジ回数記録
	LOCAL:6 += 1
	IF LOCAL:6 > 0
		;味方6人のキャラナンバーを取得する
		FOR COUNT,71,77
			LOCAL:COUNT = FLAG:COUNT
		NEXT
		;登録した配列をシャッフルする（こうすることで次の探査で最初にひっかかるキャラがランダムになる）
		FOR COUNT,71,77
			SWAP LOCAL:COUNT,LOCAL:(RAND:6 + 71)
		NEXT
		FOR COUNT,71,77
			;生きてて、水撃が効く相手をさがす
			IF LOCAL:COUNT >= 0 && MAXBASE:(LOCAL:COUNT):水撃 >= 100 && MAXBASE:(LOCAL:COUNT):水撃 < 999 && GET_STATE(CFLAG:(LOCAL:COUNT):ステート) != "DYING"
				CFLAG:ARG:ターゲット = CFLAG:(LOCAL:COUNT):ポジション
				LOCAL:5 = FLAG:(CFLAG:ARG:ターゲット+70)
				BREAK
			ENDIF
		NEXT
	ENDIF
	;対象が魅了状態の場合、2/3の確率で行動を選定しなおす
	IF GET_STATE(CFLAG:(LOCAL:5):ステート) == "CHARM" && RAND:3 >= 1	
		GOTO DECIDE_ACTION
	ENDIF
	CALL CHECK_ACTIONABLE,ARG,1602
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1602
	;水撃が効かない相手をアクエスを1回打ち込んだら覚えて、次からは撃たなくなる
	IF MAXBASE:(LOCAL:5):水撃 <= 75 || MAXBASE:(LOCAL:5):水撃 == 999
		FOR COUNT,0,LOCAL:1
			IF LOCAL:5 == LOCAL:(10+COUNT)
				;10回チェンジしたらアクエスを諦める
				IF LOCAL:6 < 10
					GOTO CHANGE_TARGET
				ELSE
					GOTO DECIDE_ACTION
				ENDIF
			ENDIF
		NEXT
		IF LOCAL:1 < 20
			LOCAL:(10 + LOCAL:1) = LOCAL:5
			LOCAL:1 += 1
		ENDIF
	;水撃弱点の相手を覚える
	ELSEIF MAXBASE:(LOCAL:5):水撃 > 100 && LOCAL:8 == 0
		LOCAL:(30+LOCAL:2) = LOCAL:5
		LOCAL:2 += 1
	ENDIF
;HP600未満からは20％の確率でマハアクエス
ELSEIF LOCAL < 70 && BASE:ARG:ＨＰ <= 600
	CALL CHECK_ACTIONABLE,ARG,1605
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 1605
	CALL RANDOM_TARGET,ARG,1605
;40％の確率で打撃
ELSE
	;対象が魅了状態の場合、2/3の確率で行動を選定しなおす
	SIF GET_STATE(CFLAG:(LOCAL:5):ステート) == "CHARM" && RAND:3 >= 1	
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 0
ENDIF
LOCAL:6 = -1


;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_51
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:3
	CASE 0
		RETURN [[キャラ:ガギソン]] , 3 + RAND:10 , CSVBASE([[キャラ:ガギソン]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:ノズチ]] , 3 + RAND:10 , CSVBASE([[キャラ:ノズチ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:コッパテング]] , 3 + RAND:10 , CSVBASE([[キャラ:コッパテング]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT
