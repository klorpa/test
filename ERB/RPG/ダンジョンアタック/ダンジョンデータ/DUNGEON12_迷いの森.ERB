;=======================================================================
;迷いの森
;ダンジョンナンバー12
;=======================================================================
;
;フラグ
;ダンジョンフラグ:12:0 = まいの話フラグ(1.話聞いてマキを救うことにした 10.話聞いてこの件は切り上げた)
;ダンジョンフラグ:12:1 = マキイベントフラグ(1.発生済)
;ダンジョンフラグ:12:2 = エリーイベントフラグ(1.発生済)
;ダンジョンフラグ:12:3 = アヤセイベントフラグ(1.発生済)
;ダンジョンフラグ:12:4 = マキ一時離脱フラグ(1.離脱しなかった 2.離脱した)
;ダンジョンフラグ:12:5 = まい連れ出しフラグ(1.連れ出した)
;
;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_12
;Deva Yugaで少女を救う方法を探すを選んでいない
SIF ダンジョンフラグ:11:7 != 2
	RETURN 0
;クリア済み
SIF ダンジョンフラグ:12:0 == 10
	RETURN 0
;麻希がいない
SIF GETCHARA([[キャラ:麻希]], 0) == -1
	RETURN 0
RETURN 1
;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_12
RESULTS = Lost Forest
;迷いの森


;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_12
PRINT Lost Forest
;迷いの森


;=======================================================================
;特殊タイル表示
;=======================================================================
@TILE_MARK_12_0_18_4
RESULTS = →
@TILE_MARK_12_0_18_17
RESULTS = →
@TILE_MARK_12_0_3_18
RESULTS = ↓

@TILE_MARK_12_1_0_5
RESULTS = ←
@TILE_MARK_12_1_0_17
RESULTS = ←

@TILE_MARK_12_2_3_0
RESULTS = ↑
@TILE_MARK_12_2_4_0
RESULTS = ↑
@TILE_MARK_12_2_8_0
RESULTS = ↑


;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_12,ARG
FLAG:現M = 0
FLAG:現X = 15
FLAG:現Y = 0
;CALL AUTOMAP

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_12
#LOCALSSIZE 1
IF ダンジョンフラグ:(FLAG:現ダンジョン):1 == 0
	CALL SHOW_PICTURE, "NONFLOORD", @"%CALLNAME:MASTER%'s hideout"
	CALL MESSAGE_WINDOW, "", @"＞After defeating Kandori, %CALLNAME:MASTER% diverted the Phantom Society's attention/＞and continued to investigate the incident he caused./＞Eventually, some facts where found. There are also some guesses.", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
	;神取を倒した後、%CALLNAME:MASTER%はファントム・ソサエティの目を盗んで、/＞彼の起こした事件についての調査を続けていた/＞その結果、不確かな推測も含むがいくつかの事実がわかった
	CALL MESSAGE_WINDOW, "", "＞The DEVA system is a machine that connects a humans mental world and the physical world./＞And in this process, the individuals mental world/＞has the potential of reshaping parts of the real world./＞And that seems to be exactly what happened at Deva Yuga.", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
	;まずDEVAシステムとは、人間の心象世界と現実世界を繋ぐ機械である/＞そしてこれにより、個人の心象世界における影響が、/＞現実世界を浸食する危険がある/＞これについては、デヴァ・ユガの顕現がまさに当てはまるのだろう
	CALL MESSAGE_WINDOW, "", "＞In order to disconnect the girl 'Maki Sonomura' from the system,/＞it's necessary to remove the interface of the system with her mental world.", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
	;＞システムに接続された少女「園村 麻希」を切り離すには/＞彼女の心象世界に置かれたシステムとの接点を除去する必要がある
	CALL MESSAGE_WINDOW, "", @"＞With the help of psycho dive, penetration of her mental world is possible./＞But before that, the Maki Sonomura working with %CALLNAME:MASTER%.../＞You decided to explain the situation to her./＞...specifically that she is as virtual existance created by the system...", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
	;心象世界への侵入はサイコダイバ―の手を借りれば可能だ/＞だが、その前に%CALLNAME:MASTER%と行動を共にしている園村 麻希に/＞現状を説明することにした/＞……彼女は、システムによって構築された仮想の存在であると言う事実を……
	CALL MESSAGE_WINDOW, "", ".........../......./....", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
	LOCAL = GETCHARA([[キャラ:麻希]], 0)
	IF 陥落(LOCAL) >= 1
		CALL MESSAGE_WINDOW, "", @"＞...when %CALLNAME:MASTER% told Maki about this, her face turned pale./＞However, she calmed a bit and looked firmly into %CALLNAME:MASTER%'s eyes.", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
		;＞……%CALLNAME:MASTER%の話を聞かされた麻希は顔を青ざめた/＞しかし、気を取り直すとしっかりと%CALLNAME:MASTER%の目を見据えた
		CALL MESSAGE_WINDOW, "Maki", "I have no choice but to do....something about this./I understand that much./...please lend me your aid for this.", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
		;麻希", "私が何とかするしかない……んだよね/それだけは、分かる/……力を貸して下さい
		ダンジョンフラグ:(FLAG:現ダンジョン):4 = 1
	ELSE
		CALL MESSAGE_WINDOW, "", @"＞...when %CALLNAME:MASTER% told Maki about this, her face turned pale and she started to tremble.", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
		;……%CALLNAME:MASTER%に話を聞かされた麻希は、顔を真っ青にして震えだした
		CALL MESSAGE_WINDOW, "Maki", "...in other words, all this....is my fault./All.../...don't look....I...I...", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
		;……つまり、私のせい……なんだ/全部……/……見ないで……私、私なんて……
		CALL MESSAGE_WINDOW, "", "＞As Maki bursts into tears,/＞her appearance begins to fade, and soon she is gone./＞It seems she escaped back into her mental world.", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
		;目に大きな涙を溜めた麻希が一声叫ぶのと同時に、/＞麻希の姿はゆっくりと掻き消えていく/＞精神世界に逃避してしまったようだ……
		;麻希一時離脱
		CFLAG:LOCAL:この場に居ないフラグ = 1
		SIF CPOS(LOCAL) > 0
			CALL REMOVE_POSITION, CPOS(LOCAL)
		ダンジョンフラグ:(FLAG:現ダンジョン):4 = 2
	ENDIF
	CALL MESSAGE_WINDOW, "", @"＞%CALLNAME:MASTER% prepared for the deployment of the psycho dive right away....", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
	;＞%CALLNAME:MASTER%は早速、サイコダイブの手筈を整えた……
	CALL MESSAGE_WINDOW, "", ".........../......./....", , "ログを残さない/ボタンを利用する/再利用しない", "CENTER", 72, 4
	CALL SHOW_PICTURE, "EMPTY"
	CALL SHOW_PICTURE, "D"
	CALL MESSAGE_WINDOW_D, "", "＞Soon, you are immersed in Maki Sonomura's mental world./＞As if to express her feelings, it's appearance is that of a dark forest.",
	;没入した、園村 麻希の心象世界/＞そこは彼女の迷いを表すかのような深く、暗い森だった
ENDIF

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_12,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT
;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_12
SELECTCASE FLAG:現M
	CASE 0
	;北東
		
		FLAG:最大X = 19
		FLAG:最大Y = 19
		CALL MAKE_FLOOR_LINE, 0,"0000000000000008000"
		CALL MAKE_FLOOR_LINE, 1,"0000000011110111110"
		CALL MAKE_FLOOR_LINE, 2,"0011111010010111110"
		CALL MAKE_FLOOR_LINE, 3,"0010001010010002000"
		CALL MAKE_FLOOR_LINE, 4,"0011111110011111119"
		CALL MAKE_FLOOR_LINE, 5,"0000100000000001000"
		CALL MAKE_FLOOR_LINE, 6,"0011111110000001000"
		CALL MAKE_FLOOR_LINE, 7,"0010100010000001000"
		CALL MAKE_FLOOR_LINE, 8,"0010111110000001000"
		CALL MAKE_FLOOR_LINE, 9,"0000002000000001000"
		CALL MAKE_FLOOR_LINE,10,"0000001000000001000"
		CALL MAKE_FLOOR_LINE,11,"0000000000000001000"
		CALL MAKE_FLOOR_LINE,12,"0111111111141111000"
		CALL MAKE_FLOOR_LINE,13,"0100000001000000000"
		CALL MAKE_FLOOR_LINE,14,"0111110001000000000"
		CALL MAKE_FLOOR_LINE,15,"0101010001411110000"
		CALL MAKE_FLOOR_LINE,16,"0111110001000011100"
		CALL MAKE_FLOOR_LINE,17,"0001000001111410119"
		CALL MAKE_FLOOR_LINE,18,"0009000000000000000"

		;イベントの設置
;		CALL SET_TILE,12,7,ダンジョンフラグ:(FLAG:現ダンジョン):1,0,4
	CASE 1
		;北西
		FLAG:最大X = 19
		FLAG:最大Y = 19
		CALL MAKE_FLOOR_LINE, 0,"0000000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0111111111100000000"
		CALL MAKE_FLOOR_LINE, 2,"0000400010100000000"
		CALL MAKE_FLOOR_LINE, 3,"0000101110100000000"
		CALL MAKE_FLOOR_LINE, 4,"9111101000100000000"
		CALL MAKE_FLOOR_LINE, 5,"0104001000100000000"
		CALL MAKE_FLOOR_LINE, 6,"0101001000100000000"
		CALL MAKE_FLOOR_LINE, 7,"0101001000100000000"
		CALL MAKE_FLOOR_LINE, 8,"0101111011100000000"
		CALL MAKE_FLOOR_LINE, 9,"0100100010100000000"
		CALL MAKE_FLOOR_LINE,10,"0111101111100000000"
		CALL MAKE_FLOOR_LINE,11,"0000000010000000000"
		CALL MAKE_FLOOR_LINE,12,"0001141110000000000"
		CALL MAKE_FLOOR_LINE,13,"0121001000000000000"
		CALL MAKE_FLOOR_LINE,14,"0001141000000000000"
		CALL MAKE_FLOOR_LINE,15,"0000001111111111110"
		CALL MAKE_FLOOR_LINE,16,"0000000000000000010"
		CALL MAKE_FLOOR_LINE,17,"9111111111111111110"
		CALL MAKE_FLOOR_LINE,18,"0000000000000000000"
	
	CASE 2
		;南西
		FLAG:最大X = 19
		FLAG:最大Y = 19
		CALL MAKE_FLOOR_LINE, 0,"0009000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0014101211101111110"
		CALL MAKE_FLOOR_LINE, 2,"0011100000100000010"
		CALL MAKE_FLOOR_LINE, 3,"0002001110100111110"
		CALL MAKE_FLOOR_LINE, 4,"0011101110100111110"
		CALL MAKE_FLOOR_LINE, 5,"0010000200100111110"
		CALL MAKE_FLOOR_LINE, 6,"0011411111100111110"
		CALL MAKE_FLOOR_LINE, 7,"0001000000111111110"
		CALL MAKE_FLOOR_LINE, 8,"0111110000000000010"
		CALL MAKE_FLOOR_LINE, 9,"0101010001000001110"
		CALL MAKE_FLOOR_LINE,10,"0101110001000004010"
		CALL MAKE_FLOOR_LINE,11,"0100001111000001010"
		CALL MAKE_FLOOR_LINE,12,"0111001010011111140"
		CALL MAKE_FLOOR_LINE,13,"0101001010040001000"
		CALL MAKE_FLOOR_LINE,14,"0111111010010004000"
		CALL MAKE_FLOOR_LINE,15,"0100000011411411110"
		CALL MAKE_FLOOR_LINE,16,"0111100001040000010"
		CALL MAKE_FLOOR_LINE,17,"0011100011111111110"
		CALL MAKE_FLOOR_LINE,18,"0000000000000000000"
	
		;イベントの設置
		;CALL SET_TILE,3,1,ダンジョンフラグ:(FLAG:現ダンジョン):0,0,4
		CALL SET_TILE,7,4,ダンジョンフラグ:(FLAG:現ダンジョン):1,0,4
		CALL SET_TILE,12,1,ダンジョンフラグ:(FLAG:現ダンジョン):2,0,4
		CALL SET_TILE,6,1,ダンジョンフラグ:(FLAG:現ダンジョン):3,0,4

	CASEELSE





ENDSELECT



;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_12
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;イベントの処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 4
	SELECTCASE FLAG:現M
		CASE 2
			IF FLAG:現X == 3 && FLAG:現Y == 1
			;マイの部屋
				CALL MESSAGE_WINDOW_D, "", "＞There is a house made of sweets..."
				;お菓子の家がある……
			ELSEIF FLAG:現X == 7 && FLAG:現Y == 4
			;麻希の引きこもってる部屋
			ENDIF
	ENDSELECT
	RETURN 1
ELSE
	CALL MESSAGE_WINDOW_D, "", "＞Nothing seems to be here."
	RETURN 1
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_12,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;イベント、ワープの処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 4
	SELECTCASE FLAG:現M
		CASE 0
			IF FLAG:現X == 11 && FLAG:現Y == 12
				FLAG:現X = 15
				FLAG:現Y = 5
			ELSEIF FLAG:現X == 10 && FLAG:現Y == 15
				FLAG:現X = 11
			ELSEIF FLAG:現X == 13 && FLAG:現Y == 17
				FLAG:現X = 12
			ENDIF
		CASE 1
			IF FLAG:現X == 4 && FLAG:現Y == 2
				FLAG:現Y = 3
			ELSEIF FLAG:現X == 3 && FLAG:現Y == 5
				FLAG:現Y = 6
			ELSEIF FLAG:現X == 5 && FLAG:現Y == 12
				FLAG:現X = 4
			ELSEIF FLAG:現X == 5 && FLAG:現Y == 14
				FLAG:現X = 6
			ENDIF
		CASE 2
			IF FLAG:現X == 4 && FLAG:現Y == 6
				FLAG:現X = 3
			ELSEIF FLAG:現X == 15 && FLAG:現Y == 10
				FLAG:現Y = 11
			ELSEIF FLAG:現X == 17 && FLAG:現Y == 12
				FLAG:現Y = 11
			ELSEIF FLAG:現X == 11 && FLAG:現Y == 13
				FLAG:現Y = 12
			ELSEIF FLAG:現X == 15 && FLAG:現Y == 14
				FLAG:現Y = 15
			ELSEIF FLAG:現X == 13 && FLAG:現Y == 15
				FLAG:現X = 14
			ELSEIF FLAG:現X == 10 && FLAG:現Y == 15
				FLAG:現X = 9
			ELSEIF FLAG:現X == 11 && FLAG:現Y == 16
				FLAG:現Y = 15
			ELSEIF FLAG:現X == 3 && FLAG:現Y == 1
				;マイの部屋
				IF ダンジョンフラグ:(FLAG:現ダンジョン):0 == 0
					CALL MESSAGE_WINDOW_D, "", "＞How far into the forest have you walked?/＞Just as you wondered about that, the trees parted and a small clearing came into view./＞There was a house made of sweets there, reminding you of fairy-tales./＞And before you, there was a little girl staring at you with frightened eyes..."
					;＞暗い森を掻き分けるようにして、どの位進んだだろう？/＞急に視界が開けたと思うと小さな広場に出た/＞そしてそこには童話に出てくるようなお菓子の家があり、/＞その前には怯えたように身を竦めながらこちらを睨む小さな女の子がいた……
					IF ダンジョンフラグ:(FLAG:現ダンジョン):4 == 1
						CALL MESSAGE_WINDOW_D, "Girl", "Oh, you are with 'Maki'..."
						;女の子", "あ、あなた、『麻希』の……
						CALL MESSAGE_WINDOW_D, "", @"＞The girl looked at %CALLNAME:MASTER% while slightly blushing."
						;女の子は%CALLNAME:MASTER%を見ると、ちょっと顔を赤らめた
					ELSE
						CALL MESSAGE_WINDOW_D, "Girl", "Oh, you hurt 'Maki'..."
						;あ、あなた、『麻希』を傷つけた……
					ENDIF
					CALL MESSAGE_WINDOW_D, "Girl", "I am Mai./...I'm the one who created this world./I have something to tell you,/please listen."
					;わたし、マイっていいます/……この世界をつくったのはわたしです/話したいことがあります/聞いてください
					CALL MESSAGE_WINDOW_D, "", "........../....../...."
					CALL MESSAGE_WINDOW_D, "", "＞It seems she is the person who manages Maki's mental world./＞Currently, the DEVA interface is protected by the most brutal part of Maki's personality./＞And it seems that personality-part aims for the destruction of this world."
					;園村 麻希の精神世界を管理する人格である、まいによると/＞現在DEVAシステムとの接点は麻希の人格中最も破壊的な人格が確保しており、/＞その人格によってこの世界は破壊されようとしているということだ
					CALL MESSAGE_WINDOW_D, "", "＞And since this destruction would be reflected by the DEVA system,/＞the real world is also threatened by this."
					;そしてこの破壊はDEVAシステムを介して現実へと反映され、/＞麻希の精神世界のみのならず現実世界を破壊する恐れがあるという
					CALL MESSAGE_WINDOW_D, "Mai", "Please. Help 'Maki'..."
					;マイ", "お願い。『麻希』を助けて…
					CALL MESSAGE_WINDOW_D, "", "＞The DEVA system reflects the events in this world into reality./＞So the destruction of this mental world could destroy the real world./＞However, by physically seperating Maki from the DEVA system/＞any damage to the real world could be prevented."
					;＞DEVAシステムはこの世界での出来事を現実へと反映する/＞よって麻希の精神世界の破壊は現実世界の破壊を引き起こすことになるだろう/＞しかし、DEVAシステムと麻希を物理的に切り離すことで/＞現実世界の破壊を防ぐことはできるだろう
					CALL INPUT_YN_D("Save Maki", "Abbandon Maki and disable the DEVA system before it can cause harm")
					;麻希を救う", "麻希を見捨てて、危険を未然に防ぐ
					IF RESULT == 0
						CALL MESSAGE_WINDOW_D, "", @"＞When %CALLNAME:MASTER% confirmed that %HIS_HER(MASTER)% intention was still to save Maki,/＞Mai told you that Maki Sonomure(or actually one aspect of her personality)/is in the back of this forest."
						;＞%CALLNAME:MASTER%の意思が変わらないのを確認すると、/＞マイはこの森の奥に園村 麻希が(正確にはその人格の一つが)居る事を教えてくれた
						CALL MESSAGE_WINDOW_D, "", "＞While Mai was using the DEVA system to shape this world,/＞it seems she has detached from Maki's personality./＞However, to reach the personality-aspect guarding the DEVA interface, her cooperation will be necessary."
						;彼女はDEVAシステムを介して己がしてしまった事で、/＞自分を責めて引きこもってしまっているらしい/＞だが、DEVAシステムを抱える破壊的な人格の下に到達するには、彼女の協力が必要らしい
						CALL INCREASE_LD,5
						ダンジョンフラグ:(FLAG:現ダンジョン):0 = 1
					ELSE
						CALL MESSAGE_WINDOW_D, "", @"＞Escaping from the mental world and reporting to the Phantom Society,/＞%CALLNAME:MASTER% decide to end this matter."
						;＞%CALLNAME:MASTER%は精神世界から脱出した上でファントムに報告を行い、/＞この件を終わりにすることにした
						CALL MESSAGE_WINDOW_D, "", "＞暫くすればこの大惨事も、/＞ごくありふれた事故、ありふれた悲劇として/＞風化していくのだろう…"
						CALL MESSAGE_WINDOW_D, "", "......./....../..."
						CALL SHOW_PICTURE, "EMPTY"
						CALL SHOW_PICTURE, "NONFLOORD", @"%CALLNAME:MASTER%'s hidout"
						;の私室
						CALL MESSAGE_WINDOW_D, "", "/＞…...soon after, the Phantom Society sent a report on the disambly of the DEVA system./＞You also received a documentation of the aquired technology./＞owever, there was no mention of the girl..."
						;＞……ほどなくしてファントムからDEVAシステムの解体の報告と/＞その調査によって得られた技術についての資料が届いた/＞そこには少女についての記述は何もない……
						CALL MESSAGE_WINDOW_D, "", "As compensation for your help in resolving the incident, ￥100000 where paid."
						;この件の報酬として、￥100000が支払われた
						MONEY += 100000
						ダンジョンフラグ:(FLAG:現ダンジョン):0 = 10
						ダンジョンフラグ:11:7 = 1
						CALL INCREASE_LC,5
						CALL INCREASE_LD,-5
						FLAG:脱出 = 1
					ENDIF
				ELSEIF ダンジョンフラグ:(FLAG:現ダンジョン):1 == 1 && ダンジョンフラグ:(FLAG:現ダンジョン):5 == 0
					;麻希イベントクリア後
					CALL MESSAGE_WINDOW_D, "Mai", "...thanks..."
					;……ありがと……
					CALL MESSAGE_WINDOW_D, "", @"＞When you told her that Maki's condition had improved,/＞Mai thanked %CALLNAME:MASTER% with a loud voice."
					;麻希の状態が良くなったのが分かるのだろう/＞マイはか細い声で%CALLNAME:MASTER%に礼を言った
					CALL MESSAGE_WINDOW_D, "Mai", "...I think things will be alright here for now.../＞...now, I'm the worst..."
					;……これでしばらくはだいじょうぶだとおもう……/＞……その間に、いちばん悪いわたしを……
					CALL MESSAGE_WINDOW_D, "", "＞....now, will you leave her to be here alone forever?"
					;……それまでの間、マイはずっと一人で此処に残るのだろうか？
					CALL INPUT_YN_D("Invite her out of the forest", "Leave her")
					;森の外へ誘う", "放っておく
					IF RESULT == 0
						CALL MESSAGE_WINDOW_D, "Mai", "...is that okay..?...I..."
						;……いいの…？…わたし……
						CALL MESSAGE_WINDOW_D, "", "＞You lightly grabbed the hands of the hestiant girl./＞I seems Mai didn't expect you to take her with you."
						;躊躇う少女の手を軽く掴む/＞マイは振りほどこうとはしなかった
						CALL ADD_NEW_COMPANION,4592,0
						CALL MESSAGE_WINDOW_D, "", "＞Mai has joined you. She is a non-combatant, but you could train her to change that."
						ダンジョンフラグ:(FLAG:現ダンジョン):5 = 1
					ENDIF
				ELSE
					CALL MESSAGE_WINDOW_D, "", "＞There is a house made of sweets..."
					;＞お菓子の家がある……
				ENDIF
			ELSEIF FLAG:現X == 7 && FLAG:現Y == 4
				;マキの引きこもってる部屋
				CALL MESSAGE_WINDOW_D, "", @"＞In this part of the forest, there is another clearing./＞To your surprise, there was bed like thoose in a hospital here./＞On the bed, \@ダンジョンフラグ:(FLAG:現ダンジョン):4 == 1? lied a girl, covered in sheets that seemed unaturally white./＞The # lied two identical girls, covered with bright white sheets./＞One\@ girl is wearing a white mask on her face..."
				;＞森が少し開けている様な空間に、/＞病院に置かれている様な飾り気のないベッドが置かれている/＞\@ダンジョンフラグ:(FLAG:現ダンジョン):4 == 1?ベッドの上には、不自然なほど真っ白なシーツに包まれて、一人の少女が横たわっている#ベッドの上には、真っ白なシーツに包まれて、全く同じ姿の二人の少女が横たわっている\@/＞少女の顔には真っ白な仮面が付いている……
				CALL MESSAGE_WINDOW_D, "Masked girl", "...who are you?/Ah, I don't care.../Just, leave me alone..."
				;仮面の少女", "……あなた、誰？/うぅん、誰でも良い……/私の事は放っておいて……"
				CALL MESSAGE_WINDOW_D, "", "＞...it seems she is blaming herself and in despair./＞I seems she doesn't know of the effect her desperation/has on the real world..."
				;……少女はひたすらに自分を責め、絶望について語りだした/＞少女は彼女の絶望が現実世界に侵食しているのを知らないようだ…
				IF FINDCHARA_B(4519,0) == 2
					;麻希がパーティにいる
					CALL MESSAGE_WINDOW_D, "Maki", "...this is, me?/No, I'm me too..../If you are me, please listen to me!"
					;麻希", "……これが、私？/ううん、これも、私……/あなたも私なんだったら、話を聞いてよ！"
				ELSEIF FINDCHARA_B(4514,0) == 2
					;エリーがパーティにいる
					CALL MESSAGE_WINDOW_D, "Eriko", "Maki.../It's not your fault..."
					;エリー", "Maki……/あなたのせいではありませんわ……
				ELSEIF FINDCHARA_B(4515,0) == 2
					;アヤセがパーティにいる
					CALL MESSAGE_WINDOW_D, "Ayase", "……園村ぁ……"
					;アヤセ", "……園村ぁ……
				ENDIF
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%は、少女を励まし、立ち上がらせようと弁を振るい始めた……/＞%CALLNAME:MASTER%の熱弁が届いたのだろうか/＞少女は黙って、首に掛けてあったコンパクトを手渡すと/＞ゆっくりと薄くなり、大気に解けるように消えた。"
				CALL MESSAGE_WINDOW_D, "", "＞コンパクトを覗き込むと、その鏡面には神社らしき物が映っている"
				LOCAL = GETCHARA(4519, 0)
				IF ダンジョンフラグ:(FLAG:現ダンジョン):4 == 2
					CALL MESSAGE_WINDOW_D, "", "＞And the other girl who was lying on the bed.../＞Maki slowly raised her face, threw away her mask and got up..."
					;して、ベッドで寝ていたもう一人の少女……/＞麻希はゆっくりと伏せていた顔を上げ、仮面を捨てて立ち上がった……
					CALL MESSAGE_WINDOW_D, "Maki", "Yes...it's true, I should not run away.../...I'll return and think on what happened..."
					;麻希", "そう…だよね、逃げちゃダメなんだよね…/…うん、１人でゆっくり考えてみたいから先に戻るね…
					CALL MESSAGE_WINDOW_D, "Maki", "...thank you."
					;……ありがとう
					CALL MESSAGE_WINDOW_D, "", "＞Saying so, Maki returned to the real world."
					;そう言うと、麻希は現実世界に戻っていった
					CFLAG:LOCAL:この場に居ないフラグ = 0
				ENDIF
				;依存度が忠誠度基準で上昇
					CFLAG:LOCAL:依存度 = MAX(CFLAG:LOCAL:依存度, BASE:LOCAL:忠誠度 / 4)
				ダンジョンフラグ:(FLAG:現ダンジョン):1 = 1
			ELSEIF FLAG:現X == 12 && FLAG:現Y == 1
				IF ダンジョンフラグ:10:10 == 2 && FINDCHARA_B(4514,0) == 2 &&  CFLAG:(RESULT:1):201 == 0
					LOCAL = RESULT:1
					;エリー和解ルートで仲間入りさせ、かつパーティに入っていて、しかもまだ調教した事が無い
					CALL MESSAGE_WINDOW_D, "", "A girl looking exactly like Eriko was standing here..."
					CALL MESSAGE_WINDOW_D, "Eriko", "Uh...wow?/Double....is this a Doppelganger?"
					;わ……私？/double……いえドッペルゲンガーと言う事……？
					CALL MESSAGE_WINDOW_D, "", "＞Wearing the same elegant face as Eriko, the girl smiled and opened her mouth to emmit a sonng-like voice."
					;少女はエリーと同じ上品な顔に見下すような笑みを浮かべて、歌う様に口を開いた
					CALL MESSAGE_WINDOW_D, "Eriko？", "Pretty...beautiful...elegant...everyone tells me that's how I am./Of course, everone likes get praised..../Thats why I'm working hard!"
					;綺麗……美人……上品……皆、私をそう言ってくれる/勿論、褒められれば嬉しいのは誰でも同じ……/だから私はこんなにも一生懸命！
					CALL MESSAGE_WINDOW_D, "Eriko？", "...but is that my real self?/Even though I think that, I can't stop!/It's all I am!/Oh poor me!!"
					;……でも、これは本当の自分なの？/そんな事を思っても止められない!/だって、それしかないんだもの！/何て可哀そうな私！！
					CALL MESSAGE_WINDOW_D, "", "＞The appearance of the laughing girl slowly changed into a different creature...＞Karma Pride appeared!"
					;そういって嘲笑う少女の姿がゆっくりと異形のものへと変わってゆく……
					FLAG:逃走不可フラグ = 1
					FLAG:会話不能フラグ = 1
					FLAG:先制不意打ちキャンセル = 1
					CALL SET_ENEMY,9,1025,55,0,0
					CALL BATTLE_START
					CALL SHOW_PICTURE, "D"
					CALL MESSAGE_WINDOW_D, "Karma Pride", "Huhu....ha....ha..."
					;くく……うふふ……ふ……
					CALL MESSAGE_WINDOW_D, "", "＞Mocking until the end, the demon was defeated."
					;最後まで嘲笑を消さないまま、異形の魔物は力尽きた
					FLAG:逃走不可フラグ = 0
					FLAG:会話不能フラグ = 0
					FLAG:先制不意打ちキャンセル = 0
					CALL MESSAGE_WINDOW_D, "", "＞Erie murmured to herself absent-mindedly."
					;それをぼうっと見ていたエリーは、小さな声で呟いた
					CALL MESSAGE_WINDOW_D, "Eriko", "Yes, it's like that.../It's what people expect and I respond to it, it's nothing bad.../...but maybe that's just how thoose who act like that think?"
					;そう、その通りかも……/期待されてそれに応える、それはいやな事ではなかったし……/……もし、ありのままに振舞ったら、貴方はどう思うのかしら
					CALL MESSAGE_WINDOW_D, "Eriko", "(What if I saw what this person is hiding?)"
					;(そして、この人が隠している物を見たら、私は？)
					;依存度が忠誠度基準で上昇
					CFLAG:LOCAL:依存度 = MAX(CFLAG:LOCAL:依存度, BASE:LOCAL:忠誠度 / 4)
					ダンジョンフラグ:(FLAG:現ダンジョン):2 = 1
				ENDIF
			ELSEIF FLAG:現X == 6 && FLAG:現Y == 1
				IF ダンジョンフラグ:10:10 == 1 && FINDCHARA_B(4515,0) == 2 &&  CFLAG:(RESULT:1):201 == 0
					LOCAL = RESULT:1
					;アヤセ和解ルートで仲間入りさせ、かつパーティに入っていて、しかもまだ調教した事が無い
					CALL MESSAGE_WINDOW_D, "", "＞A girl looking exactly like Ayase was standing there..."
					;＞アヤセと全く同じ姿をした少女が立っている……
					CALL MESSAGE_WINDOW_D, "Ayase", "You are...me?/...no, a fake!?"
					;アレ……あたし？/……に、偽物ぉ！？
					CALL MESSAGE_WINDOW_D, "Ayase？", "...why does Ayase have to live in such a place〜/People stay away from me, they leave me alone./Nobody cares about Ayase."
					;……なんで、アヤセこんなとこに居んの〜/別に良いじゃん、ほっとけば/アヤセに関係ないよね
					CALL MESSAGE_WINDOW_D, "Ayase？", "And all that efford and exercise, I don't like to excert myself〜/Ayase is already good enough isn't she?/……そんなんじゃダメだって言われてもぉ/I don't want things to be difficult! I don't want to think!!"
					;痛いのもしんどいのも嫌いだしぃ〜/今だけ良ければ良いよね？/……そんなんじゃダメだって言われてもぉ/難しい事分からない！ 考えたくない！！
					FLAG:逃走不可フラグ = 1
					FLAG:会話不能フラグ = 1
					FLAG:先制不意打ちキャンセル = 1
					CALL MESSAGE_WINDOW_D, "", "＞The appearance of the girl slowly changed into a different creature.../＞Karma Lust appeared!"
					;そういって嘲笑う少女の姿がゆっくりと異形のものへと変わってゆく……
					CALL SET_ENEMY,9,1020,45,0,0
					CALL BATTLE_START
					CALL SHOW_PICTURE, "D"
					CALL MESSAGE_WINDOW_D, "Karma Lust", "Huhu....ha....haha..."
					CALL MESSAGE_WINDOW_D, "", "＞Mocking until the end, the demon was defeated."
					;最後まで嘲笑を消さないまま、異形の魔物は力尽きた"
					FLAG:逃走不可フラグ = 0
					FLAG:会話不能フラグ = 0
					FLAG:先制不意打ちキャンセル = 0
					CALL MESSAGE_WINDOW_D, "", "＞Ayase, who was staring at the scene, murmured loudly."
					;その様子をぼうっと見ていたアヤセは、小声で呟いた
					CALL MESSAGE_WINDOW_D, "Ayase", "...Ayase has also thought about other things./Well, I don't think this is the right place for that..."
					;……アヤセだって先の事くらい考えてるよ/そりゃ、こんな調子じゃ上手く行くかどうか分かんないけど……
					IF TALENT:MASTER:140 == 1
						CALL MESSAGE_WINDOW_D, "Ayase", "And there are no candidates at hand, right?...."
						;手近に候補が居なくもないよね……
						CALL MESSAGE_WINDOW_D, "", "＞For some reason, Ayase glanced over to you."
						;アヤセは何故かちらっとこちらを見た
						;PRINTFORMW 。
					ENDIF
					CFLAG:LOCAL:依存度 = MAX(CFLAG:LOCAL:依存度, BASE:LOCAL:忠誠度 / 4)
					ダンジョンフラグ:(FLAG:現ダンジョン):3 = 1
				ENDIF
			ENDIF
	ENDSELECT
	
	CALL MAKE_FLOOR, FLAG:現ダンジョン
	CALL AUTOMAP
	
	RETURN 1
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
	SELECTCASE FLAG:現M
		CASE 0
			IF FLAG:現X == 18
				FLAG:現M = 1
				FLAG:現X = 0
			ELSE
				FLAG:現M = 2
				FLAG:現Y = 0
			ENDIF
		CASE 1
			IF FLAG:現X == 0
				FLAG:現M = 0
				FLAG:現X = 18
			ENDIF
		CASE 2
			IF FLAG:現Y == 0
				FLAG:現M = 0
				FLAG:現Y = 18
			ELSE
				FLAG:現M = 1
				FLAG:現Y = 15
			ENDIF
		CASE 3
			IF FLAG:現X == 0
				FLAG:現M = 2
				FLAG:現X = 15
			ELSE
				FLAG:現M = 0
				FLAG:現Y = 15
			ENDIF
	ENDSELECT
		
	CALL MAKE_FLOOR, FLAG:現ダンジョン
	CALL AUTOMAP
	
	

;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
ENDIF
RETURN 0


;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_12
	FLAG:エンカウント率 = FLAG:未遭遇歩数*5

;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_12
RETURN 38, 35, 42, 45
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_12
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_12
RETURN CLASS_NUM_COMMON("上級")
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;=======================================================================
@ENEMY_NUM_12, ARG
RETURN ENEMY_NUM_COMMON("上級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_12, ARG
IF FLAG:現M == 0 || FLAG:現M == 1
	SELECTCASE RAND:100
		CASE IS < 20
			;20%でおしち
			RETURN 1008, 35
		CASE IS < 40
			;20%でオセ
			RETURN 1704, 36
		CASE IS < 60
			;20%でシュテンドウジ
			RETURN 655, 38
		CASE IS < 80
			;20%でネヴァン
			RETURN 906, 37
		CASE IS < 100
			;20%でタマモ
			RETURN 807, 38
	ENDSELECT
ELSE
	SELECTCASE RAND:100
		CASE IS < 20
			;20%でヴィヴィアン
			RETURN 509, 42
		CASE IS < 40
			;20%でスキュラ
			RETURN 808, 43
		CASE IS < 60
			;20%でクチサケ
			RETURN 1009, 42
		CASE IS < 80
			;20%でサキュバス
			RETURN 407, 44
		CASE IS < 100
			;20%でマッハ
			RETURN 907, 42
	ENDSELECT
ENDIF
;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_12
;RETURN CSV番号 , 人数 , レベル
RETURN [[キャラ:シュテンドウジ]] , 3 + RAND:10 , CSVBASE([[キャラ:シュテンドウジ]] , GETNUM(BASE , "LV") , 0) + RAND:3

