;=======================================================================
;赤坂
;=======================================================================
;変数 ダンジョンフラグ:[[ダンジョン:赤坂]]:X
;0 赤坂地下の情報（侵入条件)
;1 ヒルコ
;2 ショート・ターミナル
;他ダンジョン・依頼で関係する変数
;40- メッセンジャーのフラグ
;80- 宝箱
;=======================================================================
;真2旧ロッポンギを赤坂のさらに下層のダンジョンとして統合
;ICBM落ちてないのでガイア教が落ちぶれてないように＆長老の話を変更
;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_29
;[IF_DEBUG]
;	RETURN 1
;[ENDIF]
;条件：赤坂の情報を得ている
IF ダンジョンフラグ:[[ダンジョン:赤坂]]:0
	RETURN 1
ENDIF

RETURN 0



;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_29
RESULTS = 赤坂


;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_29
PRINT 赤坂
SELECTCASE FLAG:現M
	CASE 0
		PRINTL ・Ｂ７Ｆ
	CASE 1
		PRINTL ・Ｂ６Ｆ
	CASE 2
		PRINTL ・Ｂ５Ｆ
	CASE 3
		PRINTL ・Ｂ４Ｆ
	CASE 4
		PRINTL ・Ｂ３Ｆ
	CASE 5
		PRINTL ・Ｂ２Ｆ
	CASE 6
		PRINTL ・Ｂ１Ｆ
ENDSELECT



;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_29,ARG
#LOCALSSIZE 1
FLAG:現M = 6
FLAG:現X = 11
FLAG:現Y = 23
;CALL AUTOMAP


;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_29

;=======================================================================
;脱出時のイベント
;=======================================================================
@EVENT_DUNGEON_END_29

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_29,ARG,ARG:1
RESULT = 1
SELECTCASE DA:ARG:(ARG:1)
	CASE 12
		;上にしか進めない＝上からは無理
		;上である時＝現在のYがARG:1より小さいとき
		IF ARG:1 > FLAG:現Y
			CALL SET_FLOOR_ANALYZE(2, FLAG:現ダンジョン, FLAG:現M, ARG, ARG:1)
			RETURN 0
		ENDIF
	CASE 22
		IF ARG > FLAG:現X
			CALL SET_FLOOR_ANALYZE(2, FLAG:現ダンジョン, FLAG:現M, ARG, ARG:1)
			RETURN 0
		ENDIF
	CASE 32
		IF ARG:1 < FLAG:現Y
			CALL SET_FLOOR_ANALYZE(2, FLAG:現ダンジョン, FLAG:現M, ARG, ARG:1)
			RETURN 0
		ENDIF
	CASE 42
		IF ARG < FLAG:現X
			CALL SET_FLOOR_ANALYZE(2, FLAG:現ダンジョン, FLAG:現M, ARG, ARG:1)
			RETURN 0
		ENDIF
	CASEELSE
		CALL CAN_ENTER,ARG,ARG:1
ENDSELECT
RETURN RESULT

;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_29
SELECTCASE FLAG:現M
	;Ｂ１Ｆ
	CASE 6
		FLAG:最大X = 33
		FLAG:最大Y = 25
		;						           1         2         3  
		;						 012345678901234567890123456789012
		CALL MAKE_FLOOR_LINE, 0,"                                 "
		CALL MAKE_FLOOR_LINE, 1,"     1111111 4 11111   3         "
		CALL MAKE_FLOOR_LINE, 2,"     1 2 1   2 1   1   2         "
		CALL MAKE_FLOOR_LINE, 3,"   111 1 1 1 1 1 1 11111         "
		CALL MAKE_FLOOR_LINE, 4,"   1     1 2 1 1 2   111         "
		CALL MAKE_FLOOR_LINE, 5,"   1 111 1 11121 111 111         "
		CALL MAKE_FLOOR_LINE, 6,"     111 2 1     111 1           "
		CALL MAKE_FLOOR_LINE, 7,"   1 11111 1 1112111 1           "
		CALL MAKE_FLOOR_LINE, 8,"   1   111 2 1   111 1           "
		CALL MAKE_FLOOR_LINE, 9," 4 111 1111111 1 111 11123   12e "
		CALL MAKE_FLOOR_LINE,10," 2 111 111     1 111 111     1   "
		CALL MAKE_FLOOR_LINE,11," 1 111 111 4 111 111 111 1111111 "
		CALL MAKE_FLOOR_LINE,12," 1 111   1 2 111 111         111 "
		CALL MAKE_FLOOR_LINE,13," 1 111 121111111 111111111112111 "
		CALL MAKE_FLOOR_LINE,14," 1 111         1   2         1   "
		CALL MAKE_FLOOR_LINE,15," 12111 1 4 4 4 1 111 124 11111   "
		CALL MAKE_FLOOR_LINE,16,"   111 1 2 2 2   1   1           "
		CALL MAKE_FLOOR_LINE,17,"   111 11111111111 111           "
		CALL MAKE_FLOOR_LINE,18,"   1   1 2 1 2 111 111           "
		CALL MAKE_FLOOR_LINE,19," 121 411 4 1 1 111 11111         "
		CALL MAKE_FLOOR_LINE,20,"   1       4       1   1         "
		CALL MAKE_FLOOR_LINE,21,"   11111111111 t 1 1 321         "
		CALL MAKE_FLOOR_LINE,22,"     11111 2 1 2 2 1             "
		CALL MAKE_FLOOR_LINE,23,"     11111 8 1111111             "
		CALL MAKE_FLOOR_LINE,24,"                                 "
		;						 012345678901234567890123456789012
		;						           1         2         3  
		
	;Ｂ２Ｆ
	CASE 5
		FLAG:最大X = 9
		FLAG:最大Y = 9
;								012345678
		CALL MAKE_FLOOR_LINE,0,"         "
		CALL MAKE_FLOOR_LINE,1," 52123 7 "
		CALL MAKE_FLOOR_LINE,2,"   1   2 "
		CALL MAKE_FLOOR_LINE,3," 511 111 "
		CALL MAKE_FLOOR_LINE,4," 1   1   "
		CALL MAKE_FLOOR_LINE,5," 125 1 5 "
		CALL MAKE_FLOOR_LINE,6,"   1 1 2 "
		CALL MAKE_FLOOR_LINE,7,"   12511 "
		CALL MAKE_FLOOR_LINE,8,"         "
	;Ｂ３Ｆ
	CASE 4
		FLAG:最大X = 9
		FLAG:最大Y = 9
;								012345678
		CALL MAKE_FLOOR_LINE, 0,"         "
		CALL MAKE_FLOOR_LINE, 1," 111   4 "
		CALL MAKE_FLOOR_LINE, 2," 2 1   2 "
		CALL MAKE_FLOOR_LINE, 3," 6 11111 "
		CALL MAKE_FLOOR_LINE, 4,"   11111 "
		CALL MAKE_FLOOR_LINE, 5,"   11111 "
		CALL MAKE_FLOOR_LINE, 6,"     2 2 "
		CALL MAKE_FLOOR_LINE, 7,"     3 7 "
		CALL MAKE_FLOOR_LINE, 8,"         "
	;Ｂ４Ｆ
	CASE 3
		FLAG:最大X = 33
		FLAG:最大Y = 9
		;						            1         2         3  
		;						 012345678901234567890123456789012
		CALL MAKE_FLOOR_LINE, 0,"                                 "
		CALL MAKE_FLOOR_LINE, 1," 6 1<111<11111 1111121 1111111   "
		CALL MAKE_FLOOR_LINE, 2," 2 1   ] 1 2 1 1 ]   1 1   2     "
		CALL MAKE_FLOOR_LINE, 3," 1 111 1 1 1 1 1 111 1 12e 1 t21 "
		CALL MAKE_FLOOR_LINE, 4," 1 1   1   1   1 1   1 1       1 "
		CALL MAKE_FLOOR_LINE, 5," 1 1>11111>12111 1 111 111 4 111 "
		CALL MAKE_FLOOR_LINE, 6," 1 1   [   1   ] ] 1   111 2 1 2 "
		CALL MAKE_FLOOR_LINE, 7," 12111 1112111 111>12111111111 7 "
		CALL MAKE_FLOOR_LINE, 8,"                                 "
		;						 012345678901234567890123456789012
		;						            1         2         3  

	;Ｂ５Ｆ
	CASE 2
		FLAG:最大X = 17
		FLAG:最大Y = 33
		;						           1     
		;						 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"                 "
		CALL MAKE_FLOOR_LINE, 1," 4 111 11111 1 1 "
		CALL MAKE_FLOOR_LINE, 2," 2 111   2 1 2 1 "
		CALL MAKE_FLOOR_LINE, 3," 1111111 1 11111 "
		CALL MAKE_FLOOR_LINE, 4," 2 111 2 1     1 "
		CALL MAKE_FLOOR_LINE, 5," t 111 111 111 1 "
		CALL MAKE_FLOOR_LINE, 6,"       1     1 2 "
		CALL MAKE_FLOOR_LINE, 7," 1211111<111>111 "
		CALL MAKE_FLOOR_LINE, 8,"         1     1 "
		CALL MAKE_FLOOR_LINE, 9," 111 a 1 1 321 1 "
		CALL MAKE_FLOOR_LINE,10," 2 1 2 1 1   1 1 "
		CALL MAKE_FLOOR_LINE,11," 1 1 111 11121 1 "
		CALL MAKE_FLOOR_LINE,12,"   1   2 1     1 "
		CALL MAKE_FLOOR_LINE,13," 1111111 4 321 1 "
		CALL MAKE_FLOOR_LINE,14," 2     ] 1   1 1 "
		CALL MAKE_FLOOR_LINE,15," 4 121 1 11121 1 "
		CALL MAKE_FLOOR_LINE,16," 1   1 ] 1     1 "
		CALL MAKE_FLOOR_LINE,17," 111 1 1 1 32111 "
		CALL MAKE_FLOOR_LINE,18," 1   1 1 2     1 "
		CALL MAKE_FLOOR_LINE,19," 121 1>1 11111 1 "
		CALL MAKE_FLOOR_LINE,20," 1   1 1 2   1 2 "
		CALL MAKE_FLOOR_LINE,21," 11111 1 111 111 "
		CALL MAKE_FLOOR_LINE,22," 1 2 1 ] 1 2 1   "
		CALL MAKE_FLOOR_LINE,23," 1 1 1 1 1 b 121 "
		CALL MAKE_FLOOR_LINE,24," 1 1   1         "
		CALL MAKE_FLOOR_LINE,25," 1 111 111>11111 "
		CALL MAKE_FLOOR_LINE,26," 2 1           ] "
		CALL MAKE_FLOOR_LINE,27," 1 11111 1 T 1 1 "
		CALL MAKE_FLOOR_LINE,28,"       1 1 2 2 1 "
		CALL MAKE_FLOOR_LINE,29," 11111 1 1111111 "
		CALL MAKE_FLOOR_LINE,30," 2     1   111 2 "
		CALL MAKE_FLOOR_LINE,31," 1111111112111 6 "
		CALL MAKE_FLOOR_LINE,32,"                 "
		;						 01234567890123456
		;						           1     
	;Ｂ６Ｆ
	CASE 1
		FLAG:最大X = 17
		FLAG:最大Y = 17
;								 0         1      
;								 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"                 "
		CALL MAKE_FLOOR_LINE, 1," 1112111 111 321 "
		CALL MAKE_FLOOR_LINE, 2," 1     1 1 2   1 "
		CALL MAKE_FLOOR_LINE, 3," 1 11121 1 1 3 1 "
		CALL MAKE_FLOOR_LINE, 4,"   2   1   2 2 2 "
		CALL MAKE_FLOOR_LINE, 5," a 1 111 12111 1 "
		CALL MAKE_FLOOR_LINE, 6," 2 1 2   2     1 "
		CALL MAKE_FLOOR_LINE, 7," 111 11111211121 "
		CALL MAKE_FLOOR_LINE, 8,"                 "
		CALL MAKE_FLOOR_LINE, 9," 3 1>1111111 111 "
		CALL MAKE_FLOOR_LINE,10," 2 1   [ 1 ] 1 2 "
		CALL MAKE_FLOOR_LINE,11," 11121 1 1 1 1 b "
		CALL MAKE_FLOOR_LINE,12," 1     1   2 2   "
		CALL MAKE_FLOOR_LINE,13," 123 11111 11111 "
		CALL MAKE_FLOOR_LINE,14," 1   1     1   2 "
		CALL MAKE_FLOOR_LINE,15," 1<11121111121 1 "
		CALL MAKE_FLOOR_LINE,16,"                 "
	;Ｂ７Ｆ
	CASE 0
		FLAG:最大X = 17
		FLAG:最大Y = 17
;								 0         1
;								 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"                 "
		CALL MAKE_FLOOR_LINE, 1," a 1 1111111 111 "
		CALL MAKE_FLOOR_LINE, 2," 2 1 1 2 2 1   1 "
		CALL MAKE_FLOOR_LINE, 3," 111 1 1 1 1 421 "
		CALL MAKE_FLOOR_LINE, 4,"   2 1     1   1 "
		CALL MAKE_FLOOR_LINE, 5," 11411 4 111 421 "
		CALL MAKE_FLOOR_LINE, 6," 1   1 2 2 1   1 "
		CALL MAKE_FLOOR_LINE, 7," 124 1 1 1 11111 "
		CALL MAKE_FLOOR_LINE, 8," 1   1 2 1 1   1 "
		CALL MAKE_FLOOR_LINE, 9," 11111 1 1 1 121 "
		CALL MAKE_FLOOR_LINE,10," 1   1 2 2 1   1 "
		CALL MAKE_FLOOR_LINE,11," 124 111 1 11111 "
		CALL MAKE_FLOOR_LINE,12," 1   1     1 2   "
		CALL MAKE_FLOOR_LINE,13," 124 1 4 1 1 111 "
		CALL MAKE_FLOOR_LINE,14," 1   1 2 2 1 1 2 "
		CALL MAKE_FLOOR_LINE,15," 111 1111111 1 b "
		CALL MAKE_FLOOR_LINE,16,"                 "


ENDSELECT

;=======================================================================
;特殊タイル表示
;=======================================================================
@TILE_MARK_COMMON_29_0, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	CASE 1
		RESULTS = 　
	CASEELSE
		RETURN 1
ENDSELECT

@TILE_MARK_COMMON_29_9, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	CASE 1
		RESULTS = ST
	CASE 2 , 3 , 4
		RESULTS = Ｅ
	CASEELSE
		RETURN 1
ENDSELECT

@TILE_MARK_COMMON_29_2, ARG , ARG:1 , ARG:2
SIF GET_FLOOR_ANALYZE_T(ARG:1, ARG:2) < 2
	RETURN 1
SELECTCASE ARG / 10
	CASE 1
		RESULTS = ↑
	CASE 2
		RESULTS = ←
	CASE 3
		RESULTS = ↓
	CASE 4
		RESULTS = →
	CASEELSE
		RETURN 1
ENDSELECT

@TILE_MARK_COMMON_29_4, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	;メッセンジャー
	CASE 600
		RESULTS = 鍛
	CASE 603 , 5
		RESULTS = 武
	CASE 604
		RESULTS = 酒
	CASE 605 , 6
		RESULTS = 防
	CASE 608 , 7
		SETCOLOR COLOR("RED")
		RESULTS = Ｇ
	CASE 400
		RESULTS = CB
	CASE 200 , 1 , 4
		RETURN 1
	CASEELSE
		SETCOLOR 0x00ff00
		RESULTS = ｍ
ENDSELECT

;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_29, ARGS, ARG, ARG:1
SELECTCASE ARGS
	CASE "t"
		RETURN 19
	CASE "e"
		RETURN 29
	CASE "a"
		RETURN 39
	CASE "b"
		RETURN 49
	CASE "["
		RETURN 12
	CASE "<"
		RETURN 22
	CASE "]"
		RETURN 32
	CASE ">"
		RETURN 42
ENDSELECT

;=======================================================================
;タイル調整
;=======================================================================
@MAKE_FLOOR_LINE_COMMON_29, ARG, ARG:1, ARG:2
#LOCALSIZE 30
;LOCAL:3 宝箱 ダンジョンフラグ:84~99
;LOCAL:4 イベント
;(0/0)でLOCALを消去
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
SELECTCASE ARG
	CASE 3
		IF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):(FLAG:現M+70), LOCAL:3)
			DA:(ARG:1):(ARG:2) = 1
		ELSE
			DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:3 * 10 + 3
		ENDIF
		LOCAL:3++
	CASE 4
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:4 * 10 + 4
		LOCAL:4++
	CASE 5
		;DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:5 * 10 + 5
		;LOCAL:5++
	CASE 6
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:6 * 10 + 6
		LOCAL:6++
	CASE 7
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:7 * 10 + 7
		LOCAL:7++
	CASE 8
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:8 * 10 + 8
		LOCAL:8++
	CASE 9
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:9 * 10 + 9
		LOCAL:9++
	
ENDSELECT
SIF FLAG:現M == 0 && RANGE(ARG:1 , 17 , 23) && RANGE(ARG:2 , 2 , 10) && BASE:MASTER:知恵 < 10
	DA:(ARG:1):(ARG:2) = 0
	

;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_29
CALL EVENT_DUNGEON_29 , 0 , 1
RETURN RESULT


;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_29, ARG
;ARG = 進入時の向き　0の場合ワープなど
CALL EVENT_DUNGEON_29, ARG , 0
RETURN RESULT

@EVENT_DUNGEON_29, ARG, ARG:1
#DIM 交渉可能キャラ , 7
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) % 10
;==========
;出口の処理
;==========
	CASE 8
		CALL DUNGEON_EXIT
		RETURN 1
;========
;宝の処理
;========
	CASE 3
		CALL TREASURE_29
		RETURN 1
	
;=======
;階段
;=======
	CASE 6
		CALL UPSTAIRS_29
		RETURN 1

	CASE 7
		CALL DOWNSTAIRS_29
		RETURN 1

;==========
;扉の処理
;==========
	CASE 2
		CALL DUNGEON_DOOR,ARG
		RETURN 1

;========================
;イベント
;========================
	CASE 4
		LOCAL = DA:(FLAG:現X):(FLAG:現Y) / 10 % 10
		SIF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL) && ARG:1 == 0
			RETURN 0
		SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
			;Ｂ１Ｆ　イベント
			CASE 600
				;鍛冶屋
				CALL MESSAGE_WINDOW_D , "" , "＞二人組のドワーフが鍛冶仕事をしている……"
				  IF ITEM:4070 == 1
				     CALL MESSAGE_WINDOW_D , "" , "＞あなたはドワーフ達の興味を惹きそうな/古い宝を持っているのを思い出した"
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "何か用か？　冷やかしなら…/む…　ちょっと待てそいつは…！"
				     CALL MESSAGE_WINDOW_D , "" , "＞ドワーフ達は食い入るようにDraupnirを見つめている"
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "なんとDraupnirか…/まさかお目にかかる日がくるとはな…"
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "こいつは神代の代…　気の遠くなるような大昔に/ドワーフの鍛冶兄弟によって作られたんじゃよ"
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "そしてワシ等はな、その兄弟の子孫なんじゃ/なんとも奇妙な縁じゃわい"
				     CALL MESSAGE_WINDOW_D , "" , "＞あなたはDraupnirを手渡し、好きなだけ見るよう伝えた"
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "おお、有り難い。　…では拝見いたす"
				     CALL MESSAGE_WINDOW_D , "" , "＞恭しく傅いて神宝を受け取ると、歓喜に満ちた表情で/あるいは頷き、あるいは感嘆し目を輝かせて調べ始めた"
				     CALL MESSAGE_WINDOW_D , "" , "＞気がつくと他のドワーフ達も集まり、大変な賑わいになっていた/しかし度を超えて騒ぐような者はなく、皆一様に神妙にしていた"
				     CALL MESSAGE_WINDOW_D , "親方" , "今のワシ等から見れば、素朴で稚拙な部分もある/その一方で、ワシ等では到底及びもつかん部分もある"
				     CALL MESSAGE_WINDOW_D , "親方" , "だが断言できる。これはまさしくドワーフの業…/ワシ等の根源であり目標であり、証左であり矜持なのだ"
				     CALL MESSAGE_WINDOW_D , "" , "＞ドワーフ達の中には感極まって泣き出す者もいた/道は違えど---違いすぎるが---あなたもまた一廉の職人である/彼らに共感するところはあった"
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "じゃが…　どうやらこいつは「死んで」おるようじゃ/本来の力を失っておる。並ぶものなきその魔力を"
				     CALL MESSAGE_WINDOW_D , "" , "＞あなたはDraupnirを入手した経緯を説明した"
				     CALL MESSAGE_WINDOW_D , "親方" , "ベル・デルから得たのであれば、合点もいく/オーディンが息子の死を悼んで共に埋葬したと聞くからな"
				     CALL MESSAGE_WINDOW_D , "親方" , "副葬品として冥府に渡ったため、Draupnirも「死んだ」訳だな/蘇ったベル・デルは再度死んだが、こいつは運良く現世に残った、と"
				     CALL MESSAGE_WINDOW_D , "" , "＞鍛冶兄弟が何事か言おうとする。だが親方はそれを頷いて制した/親方はあなたに向き直るとこう言った"
				     CALL MESSAGE_WINDOW_D , "親方" , "なあ旅の方/折り入って貴殿に頼みたいことがあるのだが"
				     CALL MESSAGE_WINDOW_D , "" , "＞親方は一呼吸入れたあと、続けた"
				     CALL MESSAGE_WINDOW_D , "親方" , "このDraupnirを、ワシ等に打ち直しさせてくれんか/ワシ等ならそれができる。ワシ等がやらねばならん/それこそがワシ等の生きた証となるのだ"
				     CALL MESSAGE_WINDOW_D , "" , "＞断る理由など、あろう筈もない。あなたは快諾した/ドワーフ達から歓声があがる"
				     CALL MESSAGE_WINDOW_D , "親方" , "おお！　有り難い！　なに、金も時間も取りはせぬ/即刻総出で取り掛かるとしよう"
				     CALL MESSAGE_WINDOW_D , "" , "＞親方の一声に応、と唱じ、ドワーフ達は恐ろしい速さで工房内の持ち場に散った/ドラゴンの息吹もかくやとふいごを吹き込み、瞬く間に炉は溶岩と化した"
				     CALL MESSAGE_WINDOW_D , "" , "＞傍らでは色とりどりの鉱石を金槌で砕き、小鍋に入れて、炉にくべる/金銀胴にミスリル、オリハルコン、金属たちが次々と溶けていく"
				     CALL MESSAGE_WINDOW_D , "" , "＞ひとしきり溶かし込まれた灼熱の小鍋は、虹の光彩を放ち始めた/そこに宝飾を取り払ったDraupnirが沈められる/すると奇妙なことに、Draupnirが歌いだした"
				     CALL MESSAGE_WINDOW_D , "" , "＞緩やかに、ときに気高く、ラインに住まうローレライのごとく/響き渡る歌声に合わせて、ドワーフ達も歌いだした"
				     CALL MESSAGE_WINDOW_D , "" , "＞気がつけばあなたも歌っていた。戦列を組む仲魔たちも同様だ/なんとCOMPにいた仲魔までが、勝手に飛び出して歌っている/しかしもはや些細なことは気にならなかった"
				     CALL MESSAGE_WINDOW_D , "" , "＞歌が最高潮に達したとき、用意された鋳型に小鍋の歌声が流し込まれた/片腕分だったDraupnirは、一対となって真の姿を成そうとしている"
				     CALL MESSAGE_WINDOW_D , "" , "＞歌のテンポが速くなる。Draupnirは急速に冷え固まろうとしていた/迅速に、しかし正確に宝石が嵌め込まれ、文様が刻まれる/化粧をするかのように美しく仕上げられていく"
				     CALL MESSAGE_WINDOW_D , "" , "＞仕上げの小槌が最後の一音を高らかに奏でた/歌は止み、炉の火も戻り、それまでと打って変わった静寂が訪れる"
				     CALL MESSAGE_WINDOW_D , "" , "＞鍛冶兄弟は頷き、ビロード張りのプレートに腕輪を載せて親方に渡した/親方はそれを受け取るとあなたに近づき、恭しく献上した"
				     CALL MESSAGE_WINDOW_D , "親方" , "新たな神話の誕生だな/受け取られよ。これは貴殿のために生まれたのだ"
				     CALL MESSAGE_WINDOW_D , "" , "＞工房は歓声に包まれた。冷め遣らぬ熱狂の中、あなたは再び旅路に戻った"
				     CALL MESSAGE_WINDOW_D , "" , "＞Shin Draupnirを得た"
				          ITEM:4070 -= 1
				            ITEM:4071 += 1
				                FLAG:名声 += 1000
				  ELSEIF ITEM:4071 == 1
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "よう、大将。景気はどうだ？"
				  ELSE
				     CALL MESSAGE_WINDOW_D , "鍛冶屋" , "見ての通り忙しいんだ/ひやかしなら帰ってくれ"
				  ENDIF
				     SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 601
				;メッセンジャー
				CALL MESSAGE_WINDOW_D , "コボルト" , "昔トウキョウを守っていたとかいう/将門公はバラバラに千切られて封印されたんだってね"
				CALL MESSAGE_WINDOW_D , "コボルト" , "……じゃあ今は誰がトウキョウを守ってるんだろう？"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 602
				;メッセンジャー
				CALL MESSAGE_WINDOW_D , "御隠居ドワーフ" , "この辺りに埋まっとる武器は わしらのお宝じゃ"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			;Ｂ１Ｆ　イベント
			CASE 603
				;武器屋
				CALL MESSAGE_WINDOW_D , "武器屋" , "いらっしゃい"
				$WEAPONS_603
				CALL INPUT_SELECT_D, "何の用だい？//[1]買う/[2]ステイタスを見る///[0]外に出る" , , , 2
				IF RESULT == 1
					CALL MESSAGE_WINDOW_D , "武器屋" , "何をお買い上げで？"
					;10-15レベル向けGunと弾丸を1割引きで販売
					LOCALS = 
					LOCALS += "Desert Eagle," + TOSTR(ITEMPRICE:"Desert Eagle"*9/10) + ","
					LOCALS += "Gonzo Pistol," + TOSTR(ITEMPRICE:"Gonzo Pistol"*9/10) + ","
					LOCALS += "Howa Carbine," + TOSTR(ITEMPRICE:"Howa Carbine"*9/10) + ","
					LOCALS += "Dominator," + TOSTR(ITEMPRICE:Dominator*9/10) + ","
					LOCALS += "Poison Shell," + TOSTR(ITEMPRICE:"Poison Shell"*8/10) + ","
					LOCALS += "Nerve Shell," + TOSTR(ITEMPRICE:"Nerve Shell"*8/10) + ","
					LOCALS += "Curse Shell," + TOSTR(ITEMPRICE:"Curse Shell"*8/10) + ","
					CALL DUNGEON_SHOPS, 0, LOCALS
				ELSEIF RESULT == 2
					CALL SHOP_COM_110
				ELSE
					CALL MESSAGE_WINDOW_D , "武器屋" , "毎度あり またどうぞ"
					RETURN 1
				ENDIF
				GOTO WEAPONS_603
			CASE 604
				;酒場
				CALL MESSAGE_WINDOW_D, "バーテン", "いらっしゃい"
				$BAR_LOOP
				CALL MESSAGE_WINDOW_D, "バーテン", "どういった御用で？"
				CALL INPUT_SELECT_D, "[1]酒を飲む/[2]話を聞く/////[0]外に出る" , , , 2
				IF RESULT == 0
					CALL MESSAGE_WINDOW_D, "バーテン", "また どうぞ"
				ELSEIF RESULT == 1
					CALL MESSAGE_WINDOW_D, "バーテン", "店での飲酒は未実装らしいですよ"
				ELSEIF RESULT == 2
					$BAR_LOOP_2
					CALL INPUT_SELECT_D , "＞誰に話しかける？//[1]コボルト/[2]ガイア教徒/[3]コボルト/[4]コボルト/[0]やめる" , , , 2
					IF RESULT == 0
						GOTO BAR_LOOP
					ELSEIF RESULT == 1
						CALL MESSAGE_WINDOW_D, "コボルト", "あんた新宿って街に行ったことあるかい？/あそこの地下ふかーくにゃあ、/ここみたいに妖精が中心になって作ってる街があるんだとよ"
						CALL MESSAGE_WINDOW_D, "コボルト", "そんでその妖精の街には薬屋があるんだがよ、/そこで働いてる妖精の子がえらい別嬪さんらしいんだなあ"
					ELSEIF RESULT == 2
						CALL MESSAGE_WINDOW_D, "ガイア教徒", "地上じゃあ弱虫共を騙してるメシア教徒の連中がデカイ顔をしているが/地下世界では我々ガイア教徒の方が受け入れられているのさ/……本当だぞ！"
					ELSEIF RESULT == 3
						CALL MESSAGE_WINDOW_D, "コボルト", "この街、特に北の方は歪に広がってるだろ？/ああいう所は俺達がお宝を掘り出したついでに街を広げてるのさ/……こりゃナイショの話だけどな/俺達が掘ったお宝、地上からも買いに来てるんだぜ"
					ELSEIF RESULT == 4
						CALL MESSAGE_WINDOW_D, "コボルト", "あんた戦っちゃったりする人だろ？/……いや、カッコ見りゃ分かるよ"
						CALL MESSAGE_WINDOW_D, "コボルト", "それじゃドワーフの鍛冶屋を知ってるかい？/オレ達が掘り出した武器を作り直したり/新しい武器を作り出したりしてる/ウデのいい奴でさ"
						CALL MESSAGE_WINDOW_D, "コボルト", "まあ、会っといてソンは無いね"
					ENDIF
					GOTO BAR_LOOP_2
				ENDIF
			CASE 605
				;防具屋
				CALL MESSAGE_WINDOW_D , "防具屋" , "あーら いらっしゃい"
				$ARMORS_605
				CALL INPUT_SELECT_D, "何の御用かしら？//[1]買う/[2]ステイタスを見る///[0]外に出る" , , , 2
				IF RESULT == 1
					CALL MESSAGE_WINDOW_D , "防具屋" , "何をお買い上げかしら？"
					;10-15レベル向け防具を1割引きで販売
					LOCALS = 
					LOCALS += "Iron Pumpkin," + TOSTR(ITEMPRICE:"Iron Pumpkin"*9/10) + ","
					LOCALS += "Frog Helm," + TOSTR(ITEMPRICE:"Frog Helm"*9/10) + ","
					LOCALS += "Hard Ten Gallon," + TOSTR(ITEMPRICE:"Hard Ten Gallon"*9/10) + ","
					LOCALS += "Chain Beret," + TOSTR(ITEMPRICE:"Chain Beret"*9/10) + ","
					LOCALS += "Kaiser Armor," + TOSTR(ITEMPRICE:"Kaiser Armor"*9/10) + ","
					LOCALS += "Sun Armor," + TOSTR(ITEMPRICE:"Sun Armor"*9/10) + ","
					LOCALS += "Cyber Arm," + TOSTR(ITEMPRICE:"Cyber Arm"*9/10) + ","
					LOCALS += "Arm Bridge," + TOSTR(ITEMPRICE:"Arm Bridge"*9/10) + ","
					LOCALS += "Damascus Gloves," + TOSTR(ITEMPRICE:"Damascus Gloves"*9/10) + ","
					LOCALS += "Titanium Boots," + TOSTR(ITEMPRICE:"Titanium Boots"*9/10) + ","
					LOCALS += "Safety Boots," + TOSTR(ITEMPRICE:"Safety Boots"*9/10) + ","
					LOCALS += "Aerojet," + TOSTR(ITEMPRICE:Aerojet*9/10) + ","
					CALL DUNGEON_SHOPS, 0, LOCALS
				ELSEIF RESULT == 2
					CALL SHOP_COM_110
				ELSE
					CALL MESSAGE_WINDOW_D , "防具屋" , "ありがと またいらしてねぇ"
					RETURN 1
				ENDIF
				GOTO ARMORS_605
				
			CASE 606
				;メッセンジャー
				CALL MESSAGE_WINDOW_D , "親方ドワーフ" , "スダマやコダマの連中が言うには/このトウキョウのあちこちに大和の神々が封印されとるんだとよ/なんでも天使や堕天使達の仕業らしいがね"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 607
				;メッセンジャー
				CALL MESSAGE_WINDOW_D , "ファクトリー労働者" , "まったく地霊の奴ら/人使いが荒くてもうヘロヘロだぜ"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 608
				;ガイア神殿
				CALL GAIA_TEMPLE_29
			CASE 609
				;壁の落書き
				CALL MESSAGE_WINDOW_D , "文字" , "＞地霊の街 /　働き者は歓迎"
				CALL MESSAGE_WINDOW_D , "" , "＞……と壁に書いてある"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			;Ｂ３Ｆ　イベント
			CASE 400
				;ナンディさん
				CALL MESSAGE_WINDOW_D, "ナンディさん", "ナマステ！/ワタシ インドから来た ナンディさんネ/また会えた人 おひさネー うれしネー"
				CALL MESSAGE_WINDOW_D, "ナンディさん", "コードブレーカー ネ やってネ"
				CALL MESSAGE_WINDOW_D, "", "＞ナンディさんから『XTH』と書かれたゲームのパッケージを受け取った…"
				CALL MESSAGE_WINDOW_D, "ナンディさん", "……ごめんネ いまコードブレーカー準備中ネ/また今度遊んでネ"
			;Ｂ４Ｆ　イベント
			CASE 300
				;メッセンジャー
				CALL MESSAGE_WINDOW_D , "ブラウニー" , "そこの階段から/人間たちの集まっているコミュニティに行けるよ"
				CALL MESSAGE_WINDOW_D , "ブラウニー" , "奴ら人間にしてはかっこいいんだ/まあ、言っちゃ悪いけど あんたとはくらべモンにならないね"
				CALL MESSAGE_WINDOW_D , "ブラウニー" , "だけど、あんなにかっこいいのに/地上から逃げてきたらしいんだ"
				
			;Ｂ５Ｆ　イベント
			CASE 200
				;ヒルコ
				IF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 == 0 && (依頼フラグ:13:0 != 4 || FINDCHARA_B([[キャラ:菊枝]]) != 2 )
					CALL MESSAGE_WINDOW_D , "声" , "来るな！　私に近づくな！"
					CALL MESSAGE_WINDOW_D , "ミュータント" , "帰れ！/おとなしく帰らぬと/ただではすまんぞ！"
				ELSEIF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 == 0
					LOCAL = RESULT:1
					CALL MESSAGE_WINDOW_D , "声" , "来るな！　私に近づくな！"
					CALL MESSAGE_WINDOW_D , "ミュータント" , "帰れ！/おとなしく帰らぬと/ただではすまんぞ！"
					CALL MESSAGE_WINDOW_D , "声" , "……待て！/……貴女は……！"
					CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , "お久しぶりです、ヒルコ様"
					CALL MESSAGE_WINDOW_D , "ヒルコ" , @"おお 間違いない！/菊理媛神よ、お待ちしておりましたぞ！"
					CALL MESSAGE_WINDOW_D , "ヒルコ" , @"……とするとお前が%ANATANAME("依頼した召喚士" , "")%か？" , , "再利用する"
					CALL INPUT_YN_D , "Yes" , "報酬をもらう"
					IF RESULT == 0
						CALL MESSAGE_WINDOW_D , "ヒルコ" , @"そうか 依頼通り彼女を無事救出してくれたようだな/よくやってくれた、礼を言わせてもらうぞ"
					ELSE
						CALL MESSAGE_WINDOW_D , "ヒルコ" , @"そうであったな、約束の報酬だが……"
					ENDIF
					CALL MESSAGE_WINDOW_D , "ヒルコ" , @"この大きな働きに見合う報酬をお前に渡してやりたかったのだが……/済まぬ、この地下世界では地上の金は十分には集まらぬのだ" , , "再利用する"
					CALL MESSAGE_WINDOW_D , "" , @"＞ヒルコからボロボロの紙幣￥75000分と破魔矢を2本受け取った"
					CALL MESSAGE_WINDOW_D , "" , @"＞%CALLNAME:MASTER%は95の名声を得た" , , "再利用する"
					MONEY += 75000
					FLAG:名声 += 95
					CALL GET_ITEMS , "破魔矢" , 2
					IF CFLAG:MASTER:善悪値 >= 128
						CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , "それでヒルコ様、これから如何いたしましょう？"
						CALL MESSAGE_WINDOW_D , "ヒルコ" , @"封印され、囚われている神々を救わねばなりませぬ/しかし私自身にそのような力は無く、この地に住む者達にもまた強者は居らぬ……"
						CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"神宮の森の巫女達も、散り散りになってしまいました……/再び彼らを呼び集める事も出来るかもしれませんが/出来たとしても時間は非常にかかるでしょう"
						CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"……ヒルコ様、提案が御座います/%ANATANAME("この方" , "様")%にお願いしてみては如何でしょう？"
						CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"ファントム・ソサエティに属する者ではありますが/今の私達にとって\@ TALENT:MASTER:オトコ ? 彼 # 彼女 \@は最も信用出来る方だと思います" , , "再利用する"
						CALL MESSAGE_WINDOW_D , "ヒルコ" , @"うむ……/%ANATANAME("召喚士")%よ、我々の話を聞いてもらえぬか？"
						CALL INPUT_YN_D
						IF RESULT == 0
							;地祇が騙されて天津神を封印して云々、というところはアマテラスを救出する手前になってから教えてくれる
							;それまでは都合悪いし濁す
							CALL MESSAGE_WINDOW_D , "ヒルコ" , @"そうか 聞いてくれるか"
							$Q_200
							CALL MESSAGE_WINDOW_D , "ヒルコ" , @"天津神 そして国津神という名は知っていよう/元より この大和の地は 我々大和神族が治めていたのだ"
							CALL MESSAGE_WINDOW_D , "ヒルコ" , @"だが 世紀末を迎え 悪魔の群れが 大和の地にも現れると/その混乱に乗じ ヘブライの天使達の手で 大和の神々は封印されてしまった" , , "再利用する"
							CALL MESSAGE_WINDOW_D , "ヒルコ" , @"その結果が今の大和だ/護り手たる神々さえ封じられていなければ/大和はここまで悪魔達の跳梁跋扈する地とはならなかったはずなのだ"
							CALL MESSAGE_WINDOW_D , "ヒルコ" , @"……この地に再び平穏を取り戻すには彼らの力が必要だ/神々の封印を解き 復活させねばならぬ/……それが私の責任なのだ……"
							CALL MESSAGE_WINDOW_D , "ヒルコ" , @"……どうか神々の封印を解いてはくれぬだろうか？"
							CALL INPUT_SELECT_D, "[0]Yes/[1]No/[2]報酬を要求する"
							SELECTCASE RESULT
								CASE 0
									CALL INCREASE_LD , 20
									CALL MESSAGE_WINDOW_D , "ヒルコ" , @"おおっ そうか引き受けてもらえるか"
									CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"…貴方のお気持ちに、何らかの形で報いたいのですが/知っての通り今や私にあるのはこの身一つだけです"
									CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"ですから%ANATANAME( , "様、")%私は貴方に私自身を捧げます"
									CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"貴方の望むまま、思うままに私をお使いください/この地を救うために役立ててくれると信じています" , , "再利用する"
									ダンジョンフラグ:[[ダンジョン:赤坂]]:1 = 2
									
									CFLAG:LOCAL:戦闘参加不可能 = 0
									CFLAG:LOCAL:この場に居ないフラグ = 0
									依頼フラグ:13:0 = 5
									
									CALL MESSAGE_WINDOW_D , "ヒルコ" , @"神々の封じられている地についての情報はお前の事務所に届けておこう/力なき私に代わり 必ず成し遂げてくれ/頼む……"
									
								CASE 1
									CALL MESSAGE_WINDOW_D , "ヒルコ" , @"そうか……聞いてはもらえぬか……"
									ダンジョンフラグ:[[ダンジョン:赤坂]]:1 = 1
									IF CFLAG:LOCAL:引継ぎ回数 == 0
										CFLAG:LOCAL:この場に居ないフラグ = 1
										CALL REMOVE_POSITION, CPOS(LOCAL)
									ENDIF
									
								CASE 2
									CALL INCREASE_LD , 20
									CALL MESSAGE_WINDOW_D , "ヒルコ" , @"ふむ……/いや それも当然というもの 何せそう簡単にはいかぬ仕事だ/だが 今の我々には到底お前の満足しうる報酬など用意できまい……"
									CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"私にもあるのはこの身一つ、払える物は持ち合わせていません/……しかし、もし貴方が望むのでしたら/私自身を以って報酬という事にしていただけませんか？"
									CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"元はと言えば%ANATANAME("貴方様" , "様")%に救っていただいた命です"
									CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"それが神々の封印を解く為になるならば、/%ANATANAME("貴方様" , "様")%にこの身全てを捧げても構わないと思っています" , , "再利用する"
									CALL INPUT_YN_D , "引き受ける" , "やめておく"
									IF RESULT == 0
										CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"……引き受けていただけるのですね"
										CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"感謝を。そして、今日より私は%ANATANAME("貴方様" , "様")%の物です" , , "再利用する"
										CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"どうかご満足行くまで私をお使いください/……そして、必ずや彼らを救ってください" , , "再利用する"
										ダンジョンフラグ:[[ダンジョン:赤坂]]:1 = 2
										
										CFLAG:LOCAL:この場に居ないフラグ = 0
										CFLAG:LOCAL:戦闘参加不可能 = 0
										SIF CFLAG:LOCAL:引継ぎ回数 == 0
											TALENT:LOCAL:弱味 = 1
										依頼フラグ:13:0 = 5
										CALL MESSAGE_WINDOW_D , "ヒルコ" , @"神々の封じられている地についての情報はお前の事務所に届けておこう/力なき私に代わり 必ず成し遂げてくれ/頼む……"
									ELSE
										CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"……そうですか……"
									
										ダンジョンフラグ:[[ダンジョン:赤坂]]:1 = 1
										IF CFLAG:LOCAL:引継ぎ回数 == 0
											CFLAG:LOCAL:この場に居ないフラグ = 1
											CALL REMOVE_POSITION, CPOS(LOCAL)
										ENDIF
									ENDIF
							ENDSELECT
						ELSE
							CALL MESSAGE_WINDOW_D , "ヒルコ" , @"そうか……聞いてはもらえぬか……"
							ダンジョンフラグ:[[ダンジョン:赤坂]]:1 = 1
							
							IF CFLAG:LOCAL:引継ぎ回数 == 0
								CFLAG:LOCAL:この場に居ないフラグ = 1
								CALL REMOVE_POSITION, CPOS(LOCAL)
							ENDIF
						ENDIF
						
					ELSE
						CALL MESSAGE_WINDOW_D , "ヒルコ" , @"それでは道中気を付けて帰られよ/ここは地上の者が長居すべき地ではない"
						CALL MESSAGE_WINDOW_D , @"%CALLNAME:LOCAL%" , @"%ANATANAME("貴方様" , "様")%に助けていただいた御恩は、決して忘れません/どうか、お元気で……"
						IF CFLAG:LOCAL:引継ぎ回数 == 0
							CALL キャラ削除 , LOCAL
						ENDIF
						ダンジョンフラグ:[[ダンジョン:赤坂]]:1 = -1
					ENDIF
				ELSEIF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 == 1
					
					CALLF FINDCHARA_B([[キャラ:菊枝]])
					LOCAL = RESULT:1
					GOTO Q_200
				ELSEIF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 == 2
					CALL MESSAGE_WINDOW_D , "ヒルコ" , @"神々の封じられている地についての情報は/お前の事務所に届けておこう/力なき私に代わり 必ず成し遂げてくれ/頼む……"
				
				ELSEIF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 == -1
					CALL MESSAGE_WINDOW_D , "" , @"＞誰も居ない……"
				ENDIF
			CASE 201
				;メッセンジャー
				IF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 == 0 && (依頼フラグ:13:0 != 4 || FINDCHARA_B([[キャラ:菊枝]]) != 2 )
					CALL MESSAGE_WINDOW_D , "ミュータント" , "なんだお前は？/お前などが来ても あの方に会えるものか"
				ELSEIF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 != 0
					CALL MESSAGE_WINDOW_D , "ミュータント" , "ヒルコ様は/封印されし神々の秘密を知っていたので/追われていたのだ"
				ELSE
					CALL MESSAGE_WINDOW_D , "ミュータント" , "なんだお前は……/あっ貴女は！"
					CALL MESSAGE_WINDOW_D , "ミュータント" , "どうぞお通り下さい/あの方はこの奥にいらっしゃいます"
				ENDIF
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 202
				;メッセンジャー
				IF ダンジョンフラグ:[[ダンジョン:赤坂]]:1 == 0
					CALL MESSAGE_WINDOW_D , "ミュータント" , "…お前ら何しにきた？"
					CALL MESSAGE_WINDOW_D , "ミュータント" , "ここはお前らのような/普通の人間が来る所とは違う/さっさと帰りな" , "/" , "ログを残さない/ボタンを利用する/再利用する"
				ELSE
					CALL MESSAGE_WINDOW_D , "ミュータント" , "……我々ミュータントは/地上でしいたげられたあげく/地下に追放されたのだ"
				ENDIF
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			;Ｂ７Ｆ　イベント
			CASE 0
				CALL MESSAGE_WINDOW_D , "ミュータント" , "政府やメシア教徒達は異形の私達を/地上より追放した/住む場所を失くした私達には/地下へ逃げるほか 生きる道は無かった……"
				CALL MESSAGE_WINDOW_D ,  "ミュータント" , "この恨み……/地上が滅びる日まで 奴らが死に絶えるその時まで/決して忘れはしない……"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
				
			CASE 1
				;壁の落書き
				CALL MESSAGE_WINDOW_D , "文字" , "＞“ミュータント・コミュニティー”/　地上の人間 入るべからず"
				CALL MESSAGE_WINDOW_D , "" , "＞……と壁に書いてある"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 2
				;メッセンジャー
				CALL MESSAGE_WINDOW_D , "ガイア教徒" , "この東京の地には大和神族の神々が封印されているという"
				CALL MESSAGE_WINDOW_D , "ガイア教徒" , "彼らの協力を得て、/我々ガイア教の仇敵メシア教徒どもを討ち滅ぼすべく/この神々の復活を待っているのだ……"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 3
				;メッセンジャー
				CALL MESSAGE_WINDOW_D , "ミュータント" , "将門公には魔界にも行ける力があるそうだ"
				CALL MESSAGE_WINDOW_D ,  "ミュータント" , "……こんな所で暮らしてたってユメもキボウも無いし/いっそ魔界に連れて行ってもらいたいよ" , "/" , "ログを残さない/ボタンを利用する/再利用する"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 4
				;長老
				IF !GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL)
					CALL MESSAGE_WINDOW_D , "ミュータント長老" , "……地上の街がそうであるように、ここも元々トウキョウの街だった/だがここが悪魔の巣窟と化し 封鎖され打ち捨てられると/いつしか地上の人間には忘れ去られ/地上を追われた者だけがひっそりと暮らすようになった"
					CALL MESSAGE_WINDOW_D , "ミュータント" , "普通の人間やメシア教徒達は/我々のような醜いミュータントが/地上に住むことを許さず追放した"
					CALL MESSAGE_WINDOW_D , "ミュータント" , "我々はやっとの思いでここへ逃げ延び/そして光から遠ざけられながら生きてきたのだ"
				ENDIF
				CALL MESSAGE_WINDOW_D , "ミュータント" , "……我々は何もいらぬ 何も望まぬ/ただもう一度 光が欲しい……"
				SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):(40+FLAG:現M) , LOCAL
			CASE 5
				;武器屋
				CALL MESSAGE_WINDOW_D , "武器屋" , "いらっしゃい"
				$WEAPONS_5
				CALL INPUT_SELECT_D, "何の用だい？//[1]買う/[2]ステイタスを見る///[0]外に出る" , , , 2
				IF RESULT == 1
					CALL MESSAGE_WINDOW_D , "武器屋" , "何をお買い上げで？"
					;20レベル向けGunと弾丸を1割引きで販売
					LOCALS = 
					LOCALS += "Gyrojet," + TOSTR(ITEMPRICE:Gyrojet*9/10) + ","
					LOCALS += "Remington Ｍ710," + TOSTR(ITEMPRICE:"Remington Ｍ710"*9/10) + ","
					LOCALS += "Holy Shell," + TOSTR(ITEMPRICE:"Holy Shell"*8/10) + ","
					LOCALS += "Curse Shell," + TOSTR(ITEMPRICE:"Curse Shell"*8/10) + ","
					LOCALS += "Dispel Shot," + TOSTR(ITEMPRICE:"Dispel Shot"*8/10) + ","
					CALL DUNGEON_SHOPS, 0, LOCALS
				ELSEIF RESULT == 2
					CALL SHOP_COM_110
				ELSE
					CALL MESSAGE_WINDOW_D , "武器屋" , "毎度あり またどうぞ"
					RETURN 1
				ENDIF
				GOTO WEAPONS_5
			CASE 6
				;防具屋
				CALL MESSAGE_WINDOW_D , "防具屋" , "あーら いらっしゃい"
				$ARMORS_6
				CALL INPUT_SELECT_D, "何の御用かしら？//[1]買う/[2]ステイタスを見る///[0]外に出る" , , , 2
				IF RESULT == 1
					CALL MESSAGE_WINDOW_D , "防具屋" , "何をお買い上げかしら？"
					;20レベル向け防具を1割引きで販売
					LOCALS = 
					LOCALS += "Plate Bandana," + TOSTR(ITEMPRICE:"Plate Bandana"*9/10) + ","
					LOCALS += "Tang Lion Happi," + TOSTR(ITEMPRICE:"Tang Lion Happi"*9/10) + ","
					LOCALS += "Whirlwind Gauntlet," + TOSTR(ITEMPRICE:"Whirlwind Gauntlet"*9/10) + ","
					LOCALS += "CrscntMoon Greaves," + TOSTR(ITEMPRICE:"CrscntMoon Greaves"*9/10) + ","
					CALL DUNGEON_SHOPS, 0, LOCALS
				ELSEIF RESULT == 2
					CALL SHOP_COM_110
				ELSE
					CALL MESSAGE_WINDOW_D , "防具屋" , "ありがと またいらしてねぇ"
					RETURN 1
				ENDIF
				GOTO ARMORS_6
			CASE 7
				;ガイア神殿
				CALL GAIA_TEMPLE_29
		ENDSELECT
		RETURN 1
		
;========================
;その他
;========================
	CASE 9
		SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
			;ショート・ターミナル
			CASE 1
				CALLFORM S_TERMINAL_{FLAG:現ダンジョン}
			;エレベーター
			CASE 2
				CALL DUNGEON_ELEVATOR, 29
			CASE 3
				CALL DUNGEON_ELEVATOR, 39
			CASE 4
				CALL DUNGEON_ELEVATOR, 49
		ENDSELECT
		RETURN 1
ENDSELECT
RETURN 0

;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_29
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	CASE 600
		LOCALS = PoisonArrow
		LOCAL = 1
	CASE 601
		LOCALS = 魔法の箱
		LOCALS:1 = ガーネット
		LOCALS:2 = Life stone
		LOCAL = 8
		LOCAL:1 = 1
	CASE 602
		LOCALS = LuIncense
		LOCAL = 1
	CASE 500
		LOCALS = 魔法の箱
		LOCALS:1 = ダイヤモンド
		LOCALS:2 = Life stone
		LOCAL = 8
		LOCAL:1 = 1
	CASE 400
		LOCALS = Youtou Nihil
		LOCAL = 1
	CASE 200
		LOCALS = MegidoStone
		LOCAL = 1
	CASE 201
		LOCALS = 善行の石
		LOCAL = 1
	CASE 202
		LOCALS = Dis-Stone
		LOCAL = 2
	CASE 100
		LOCALS = Nameless Sword
		LOCAL = 1
	CASE 101
		LOCALS = 魔法の箱
		LOCALS:1 = パール
		LOCALS:2 = Life stone
		LOCAL = 8
		LOCAL:1 = 1
	CASE 102
		LOCALS = メタルカード
		LOCAL = 1
	CASE 103
		LOCALS = 魔法の箱
		LOCALS:1 = ルビー
		LOCALS:2 = Life stone
		LOCAL = 8
		LOCAL:1 = 1
	CASEELSE
ENDSELECT
IF LOCALS == "￥"
	CALL GET_TREASURE_MONEY, 0, LOCAL, FLAG:現ダンジョン, FLAG:現M + 70, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
ELSEIF LOCALS == "魔貨"
	CALL GET_TREASURE_MONEY, 1, LOCAL, FLAG:現ダンジョン, FLAG:現M + 70, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
ELSEIF LOCALS == "MAG" || LOCALS == "ＭＡＧ"
	CALL GET_TREASURE_MAG, 0, LOCAL, FLAG:現ダンジョン, FLAG:現M + 70, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
ELSEIF LOCALS == "罠"
	;LOCALS:1 種類 LOCAL ダメージ量or確率 LOCALS:2 相性
	CALL GET_TREASURE_TRAP, LOCALS:1, LOCAL, GET_TYPE_NUM(LOCALS:2), FLAG:現ダンジョン, FLAG:現M + 70, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
ELSEIF LOCALS == "魔法の箱"
	IF GETNUM(ITEM, LOCALS:1) == -1 || GETNUM(ITEM, LOCALS:2) == -1
		PRINTL ＞アイテム名の指定が間違ってます
		PRINTW ＞アイテムの取得処理を中断します
	ELSE
		CALL MAGIC_BOX , LOCAL , GETNUM(ITEM, LOCALS:1) , GETNUM(ITEM, LOCALS:2) , LOCAL:1 , FLAG:現ダンジョン, FLAG:現M + 70, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	ENDIF
ELSE
	IF GETNUM(ITEM, LOCALS) == -1
		PRINTL ＞アイテム名の指定が間違ってます
		PRINTW ＞アイテムの取得処理を中断します
	ELSE
		CALL GET_TREASURE, GETNUM(ITEM, LOCALS), LOCAL , FLAG:現ダンジョン, FLAG:現M + 70, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	ENDIF
ENDIF
RETURN 1

;=======================================================================
;階段置き場
;=======================================================================
@UPSTAIRS_29
;位置とIDが対応しているはずなので、同IDの階段を探させれば良い
CALL DUNGEON_UPSTAIRS_ID, FLAG:現M+1 , DA:(FLAG:現X):(FLAG:現Y) + 1000 + 1
RETURN 1

@DOWNSTAIRS_29
CALL DUNGEON_DOWNSTAIRS_ID, FLAG:現M-1 , DA:(FLAG:現X):(FLAG:現Y) - 1000 - 1
RETURN 1
;=======================================================================
;エレベーター定義
;=======================================================================
@DUNGEON_ELEVATOR_29, ARG
VARSET RESULT, -1
VARSET RESULTS
SELECTCASE ARG
	CASE 29
		RESULT:1 = 6
		RESULT:2 = 3
		RETURN -1
	CASE 39
		RESULT:1 = 2
		RESULT:2 = 1
		RESULT:3 = 0
		RETURN -1
	CASE 49
		RESULT:1 = 2
		RESULT:2 = 1
		RESULT:3 = 0
		RETURN -1
ENDSELECT
;=======================================================================
;Ｓターミナル
;=======================================================================
@S_TERMINAL_29
#LOCALSSIZE 1
SELECTCASE FLAG:現M
	CASE 6
		SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):2 , 0
	CASE 3
		SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):2 , 1
	CASE 2
		SETBIT ダンジョンフラグ:(FLAG:現ダンジョン):2 , 2
ENDSELECT
LOCALS = 
SIF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):2 , 0) && FLAG:現M != 6
	LOCALS += "[1] Ｂ１Ｆへ/"
SIF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):2 , 1) && FLAG:現M != 3
	LOCALS += "[2] Ｂ４Ｆへ/"
SIF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):2 , 2) && FLAG:現M != 2
	LOCALS += "[3] Ｂ５Ｆへ/"
LOCALS += "[0] 利用しない/[100] 自室へ戻る"
CALL MESSAGE_WINDOW_D, "", "＞Ｓターミナルがある/＞どうしますか？"
CALL INPUT_SELECT_D, LOCALS
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 100
	FLAG:脱出 = 1
	RETURN 0
ELSEIF RESULT == 1
	CALL DUNGEON_WORP_ID,6,19,1
	RETURN 0
ELSEIF RESULT == 2
	CALL DUNGEON_WORP_ID,3,19,1
	RETURN 0
ELSE
	CALL DUNGEON_WORP_ID,2,19,1
	RETURN 0
ENDIF
RETURN 0

;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_29
SELECTCASE FLAG:現M
	CASEELSE
		FLAG:エンカウント率 = MAX(FLAG:未遭遇歩数 * 5 - 25, 0)
		FLAG:MAG消費率 = 100
ENDSELECT
FLAG:エンカウント率 = 0
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_29
#LOCALSIZE 3
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF

;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_29
SELECTCASE FLAG:現M
	;地霊街
	CASE 3 TO 6
		RETURN CLASS_NUM_COMMON("初級")
	;ミュータント・コミュニティー
	CASEELSE
		RETURN CLASS_NUM_COMMON("中級")
ENDSELECT

;=======================================================================
;敵の総数
;=======================================================================
@ENEMY_NUM_29, ARG
SELECTCASE FLAG:現M
	;地霊街
	CASE 3 TO 6
		RETURN ENEMY_NUM_COMMON("中級", ARG)
	;ミュータント・コミュニティー
	CASEELSE
		RETURN ENEMY_NUM_COMMON("上級", ARG)
ENDSELECT
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_29, ARG
SELECTCASE FLAG:現M
	;赤坂
	;地霊の街
	CASE 3 TO 6
		SELECTCASE RAND:100
			CASE IS < 15
				RETURN [[キャラ:ノッカー]], 15
			CASE IS < 30
				RETURN [[キャラ:ブラウニー]], 17
			CASE IS < 50
				RETURN [[キャラ:カハク]], 17
			CASE IS < 70
				RETURN [[キャラ:ツチグモ]], 18
			CASE IS < 85
				RETURN [[キャラ:コッパテング]], 19
			CASE IS < 95
				RETURN [[キャラ:ドヴェルガー]], 23
			CASE IS < 100
				IF RAND:2 == 1
					RETURN [[キャラ:カラステング]], 28
				ELSEIF RAND:2 == 1
					RETURN [[キャラ:ウベルリ]], 33
				ELSE
					RETURN [[キャラ:ティターン]], 45
				ENDIF
		ENDSELECT
	;ミュータント・コミュニティー
	CASE 0 TO 2
		SELECTCASE RAND:100
			CASE IS < 15
				RETURN [[キャラ:ダークエルフ]], 19
			CASE IS < 30
				RETURN [[キャラ:ツチグモ]], 19
			CASE IS < 45
				RETURN [[キャラ:ノズチ]], 19
			CASE IS < 60
				RETURN [[キャラ:特攻隊]], 20
			CASE IS < 75
				RETURN [[キャラ:ネコマタ]], 22
			CASE IS < 90
				RETURN [[キャラ:ドヴェルガー]], 25
			CASE IS < 100
				RETURN [[キャラ:バンシー]], 27
		ENDSELECT
ENDSELECT
;=================================
;特殊調整
;=================================
@SET_ENEMY_STATUS_29, ARG
#LOCALSIZE 1
SELECTCASE NO:ARG
	CASE [[キャラ:ノッカー]]
		ABL:ARG:スキル3 = [[スキル:ディアラマ]]
		ABL:ARG:スキル5 = [[スキル:ソニックパンチ]]
		ABL:ARG:スキル2 = [[スキル:マグナス]]
		ABL:ARG:スキル7 = [[スキル:マハ・マグナ]]
	CASE [[キャラ:ブラウニー]]
		ABL:ARG:スキル2 = [[スキル:グライバ]]
		ABL:ARG:スキル5 = [[スキル:マハ・グライ]]
	CASE [[キャラ:カハク]]
		ABL:ARG:スキル1 = [[スキル:アギラオ]]
		ABL:ARG:スキル4 = [[スキル:ペンパトラ]]
	CASEELSE
ENDSELECT

@GAIA_TEMPLE_29
IF ABL:MASTER:属性LC == 1
	CALL MESSAGE_WINDOW_D , "ガイア教司祭" , "ロウサイドの使徒め即刻この場から立ち去れぃ！"
	IF MONEY < 5000
		RETURN 1
	ELSE
		CALL MESSAGE_WINDOW_D , "ガイア教司祭" , "……が、￥5000を寄進するのであれば便宜を図っても良いぞ"
		CALL INPUT_YN, "払う" , "払わない"
		SIF RESULT == 1
			RETURN 1
		MONEY -= 5000
	ENDIF
ENDIF
;ガイア神殿
CALL MESSAGE_WINDOW_D , "司祭" , "ここは ガイア神殿"
$GAIA
CALL INPUT_SELECT_D, "何の用だ？//[1]アイテムを買う/[2]ステイタスを見る///[0]外に出る" , , , 2
IF RESULT == 1
	CALL MESSAGE_WINDOW_D , "司祭" , "どれにするのだ？"
	;カオス系アイテムを1割引きで
	LOCALS = 
	LOCALS += "PoisonArrow," + TOSTR(ITEMPRICE:"PoisonArrow"*9/10) + ","
	LOCALS += "蠱毒皿," + TOSTR(ITEMPRICE:蠱毒皿*9/10) + ","
	LOCALS += "DeadlyNeedle," + TOSTR(ITEMPRICE:DeadlyNeedle*9/10) + ","
	LOCALS += "Prayer Beads," + TOSTR(ITEMPRICE:"Prayer Beads"*9/10) + ","
	CALL DUNGEON_SHOPS, 0, LOCALS
ELSEIF RESULT == 2
	CALL SHOP_COM_110
ELSE
	CALL MESSAGE_WINDOW_D , "司祭" , "この世に混沌をもたらさん"
	RETURN 1
ENDIF
GOTO GAIA


;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_29
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:4
	CASE 0
		RETURN [[キャラ:ウベルリ]] , 3 + RAND:10 , CSVBASE([[キャラ:ウベルリ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:ティターン]] , 3 + RAND:10 , CSVBASE([[キャラ:ティターン]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:ツチグモ]] , 3 + RAND:10 , CSVBASE([[キャラ:ツチグモ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 3
		RETURN [[キャラ:ノズチ]] , 3 + RAND:10 , CSVBASE([[キャラ:ノズチ]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT







;=======================================================================
;ゲームオーバー
;=======================================================================




