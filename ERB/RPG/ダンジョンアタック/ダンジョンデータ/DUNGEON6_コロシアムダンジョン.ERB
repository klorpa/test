
;=======================================================================
;コロシアムダンジョン
;ボス・なし
;ダンジョンナンバー6　ＭＡＰ0~2
;ダンジョンフラグ:6:0 イベント進行状況　1 ダンジョン・マッチ挑戦中　2ダンジョンマッチ攻略済み
;ダンジョンフラグ:6:1〜　宝箱
;=======================================================================




;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_6
SIF 闘技場フラグ:19:0 == 0
	RETURN 0
RETURN 1



;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_6
RESULTS = Colosseum Dungeon


;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_6
IF FLAG:現M == 1
	PRINTL Ring
ELSE
	PRINTL Colosseum Dungeon
ENDIF

;=======================================================================
;特殊タイル表示
;=======================================================================
@TILE_MARK_6_2_7_2
RESULTS = Ｔ


;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_6,ARG
FLAG:現M = 0
FLAG:現X = 9
FLAG:現Y = 15
;CALL AUTOMAP

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_6
IF ダンジョンフラグ:6:0 == 0
	CALL SHOW_FLOOR
	CALL SHOW_NOW_FORMATION_P,0,2,,2
	FLAG:コマンド表示行数 = LINECOUNT
	CALL MESSAGE_WINDOW_D, , @"＞As %CALLNAME:MASTER% went to explore the dungeon,/＞a guard called out to %HIM_HER(MASTER)%./＞Apparently there is a certain warrior fighting in the ring/＞and he is looking for more challengers."
	;コロシアムダンジョンを探索しようとする%CALLNAME:MASTER%達に、/＞警備員が声を掛けてきた/＞どうやら現在とある戦士とのダンジョン･マッチに/＞挑戦する人間を募集しているようだ"
	CALL MESSAGE_WINDOW_D, , "＞Do you want to challenge him?//※You can't escape the dungeon before completing the challenge/ and there will be more demon encounters too."
	;挑戦しますか？//※挑戦中はダンジョンから脱出できず、/　ダンジョン内に通常より多くの悪魔が放たれます"
	CALL INPUT_YN_D,"Yes","No"
	IF RESULT == 0
		CALL MESSAGE_WINDOW_D, , "＞You decided to challenge this warrior."
		;ダンジョン・マッチに挑戦します"
		CALL MESSAGE_WINDOW_D, , "【Get through the dungeon and to the stairs leading to the ring!】"
		;【ダンジョンを潜り抜け、リングへと続く道へ向かえ！】
		FLAG:脱出スキル禁止 = 1
		ダンジョンフラグ:6:0 = 1
	ELSE
		CALL MESSAGE_WINDOW_D, , "＞You decided to expore the dungeon normally for now."
		;通常通りダンジョンの探索を行います
	ENDIF
ENDIF

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_6,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT

;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_6
SELECTCASE FLAG:現M
	CASE 0
		FLAG:最大X = 17
		FLAG:最大Y = 17
		;						 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"00000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0  121 1211111  0"
		CALL MAKE_FLOOR_LINE, 2,"0  1 1       1  0"
		CALL MAKE_FLOOR_LINE, 3,"0111 111111121110"
		CALL MAKE_FLOOR_LINE, 4,"01             20"
		CALL MAKE_FLOOR_LINE, 5,"0121 11111 1 1 10"
		CALL MAKE_FLOOR_LINE, 6,"01   1 2 1 2 1  0"
		CALL MAKE_FLOOR_LINE, 7,"01 111 6 11111210"
		CALL MAKE_FLOOR_LINE, 8,"01 111     111 10"
		CALL MAKE_FLOOR_LINE, 9,"01211111 6 111 10"
		CALL MAKE_FLOOR_LINE,10,"0  1 2 1 2 1   10"
		CALL MAKE_FLOOR_LINE,11,"01 1 1 11111 1210"
		CALL MAKE_FLOOR_LINE,12,"02             10"
		CALL MAKE_FLOOR_LINE,13,"011121111111 1110"
		CALL MAKE_FLOOR_LINE,14,"0  1       1 1  0"
		CALL MAKE_FLOOR_LINE,15,"0  1111128 121  0"
		CALL MAKE_FLOOR_LINE,16,"00000000000000000"
		DA:7:7 = 16
		DA:9:9 = 26
		;宝箱の設置
		CALL SET_TILE,1,11,ダンジョンフラグ:6:1,0,3
		CALL SET_TILE,5,11,ダンジョンフラグ:6:2,0,3
		CALL SET_TILE,13,11,ダンジョンフラグ:6:3,0,3
		CALL SET_TILE,3,5,ダンジョンフラグ:6:4,0,3
		CALL SET_TILE,11,5,ダンジョンフラグ:6:5,0,3
		CALL SET_TILE,15,5,ダンジョンフラグ:6:6,0,3
		CALL SET_TILE,7,1,ダンジョンフラグ:6:7,0,3
		
		;イベントの設置
		CALL SET_TILE,9,15,ダンジョンフラグ:6:0,1,1
	CASE 1
		FLAG:最大X = 9
		FLAG:最大Y = 11
		;						 01234567890
		CALL MAKE_FLOOR_LINE, 0,"         "
		CALL MAKE_FLOOR_LINE, 1," 7       "
		CALL MAKE_FLOOR_LINE, 2," 1111111 "
		CALL MAKE_FLOOR_LINE, 3," 1111111 "
		CALL MAKE_FLOOR_LINE, 4," 1111111 "
		CALL MAKE_FLOOR_LINE, 5," 1111111 "
		CALL MAKE_FLOOR_LINE, 6," 1111111 "
		CALL MAKE_FLOOR_LINE, 7," 1111111 "
		CALL MAKE_FLOOR_LINE, 8," 1111111 "
		CALL MAKE_FLOOR_LINE, 9,"       7 "
		CALL MAKE_FLOOR_LINE,10,"         "
		DA:4:4 = 11
		DA:4:6 = 21
	CASEELSE
ENDSELECT



;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_6

;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 3
	CALL TREASURE_6
	RETURN RESULT
	
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 6
	CALL UPSTAIRS_6
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 7
	CALL DOWNSTAIRS_6
	RETURN RESULT


ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
	CALL DUNGEON_TERMINAL
	RETURN 1
ELSE
	CALL MESSAGE_WINDOW_D, , "＞There seems to be nothing here."
	;何も無いようだ
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_6,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 3
	CALL TREASURE_6
	RETURN RESULT
	
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 6
	CALL UPSTAIRS_6
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 7
	CALL DOWNSTAIRS_6
	RETURN RESULT


;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1


;========================
;イベント
;========================
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	IF ダンジョンフラグ:6:0 == 0
		CALL MESSAGE_WINDOW_D, , "＞There is a staircase leading to the ring./＞But you have no business there."
		;リングへと続く階段がある/＞今は用は無い
	ELSEIF ダンジョンフラグ:6:0 == 1
		CALL MESSAGE_WINDOW_D, , "＞There is a staircase leading to the ring./＞Do you want to climb it?"
		;リングへと続く階段がある/＞昇りますか？
		CALL INPUT_YN_D,"Yes","No"
		SIF RESULT == 1
			RETURN 1
		;入ってきた階段を取得
		LOCAL = DA:(FLAG:現X):(FLAG:現Y) / 10
		FLAG:逃走不可フラグ = 1
		FLAG:会話不能フラグ = 1
		FLAG:先制不意打ちキャンセル = 1
		CALL MESSAGE_WINDOW_D, , "....../..../..."
		;リングにワープ
		CALL DUNGEON_WORP_ID, 1, DA:(FLAG:現X):(FLAG:現Y) - 5, 1
		;クマ配置
		IF FLAG:現Y == 6
			DA:4:4 = 4
		ELSE
			DA:4:6 = 4
		ENDIF
		;部屋をアナライズし表示更新
		CALL FLOOR_ANALYZE
		CALL SHOW_FLOOR
		CALL SHOW_NOW_FORMATION_P,0,2,,2
		FLAG:コマンド表示行数 = LINECOUNT
		CALL MESSAGE_WINDOW_D, , "＞In the ring, there is a man in armor, carrying a sword."
		;リングにはプロテクターを身に付け、剣を背負う男が立っている
		CALL MESSAGE_WINDOW_D, "Warrior", "キサマが……"
		;戦士
		CALL MESSAGE_WINDOW_D, , "＞You feel a sharp gaze on you..."
		;なにやら鋭い視線を感じる…
		CALL MESSAGE_WINDOW_D, , "＞Looking arround, you notice a man dressed with samuria-like armor, /＞who seems to be watching you from his VIP seat./＞It seems the armored man is no ordinary person./＞Even from a distance that is apparent."
		;視線を感じる方を見ると、/＞鎧に身を包んだ武士のようないでたちの男がVIP席から見下ろしていた/＞腕組みして観戦するその男が只者ではないことは/＞遠目にもはっきりとわかる。
		CALL MESSAGE_WINDOW_D, "Warrior", "....Hey! Where are you looking!/Don't ignore people like that!"
		;……おい！どこを見てる！/人の話を聞け！
		CALL MESSAGE_WINDOW_D, , "＞The man in front of you seems to be quite unhappy with your lack of attention to him."
		;気が付くと目の前にいる方の男がなにやら憤慨していた
		CALL MESSAGE_WINDOW_D, "Warrior", "You're a brave person to ignore me./Or a fool./Well. You made it this far at least./I'll test you in battle!"
		;@"このオレを無視するとは大した奴だ/それともただのバカか/まあいい。よくここまで突破できたな/褒めてやろう 貴様の戦いを"
		CALL MESSAGE_WINDOW_D, "Warrior", "And..."
		;そして……
		CALL MESSAGE_WINDOW_D, , "＞The man draws his sword and takes a stance that reminds you of a hawk."
		;男が剣を抜き放ち、荒ぶる鷹のように身構える
		CALL MESSAGE_WINDOW_D, "Warrior", "I'll grieve your death/when I'm done with you!"
		;悲しんでやろう/貴様の最後を！
		CALL MESSAGE_WINDOW_D, , "＞Warrior Red Bear has appeared!"
		;戦士 レッド･ベアーが一体出た！
		CALL SET_ENEMY,9,[[キャラ:レッド・ベアー]],20,0,1
		CALL BATTLE_START
		CALL SHOW_FLOOR
		CALL SHOW_NOW_FORMATION_P,0,2,,2
		FLAG:コマンド表示行数 = LINECOUNT
		CALL MESSAGE_WINDOW_D, "Warrior Red Bear", "Just one more win and I would have..../I would have made it to the center...."
		;"戦士 レッド・ベアー", "あと一回勝てば…/センターに行くことができたのに……"
		CALL MESSAGE_WINDOW_D, , "＞Red Bear collapsed."
		;レッド･ベアーは力尽きた
		FLAG:逃走不可フラグ = 0
		FLAG:会話不能フラグ = 0
		FLAG:先制不意打ちキャンセル = 0
		CALL MESSAGE_WINDOW_D, , @"＞Got ￥50000 as price money./＞%CALLNAME:MASTER%'s fame increased by 80."
		;＞￥50000を賞金として受け取った/＞%CALLNAME:MASTER%は80の名声を得た"
		MONEY += 50000
		FLAG:名声 += 80
		FLAG:闘技場名声 += 50
		FLAG:脱出スキル禁止 = 0
		ダンジョンフラグ:6:0 = 2
		DA:9:15 = 8
		CALL MESSAGE_WINDOW_D, , "...../..../..."
		CALL DUNGEON_WORP_ID, 0, DA:(FLAG:現X):(FLAG:現Y) + 5, 1
		CALL SHOW_FLOOR
		CALL SHOW_NOW_FORMATION_P,0,2,,2
		FLAG:コマンド表示行数 = LINECOUNT
		CALL MESSAGE_WINDOW_D, , "＞When you left the ring, a guard hands you a letter./＞There is only a single sentence written on it./＞「Proud winner, you earned the right to challenge us」"
		;＞リングを出ると、警備員に手紙を渡された/＞手紙には達筆な文字でただ一文書かれている/＞「誇り高き勝者よ 貴殿に我々へ挑戦する権利を与えよう」
		CALL MESSAGE_WINDOW_D, , "＞It seems you can now challenge the mysterious samurai."
		;謎の武士へ挑戦する権利を得た
		RETURN 1
	ELSEIF ダンジョンフラグ:6:0 == 2
		CALL MESSAGE_WINDOW_D, , "＞There is a staircase leading to the ring./＞But you already finished the challenge."
		;リングへと続く階段がある/＞もう用は無い
	ENDIF
	
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
	CALL DUNGEON_TERMINAL
	RETURN 1
ENDIF
RETURN 0


;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_6
SELECTCASE FLAG:現M
	CASE 0
		IF FLAG:現X == 1 && FLAG:現Y == 11
			CALL GET_TREASURE,2006,1,FLAG:現ダンジョン,1
			
		ELSEIF FLAG:現X == 5 && FLAG:現Y == 11
			CALL GET_TREASURE,2501,1,FLAG:現ダンジョン,2
			
		ELSEIF FLAG:現X == 13 && FLAG:現Y == 11
			CALL GET_TREASURE_MONEY,1,200,FLAG:現ダンジョン,3
			
		ELSEIF FLAG:現X == 3 && FLAG:現Y == 5
			CALL GET_TREASURE,1117,1,FLAG:現ダンジョン,4
			
		ELSEIF FLAG:現X == 11 && FLAG:現Y == 5
			CALL GET_TREASURE,4504,1,FLAG:現ダンジョン,5
			
		ELSEIF FLAG:現X == 15 && FLAG:現Y == 5
			CALL GET_TREASURE,1004,1,FLAG:現ダンジョン,6
			
		ELSEIF FLAG:現X == 7 && FLAG:現Y == 1
			CALL GET_TREASURE,1010,1,FLAG:現ダンジョン,7
		ENDIF
ENDSELECT
RETURN 1

;=======================================================================
;階段置き場
;=======================================================================
@UPSTAIRS_6
SELECTCASE FLAG:現M
	CASE 0
		SIF FLAG:現X == 5 && FLAG:現Y == 4
			CALL DUNGEON_UPSTAIRS,1,5,4
	CASE 1
		SIF FLAG:現X == 5 && FLAG:現Y == 2
			CALL DUNGEON_UPSTAIRS,2,5,2
	CASEELSE
ENDSELECT
RETURN 1

@DOWNSTAIRS_6
SELECTCASE FLAG:現M
	CASE 1
		SIF FLAG:現X == 5 && FLAG:現Y == 4
			CALL DUNGEON_DOWNSTAIRS,0,5,4
	CASE 2
		SIF FLAG:現X == 5 && FLAG:現Y == 2
			CALL DUNGEON_DOWNSTAIRS,1,5,2
	CASEELSE
ENDSELECT
RETURN 1

;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_6
SELECTCASE FLAG:現M
	CASE 0
		IF FLAG:未遭遇歩数 < 4
			FLAG:エンカウント率 = 0
		ELSE
			FLAG:エンカウント率 = FLAG:未遭遇歩数*5
		ENDIF
		SIF ダンジョンフラグ:6:0 == 1
			FLAG:エンカウント率 += 15
	CASEELSE
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
ENDSELECT


;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_6
RETURN 18, 15, 17, 20
;=======================================================================
;エンカウント処理
;ダンジョンフラグ:6:0 == 1はダンジョンマッチ挑戦中を示す
;=======================================================================
@CHECK_ENCOUNT_6
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF

;=======================================================================
;敵の種族の種類数
;ダンジョンマッチ中は増える
;=======================================================================
@CLASS_NUM_6
SELECTCASE RAND:100
	CASE IS < (40 - (ダンジョンフラグ:6:0 == 1) * 20)
		RETURN 1
	CASE IS < (100 - (ダンジョンフラグ:6:0 == 1) * 20)
		RETURN 2
	CASEELSE
		RETURN 3
ENDSELECT
;=======================================================================
;敵の総数
;ダンジョンマッチ中は10体固定
;=======================================================================
@ENEMY_NUM_6, ARG
SIF ダンジョンフラグ:6:0 == 1
	RETURN 10
RETURN ENEMY_NUM_COMMON("初級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_6, ARG
SELECTCASE RAND:100
	CASE IS < 20
		;30%でショボ
		RETURN 902,15
	CASE IS < 45
		;25%でキヨヒメ
		RETURN 703,17
	CASE IS < 70
		;25%でネコマタ
		RETURN 801,15
	CASE IS < 90
		;20%でカシマレイコ
		RETURN 1004,17
	CASE IS < 100
		;10%でペルセポネ
		RETURN 202,16
ENDSELECT
