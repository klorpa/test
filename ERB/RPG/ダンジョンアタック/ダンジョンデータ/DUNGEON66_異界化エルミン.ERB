
;=======================================================================
;異界化エルミン
;フラグ
;=======================================================================




;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_66
;特になし
SIF イベントフラグ:23:0 == 2 && ダンジョンフラグ:66:1 == 0
	RETURN 1
RETURN 0


;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_66
RESULTS = 異界化エルミン学園


;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_66
PRINT 異界化エルミン学園
SELECTCASE FLAG:現M
	CASE 0
		PRINTL １Ｆエリア１
	CASE 1
		PRINTL １Ｆエリア２
	CASE 2
		PRINTL １Ｆエリア３
	CASE 3
		PRINTL ２Ｆエリア１
	CASE 4
		PRINTL ２Ｆエリア２
	CASE 5
		PRINTL １Ｆエリア４
	CASEELSE
		PRINTL
ENDSELECT


;=======================================================================
;特殊タイル表示
;=======================================================================
@TILE_MARK_66_0_4_0
RESULTS = ↑
@TILE_MARK_66_0_7_0
RESULTS = ↑

@TILE_MARK_66_4_3_0
RESULTS = ↑
@TILE_MARK_66_4_5_0
RESULTS = ↑

@TILE_MARK_66_1_4_14
RESULTS = ↓
@TILE_MARK_66_1_7_14
RESULTS = ↓

@TILE_MARK_66_3_3_14
RESULTS = ↓
@TILE_MARK_66_3_5_14
RESULTS = ↓

@TILE_MARK_66_1_14_7
RESULTS = →
@TILE_MARK_66_1_14_9
RESULTS = →

@TILE_MARK_66_2_0_7
RESULTS = ←
@TILE_MARK_66_2_0_9
RESULTS = ←

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_66,ARG
FLAG:現M = 0
FLAG:現X = 12
FLAG:現Y = 13
;CALL AUTOMAP

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_66
CALL MESSAGE_WINDOW_D, "", "＞いよいよ御影町の住人達を町の外へ逃がす時が来た。/＞その為に住人達を聖エルミン学園に集めた、その時だった。"
CALL MESSAGE_WINDOW_D, "？？？", "/　　　　逃　が　さ　な　い"
CALL MESSAGE_WINDOW_D, "", "＞何処からともなくそんな声が響き、/＞世界が急速に変質していく……"
CALL MESSAGE_WINDOW_D, "", "………/……/…"
CALL MESSAGE_WINDOW_D, "", @"＞ここは教室……だろうか？集まっていた筈の人達は誰もいない。/＞……これほどの速度で異界化が発生したところをみると、相当強力な悪魔が原因のようだ。/＞御影町での最後の任務が困難なものになる事を確信し、/＞%CALLNAME:MASTER%は気合を入れなおした。"

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_66,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT
;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_66
SELECTCASE FLAG:現M
	CASE 0
	;１階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;X座標をこんな形でふっとくと、見やすいかもしれません
;				 	 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"000090090000000"
		CALL MAKE_FLOOR_LINE, 1,"000111111100000"
		CALL MAKE_FLOOR_LINE, 2,"010100000111110"
		CALL MAKE_FLOOR_LINE, 3,"012100000000000"
		CALL MAKE_FLOOR_LINE, 4,"010100000000000"
		CALL MAKE_FLOOR_LINE, 5,"000100000000000"
		CALL MAKE_FLOOR_LINE, 6,"010111100000000"
		CALL MAKE_FLOOR_LINE, 7,"012100100000000"
		CALL MAKE_FLOOR_LINE, 8,"010100100000000"
		CALL MAKE_FLOOR_LINE, 9,"000100100000000"
		CALL MAKE_FLOOR_LINE,10,"010100101111110"
		CALL MAKE_FLOOR_LINE,11,"012100001000200"
		CALL MAKE_FLOOR_LINE,12,"010100001001110"
		CALL MAKE_FLOOR_LINE,13,"000111111101110"
		CALL MAKE_FLOOR_LINE,14,"000000000000000"

		;宝箱の設置
		CALL SET_TILE,6, 10, ダンジョンフラグ:66:2 ,0, 13
		
		;イベントの設置(Bマーケット)
		CALL SET_TILE,1,3,ダンジョンフラグ:66:8,0,74
	CASE 1
	;２階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;X座標をこんな形でふっとくと、見やすいかもしれません
;				 	 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"000111111100110"
		CALL MAKE_FLOOR_LINE, 2,"012101000101100"
		CALL MAKE_FLOOR_LINE, 3,"010101000101000"
		CALL MAKE_FLOOR_LINE, 4,"012101010111000"
		CALL MAKE_FLOOR_LINE, 5,"000101110001100"
		CALL MAKE_FLOOR_LINE, 6,"011100010000110"
		CALL MAKE_FLOOR_LINE, 7,"010010000000019"
		CALL MAKE_FLOOR_LINE, 8,"010010000000010"
		CALL MAKE_FLOOR_LINE, 9,"011011000000019"
		CALL MAKE_FLOOR_LINE,10,"001111001110010"
		CALL MAKE_FLOOR_LINE,11,"011001000100000"
		CALL MAKE_FLOOR_LINE,12,"001000001110000"
		CALL MAKE_FLOOR_LINE,13,"001111111010000"
		CALL MAKE_FLOOR_LINE,14,"000090090000000"

		;宝箱の設置
		CALL SET_TILE,1, 3, ダンジョンフラグ:66:3 ,0, 23
		;イベントの設置(病院)
		CALL SET_TILE,13,1,ダンジョンフラグ:66:9,0,84
	CASE 2
	;3階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;X座標をこんな形でふっとくと、見やすいかもしれません
;				 	 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"000000011001110"
		CALL MAKE_FLOOR_LINE, 2,"011110110001110"
		CALL MAKE_FLOOR_LINE, 3,"010011100000200"
		CALL MAKE_FLOOR_LINE, 4,"000001000111110"
		CALL MAKE_FLOOR_LINE, 5,"001111111100000"
		CALL MAKE_FLOOR_LINE, 6,"011000001000000"
		CALL MAKE_FLOOR_LINE, 7,"910000011100000"
		CALL MAKE_FLOOR_LINE, 8,"010011110111110"
		CALL MAKE_FLOOR_LINE, 9,"910010000010100"
		CALL MAKE_FLOOR_LINE,10,"010011000010100"
		CALL MAKE_FLOOR_LINE,11,"010001111110100"
		CALL MAKE_FLOOR_LINE,12,"010000020000200"
		CALL MAKE_FLOOR_LINE,13,"010001111110100"
		CALL MAKE_FLOOR_LINE,14,"000000000000000"

;(ヒーホー君)
		;イベントの設置
		CALL SET_TILE,12,1,ダンジョンフラグ:66:4,0,14
		;イベントの設置(アヤセ陥落)
		CALL SET_TILE,12,13,ダンジョンフラグ:66:5,0,44
		;イベントの設置
		CALL SET_TILE,12,1,ダンジョンフラグ:66:4,1,34

	CASE 3
	;4階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;X座標をこんな形でふっとくと、見やすいかもしれません
;				 	 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"011100121010110"
		CALL MAKE_FLOOR_LINE, 2,"010111100012110"
		CALL MAKE_FLOOR_LINE, 3,"010100001010110"
		CALL MAKE_FLOOR_LINE, 4,"010111101010000"
		CALL MAKE_FLOOR_LINE, 5,"010000101110000"
		CALL MAKE_FLOOR_LINE, 6,"010000101000000"
		CALL MAKE_FLOOR_LINE, 7,"012110111111110"
		CALL MAKE_FLOOR_LINE, 8,"010110000000010"
		CALL MAKE_FLOOR_LINE, 9,"010110000000010"
		CALL MAKE_FLOOR_LINE,10,"010110000000010"
		CALL MAKE_FLOOR_LINE,11,"012110000000010"
		CALL MAKE_FLOOR_LINE,12,"010000000000010"
		CALL MAKE_FLOOR_LINE,13,"011111111001110"
		CALL MAKE_FLOOR_LINE,14,"000909000000000"

		;イベントの設置
		CALL SET_TILE,13,2,ダンジョンフラグ:66:4,1,24


	CASE 4
	;5階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;X座標をこんな形でふっとくと、見やすいかもしれません
;				 	 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"000909000000000"
		CALL MAKE_FLOOR_LINE, 1,"011111100000000"
		CALL MAKE_FLOOR_LINE, 2,"010000000000000"
		CALL MAKE_FLOOR_LINE, 3,"011111100000000"
		CALL MAKE_FLOOR_LINE, 4,"010000110000000"
		CALL MAKE_FLOOR_LINE, 5,"010110100000000"
		CALL MAKE_FLOOR_LINE, 6,"010100000000000"
		CALL MAKE_FLOOR_LINE, 7,"011101000000000"
		CALL MAKE_FLOOR_LINE, 8,"001001001111170"
		CALL MAKE_FLOOR_LINE, 9,"001001101000000"
		CALL MAKE_FLOOR_LINE,10,"001100101011110"
		CALL MAKE_FLOOR_LINE,11,"000101111110010"
		CALL MAKE_FLOOR_LINE,12,"010100010000010"
		CALL MAKE_FLOOR_LINE,13,"011111110111110"
		CALL MAKE_FLOOR_LINE,14,"000000000000000"
		;イベントの設置(エリー陥落)
		CALL SET_TILE,9,13,ダンジョンフラグ:66:6,0,54
	CASE 5
	;6階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;X座標をこんな形でふっとくと、見やすいかもしれません
;				 	 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"000011101111000"
		CALL MAKE_FLOOR_LINE, 2,"000011121001000"
		CALL MAKE_FLOOR_LINE, 3,"000011101001000"
		CALL MAKE_FLOOR_LINE, 4,"000000001000000"
		CALL MAKE_FLOOR_LINE, 5,"000011101000000"
		CALL MAKE_FLOOR_LINE, 6,"000011121000000"
		CALL MAKE_FLOOR_LINE, 7,"000011101000000"
		CALL MAKE_FLOOR_LINE, 8,"000000001011160"
		CALL MAKE_FLOOR_LINE, 9,"011111111010000"
		CALL MAKE_FLOOR_LINE,10,"010000001010000"
		CALL MAKE_FLOOR_LINE,11,"010111101110000"
		CALL MAKE_FLOOR_LINE,12,"012111100000000"
		CALL MAKE_FLOOR_LINE,13,"010111100000000"
		CALL MAKE_FLOOR_LINE,14,"000000000000000"
		;イベントの設置(ゆきの陥落)
		CALL SET_TILE,4,2,ダンジョンフラグ:66:7,0,64
		;イベントの設置(BOSS)
		CALL SET_TILE,5,12,ダンジョンフラグ:66:10,0,94
	CASEELSE

ENDSELECT



;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_66

;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
;% 10は10で割ったあまり、つまり下一桁がでてきます
;そして、下一桁はタイルの種類を表す
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_66
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 6
	CALL UPSTAIRS_66
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 7
	CALL DOWNSTAIRS_66
	RETURN RESULT
ELSE
	PRINTW Nothing seems to be here.
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_66,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) % 10 == 8
	CALL DUNGEON_EXIT
	RETURN 1
;==========
;マップ遷移の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) == 9
	SELECTCASE FLAG:現M
		CASE 0
			IF FLAG:現Y == 0
				FLAG:現M = 1
				FLAG:現Y = 14
			ENDIF
		CASE 1
			IF FLAG:現Y == 14
				FLAG:現M = 0
				FLAG:現Y = 0
			ELSE
				FLAG:現M = 2
				FLAG:現X = 0
			ENDIF
		CASE 2
			IF FLAG:現X == 0
				FLAG:現M = 1
				FLAG:現X = 14
			ENDIF
		CASE 3
			IF FLAG:現Y == 14
				FLAG:現M = 4
				FLAG:現Y = 0
			ENDIF
		CASE 4
			IF FLAG:現Y == 0
				FLAG:現M = 3
				FLAG:現Y = 14
			ENDIF

	ENDSELECT
	CALL MAKE_FLOOR, FLAG:現ダンジョン
	CALL AUTOMAP
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_66
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	CALL UPSTAIRS_66
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL DOWNSTAIRS_66
	RETURN RESULT

;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1


;========================
;イベント
;========================
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4

	SELECTCASE DA:(FLAG:現X):(FLAG:現Y) /10
		CASE 1
			CUSTOMDRAWLINE =
			CALL MESSAGE_WINDOW_D, "", "＞そこには、2体のジャックフロストがいた/＞片方は、学生服を着込んでおり、School Capまで被っている。/＞もう片方はなんとセーラー服だ……。"
			CALL MESSAGE_WINDOW_D, "ジャックフロスト？", "ん、キミたちは学生には見えないホ？/学生でもないのに、学校になんの用だホ？"
			CALL MESSAGE_WINDOW_D, "", "＞……事情を説明すると、/＞ジャックフロストは納得したように大きくうなずいた。"
			CALL MESSAGE_WINDOW_D, "ジャックフロスト？", "なるホど〜/だから、折角学校に来たのに学生が誰もいなかったんだホ/これじゃ、学校で友達作る予定が台無しホ/…でも解決してくれるなら、協力するホ"
			CALL MESSAGE_WINDOW_D, "ジャックフロスト？", "着いてくるホ"
			CALL MESSAGE_WINDOW_D, "", @"＞ヒーホー君と名乗るジャックフロストが言うには、/＞この先に進むための隠し通路は開通と維持に特殊な魔力が必要らしく、/＞%CALLNAME:MASTER%や、%CALLNAME:MASTER%の仲魔には現状不可能らしい。"
			CALL MESSAGE_WINDOW_D, "ヒーホー君", "じゃあ、おいらは元のフロアに戻るホ/ここにはマイハニーのヒホ子ちゃんが残ってくれるから、/戻りたい時は言ってくれホ"
			;ゆきのがパーティにいて、陥落している
			IF FINDCHARA_B([[キャラ:ゆきのさん]])== 2 && 陥落(RESULT:1) && ABL:(RESULT:1):初期ペルソナ != [[キャラ:ドゥルガー(ペルソナ)]]
				CALL MESSAGE_WINDOW_D, "ヒーホー君", "……あ、そうだ。/さっき、こんなの拾ったホ/たぶん、キミ達が持ってる方が役に立つ気がするホ"
				CALL MESSAGE_WINDOW_D, "", "＞ヒーホー君は思い出したようにそう言って、/＞ポケットから取り出した何かを押しつけた。"
				;ゆきの初期ペルソナバージョンアップ
				CALL MESSAGE_WINDOW_D, "", "＞ゆきのが受け取ったのは野牛の角の飾り物の様に見える。/＞直後、ゆきのの背後から彼女のペルソナ「ヴェスタ」が出現する。/＞角は燃えるように光を放ち、ヴェスタに吸い込まれるように消えた。"
				CALL MESSAGE_WINDOW_D, "", "＞ゆきのの初期ペルソナが成長した！"
				ABL:(RESULT:1):初期ペルソナ = [[キャラ:ドゥルガー(ペルソナ)]]
				CALL SYNC_STATUS, RESULT:1
			ENDIF
			IF TALENT:MASTER:140
				CALL MESSAGE_WINDOW_D, "ヒーホー君", "……ただし、ヒホ子ちゃんが幾ら可愛いからって手を出したら許さないホ"
			ELSE
				CALL MESSAGE_WINDOW_D, "ヒーホー君", "ただし、幾ら頼りになるからって、オイラに惚れちゃダメだホ"
			ENDIF
			CALL MESSAGE_WINDOW_D, "", "＞そう言って、ヒーホー君は上機嫌で戻っていった。"
			FLAG:現M = 3
			FLAG:現X = 13
			FLAG:現Y = 2
			ダンジョンフラグ:66:4 = 1
			CALL MAKE_FLOOR, FLAG:現ダンジョン
			CALL AUTOMAP
			RETURN 1
		CASE 2
			CUSTOMDRAWLINE =
			CALL MESSAGE_WINDOW_D, "", "＞そこには、セーラー服を無理矢理纏ったジャックフロストがいた。"
			CALL MESSAGE_WINDOW_D, "ヒホ子ちゃん", "向こうに戻るホ？/ならついて来るホ！"
			FLAG:現M = 2
			FLAG:現X = 12
			FLAG:現Y = 1
			CALL MAKE_FLOOR, FLAG:現ダンジョン
			CALL AUTOMAP
		CASE 3
			CUSTOMDRAWLINE =
			CALL MESSAGE_WINDOW_D, "", "＞そこには、学生服を纏ったジャックフロストがいた。"
			CALL MESSAGE_WINDOW_D, "ヒーホー君", "先に進むホ？/ならついて来るホ！"
			;ゆきのがパーティにいて、陥落している
			IF FINDCHARA_B([[キャラ:ゆきのさん]])== 2 && 陥落(RESULT:1) && ABL:(RESULT:1):初期ペルソナ != [[キャラ:ドゥルガー(ペルソナ)]]
				CALL MESSAGE_WINDOW_D, "ヒーホー君", "……あ、そうだ。/さっき、こんなの拾ったホ/たぶん、キミ達が持ってる方が役に立つ気がするホ"
				CALL MESSAGE_WINDOW_D, "", "＞ヒーホー君は思い出したようにそう言って、/＞ポケットから取り出した何かを押しつけた。"
				;ゆきの初期ペルソナバージョンアップ
				CALL MESSAGE_WINDOW_D, "", "＞ゆきのが受け取ったのは野牛の角の飾り物の様に見える。/＞直後、ゆきのの背後から彼女のペルソナ「ヴェスタ」が出現する。/＞角は燃えるように光を放ち、ヴェスタに吸い込まれるように消えた。"
				CALL MESSAGE_WINDOW_D, "", "＞ゆきのの初期ペルソナが成長した！"
				ABL:(RESULT:1):初期ペルソナ = [[キャラ:ドゥルガー(ペルソナ)]]
				CALL SYNC_STATUS, RESULT:1
			ENDIF
			FLAG:現M = 3
			FLAG:現X = 13
			FLAG:現Y = 2
			CALL MAKE_FLOOR, FLAG:現ダンジョン
			CALL AUTOMAP
		CASE 4
;アヤセがパーティにいて、陥落している
			IF FINDCHARA_B([[キャラ:アヤセ]])== 2 && 陥落(RESULT:1) && ダンジョンフラグ:66:5 == 0
				CALL MESSAGE_WINDOW_D, "アヤセ", @"あれ？//ねぇねぇ、%CALLNAME:MASTER%ー？"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%が振り向くと、/＞廊下の隅にしゃがみこんだアヤセが何かを手に持っていた。/＞……割れた鏡の破片の様だ。"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%が捨てるように指示するより早く、アヤセの手の中でそれが発光し始めた。/＞……光が収まった時、アヤセの手の中には一枚の仮面があった。/＞その表面は鏡のようだが、不思議な事にこれを被っても視界が遮られる事はない。/＞アヤセはしばらく様々な角度から眺めた後、"
				CALL MESSAGE_WINDOW_D, "アヤセ", "ん〜、なんだか分からないけど、/役に立つ？のかな？"
				CALL MESSAGE_WINDOW_D, "", @"＞首をかしげながら、仮面を%CALLNAME:MASTER%に手渡した。"
				CALL MESSAGE_WINDOW_D, "", "＞Spiegel Maskを手に入れた。"
				ITEM:"Spiegel Mask" += 1
				ダンジョンフラグ:66:5 = 1
			ELSE
				CALL MESSAGE_WINDOW_D, "", "＞そこには、何もない。"
			ENDIF
		CASE 5
;エリーがパーティにいて、陥落している
			IF FINDCHARA_B([[キャラ:エリー]])== 2 && 陥落(RESULT:1) && ダンジョンフラグ:66:6 == 0
				CALL MESSAGE_WINDOW_D, "エリー", "あら？/これは……mirror？"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%が振り向くと、/＞エリーが足元に転がっていた何かを拾い上げた。/＞……割れた鏡の破片の様だ。"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%が放っておくように指示するより早く、エリーの手の中でそれが発光し始めた。/＞……光が収まった時、エリーの手の中には一振りの剣があった。/＞まるでガラスを打ち欠いて作ったようにも見えるが、ガラスのように脆くはない様だ。/＞エリーは確かめるように素振りをし、"
				CALL MESSAGE_WINDOW_D, "エリー", "……判断はお任せしますけど、実用性はありそうですわ"
				CALL MESSAGE_WINDOW_D, "", @"＞そう言って、剣を%CALLNAME:MASTER%に手渡した。"
				CALL MESSAGE_WINDOW_D, "", "＞Spiegel Bladeを手に入れた。"
				ITEM:"Spiegel Blade" += 1
				ダンジョンフラグ:66:6 = 1
			ELSE
				CALL MESSAGE_WINDOW_D, "", "＞そこには、何もない。"
			ENDIF
		CASE 6
;ゆきのがパーティにいて、陥落している
			IF FINDCHARA_B([[キャラ:ゆきのさん]])== 2 && 陥落(RESULT:1) && ダンジョンフラグ:66:7 == 0
				CALL MESSAGE_WINDOW_D, "ゆきの", "ん、……なんだいこれは？"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%が振り向くと、/＞ゆきのが窓枠にあった何かを手に取るところだった。/＞……割れた鏡の破片の様だ。"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%が放っておくように指示するより早く、ゆきのの手の中でそれが発光し始めた。/＞……光が収まった時、ゆきのの手の中には一着の鎧があった。/＞磨かれた表面は、まるで鏡のようだ。/＞ゆきのは何度か手でコンコンと叩くと、"
				CALL MESSAGE_WINDOW_D, "ゆきの", "……使えるかな、これ？"
				CALL MESSAGE_WINDOW_D, "", @"＞ゆきのは、鎧を%CALLNAME:MASTER%に手渡した。"
				CALL MESSAGE_WINDOW_D, "", "＞Spiegel Mailを手に入れた。"
				ITEM:"Spiegel Mail" += 1
				ダンジョンフラグ:66:7 = 1
			ELSE
				CALL MESSAGE_WINDOW_D, "", "＞そこには、何もない。"
			ENDIF
		CASE 7
;救出依頼2をクリアしてる
			IF 依頼フラグ:16:0 >= 1 && ダンジョンフラグ:66:8 == 0
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%の視界の隅で何かが光った。"
				CALL MESSAGE_WINDOW_D, "", @"＞……拾い上げて見ると割れた鏡の破片の様だ。/＞%CALLNAME:MASTER%が捨てようとすると、手の中でそれが発光し始めた。/＞……光が収まった時、手の中には鏡の破片の代わりに籠手があった。/＞磨かれた表面は、まるで鏡のようだ。"
				CALL MESSAGE_WINDOW_D, "", "＞Spiegel Shieldを手に入れた。"
				ITEM:"Spiegel Shield" += 1
				ダンジョンフラグ:66:8 = 1
			ELSE
				CALL MESSAGE_WINDOW_D, "", "＞そこには、何もない。"
			ENDIF
		CASE 8
;救出依頼3をクリアしてる
			IF 依頼フラグ:16:0 == 2 && ダンジョンフラグ:66:9 == 0
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%の視界の隅で何かが光った。"
				CALL MESSAGE_WINDOW_D, "", @"＞……拾い上げて見ると割れた鏡の破片の様だ。/＞%CALLNAME:MASTER%が捨てようとすると、手の中でそれが発光し始めた。/＞……光が収まった時、手の中には鏡の破片の代わりに靴があった。/＞磨かれた表面は、まるで鏡のようだ。"
				CALL MESSAGE_WINDOW_D, "", "＞Spiegel Backを手に入れた。"
				ITEM:"Spiegel Back" += 1
				ダンジョンフラグ:66:9 = 1
			ELSE
				CALL MESSAGE_WINDOW_D, "", "＞そこには、何もない。"
			ENDIF
		CASE 9
;BOSS　アスラ女王
			CALL MESSAGE_WINDOW_D, "", "＞ここが異界の最奥のようだ。/＞そこには、三つの顔を持つ巨大な女が待ち構えていた。/＞相対しているだけで感じる重圧は、/＞目の前の存在が異界化の元凶である事を確信するに足るものだった。"
			CALL MESSAGE_WINDOW_D, "？？？", "……どうして？/ここは私の世界なのに……/私にはここ以外何もないのに……/ここで位好きなようにさせてくれても良いじゃない！"
			CALL MESSAGE_WINDOW_D, "", "＞こいつにどんな事情があるのかは知らないが、/＞こいつのせいでここに閉じ込められているという事と、/＞こいつをどうにかしなければ、/＞多くの非戦闘員を脱出させるなど不可能だと言う事だけは確かだ。"
			CALL MESSAGE_WINDOW_D, "", "/　　　魔王 アシュラ女王が１体出た！"
			CUSTOMDRAWLINE =
			WAIT
			FLAG:逃走不可フラグ = 1
			FLAG:会話不能フラグ = 1
			FLAG:先制不意打ちキャンセル = 1
			CALL SET_ENEMY,9,[[キャラ:アスラ女王]], 70, 0, 1
			NAME:(CHARANUM-1) = アシュラ女王
			CALLNAME:(CHARANUM-1) = アシュラ女王
			CFLAG:(CHARANUM-1):ＨＰ補正 = 3000
			CALL SYNC_STATUS,(CHARANUM-1)
			CALL HEALTH_CHARA,(CHARANUM-1)
			CFLAG:(CHARANUM-1):行動回数 = 2
			CSTR:(CHARANUM-1):思考パターン = 222
			CALL BATTLE_START
			FLAG:逃走不可フラグ = 0
			FLAG:会話不能フラグ = 0
			FLAG:先制不意打ちキャンセル = 0
			CALL MESSAGE_WINDOW_D, "アシュラ女王", "……どうし…て……"
			CALL MESSAGE_WINDOW_D, "", "＞絞り出すように言葉を吐き、アシュラ女王は崩れ落ち消えていった。/＞そして同時に周りの異界化が解けていく……/＞これで、これ以上の妨害は入らないだろう。"
		CALL MESSAGE_WINDOW_D, "", "…………/………/……/…"
		CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%'s Office"
		;マヨーネＧＥＴ済み
			IF イベントフラグ:1:0 == 3
				CALL MESSAGE_WINDOW_D, "幹部", "報告は見せて貰った。//……実に良い仕事をしてくれた。"
				CALL MESSAGE_WINDOW_D, "幹部", "君が確保してくれたDEVAシステムは、/新たに専用の部署を立ち上げ研究する事が決定した。/また、君が救出した住人の中には/うちに有形無形の協力をしてくれる者も出てきそうだ。"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%の報告書を受け取ったファントム・ソサエティの幹部は、/＞満足気にそう言ったあと電話を切った。"
			ELSE
				CALL MESSAGE_WINDOW_D, "Mayone", "DEVAシステムは貴重なサンプルとして、こちらで確保させて頂きました。//……素晴らしい成果を上げましたわね。"
				CALL MESSAGE_WINDOW_D, "Mayone", "既に上では専用の部署を立ち上げる方向で話が進んでいるそうですわ。/多くの住人を救出した事も評価に値しますわね。/中には金や権力を持つ者もおりますし。"
				CALL MESSAGE_WINDOW_D, "", @"＞%CALLNAME:MASTER%の報告書を受け取ったマヨーネは、/＞満足気にそう言ったあと電話を切った。"
			ENDIF
			CALL MESSAGE_WINDOW_D, "", @"＞今回の件で組織内での%CALLNAME:MASTER%の立場はかなり良いものとなるだろう。/＞今後の立ち回りを考えつつ、%CALLNAME:MASTER%はたまった書類に目を通し始めた……"
	;地下鉄侵入禁止
			ダンジョンフラグ:11:7 = 1
;			CALL MESSAGE_WINDOW_D, "", "/―――女神異聞録ペルソナ アナザーエンド―――"
			ダンジョンフラグ:66:1 = 1
;
		IF CHECK_TIMELIMIT(1,DAY < 51)
			CALL MESSAGE_WINDOW_D, "", "/Ending No.029 (女神異聞録ペルソナ　雪の女王編エンド　be your another Mind)"
			FLAG:5 = 9
			SETBIT FLAG:キャンセル済みED,29
			SETBIT FLAG:発見ED,29
			FLAG:脱出 = 1
			PRINTFORML [0]End [1]Continue [2]Start new-game plus
			;[0]終わる [1]続ける [2]一部のデータを引き継いで、新しく始める
			CALL INPUTINT,0, 1, 2
			IF RESULT == 0
				QUIT
			ELSEIF RESULT == 1
				CALL REQUEST_50_DAY_END
			ELSEIF RESULT == 2
				CALL START_NEWGAME
				RETURN 1
			ENDIF
		ENDIF
			CALL MESSAGE_WINDOW_D, "", "＞この件の功績により、/＞￥1000000　組織貢献度500000　名声600/＞を得ました。"
			MONEY += 1000000
			FLAG:組織貢献度 += 500000
			FLAG:名声 += 600
			FLAG:脱出 = 1
	ENDSELECT
ENDIF
RETURN 0

;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_66
FLAG:エンカウント率 = FLAG:未遭遇歩数*5
SIF FLAG:未遭遇歩数 < 5
	FLAG:エンカウント率 = 0

;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_66
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_66
RETURN CLASS_NUM_COMMON("最上級")
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;=======================================================================
@ENEMY_NUM_66, ARG
RETURN ENEMY_NUM_COMMON("最上級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_66, ARG
SELECTCASE RAND:100
	CASE IS < 9
		RETURN 1020, 55
	CASE IS < 18
		RETURN 1021, 56
	CASE IS < 27
		RETURN 1022, 57
	CASE IS < 36
		RETURN 1023, 58
	CASE IS < 45
		RETURN 1024, 59
	CASE IS < 54
		RETURN 1025, 60
	CASE IS < 63
		RETURN 356, 56
	CASE IS < 72
		RETURN 706, 55
	CASE IS < 81
		RETURN 656, 56
	CASE IS < 90
		;9%　邪龍ムシュフシュLV48(55) 
		RETURN [[キャラ:ムシュフシュ]], 55
	CASEELSE
		RETURN 408, 56
ENDSELECT
	


;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_66
;/10はIDを10で割る。つまり、下一桁をなくして、二桁目と三桁目を二桁の数字として取り出している
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
			;IDを示しています。つまり、これはIDが1のとき、2の時で分岐
			CASE 1
				;GET_TREASURE,アイテム番号、入手個数、ダンジョンナンバー、あけたときONにするフラグの番号
				;1001は傷薬で、それを1個、ダンジョンナンバーは宝箱あけるときはよそのダンジョンをあけることがないだろうのでFLAG:現ダンジョンが無難です
				;そして、フラグはダンジョンフラグ:1(エコービル):0を1にするということです
				;MAPをつくるときにこの宝箱はダンジョンフラグ:1(エコービル):0が0のときしか置かないことになってるのでこれで消えます
				CALL GET_TREASURE,[[アイテム:ChakraPot]],1,FLAG:現ダンジョン,2
			CASE 2
				CALL GET_TREASURE,[[アイテム:BeadChain]],1,FLAG:現ダンジョン,3
ENDSELECT
RETURN 1

;=======================================================================
;階段置き場
;=======================================================================
@UPSTAIRS_66
SELECTCASE FLAG:現M
	CASE 5
		CALL DUNGEON_UPSTAIRS,4,FLAG:現X,FLAG:現Y
	CASEELSE
ENDSELECT
RETURN 1

@DOWNSTAIRS_66
SELECTCASE FLAG:現M
	CASE 4
		CALL DUNGEON_UPSTAIRS,5,FLAG:現X,FLAG:現Y
	CASEELSE
ENDSELECT
RETURN 1


;女王思考パターン
@SET_ACTION_222,ARG
IF RAND:3 == 0
	LOCAL = 0
ELSEIF RAND:2 == 0
	LOCAL = 1
ELSE
	LOCAL = 2
ENDIF

IF LOCAL == 0 && CFLAG:ARG:行動した回数 == 0
	MAXBASE:ARG:40 = 100
	MAXBASE:ARG:41 = 100
	MAXBASE:ARG:42 = 100
	MAXBASE:ARG:43 = 100
	MAXBASE:ARG:44 = 100
	MAXBASE:ARG:45 = 100
	MAXBASE:ARG:46 = 100
	MAXBASE:ARG:47 = 100
	MAXBASE:ARG:52 = 100
	MAXBASE:ARG:53 = 100
	MAXBASE:ARG:54 = 100
	MAXBASE:ARG:55 = 100
	MAXBASE:ARG:56 = 100
ELSEIF LOCAL == 1 && CFLAG:ARG:行動した回数 == 0
	IF RAND:4 == 0
		MAXBASE:ARG:40 = 0
	ELSE
		MAXBASE:ARG:40 = 50
		IF RAND:3 == 0
			MAXBASE:ARG:41 = 0
		ELSE
			MAXBASE:ARG:41 = 50
			IF RAND:2 == 0
				MAXBASE:ARG:42 = 0
				MAXBASE:ARG:43 = 50
			ELSE
				MAXBASE:ARG:42 = 50
				MAXBASE:ARG:43 = 0
			ENDIF
		ENDIF
	ENDIF
	IF RAND:4 == 0
		MAXBASE:ARG:44 = 150
		MAXBASE:ARG:45 = 150
		MAXBASE:ARG:46 = 100
		MAXBASE:ARG:47 = 100
		MAXBASE:ARG:52 = 100
		MAXBASE:ARG:53 = 100
		MAXBASE:ARG:54 = 100
		MAXBASE:ARG:55 = 100
		MAXBASE:ARG:56 = 100
	ELSEIF RAND:3 == 0
		MAXBASE:ARG:44 = 100
		MAXBASE:ARG:45 = 100
		MAXBASE:ARG:46 = 150
		MAXBASE:ARG:47 = 150
		MAXBASE:ARG:52 = 100
		MAXBASE:ARG:53 = 100
		MAXBASE:ARG:54 = 100
		MAXBASE:ARG:55 = 100
		MAXBASE:ARG:56 = 100
	ELSEIF RAND:2 == 0
		MAXBASE:ARG:44 = 100
		MAXBASE:ARG:45 = 100
		MAXBASE:ARG:46 = 100
		MAXBASE:ARG:47 = 100
		MAXBASE:ARG:52 = 150
		MAXBASE:ARG:53 = 100
		MAXBASE:ARG:54 = 150
		MAXBASE:ARG:55 = 100
		MAXBASE:ARG:56 = 100
	ELSE
		MAXBASE:ARG:44 = 100
		MAXBASE:ARG:45 = 100
		MAXBASE:ARG:46 = 100
		MAXBASE:ARG:47 = 100
		MAXBASE:ARG:52 = 100
		MAXBASE:ARG:53 = 150
		MAXBASE:ARG:54 = 100
		MAXBASE:ARG:55 = 100
		MAXBASE:ARG:56 = 150
	ENDIF
ELSEIF LOCAL == 2 && CFLAG:ARG:行動した回数 == 0
	IF RAND:4 == 0
		MAXBASE:ARG:40 = 150
		MAXBASE:ARG:41 = 100
		MAXBASE:ARG:42 = 120
		MAXBASE:ARG:43 = 100
	ELSEIF RAND:3 == 0
		MAXBASE:ARG:40 = 120
		MAXBASE:ARG:41 = 150
		MAXBASE:ARG:42 = 100
		MAXBASE:ARG:43 = 100
	ELSEIF RAND:2 == 0
		MAXBASE:ARG:40 = 100
		MAXBASE:ARG:41 = 100
		MAXBASE:ARG:42 = 150
		MAXBASE:ARG:43 = 120
	ELSE
		MAXBASE:ARG:40 = 100
		MAXBASE:ARG:41 = 120
		MAXBASE:ARG:42 = 100
		MAXBASE:ARG:43 = 150
	ENDIF
	IF RAND:4 == 0
		MAXBASE:ARG:44 = 0
		MAXBASE:ARG:45 = 0
		MAXBASE:ARG:46 = 50
		MAXBASE:ARG:47 = 50
		MAXBASE:ARG:52 = 50
		MAXBASE:ARG:53 = 50
		MAXBASE:ARG:54 = 50
		MAXBASE:ARG:55 = 50
		MAXBASE:ARG:56 = 50
	ELSEIF RAND:3 == 0
		MAXBASE:ARG:44 = 50
		MAXBASE:ARG:45 = 50
		MAXBASE:ARG:46 = 0
		MAXBASE:ARG:47 = 0
		MAXBASE:ARG:52 = 50
		MAXBASE:ARG:53 = 50
		MAXBASE:ARG:54 = 50
		MAXBASE:ARG:55 = 50
		MAXBASE:ARG:56 = 50
	ELSEIF RAND:2 == 0
		MAXBASE:ARG:44 = 50
		MAXBASE:ARG:45 = 50
		MAXBASE:ARG:46 = 50
		MAXBASE:ARG:47 = 50
		MAXBASE:ARG:52 = 0
		MAXBASE:ARG:53 = 50
		MAXBASE:ARG:54 = 0
		MAXBASE:ARG:55 = 50
		MAXBASE:ARG:56 = 50
	ELSE
		MAXBASE:ARG:44 = 50
		MAXBASE:ARG:45 = 50
		MAXBASE:ARG:46 = 50
		MAXBASE:ARG:47 = 50
		MAXBASE:ARG:52 = 50
		MAXBASE:ARG:53 = 0
		MAXBASE:ARG:54 = 50
		MAXBASE:ARG:55 = 50
		MAXBASE:ARG:56 = 0
	ENDIF
ENDIF
IF CFLAG:ARG:行動した回数 == 0
	IF CFLAG:ARG:攻撃強化 < -4 || CFLAG:ARG:命中強化 < -4 || CFLAG:ARG:回避強化 < -4 || CFLAG:ARG:魔法威力強化 < -4
		CFLAG:ARG:入力行動 = [[スキル:デ・クンダ]]
	ELSEIF RAND:2 == 0
		CFLAG:ARG:入力行動 = [[スキル:ランダマイザ]]
	ELSE
		CFLAG:ARG:入力行動 = 0
	ENDIF
ELSEIF CFLAG:ARG:行動した回数 == 1
	IF RAND:4 == 0
		CFLAG:ARG:入力行動 = [[スキル:ファイアストーム]]
	ELSEIF RAND:3 == 0
		CFLAG:ARG:入力行動 = [[スキル:雷の洗礼]]
	ELSEIF RAND:2 == 0
		CFLAG:ARG:入力行動 = [[スキル:絶対零度]]
	ELSE
		CFLAG:ARG:入力行動 = [[スキル:旋風陣]]
	ENDIF
ELSE
	IF RAND:3 == 0
		CFLAG:ARG:入力行動 = [[スキル:利剣乱舞]]
	ELSEIF RAND:2 == 0
		CFLAG:ARG:入力行動 = [[スキル:アカシャアーツ]]
	ELSE
		CFLAG:ARG:入力行動 = 0
	ENDIF
ENDIF
CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動

;アクション実行不能なら自動的に通常攻撃にする
CALL CHECK_ACTIONABLE, ARG, CFLAG:ARG:入力行動
IF RESULT == 0
	CFLAG:ARG:入力行動 = 0
	CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動
ENDIF


;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_66
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:3
	CASE 0
		RETURN [[キャラ:インキュバス]] , 3 + RAND:10 , CSVBASE([[キャラ:インキュバス]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:ドミニオン]] , 3 + RAND:10 , CSVBASE([[キャラ:ドミニオン]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:ベルセルク]] , 3 + RAND:10 , CSVBASE([[キャラ:ベルセルク]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT




