
;=======================================================================
;熱気立つ大浴場
;ボス・完二影
;ダンジョンナンバー73　ＭＡＰ0~6
;フラグ
;ダンジョンフラグ:73:0から6　 宝箱
;ダンジョンフラグ:73:7　　　　中ボス攻略
;ダンジョンフラグ:73:8　　　　ボス攻略
;ダンジョンフラグ:73:9　　　　スタート時のイベント
;=======================================================================




;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_73
;スタジオでまた依頼を受ける事
IF イベントフラグ:12:0 > 1
	RETURN 1
ENDIF

;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_73
RESULTS = Steamy Bathhouse


;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_73
PRINT Steamy Bathhouse 
SELECTCASE FLAG:現M
	CASE 0
		PRINTL   Hall １
	CASE 1
		PRINTL   Hall ２
	CASE 2
		PRINTL   Hall ３
	CASE 3
		PRINTL   Hall ４
	CASE 4
		PRINTL   Hall ５
	CASE 5
		PRINTL   Hall ６
	CASE 6
		PRINTL   Hall ７
	CASEELSE
		PRINTL 
ENDSELECT


;=======================================================================
;特殊タイル表示
;=======================================================================

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_73,ARG
FLAG:現M = 0
FLAG:現X = 18
FLAG:現Y = 10
;CALL AUTOMAP

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_73

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_73,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT
;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_73
SELECTCASE FLAG:現M
	CASE 0
	;一階
		FLAG:最大X = 20
		FLAG:最大Y = 14
;								 01234567890123456789
		CALL MAKE_FLOOR_LINE, 0,"                    "
		CALL MAKE_FLOOR_LINE, 1," 1111               "
		CALL MAKE_FLOOR_LINE, 2," 1  11111111111     "
		CALL MAKE_FLOOR_LINE, 3,"       2      11211 "
		CALL MAKE_FLOOR_LINE, 4,"      11         11 "
		CALL MAKE_FLOOR_LINE, 5,"    1216          2 "
		CALL MAKE_FLOOR_LINE, 6,"    1             1 "
		CALL MAKE_FLOOR_LINE, 7," 111111111     1121 "
		CALL MAKE_FLOOR_LINE, 8,"         2        1 "
		CALL MAKE_FLOOR_LINE, 9,"         11   1   1 "
		CALL MAKE_FLOOR_LINE,10,"         112121   1 "
		CALL MAKE_FLOOR_LINE,11,"              1   8 "
		CALL MAKE_FLOOR_LINE,12,"              1     "
		CALL MAKE_FLOOR_LINE,13,"                    "
		
		CALL SET_TILE,14, 12, ダンジョンフラグ:73:0,0,13
		
		DA:7:5 += 10
		
	CASE 1
	;２階
		FLAG:最大X = 15
		FLAG:最大Y = 20
;								 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," 11 11         "
		CALL MAKE_FLOOR_LINE, 2," 1   1         "
		CALL MAKE_FLOOR_LINE, 3," 1   1211111   "
		CALL MAKE_FLOOR_LINE, 4," 11111     1   "
		CALL MAKE_FLOOR_LINE, 5,"           121 "
		CALL MAKE_FLOOR_LINE, 6," 11111111111 1 "
		CALL MAKE_FLOOR_LINE, 7,"     2       1 "
		CALL MAKE_FLOOR_LINE, 8,"    11  1    1 "
		CALL MAKE_FLOOR_LINE, 9,"    1   1    2 "
		CALL MAKE_FLOOR_LINE,10,"    1   1   61 "
		CALL MAKE_FLOOR_LINE,11,"    1211111211 "
		CALL MAKE_FLOOR_LINE,12,"    1   1      "
		CALL MAKE_FLOOR_LINE,13,"    1          "
		CALL MAKE_FLOOR_LINE,14,"    1  11 1    "
		CALL MAKE_FLOOR_LINE,15,"    1111121    "
		CALL MAKE_FLOOR_LINE,16,"       11 1 7  "
		CALL MAKE_FLOOR_LINE,17,"          1 1  "
		CALL MAKE_FLOOR_LINE,18,"          111  "
		CALL MAKE_FLOOR_LINE,19,"               "
		
		CALL SET_TILE,2, 1, ダンジョンフラグ:73:1,0, 23

		DA:12:16 += 10
		DA:12:10 += 20

	CASE 2
	;３階
		FLAG:最大X = 13
		FLAG:最大Y = 20
;								 0123456789012
		CALL MAKE_FLOOR_LINE, 0,"             "
		CALL MAKE_FLOOR_LINE, 1,"        1211 "
		CALL MAKE_FLOOR_LINE, 2,"      1   11 "
		CALL MAKE_FLOOR_LINE, 3,"      1    2 "
		CALL MAKE_FLOOR_LINE, 4,"      1211 1 "
		CALL MAKE_FLOOR_LINE, 5,"      2    1 "
		CALL MAKE_FLOOR_LINE, 6,"      16   1 "
		CALL MAKE_FLOOR_LINE, 7,"      112121 "
		CALL MAKE_FLOOR_LINE, 8,"           1 "
		CALL MAKE_FLOOR_LINE, 9,"           1 "
		CALL MAKE_FLOOR_LINE,10,"           1 "
		CALL MAKE_FLOOR_LINE,11,"           2 "
		CALL MAKE_FLOOR_LINE,12,"          11 "
		CALL MAKE_FLOOR_LINE,13,"        1211 "
		CALL MAKE_FLOOR_LINE,14,"        2    "
		CALL MAKE_FLOOR_LINE,15," 112111111   "
		CALL MAKE_FLOOR_LINE,16," 11          "
		CALL MAKE_FLOOR_LINE,17," 2           "
		CALL MAKE_FLOOR_LINE,18," 1           "
		CALL MAKE_FLOOR_LINE,19," 7           "
		
		CALL SET_TILE,6,2,ダンジョンフラグ:73:2,0,33

		DA:1:19 += 20
		DA:7:6 += 30

	CASE 3
	;４階
		FLAG:最大X = 13
		FLAG:最大Y = 20
;								 0123456789012
		CALL MAKE_FLOOR_LINE, 0,"             "
		CALL MAKE_FLOOR_LINE, 1,"  111111111  "
		CALL MAKE_FLOOR_LINE, 2,"  1   1   1  "
		CALL MAKE_FLOOR_LINE, 3,"  1   7   1  "
		CALL MAKE_FLOOR_LINE, 4,"  1   T   1  "
		CALL MAKE_FLOOR_LINE, 5,"  1       1  "
		CALL MAKE_FLOOR_LINE, 6,"  111111111  "
		CALL MAKE_FLOOR_LINE, 7,"      2      "
		CALL MAKE_FLOOR_LINE, 8,"     111     "
		CALL MAKE_FLOOR_LINE, 9,"     111     "
		CALL MAKE_FLOOR_LINE,10,"      1      "
		CALL MAKE_FLOOR_LINE,11,"      2      "
		CALL MAKE_FLOOR_LINE,12," 11111111111 "
		CALL MAKE_FLOOR_LINE,13," 1           "
		CALL MAKE_FLOOR_LINE,14," 1    1   11 "
		CALL MAKE_FLOOR_LINE,15," 1211111   1 "
		CALL MAKE_FLOOR_LINE,16," 1    1    2 "
		CALL MAKE_FLOOR_LINE,17," 1        61 "
		CALL MAKE_FLOOR_LINE,18," 11111111211 "
		CALL MAKE_FLOOR_LINE,19,"             "
		
		CALL SET_TILE,6,8,ダンジョンフラグ:73:7,0,14
		CALL SET_TILE,7,15,ダンジョンフラグ:73:3,0,43
		
		DA:6:3 += 30
		DA:10:17 += 40

	CASE 4
	;5階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;								 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," 112111        "
		CALL MAKE_FLOOR_LINE, 2," 11   111216 1 "
		CALL MAKE_FLOOR_LINE, 3," 2        11 1 "
		CALL MAKE_FLOOR_LINE, 4," 1         2 1 "
		CALL MAKE_FLOOR_LINE, 5," 1         121 "
		CALL MAKE_FLOOR_LINE, 6," 1           1 "
		CALL MAKE_FLOOR_LINE, 7," 1           1 "
		CALL MAKE_FLOOR_LINE, 8," 1           1 "
		CALL MAKE_FLOOR_LINE, 9," 1211        1 "
		CALL MAKE_FLOOR_LINE,10," 1  111      1 "
		CALL MAKE_FLOOR_LINE,11," 2    1      2 "
		CALL MAKE_FLOOR_LINE,12," 11         11 "
		CALL MAKE_FLOOR_LINE,13," 11217   11211 "
		CALL MAKE_FLOOR_LINE,14,"               "
		
		CALL SET_TILE,13,2,ダンジョンフラグ:73:4,0,53
		
		DA:5:13 += 40
		DA:11:2 += 50

	CASE 5
	;６階
		FLAG:最大X = 15
		FLAG:最大Y = 15
;								 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1,"   11 1        "
		CALL MAKE_FLOOR_LINE, 2,"   1  11       "
		CALL MAKE_FLOOR_LINE, 3,"   1   11      "
		CALL MAKE_FLOOR_LINE, 4,"   1    111216 "
		CALL MAKE_FLOOR_LINE, 5,"   121      11 "
		CALL MAKE_FLOOR_LINE, 6,"   1 2       2 "
		CALL MAKE_FLOOR_LINE, 7,"   1 11      1 "
		CALL MAKE_FLOOR_LINE, 8,"   1 112111  1 "
		CALL MAKE_FLOOR_LINE, 9," 111      1121 "
		CALL MAKE_FLOOR_LINE,10," 1           1 "
		CALL MAKE_FLOOR_LINE,11," 2           1 "
		CALL MAKE_FLOOR_LINE,12," 11   7      1 "
		CALL MAKE_FLOOR_LINE,13," 112111      1 "
		CALL MAKE_FLOOR_LINE,14,"               "
		
		CALL SET_TILE,13,13,ダンジョンフラグ:73:5,0,63

		DA:6:12 += 50
		DA:13:4 += 60

	CASE 6
	;7階
		FLAG:最大X = 10
		FLAG:最大Y = 10
;								 0123456789
		CALL MAKE_FLOOR_LINE, 0,"          "
		CALL MAKE_FLOOR_LINE, 1,"   111    "
		CALL MAKE_FLOOR_LINE, 2,"   111    "
		CALL MAKE_FLOOR_LINE, 3,"   111    "
		CALL MAKE_FLOOR_LINE, 4,"    2     "
		CALL MAKE_FLOOR_LINE, 5,"    1     "
		CALL MAKE_FLOOR_LINE, 6,"    1T    "
		CALL MAKE_FLOOR_LINE, 7,"    1     "
		CALL MAKE_FLOOR_LINE, 8,"    7     "
		CALL MAKE_FLOOR_LINE, 9,"          "
		
		CALL SET_TILE,4,2,ダンジョンフラグ:73:8,0,24
		CALL SET_TILE,4,2,ダンジョンフラグ:73:8,1,34
		
		DA:4:8 += 60
		

	CASEELSE
ENDSELECT
;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_73

;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
;% 10は10で割ったあまり、つまり下一桁がでてきます
;そして、下一桁はタイルの種類を表す
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_73
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	CALL UPSTAIRS_73
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL DOWNSTAIRS_73
	RETURN RESULT
ELSEIF DA:(FLAG:現X):(FLAG:現Y) %10 == 9
	SELECTCASE DA:(FLAG:現X):(FLAG:現Y) /10
	CASE 0
		CALL DUNGEON_TERMINAL
		RETURN 1
	ENDSELECT
ELSE
	PRINTW Nothing seems to be here.
	RETURN 0
ENDIF






;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_73,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) % 10 == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_73
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	CALL UPSTAIRS_73
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL DOWNSTAIRS_73
	RETURN RESULT

;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
;========================
;イベント
;========================
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4
	SELECTCASE DA:(FLAG:現X):(FLAG:現Y) /10
		CASE 1
			CUSTOMDRAWLINE =
			PRINTL A man wearing a loincloth with bloodshot eyes was standing there
			PRINTW 
			PRINTW 『Welcome, to the world of men!』
			;『ようこそ、男の世界へ！』
			IF TALENT:MASTER:140 == 0 || TALENT:MASTER:157 == 1
				PRINTL 『Boo this naughty girl's sudden intrusion！』
				PRINTW 『To kick out this girl, we prepared a special stage tonight！』
			ELSE
				PRINTL 『The venue's heating up the entry of a fine young man！』
				PRINTW 『To celebrate our encounter with this nice boy, I've prepared a special stage tonight！』
			ENDIF
			PRINTW
			PRINTL 　　　　　　　　　　　　　　　　　　 ﾀ
			PRINTL 『Time-unlimited one round match! Which will be the last to stay erect？』
			PRINTW 『Now, show us your hot blood！』
				FLAG:逃走不可フラグ = 1
				FLAG:会話不能フラグ = 1
				FLAG:先制不意打ちキャンセル = 1
				CUSTOMDRAWLINE =
				WAIT
				CALL SET_ENEMY,9,3269,30,0,1
				CFLAG:(CHARANUM-1):行動回数 = 2
				CFLAG:(CHARANUM-1):ＨＰ補正 = 500
				MAXBASE:(CHARANUM-1):破魔 = 0
				MAXBASE:(CHARANUM-1):呪殺 = 0
				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)
				CALL BATTLE_START
				FLAG:逃走不可フラグ = 0
				FLAG:会話不能フラグ = 0
				FLAG:先制不意打ちキャンセル = 0
				WAIT
			PRINTL
			PRINTL ＞You defeated an eerie shadow
			;不気味なシャドウを倒した
			PRINTW 
			ダンジョンフラグ:73:7 = 1
			DA:(FLAG:現X):(FLAG:現Y) = 1
			RETURN 1
		CASE 2
			CUSTOMDRAWLINE =
			PRINTW In the center of the room, there are two men with the same face.
			;部屋の中央で同じ顔をした二人の男が立っている。
			PRINTL 『I dislike women!』
			;『女は嫌いだ！』
			PRINTL 『So arrogant and self-centered! They cry if you get angry, they gossip behind your back, they spread nasty lies…』
			PRINTL 『They look at me like some…some disgusting THING and say that I'm a weirdo…』
			PRINTL 『Laughing all the while。』
			PRINTW 
			PRINTL 『"You like to sew? What a queer!" "Painting is so not you."』
			PRINTW 『"But you're a guy…" "You don't act like a guy…" "Why aren't you manly…"…』
			PRINTL 『Women are scary...』
			;『女は怖いよなぁ…』
			PRINTW 
			PRINTL 「No… Nuh-uh! No way！」
			PRINTW 「There's no way in hell that you're me！！」
			PRINTL 
			PRINTW  ＞The strange-feeling man's eyes glowed suspiciously
			PRINTW It could be interesting to catch the shadow, or you could just defeat it.
			;このまま彼を見殺しにして影の方を捕まえるのも面白いかもしれない…
			CALL INPUT_YN,"Help and recruit the shadow it","Defeat it"
			IF RESULT == 0
				PRINTL It's not what teddie wanted, but you are gonna help the shadow.
				;クマには悪いが影を手助けすることにしよう
				IF TALENT:MASTER:140 == 0 || TALENT:MASTER:157 == 1
					PRINTL 『What a bad girl, not calling ahead』
					PRINTW 『Or maybe you're a yaoi fangirl? Then I'll give you an exceptional show！』
				ELSE
					PRINTL 『What a nice guy. I'm looking forward to it』
					PRINTW 『I'll do you too! Let's carve an unforgetabble memory！』
				ENDIF
					FLAG:逃走不可フラグ = 1
					FLAG:会話不能フラグ = 1
					FLAG:先制不意打ちキャンセル = 1
					CUSTOMDRAWLINE =
					WAIT
					CALL SET_ENEMY,9,4565,35,0,1
					CFLAG:(CHARANUM-1):行動回数 = 2
					CFLAG:(CHARANUM-1):ＨＰ補正 = 1000
					MAXBASE:(CHARANUM-1):破魔 = 0
					MAXBASE:(CHARANUM-1):呪殺 = 0
					CALL SYNC_STATUS,(CHARANUM-1)
					CALL HEALTH_CHARA,(CHARANUM-1)
					CALL BATTLE_START
					FLAG:逃走不可フラグ = 0
					FLAG:会話不能フラグ = 0
					FLAG:先制不意打ちキャンセル = 0
					WAIT
				PRINTW ＜You defeated Kanji.
				PRINTL 
				PRINTL 「Damn it...」
				;「畜生…」
				PRINTL
 				IF TALENT:MASTER:140 == 0 || TALENT:MASTER:157 == 1
					PRINTL 『Bad girl, are you still there？』
					PRINTW 『What!? You know places where I can have good men！？』
					PRINTW 『You should have said that earlier! Bring me there already！』
					PRINTW 『Let's go! To a dazzling men' paradise！』
				ELSE
					PRINTL 『Nice boy, you have very good technique』
					PRINTW 『I want to deepen my love with you, I'll come along…that okay？』
					PRINTW 『Excellent! It only hurts at first！』
					PRINTW He had a chair with a vibe attached to it
				ENDIF
				PRINTW Kanji's shadow went into your ＣＯＭＰ without waiting for your permission.
				;完二の影は勝手にＣＯＭＰの中に入っていった。
				CALL ADD_NEW_COMPANION,3266,200
			ELSEIF RESULT == 1
				PRINTW ...you will help him as requested by Teddie.
				;…クマの依頼通り彼を助けよう。
				PRINTW 『Hehe...hehehe....you are me, me!』
				;『ふふ…ふふふふふ…ボクはキミ、キミさァァ！！』
				PRINTW 
				FLAG:逃走不可フラグ = 1
				FLAG:会話不能フラグ = 1
				FLAG:先制不意打ちキャンセル = 1
				CUSTOMDRAWLINE =
				WAIT
				CALL SET_ENEMY,9,3266,35,0,1
				CFLAG:(CHARANUM-1):行動回数 = 2
				CFLAG:(CHARANUM-1):ＨＰ補正 = 1000
				MAXBASE:(CHARANUM-1):破魔 = 0
				MAXBASE:(CHARANUM-1):呪殺 = 0
				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)
				
				CALL SET_ENEMY,8,3267,35,0,1
				MAXBASE:(CHARANUM-1):破魔 = 0
				MAXBASE:(CHARANUM-1):呪殺 = 0
				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)
				
				CALL SET_ENEMY,10,3268,35,0,1
				MAXBASE:(CHARANUM-1):破魔 = 0
				MAXBASE:(CHARANUM-1):呪殺 = 0
				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)
				CALL BATTLE_START
				FLAG:逃走不可フラグ = 0
				FLAG:会話不能フラグ = 0
				FLAG:先制不意打ちキャンセル = 0
				WAIT
				PRINTL 『Cumming〜…』
				PRINTL 
				PRINTW ＜You knocked down Kanji's shadow
				;完二の影を倒した。
				PRINTL The boy fell down where the shadow was.
				PRINTW Will you take him with you?
				CALL INPUT_YN,"Yes","No"
				IF RESULT == 1
				;NO
					PRINTL You decided to leave him here.
				ELSE
					PRINTL You decided to take the male student with you.
					PRINTW Tatsumi Kanji became a slave.
					CALL ADD_NEW_COMPANION,4565,200
				ENDIF
			ENDIF
			ダンジョンフラグ:73:8 = 1
			FLAG:脱出 = 1
			RETURN 1
		CASE 3
			PRINTW ＞A suspicious ceremony is taking place here...
			;怪しい儀式が行われている……
			PRINTL 
			PRINTL 「Shit, shit, shit!」
			PRINTL 「Some of you idiots aren't virgins!」
			;みんなボクたちに処女じゃないからってバカにして！
			PRINTL 「When summoning 『Very powerful demons』, you absolutely need a hymen!」
			;こうなったら『ごりっぱな悪魔』を召喚して、処女膜なんかケチョンケチョンだ！
			PRINTW 
			PRINTL 「....thats right, thats right!」
			;……そうです、その通りです！
			PRINTL 「Please summon that 『Very powerful demon』!」
			;トットと、あの『ごりっぱな悪魔』を召喚してください！
			PRINTL 「Please summon the powerful Mara!....thanks」
			;ごりっぱなマーラ様を召喚してください！……ありがたやー。
			PRINTW 
			PRINTL ＞It seems the shadows ritual is stuck.
			PRINTL 　Apparently, it's a problem some of them are missing their hymens.
			PRINTL 　Then, Baphomet, standing in their center, spoke.
			PRINTW 
			PRINTL 「...hehehe, well, as I just said, we are not yet ready to perform the summoning.」
			;……フッフッフッ、先ほどから申し上げている通り、まだ召喚のときではなーいのです。
			PRINTL 「『Mara』 is a very powerful demon...」
			;『マーラ様』はごりっぱな悪魔…
			PRINTL 「Therefore, we can't afford to fail in our summoning. Terrible things might happen if we do.」
			;「万全を期して召喚に臨まねば、恐ろしい事態を引き起こーすでしょう。」
			PRINTW 
			PRINTL 「What are you talking about, hurry it up!」
			;「なに言ってるんだ、はやくしろ！」
			PRINTL 「They don't have hymens, we can't let them live any second longer!」
			;「処女膜がある奴らなんて、一秒でも生かしておけないよ！」
			PRINTW 
			PRINTL Suddenly, the selfish shadow seems to notice you.
			;勝手なことを言っていたシャドウたちはあなたに気がついたようだ。
				IF TALENT:MASTER:140 == 0 || TALENT:MASTER:157 == 1
					PRINTL 「Great, there's a new female! Is she a virgin?!」
					;「ひょえぇぇぇぇぇ！し、し、し、処女だぁぁぁぁぁぁ！！」
					PRINTW 「Well, hurry up and summon 『Mara』 already!」
					;早く『マーラ様』を召喚してメッタメタにぃぃぃぃぃい
				ELSE
					PRINTL 「Great, there's a summoner!」
					;「そこのサマナーのオニイサン、アンタからも言ってやれ。」
					PRINTW 「Hurry and summon 『Mara』 before he can stop us!」
					;「早く『マーラ様』を召喚して、ボクたちを楽にしろと！」
				ENDIF
			PRINTW 
			PRINTL 「Well, do you know what might happen?」
			;「どうなっても、知りませーんよ？」
			PRINTL Saying so, Baphoment cast a series of dubious spells.
			PRINTW 
			PRINTL 「...because we where in a hurry, Mara has been summoned in an incomplete state, close to a slime.」
			;「……慌てたあまり、スライムに近い不完全な状態のマーラ様を召喚してしまーった。」
			PRINTL
			PRINTL 「Ahhhh! W-we failed!」
			;ひえぇぇぇぇぇぇぇ！し、し、し、失敗だぁぁぁぁぁ！！
			PRINTL 
			PRINTL  ...Gug...Gi...............Oma...Ella......
			;…ググ…ギ……………オマ…エラ……
			PRINTL ...Gee...Gug.........G...Yoku...mo.......
			;…ギ……グギ………ガ…ヨク…モ……
			PRINTW 
				FLAG:逃走不可フラグ = 1
				FLAG:会話不能フラグ = 1
				FLAG:先制不意打ちキャンセル = 1
				CALL SET_ENEMY,9,259,50,0,1
				CFLAG:(CHARANUM-1):ＨＰ補正 = 2000
				CFLAG:(CHARANUM-1):行動回数 = 2
				MAXBASE:(CHARANUM-1):破魔 = 0
				MAXBASE:(CHARANUM-1):呪殺 = 0
				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)
				CALL BATTLE_START
				FLAG:逃走不可フラグ = 0
				FLAG:会話不能フラグ = 0
				FLAG:先制不意打ちキャンセル = 0
				WAIT
			PRINTW ＞You defeated the incomplete Mara.
			PRINTL 　All the other shadows are gone...
			PRINTW 　It seems they ran away in the confusion.
			ダンジョンフラグ:73:8 = 2
			DA:(FLAG:現X):(FLAG:現Y) = 1
			FLAG:脱出 = 1
			RETURN 1
	ENDSELECT
ELSEIF DA:(FLAG:現X):(FLAG:現Y) %10 == 9
	SELECTCASE DA:(FLAG:現X):(FLAG:現Y) /10
		CASE 0
			CALL DUNGEON_TERMINAL
			RETURN 1
	ENDSELECT
ENDIF
RETURN 0


;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_73
SELECTCASE FLAG:現M
	CASE 0
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
	CASE 1
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
	CASE 2
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
	CASE 3
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
	CASE 4
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
	CASE 5
		FLAG:エンカウント率 = 10 + FLAG:未遭遇歩数*5
	CASE 6
		FLAG:エンカウント率 = 10 + FLAG:未遭遇歩数*5
	CASEELSE
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
ENDSELECT

;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_73
RETURN 25, 24, 29, 35
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_73
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_73
RETURN CLASS_NUM_COMMON("中級")
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;=======================================================================
@ENEMY_NUM_73, ARG
RETURN ENEMY_NUM_COMMON("中級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_73, ARG
SELECTCASE RAND:100
	CASE IS < 10
	RETURN [[キャラ:セエレ]],25
	CASE IS < 20
	RETURN [[キャラ:くびなしライダー]],24
	CASE IS < 30
	RETURN [[キャラ:ミシャグジさま]],26
	CASE IS < 40
	RETURN [[キャラ:フライデイ]],24
	CASE IS < 50
	RETURN [[キャラ:ナーガ]],28
	CASE IS < 60
	RETURN [[キャラ:鋼鉄のギガス]],25
	CASE IS < 75
	RETURN [[キャラ:インキュバス]],29
	CASE IS < 85
	RETURN [[キャラ:ゾンビくん]],25
	CASE IS < 100
	RETURN [[キャラ:オニ]],26
ENDSELECT
;=======================================================================
;特殊調整
;=======================================================================
@SET_ENEMY_STATUS_73, ARG
IF ABL:(ARG):種族 == 32
	SELECTCASE RAND:100
		CASE IS < 60
			ABL:ARG:アイテム3 = GETNUM(ITEM, "Lead Medal")
		CASE IS < 80
			ABL:ARG:アイテム3 = GETNUM(ITEM, "Steel Medal")
		CASE IS < 95
			ABL:ARG:アイテム3 = GETNUM(ITEM, "Silver Medal")
		CASEELSE
			ABL:ARG:アイテム3 = GETNUM(ITEM, "Golden Medal")
	ENDSELECT
	ABL:(ARG):入手確率3 = ITEM:(ABL:ARG:アイテム3) >= 10 ? 0 # 10
ENDIF

;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_73
;/10はIDを10で割る。つまり、下一桁をなくして、二桁目と三桁目を二桁の数字として取り出している
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
			;IDを示しています。つまり、これはIDが1のとき、2の時で分岐
			CASE 1
				;GET_TREASURE,アイテム番号、入手個数、ダンジョンナンバー、あけたときONにするフラグの番号
				;1001は傷薬で、それを1個、ダンジョンナンバーは宝箱あけるときはよそのダンジョンをあけることがないだろうのでFLAG:現ダンジョンが無難です
				;そして、フラグはダンジョンフラグ:1(エコービル):0を1にするということです
				;MAPをつくるときにこの宝箱はダンジョンフラグ:1(エコービル):0が0のときしか置かないことになってるのでこれで消えます
				CALL GET_TREASURE,[[アイテム:BalmOfLife]],1,FLAG:現ダンジョン,0
			CASE 2
				CALL GET_TREASURE,[[アイテム:Baseball Bat]],1,FLAG:現ダンジョン,1
			CASE 3
				CALL GET_TREASURE,[[アイテム:OxBezoar]],2,FLAG:現ダンジョン,2
			CASE 4
				CALL GET_TREASURE,[[アイテム:YamataNoDrink]],2,FLAG:現ダンジョン,3
			CASE 5
				CALL GET_TREASURE,[[アイテム:スキルカード【二段突き】]],1,FLAG:現ダンジョン,4
			CASE 6
				CALL GET_TREASURE,[[アイテム:Dis-Charm]],3,FLAG:現ダンジョン,5
ENDSELECT
RETURN 1

;=======================================================================
;階段置き場
;=======================================================================
@UPSTAIRS_73
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) /10
	;IDを示す
	CASE 1
			;階層1で、17が入ってるタイル…つまり、IDが1の7（下り階段）に飛ぶ
			CALL DUNGEON_UPSTAIRS_ID,1,17
	CASE 2
			CALL DUNGEON_UPSTAIRS_ID,2,27
	CASE 3
			CALL DUNGEON_UPSTAIRS_ID,3,37
	CASE 4
			CALL DUNGEON_UPSTAIRS_ID,4,47
	CASE 5
			CALL DUNGEON_UPSTAIRS_ID,5,57
	CASE 6
			CALL DUNGEON_UPSTAIRS_ID,6,67
	CASEELSE
ENDSELECT
RETURN 1

@DOWNSTAIRS_73
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	CASE 1
			;階層0で、16が入ってるタイル、つまり、IDが1の6（昇り階段）に飛ぶ
			CALL DUNGEON_DOWNSTAIRS_ID,0,16
	CASE 2
			CALL DUNGEON_DOWNSTAIRS_ID,1,26
	CASE 3
			CALL DUNGEON_DOWNSTAIRS_ID,2,36
	CASE 4
			CALL DUNGEON_DOWNSTAIRS_ID,3,46
	CASE 5
			CALL DUNGEON_DOWNSTAIRS_ID,4,56
	CASE 6
			CALL DUNGEON_DOWNSTAIRS_ID,5,66
	CASEELSE
ENDSELECT
RETURN 1

;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_73
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:5
	CASE 0
		RETURN [[キャラ:インキュバス]] , 3 + RAND:10 , CSVBASE([[キャラ:インキュバス]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:鋼鉄のギガス]] , 3 + RAND:10 , CSVBASE([[キャラ:鋼鉄のギガス]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:オニ]] , 3 + RAND:10 , CSVBASE([[キャラ:オニ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 3
		RETURN [[キャラ:ナーガ]] , 3 + RAND:10 , CSVBASE([[キャラ:ナーガ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 4
		RETURN [[キャラ:ゾンビくん]] , 3 + RAND:10 , CSVBASE([[キャラ:ゾンビくん]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT
