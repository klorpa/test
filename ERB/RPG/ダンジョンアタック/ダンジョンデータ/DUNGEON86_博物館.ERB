
;=======================================================================
;博物館
;ボス・ダンタリアン
;ダンジョンナンバー86　ＭＡＰ0~3
;ダンジョンフラグ:86:0 ダンジョン進行フラグ　1,ダンタリアン撃破
;ダンジョンフラグ:86:1 スタート時イベント
;ダンジョンフラグ:86:2 
;=======================================================================

;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_86
IF  ダンジョンフラグ:78:0 > 0 & !ダンジョンフラグ:86:0
	RETURN 1
ENDIF

;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_86
RESULTS = Museum
;博物館

;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_86
PRINT Museum
;博物館
SELECTCASE FLAG:現M
	CASE 0
		PRINTL １Ｆ
	CASE 1
		PRINTL ２Ｆ
	CASE 2
		PRINTL ３Ｆ
	CASE 3
		PRINTL ４Ｆ
ENDSELECT


;=======================================================================
;特殊タイル表示
;=======================================================================
@TILE_MARK_COMMON_86_1, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	CASE 11 , 21 , 31 , 41 , 51 , 61 , 71 , 81
		RESULTS = 穴
	CASEELSE
		RETURN 1
ENDSELECT

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_86,ARG
FLAG:現M = 0
FLAG:現X = 3
FLAG:現Y = 11
;CALL AUTOMAP
;=======================================================================
;攻略開始時のイベント
;=======================================================================
@EVENT_DUNGEON_START_86
IF ダンジョンフラグ:86:1 == 0
CALL MESSAGE_WINDOW_D, "", "＞You where contracted to defeat a demon in this museum and that's why you are here."
;博物館を占拠した悪魔を倒す依頼を受けたあなたは博物館にやってきた
CALL MESSAGE_WINDOW_D, "", "＞There is a noticable presence in the museum."
;博物館の中から妖しい気があふれている
CALL MESSAGE_WINDOW_D, "", "＞It seems this has really become a demon nest."
;悪魔の巣になっているのは本当のようだ
ダンジョンフラグ:86:1 = 1
ENDIF
;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_86,ARG,ARG:1
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT

;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_86
SELECTCASE FLAG:現M
	CASE 0
	;１階
		FLAG:最大X = 15
		FLAG:最大Y = 14
;								 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," $ $ 6211111   "
		CALL MAKE_FLOOR_LINE, 2," 11a       2   "
		CALL MAKE_FLOOR_LINE, 3," 2   111111111 "
		CALL MAKE_FLOOR_LINE, 4," 11111   1   1 "
		CALL MAKE_FLOOR_LINE, 5," 111112# 1   1 "
		CALL MAKE_FLOOR_LINE, 6," 111     1   1 "
		CALL MAKE_FLOOR_LINE, 7," 111 $11111$ 1 "
		CALL MAKE_FLOOR_LINE, 8,"   2         1 "
		CALL MAKE_FLOOR_LINE, 9," 11111 1b11 11 "
		CALL MAKE_FLOOR_LINE,10," 11111 1  1213 "
		CALL MAKE_FLOOR_LINE,11," 11c11 $ $1    "
		CALL MAKE_FLOOR_LINE,12,"   2           "
		CALL MAKE_FLOOR_LINE,13,"   8           "


	CASE 1
	;２階
		FLAG:最大X = 14
		FLAG:最大Y = 14
;								 01234567890123
		CALL MAKE_FLOOR_LINE, 0,"              "
		CALL MAKE_FLOOR_LINE, 1," 111127 d1111 "
		CALL MAKE_FLOOR_LINE, 2," 2      1   1 "
		CALL MAKE_FLOOR_LINE, 3," % eA 311   1 "
		CALL MAKE_FLOOR_LINE, 4," 1    111   1 "
		CALL MAKE_FLOOR_LINE, 5," 11111111   1 "
		CALL MAKE_FLOOR_LINE, 6,"            1 "
		CALL MAKE_FLOOR_LINE, 7," 11f111111121 "
		CALL MAKE_FLOOR_LINE, 8," 1   $   B1 1 "
		CALL MAKE_FLOOR_LINE, 9," 1 $   $  1 1 "
		CALL MAKE_FLOOR_LINE,10," 1 11111$ $ 1 "
		CALL MAKE_FLOOR_LINE,11," 1   2      1 "
		CALL MAKE_FLOOR_LINE,12," 111Cg1$ 6211 "
		CALL MAKE_FLOOR_LINE,13,"              "

CASE 2
	;３階
		FLAG:最大X = 15
		FLAG:最大Y = 14
;								 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," 11111111D111$ "
		CALL MAKE_FLOOR_LINE, 2," 1      2 2    "
		CALL MAKE_FLOOR_LINE, 3," 1 E2111111111 "
		CALL MAKE_FLOOR_LINE, 4," 1   111111111 "
		CALL MAKE_FLOOR_LINE, 5," 1        2    "
		CALL MAKE_FLOOR_LINE, 6," 11F1111111111 "
		CALL MAKE_FLOOR_LINE, 7," 2   31111   1 "
		CALL MAKE_FLOOR_LINE, 8," 111   1 1   1 "
		CALL MAKE_FLOOR_LINE, 9,"   1   1 1   1 "
		CALL MAKE_FLOOR_LINE,10," 6 1   1 111 $ "
		CALL MAKE_FLOOR_LINE,11," 2 1 h 1   1   "
		CALL MAKE_FLOOR_LINE,12," 111 G 127 11$ "
		CALL MAKE_FLOOR_LINE,13,"               "

	CASE 3
	;４階
		FLAG:最大X = 15
		FLAG:最大Y = 13
;								 012345678901234
		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1,"       !       "
		CALL MAKE_FLOOR_LINE, 2,"       2       "
		CALL MAKE_FLOOR_LINE, 3,"     11111     "
		CALL MAKE_FLOOR_LINE, 4,"     11111     "
		CALL MAKE_FLOOR_LINE, 5,"     11111     "
		CALL MAKE_FLOOR_LINE, 6,"       1       "
		CALL MAKE_FLOOR_LINE, 7," 1111111111111 "
		CALL MAKE_FLOOR_LINE, 8," 2 1     1   1 "
		CALL MAKE_FLOOR_LINE, 9," 7 1 H $21   1 "
		CALL MAKE_FLOOR_LINE,10,"   1 2       1 "
		CALL MAKE_FLOOR_LINE,11," 111 111111111 "
		CALL MAKE_FLOOR_LINE,12,"               "
	CASEELSE


ENDSELECT

;=======================================================================
;タイル調整
;=======================================================================
@MAKE_FLOOR_LINE_COMMON_86, ARG, ARG:1, ARG:2
#LOCALSIZE 30
;LOCAL:3 宝箱 ダンジョンフラグ:84~99
;LOCAL:4 イベント
;(0/0)でLOCALを消去
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
SELECTCASE ARG
	CASE 3
		IF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):(FLAG:現M+70), LOCAL:3)
			DA:(ARG:1):(ARG:2) = 1
		ELSE
			DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:3 * 10 + 3
		ENDIF
		LOCAL:3++
	CASE 4
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:4 * 10 + 4
		LOCAL:4++
	CASE 5
		;DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:5 * 10 + 5
		;LOCAL:5++
	CASE 6
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:6 * 10 + 6
		LOCAL:6++
	CASE 7
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:7 * 10 + 7
		LOCAL:7++
	CASE 8
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:8 * 10 + 8
		LOCAL:8++
	CASE 9
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:9 * 10 + 9
		LOCAL:9++
ENDSELECT


;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_86, ARGS, ARG, ARG:1
SELECTCASE ARGS

	CASE "a"
		RETURN 101
	CASE "A"
		RETURN 111
	CASE "b"
		RETURN 201
	CASE "B"
		RETURN 211
	CASE "c"
		RETURN 301
	CASE "C"
		RETURN 311
	CASE "d"
		RETURN 401
	CASE "D"
		RETURN 411
	CASE "e"
		RETURN 501
	CASE "E"
		RETURN 511
	CASE "f"
		RETURN 601
	CASE "F"
		RETURN 611
	CASE "g"
		RETURN 701
	CASE "G"
		RETURN 711
	CASE "h"
		RETURN 801
	CASE "H"
		RETURN 811
	CASE "#"
		RETURN 14
	CASE "$"
		RETURN 24
	CASE "%"
		RETURN 34
	CASE "!"
		RETURN 1004
ENDSELECT


;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_86
CALL EVENT_DUNGEON_86
RETURN RESULT

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_86,ARG
;ARG = 進入時の向き　0の場合ワープなど
CALL EVENT_DUNGEON_86, ARG , 0
RETURN RESULT

@EVENT_DUNGEON_86, ARG, ARG:1
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) % 10
;==========
;ワープの処理
;==========
CASE 1
CALL FLOOR_86
RETURN RESULT

;==========
;出口の処理
;==========
CASE 8
CALL DUNGEON_EXIT
RETURN 1
;==========
;扉の処理
;==========
CASE 2
CALL DUNGEON_DOOR,ARG
RETURN 1
;========
;宝の処理
;========
CASE 3
CALL TREASURE_86
RETURN 1

;=======
;階段
;=======
CASE 6
CALL UPSTAIRS_86
RETURN RESULT

CASE 7
CALL DOWNSTAIRS_86
RETURN RESULT
;==========
;イベント
;==========
CASE 4
	SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	CASE  1
		CALL MESSAGE_WINDOW_D, "", "＞There is a shrine exhibited here."
		;祠が展示されている
		CALL MESSAGE_WINDOW_D, "", "＞\"--- From the shape, it is thought to be older than the Nara era, but / it is not clear for what purpose it was built.\" / is written. "
		;「―――形状から、奈良時代以前の古いものと考えられますが、/何の目的で建てられたかは、明らかではありません。」/と書かれている。
		CALL MESSAGE_WINDOW_D, "", "> It seems that it was built to seal something, / It was moved from the original place and it was exhibited here / it is only a sign of weakness now "
		;＞なにかを封じるために建てられてたようだが、/本来の場所から動かされここに展示されて/今は弱々しい気配がするだけだ"
	CASE  2
		IF ダンジョンフラグ:86:2 < 6
		CALL MESSAGE_WINDOW_D, "", "There is a bronze statue of a woman."
		;女性のブロンズ像がある
		ダンジョンフラグ:86:2 += 1
		ELSE
		CALL MESSAGE_WINDOW_D, "", "...you feel there are too many bronze statues of women here.."
		;……女性のブロンズ像が沢山有りすぎる気がする
		ENDIF
	CASE  3
		IF ダンジョンフラグ:86:1 == 1
		CALL MESSAGE_WINDOW_D, "", "＞There are demons carrying bronze statues."
		;悪魔たちがブロンズ像を運んでいる
		CALL MESSAGE_WINDOW_D, "Sandman", "I wonder why Dantalion likes these human figures."
		;ザントマン	またダンタリアン様は人間を像にしたのかよ
		CALL MESSAGE_WINDOW_D, "Incubus", "I don't know about his tastes..."
		;インキュバス	あの方の趣味はわかんないよな
		CALL MESSAGE_WINDOW_D, "", "＞The demons noticed you!"
		;悪魔たちがこちらに気付いた！
		CALL MESSAGE_WINDOW_D, "Sandman", "...!　An intruder!?"
		;…！　お前侵入者か！？
				FLAG:逃走不可フラグ = 1
				FLAG:会話不能フラグ = 1
				FLAG:先制不意打ちキャンセル = 1
				CALL SET_ENEMY,8,[[キャラ:ザントマン]],35,0,1
				
				CALL SET_ENEMY,10,[[キャラ:インキュバス]],35,0,1

				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)		
				CALL BATTLE_START
				FLAG:逃走不可フラグ = 0
				FLAG:会話不能フラグ = 0
				FLAG:先制不意打ちキャンセル = 0
		CALL MESSAGE_WINDOW_D, "", "＞You defeated the demons."
		;悪魔たちを倒した！
		CALL MESSAGE_WINDOW_D, "", "＞It seems the bronze statues you saw in the museum/are people changed by this incident."
		;どうやら博物館に配置されているブロンズ像は/博物館に取り残された人間が変えられたもののようだ。
		CALL MESSAGE_WINDOW_D, "", "＞If you defeat the demon behind this incident, they may return to normal/but there is nothing you can do for them here."
		;博物館を支配する悪魔を倒せば元に戻るかもしれないが、/今はどうすることも出来ない。
		ダンジョンフラグ:86:1 = 2
		ENDIF
	CASE  100
			IF ダンジョンフラグ:86:0 == 0
				CALL MESSAGE_WINDOW_D, "", "＞"
				CALL MESSAGE_WINDOW_D, "Dantalion", "I hope you took the time to admire my collection on your way here."
				;ダンタリアン", "どうだね、ここへ来るまでに私のコレクションを楽しんでもらえたかい。"
				CALL MESSAGE_WINDOW_D, "Dantalion", "I kill two birds with one stone, building my collection and gathering the souls of foolish humans."
				CALL MESSAGE_WINDOW_D, "Dantalion", "The point of this museum is to capture many human souls."
				CALL MESSAGE_WINDOW_D, "Dantalion", "All of this museum will empower my strength!"
				;この博物館のすべてが私のエナジーになるのだ！
				IF IS_LOOKSLIKE_MALE(MASTER)
					CALL MESSAGE_WINDOW_D, "Dantalion", "Well, even you could give me energy."
					;では、君も私のエナジーとなるがよい。
				ELSE
					CALL MESSAGE_WINDOW_D, "Dantalion", "You too, should join my collection."
					;君も私のコレクションに加わるがよい。
				ENDIF
				CALL MESSAGE_WINDOW_D, "", "One Fallen Dantalion appeared!"
				;堕天使 ダンタリアンが１体出た！
				FLAG:逃走不可フラグ = 1
				FLAG:会話不能フラグ = 1
				FLAG:先制不意打ちキャンセル = 1
				CALL SET_ENEMY,9,[[キャラ:ダンタリアン]],40,0,1
				CFLAG:(CHARANUM-1):行動回数 = 2
				CFLAG:(CHARANUM-1):ＨＰ補正 += 1000
				CFLAG:(CHARANUM-1):ＭＰ補正 = 9000
				ABL:(CHARANUM-1):アイテム1 = [[アイテム:InIncense]]
				ABL:(CHARANUM-1):入手確率1 = 100
				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)		
				CALL BATTLE_START
				FLAG:逃走不可フラグ = 0
				FLAG:会話不能フラグ = 0
				FLAG:先制不意打ちキャンセル = 0
				CALL MESSAGE_WINDOW_D, "Dantalion", "Wah, my energy...../My collection...."
				;わ、私のエナジーが……/私のコレクションが……
				CALL MESSAGE_WINDOW_D, "", "＞Fallen　Dantalion has been defeated!"
				;ダンタリアンを倒した！
				CALL MESSAGE_WINDOW_D, "", "＞For subduing Dantalion, ￥25000 were paid and you also got 20 fame."
				;ダンタリアンを討伐したことにより、口座に￥25000が支払われ、名声20を得た
				MONEY += 25000
				FLAG:名声 += 20
				ダンジョンフラグ:86:0 = 1
				RETURN 1
		ENDIF
	ENDSELECT
ENDSELECT
RETURN 0


;=======================================================================
;階段置き場
;=======================================================================
@UPSTAIRS_86
;位置とIDが対応しているはずなので、同IDの階段を探させれば良い
SELECTCASE FLAG:現M
	CASEELSE
		CALL DUNGEON_UPSTAIRS_ID, FLAG:現M+1 , DA:(FLAG:現X):(FLAG:現Y) + 1000 + 1
ENDSELECT
RETURN 1

@DOWNSTAIRS_86
SELECTCASE FLAG:現M
	CASEELSE
		CALL DUNGEON_DOWNSTAIRS_ID, FLAG:現M-1 , DA:(FLAG:現X):(FLAG:現Y) - 1000 - 1
ENDSELECT
RETURN 1


;-----------------------------------------------------------------------
;タイルイベント
;-----------------------------------------------------------------------
@FLOOR_86, ARG
#LOCALSIZE 4
#LOCALSSIZE 2
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	CASE 0 , 10 , 20 , 30 , 40 , 50 , 60 , 70 , 80
		RETURN 0
	;ワープゾーン
	CASEELSE
		;ワープゾーン関係
		;100の位がID
		;10の位が0なら上層に、1なら下層に行く
		LOCAL = DA:(FLAG:現X):(FLAG:現Y) / 10 % 10 ? DA:(FLAG:現X):(FLAG:現Y) -10 # DA:(FLAG:現X):(FLAG:現Y) + 10
		LOCAL:1 = DA:(FLAG:現X):(FLAG:現Y) / 10 % 10 ? FLAG:現M - 1 # FLAG:現M + 1
		CALL MESSAGE_WINDOW_D, "", "＞It's a pitfall!"
		;落とし穴だ！
		CALL SHOW_PICTURE, "EMPTY"
		CALL DUNGEON_WORP_ID, LOCAL:1, LOCAL, 1
ENDSELECT
;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_86
#LOCALSIZE 9
LOCAL = FLAG:現M + 70
LOCAL:1 = (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	;マップ番号*100+そのマップで何番目の宝箱かを示す数値でどの宝箱かを判別します。
	;例えば0の場合、0版のマップの0番目の宝箱であることを表しています。
	CASE 0
		CALL GET_TREASURE, [[アイテム:Sky Piercer]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
	CASE 100
		CALL GET_TREASURE, [[アイテム:Training Headgear]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
	CASE 200
		CALL GET_TREASURE, [[アイテム:Skull Keikogi]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
ENDSELECT
RETURN 1


;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_86
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
SIF FLAG:未遭遇歩数 < 5
	FLAG:エンカウント率 = 0
;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_86
RETURN 30, 29, 34, 40
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_86
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;RETURN X でXが種類の数になります
;あまりいろんなのが混ざっているとCOOPがとりにくくなったり
;敵の攻撃属性が多くなる傾向にあるので、
;種類の数は1・2種類を主体に時々3種類くらいを基本にするのが望ましいと思われます
;=======================================================================
@CLASS_NUM_86
RETURN CLASS_NUM_COMMON("中級")
;=======================================================================
@ENEMY_NUM_86, ARG
RETURN ENEMY_NUM_COMMON("中級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_86, ARG
SELECTCASE RAND:10
	CASE IS < 1
		RETURN [[キャラ:タクヒ]],33
	CASE IS < 2
		RETURN [[キャラ:ヴィーヴル]],30
	CASE IS < 3
		RETURN [[キャラ:カラドリウス]],30
	CASE IS < 4
		RETURN [[キャラ:プリンシパリティ]],29
	CASE IS < 5
		RETURN [[キャラ:ローレライ]],31
	CASE IS < 6
		RETURN [[キャラ:ショウジョウ]],32
	CASE IS < 7
		RETURN [[キャラ:ザントマン]],30
	CASE IS < 8
		RETURN [[キャラ:インキュバス]],34
	CASE IS < 9
		RETURN [[キャラ:スチュパリデス]],32
	CASE IS < 10
		RETURN [[キャラ:ヌエ]],31
ENDSELECT

;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_86
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:3
	CASE 0
		RETURN [[キャラ:ショウジョウ]] , 3 + RAND:10 , CSVBASE([[キャラ:ショウジョウ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:ザントマン]] , 3 + RAND:10 , CSVBASE([[キャラ:ザントマン]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:タクヒ]] , 3 + RAND:10 , CSVBASE([[キャラ:タクヒ]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT

