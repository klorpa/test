;=======================================================================
;サンプルダンジョン
;ダンジョンのテンプレートみたいなものです
;ダンジョンナンバー0
;=======================================================================

;ダンジョンフラグ:83:0 = 1.クリアフラグ
;ダンジョンフラグ:83:1 = 1.初回
;ダンジョンフラグ:83:2 = 1.カードキー
;ダンジョンフラグ:83:3 = 1.扉のチェック
;ダンジョンフラグ:83:10- = 1.宝箱



;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_83
IF  ダンジョンフラグ:79:0 == 1 & !ダンジョンフラグ:83:0
	RETURN 1
ENDIF

;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_83
RESULTS = Hirasaki Police Station
;平崎警察署

;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_83
PRINT  Hirasaki Police Station 
;平崎警察署
SELECTCASE FLAG:現M
	CASE 0
		PRINTL B1F
	CASE 1
		PRINTL 1F
	CASE 2
		PRINTL 2F
	CASE 3
		PRINTL 3F
	CASEELSE
		PRINTL
ENDSELECT
;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_83,ARG
FLAG:現M = 1
FLAG:現X = 7
FLAG:現Y = 11

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_83
IF ダンジョンフラグ:83:1 == 0
	CALL SHOW_PICTURE, "NONFLOORD"
	CALL MESSAGE_WINDOW_D, "", @"＞After %CALLNAME:MASTER% received a request from the Hirasaki police,/＞%HE_SHE(MASTER)% stepped into the interior of the station."
	;平崎警察からの依頼を受けた%CALLNAME:MASTER%は/＞異界化した署内に足を踏み入れた
	CALL MESSAGE_WINDOW_D, "", "＞This station is managed by a traitor/and you where requested to help with the purge./＞You'll likely have to face the traitor."
	;この警察署は粛清を依頼された組織の裏切り者が管理する場所だ/＞おそらくあなたを向え討つ気なのだろう
	ダンジョンフラグ:83:1 = 1
ENDIF

;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_83
SELECTCASE FLAG:現M
	CASE 0
		FLAG:最大X = 13 
		FLAG:最大Y = 16 
		;                        0123456789012
		CALL MAKE_FLOOR_LINE, 0,"             "
		CALL MAKE_FLOOR_LINE, 1," D211 6 112D "
		CALL MAKE_FLOOR_LINE, 2," D    2    D "
 		CALL MAKE_FLOOR_LINE, 3," DDDDDDDDDDD "
		CALL MAKE_FLOOR_LINE, 4," D  2 D 2  D "
		CALL MAKE_FLOOR_LINE, 5," D  4 D 4  D "
		CALL MAKE_FLOOR_LINE, 6," D    D    D "
		CALL MAKE_FLOOR_LINE, 7," DDDDDD4DDDD "
		CALL MAKE_FLOOR_LINE, 8," D   D D   D "
		CALL MAKE_FLOOR_LINE, 9," D   D D   D "
		CALL MAKE_FLOOR_LINE,10," DD4DDDDDDD4 "
		CALL MAKE_FLOOR_LINE,11,"     D D   D "
		CALL MAKE_FLOOR_LINE,12," DDD D D   D "
		CALL MAKE_FLOOR_LINE,13," 2 D       D "
		CALL MAKE_FLOOR_LINE,14," E DDDDDDDDD "
		CALL MAKE_FLOOR_LINE,15,"             "
		;                        0123456789012
		
		
	CASE 1
		FLAG:最大X = 15 
		FLAG:最大Y = 13 
		;                        012345678901234
 		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," 41 4 716 4 14 "
		CALL MAKE_FLOOR_LINE, 2,"  1 2  2  2 1  "
		CALL MAKE_FLOOR_LINE, 3,"  1 1111111 1  "
		CALL MAKE_FLOOR_LINE, 4," 11    1    11 "
		CALL MAKE_FLOOR_LINE, 5,"  1 4     4 1  "
		CALL MAKE_FLOOR_LINE, 6,"  1 1  4  1 1  "
		CALL MAKE_FLOOR_LINE, 7," 41 2  2  2 14 "
		CALL MAKE_FLOOR_LINE, 8,"    1114111    "
		CALL MAKE_FLOOR_LINE, 9," 13 1111111    "
		CALL MAKE_FLOOR_LINE,10," 2  11 2 11    "
		CALL MAKE_FLOOR_LINE,11," E     8       "
		CALL MAKE_FLOOR_LINE,12,"               "
		;                        012345678901234
		
		
		CASE 2
		FLAG:最大X = 15 
		FLAG:最大Y = 13 
		;                        012345678901234
 		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," 1 421 7 121 1 "
		CALL MAKE_FLOOR_LINE, 2," 1   1 2 1   1 "
		CALL MAKE_FLOOR_LINE, 3," 1   11111   1 "
		CALL MAKE_FLOOR_LINE, 4," 1           1 "
		CALL MAKE_FLOOR_LINE, 5," 1111111111111 "
		CALL MAKE_FLOOR_LINE, 6," 1           1 "
		CALL MAKE_FLOOR_LINE, 7," 1   1 111   1 "
		CALL MAKE_FLOOR_LINE, 8," 1   1 2 1   1 "
		CALL MAKE_FLOOR_LINE, 9," 1 421 6 1   1 "
		CALL MAKE_FLOOR_LINE,10," 2   1   1   1 "
		CALL MAKE_FLOOR_LINE,11," E   11111 111 "
		CALL MAKE_FLOOR_LINE,12,"               "
		;                        012345678901234
		
		
		
		CASE 3
		FLAG:最大X = 15 
		FLAG:最大Y = 13 
		;                        012345678901234
 		CALL MAKE_FLOOR_LINE, 0,"               "
		CALL MAKE_FLOOR_LINE, 1," 5111 4 111513 "
		CALL MAKE_FLOOR_LINE, 2,"    1 2   1  2 "
		CALL MAKE_FLOOR_LINE, 3," 3  1 1   1  1 "
		CALL MAKE_FLOOR_LINE, 4," 1  1 1   1  1 "
		CALL MAKE_FLOOR_LINE, 5," 1111211111111 "
		CALL MAKE_FLOOR_LINE, 6," 1  1 1   1  1 "
		CALL MAKE_FLOOR_LINE, 7," 1  1 1   1  1 "
		CALL MAKE_FLOOR_LINE, 8," 1  1 2   1  1 "
		CALL MAKE_FLOOR_LINE, 9," 1  1 7   1  1 "
		CALL MAKE_FLOOR_LINE,10," 2 11     1  2 "
		CALL MAKE_FLOOR_LINE,11," E 111111515 3 "
		CALL MAKE_FLOOR_LINE,12,"               "
		;                        012345678901234
		
	
	CASEELSE
ENDSELECT


;=======================================================================
;タイル調整
;=======================================================================
@MAKE_FLOOR_LINE_COMMON_83, ARG, ARG:1, ARG:2
#LOCALSIZE 20
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
SELECTCASE ARG
	CASE 3
		IF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):(FLAG:現M+10), LOCAL:3)
			DA:(ARG:1):(ARG:2) = 1
			ELSE
			DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:3 * 10 + 3
		ENDIF
		LOCAL:3++
	CASE 4 TO 9
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:ARG * 10 + ARG
		LOCAL:ARG++
	
ENDSELECT

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_83,ARG,ARG:1


IF FLAG:現M == 3 && ARG == 6 && ARG:1 == 2 
	CALL MESSAGE_WINDOW_D, "", "＞This seems to be the chief's office/＞Open the door?"
	;ここは署長室のようだ/＞扉を開けますか？
			CALL INPUT_YN_D,"Yes","No"
			IF RESULT == 0
				IF ダンジョンフラグ:83:2 == 0
				CALL MESSAGE_WINDOW_D, "", "＞It's locked./＞It seems the door won't open without a keycard."
				;＞ロックが掛かっている/＞カードキーがなければ開かないようだ
				RESULT = 1
				ダンジョンフラグ:83:3 = 1
				ENDIF
			ELSE
			ENDIF
		SIF RESULT
			RETURN 0
ENDIF

CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT

;=======================================================================
;タイル定義
;=======================================================================
@TILE_MARK_COMMON_83_4, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	CASE 2,3,4
	IF FLAG:ライトマ || (EQUIP:MASTER:DarkScanner && NUM_SUMMONER())
		RESULTS = \@GET_FLOOR_ANALYZE_T(ARG:1, ARG:2) == 2 ?Ｗ#□\@
	ELSE
		SETCOLOR 0x404040
		RESULTS = □
	ENDIF
	CASE 0,1,109,200,201
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	CASE 300
		RESULTS = ！
	CASEELSE
		RESULTS = \@GET_FLOOR_ANALYZE_T(ARG:1, ARG:2) == 2 ?Ｗ#□\@
ENDSELECT

@TILE_MARK_COMMON_83_D, ARG, ARG:1, ARG:2
IF FLAG:ライトマ || (EQUIP:MASTER:DarkScanner && NUM_SUMMONER())
	SELECTCASE ARG/10
		CASEELSE
			RESULTS = □
	ENDSELECT
ELSE
	SETCOLOR 0x404040
	SELECTCASE ARG/10
		CASEELSE
			RESULTS = □
	ENDSELECT
ENDIF

;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_83

;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) % 10 == 8
		CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TRESURING_CALLING_83
	RETURN RESULT

;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	CALL UPSTAIRS_83
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL DOWNSTAIRS_83
	RETURN RESULT

;==========
;イベントの処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4

SELECTCASE FLAG:現M
	CASE 0
		IF FLAG:現X == 4 && FLAG:現Y == 5
			CALL MESSAGE_WINDOW_D, "Suspect", "Do the police also like costume parties?/There's shouting and crying and things like demons roam about./Well, I also did stupid things in my time, wearing masks like that../I never saw something so ugly."
			;容疑者", "警察も仮装パーティーなんて、やるんか？/ギャーギャー叫びながら、悪魔みたいなのが歩き回ってるんだぜ。/俺も悪いことは、かなりやった方だが、奴らのつけてる仮面ほど、/まがまがしい表情は見たことがねえよ。"
		ELSEIF  FLAG:現X == 8 && FLAG:現Y == 5
			IF ダンジョンフラグ:83:2 == 0
				CALL MESSAGE_WINDOW_D, "Mr. Hyakutake", "I never though I'd be tied up in a case like this.../We came to investigate the reports of manster terror in this place,/I tried to tell the chief to stop."
				;百地警部", "まさか、おれがブタ箱にぶちこまれるとは、思わなかった。/この所の一連の化け物騒ぎを捜査を進めようとしたら、/署長が止めるように言って来やがった。
				CALL MESSAGE_WINDOW_D, "Mr. Hyakutake", "断ったら、即、ブタ箱行きでこのザマだ。/Apparently, it seems that the chief has caused this series of monsters./If you take care of the chief, maybe the mosters will disappear too." 
				;断ったら、即、ブタ箱行きでこのザマだ。/どうやら、一連の化け物騒ぎには、署長もからんでるらしい。/ひと思いに署長をヤっちまえば、化け物どもも、いなくなるかもしれんがな。
				IF ダンジョンフラグ:83:3 == 1
					CALL MESSAGE_WINDOW_D, "Mr. Hyakutake", "Hm?　A keycard?　Okay, I'll lend you one./Please take care of the monsters./As long as they are here, you can keep the keycard." 
					;ん？　カードキーだと？　いいぜ、お前に貸してやる。/このままじゃここでお陀仏しちまうしな。/化け物ども消えるまでやり過ごさせてもらうぜ
					CALL MESSAGE_WINDOW_D, "", "＞You got a keycard."
					;カードキーを手に入れた
					ダンジョンフラグ:83:2 = 1
				ENDIF
			ELSE
				CALL MESSAGE_WINDOW_D, "Mr. Hyakutake", "You can keep the keycard until the monsters are gone."
				;俺は化け物ども消えるまでやり過ごさせてもらうぜ
			ENDIF
		ENDIF
		
	CASE 1
		IF FLAG:現X == 7 && FLAG:現Y == 8
			CALL MESSAGE_WINDOW_D, "磯野刑事", "百地警部が、不当に監禁されてしまったので救出に向かうところなのだが…/署内は化け物がウロウロして、警部の所在も分からず、手が出せないのだ。/……ば、化け物が怖いわけじゃないぞ。"
			CALL MESSAGE_WINDOW_D, "磯野刑事", "で、化け物退治の専門家、デビルサマナーなる者がいるというので、/署内の化け物退治を依頼したのだ。/代理人には、￥1000000.も取られたが、尊敬する百地警部を助けるためだ。"
			CALL MESSAGE_WINDOW_D, "磯野刑事", "金には、かえられん。/……かえられんが、ヘソクリがパーだ……/せっかく、ダービー当てたのに……。"
		ENDIF
	CASE 2
		IF FLAG:現X == 3 && FLAG:現Y == 1
			CALL MESSAGE_WINDOW_D, "Policeman", "ほ、本官、警察の命とも言うべき/拳Gunの身柄を確保したいのでありますが……/フクザツ怪奇な瞬間移動地帯によって、進路を阻まれておるのであります！"
			;警官
			CALL MESSAGE_WINDOW_D, "Policeman", "Depending on the direction I enter from,/I end up in a different place!/Well, it feels like entering some kind of labyrinth! "
			;同じ場所なのに入る方角によって/別の場所に行ったりするのであります！/ま、まさに迷宮入りなのであります！ 
		ELSEIF  FLAG:現X == 3 && FLAG:現Y == 9
			CALL MESSAGE_WINDOW_D, "Policeman", "ひえー、悪魔が出たあ！ 悪魔だ、やつらは悪魔なんだ。/お、俺は警官だぞぉ！ そうだ、俺は知らん。/あいつらの相手は自衛隊が、すればいいんだ！"
		ENDIF
		ENDSELECT
	RETURN 1
	 
ELSE
	CALL MESSAGE_WINDOW_D, "", "＞It seems there is nothing here."
	;何も無いようだ
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_83,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) % 10 == 8
	CALL DUNGEON_EXIT
	RETURN 1

;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TRESURING_CALLING_83
	RETURN RESULT
	
;========
;罠の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 ==  5 
	CALL TRAP_83
	RETURN RESULT
	
;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1

;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	CALL UPSTAIRS_83
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL DOWNSTAIRS_83
	RETURN RESULT
	
;==========
;イベントの処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4
	SELECTCASE FLAG:現M 
	CASE 0
			IF FLAG:現X == 3 && FLAG:現Y == 10
				FLAG:現X = 8
				FLAG:現Y = 1
			ELSEIF FLAG:現X == 11 && FLAG:現Y == 10
				FLAG:現X = 6
				FLAG:現Y = 5
			ELSEIF FLAG:現X == 7 && FLAG:現Y == 7
			IF FLAG:現Y > ARG
				FLAG:現X = 4
				FLAG:現Y = 1
			ELSE
				FLAG:現X = 11
				FLAG:現Y = 11
			ENDIF
		ENDIF
	CASE 1
			IF FLAG:現X == 7 && FLAG:現Y == 6
				FLAG:現X = 10
				FLAG:現Y = 1
			ELSEIF FLAG:現X == 10 && FLAG:現Y == 1
				FLAG:現X = 7
				FLAG:現Y = 9
			ELSEIF FLAG:現X == 4 && FLAG:現Y == 1
				FLAG:現X = 7
				FLAG:現Y = 6
			ELSEIF FLAG:現X == 4 && FLAG:現Y == 5
				FLAG:現X = 13
				FLAG:現Y = 4
			ELSEIF FLAG:現X == 10 && FLAG:現Y == 5
				FLAG:現X = 1
				FLAG:現Y = 4
			ELSEIF FLAG:現X == 13 && FLAG:現Y == 1
				FLAG:現X = 7
				FLAG:現Y = 4
			ELSEIF FLAG:現X == 13 && FLAG:現Y == 7
				FLAG:現X = 1
				FLAG:現Y = 1
			ELSEIF FLAG:現X == 1 && FLAG:現Y == 1
				FLAG:現X = 13
				FLAG:現Y = 7
			ELSEIF FLAG:現X == 1 && FLAG:現Y == 7
				FLAG:現X = 13
				FLAG:現Y = 1
			ENDIF
		CASE 3
		IF FLAG:現X == 6 && FLAG:現Y == 1
		CALL MESSAGE_WINDOW_D, "Police Chief", "Please knock when entering the chief's office."
		;警察署長", "署長室に入る時はノックをしたまえ。"
		CALL MESSAGE_WINDOW_D, "Police Chief", @"Ah you.../You must be one of the rats who defeated our collaborators,/one after the other../%CALLNAME:MASTER%, was it?"
		;君は確か……/我々の協力者たちを次々と倒していると言うネズミ……/%CALLNAME:MASTER%くん……だったかな。
		CALL MESSAGE_WINDOW_D, "Police Chief", "Well, there is little a rat like you can do anyway./The plan is progressing well."
		;まあ、君のようなネズミ一匹が何をしようと、関係ない。/計画は、着々と進んでいる。
		CALL MESSAGE_WINDOW_D, "Police Chief", "Yes, most seals have been destroyed./And when the power of the last seal vanishes.."
		;そう、殆どの封印は破壊され、/あとは最後の封印の力が無くなった時……
		CALL MESSAGE_WINDOW_D, "Police Chief", "Well, thats nothing you have to worry about./Because this is the end of the road for you."
		;これ以上は、君が知っても無駄だろう。/なぜなら、君はここで最期を迎えるのだ。
		CALL MESSAGE_WINDOW_D, "", "＞The appearance of the Police Chief shifts into that of a demon."
		;警察署長の姿がみるみると悪魔の姿に変わっていく
		CALL MESSAGE_WINDOW_D, "Kumbhanda", "That you get to die by my hands,/you should be glad."
		;クバンダ	一兵卒が組織の幹部たる我が手にかかり死ねることを、/幸せに思うがよい。
		CALL MESSAGE_WINDOW_D, "", "＞１ Haunt Kumbhanda appeared!"
		
		FLAG:逃走不可フラグ = 1
		FLAG:会話不能フラグ = 1
		FLAG:先制不意打ちキャンセル = 1
		
			;クバンダ
				CALL SET_ENEMY,9,[[キャラ:クバンダ]],53,0,1
				CFLAG:(CHARANUM-1):ＨＰ補正 = 3500
				CFLAG:(CHARANUM-1):ＭＰ補正 = 8000
				CFLAG:(CHARANUM-1):行動回数 = 2
				CFLAG:(CHARANUM-1):速度補正 = 15
				BASE:(CHARANUM-1):力 += 5
				BASE:(CHARANUM-1):知恵 += 5
				BASE:(CHARANUM-1):魔力 += 5
				BASE:(CHARANUM-1):速さ += 10
				BASE:(CHARANUM-1):電撃 = 75
				BASE:(CHARANUM-1):衝撃 = 75
				BASE:(CHARANUM-1):疾風 = 75
				ABL:(CHARANUM-1):最低攻撃回数 = 2
				ABL:(CHARANUM-1):最大攻撃回数 = 3
				ABL:(CHARANUM-1):スキル1 = [[スキル:闇の審判]]
				ABL:(CHARANUM-1):スキル2 = [[スキル:絶対零度]]
				ABL:(CHARANUM-1):スキル3 = [[スキル:マカラカーン]]
				ABL:(CHARANUM-1):スキル4 = [[スキル:魔界の調べ]]
				ABL:(CHARANUM-1):スキル5 = [[スキル:メギドラ]]
				ABL:(CHARANUM-1):スキル6 = [[スキル:大冷界]]
				ABL:(CHARANUM-1):アイテム3 = GETNUM(ITEM, "EnIncense")
				ABL:(CHARANUM-1):入手確率3 = 100
				CSTR:(CHARANUM-1):思考パターン = D83_クバンダ
				CALL SYNC_STATUS,(CHARANUM-1)
				CALL HEALTH_CHARA,(CHARANUM-1)
				CALL BATTLE_START
		FLAG:逃走不可フラグ = 0
		FLAG:会話不能フラグ = 0
		FLAG:先制不意打ちキャンセル = 0
		
		CALL MESSAGE_WINDOW_D, "Kumbhanda", "...I didn't think this wuld end like that./However, it won't be long befor e the power of the last seal is gone./Then, you'll also meet your end..."
		;……私が最期を迎えるとはな。/だが、封印の力が無くなるまで、まもなくだ/その時は、君も最期だ……。
		CALL MESSAGE_WINDOW_D, "", "＞Haunt Kumbhanda defeated..."
		CALL MESSAGE_WINDOW_D, "", "＞For subjugating the traitor and defeating the Haunt Kumbhanda,/＞you where paid ￥50000 and your fame increased by 200."
		;強行派の裏切り者・幽鬼 クバンダを討伐したことにより、/＞報酬として口座に￥50000が支払われ、名声200を得た
		MONEY += 50000
		FLAG:名声 += 200
		ダンジョンフラグ:83:0 = 1
		FLAG:脱出 = 1
		ENDIF
	ENDSELECT
ENDIF

;クバンダ戦闘行動
@SET_ACTION_D83_クバンダ,ARG
;入力行動を-1とターゲットを-1にしておく
CFLAG:ARG:入力行動 = -1
CFLAG:ARG:ターゲット = -1

IF CFLAG:ARG:１moreフラグ == 2
	IF RAND:2 == 0
CFLAG:ARG:入力行動 = [[スキル:大冷界]]
	ELSE
CFLAG:ARG:入力行動 = [[スキル:メギドラ]]
	ENDIF
	
ELSEIF CFLAG:ARG:行動した回数 == 0
	SELECTCASE RAND:100
		CASE 0 TO 20
			CFLAG:ARG:入力行動 = [[スキル:絶対零度]]
		CASE 21 TO 50
			CFLAG:ARG:入力行動 = [[スキル:マカラカーン]]
		CASE 51 TO 80
			CFLAG:ARG:入力行動 = [[スキル:魔界の調べ]]
		CASEELSE
			CFLAG:ARG:入力行動 = [[スキル:闇の審判]]
		ENDSELECT
ELSE
		SELECTCASE RAND:100
		CASE 0 TO 24
			CFLAG:ARG:入力行動 = [[スキル:絶対零度]]
		CASE 25 TO 49
			CFLAG:ARG:入力行動 = [[スキル:魔界の調べ]]
		CASE 50 TO 75
			CFLAG:ARG:入力行動 = [[スキル:メギドラ]]
		CASE 76 TO 90
			CFLAG:ARG:入力行動 = [[スキル:ＡＴＴＡＣＫ]]
		CASEELSE
			CFLAG:ARG:入力行動 = [[スキル:大冷界]]
		ENDSELECT
	ENDIF
	
;アクション実行不能
CALL CHECK_ACTIONABLE, ARG, CFLAG:ARG:入力行動
SIF RESULT == 0
	CFLAG:ARG:入力行動 = 0
;ここまでターゲットが決まってなかったら、ランダムターゲットをよんで決定
SIF CFLAG:ARG:ターゲット == -1
	CALL RANDOM_TARGET, ARG, CFLAG:ARG:入力行動
RETURN 1

;========
;階段移動
;========
@UPSTAIRS_83
SELECTCASE FLAG:現M
	CASE 0
		SIF FLAG:現X == 6 && FLAG:現Y == 1
			CALL DUNGEON_UPSTAIRS,1,6,1
	CASE 1
		SIF FLAG:現X == 8 && FLAG:現Y == 1
			CALL DUNGEON_UPSTAIRS,2,7,1
	CASE 2
		SIF FLAG:現X == 7 && FLAG:現Y == 9
			CALL DUNGEON_UPSTAIRS,3,6,9
		ENDSELECT
RETURN 1

@DOWNSTAIRS_83
SELECTCASE FLAG:現M
	CASE 1
		SIF FLAG:現X == 6 && FLAG:現Y == 1
			CALL DUNGEON_DOWNSTAIRS,0,6,1
	CASE 2
		SIF FLAG:現X == 7 && FLAG:現Y == 1
			CALL DUNGEON_DOWNSTAIRS,1,8,1
	CASE 3
		SIF FLAG:現X == 6 && FLAG:現Y == 9
			CALL DUNGEON_DOWNSTAIRS,2,7,9
		ENDSELECT
RETURN 1
;=======================================================================
;エレベーター
;=======================================================================
@DUNGEON_ELEVATOR_83, ARG
RESULT:1 = 0
RESULT:2 = 1
RESULT:3 = 2
RESULT:4 = 3
RESULTS:1 = Ｂ１Ｆ
RESULTS:2 = １Ｆ
RESULTS:3 = ２Ｆ
RESULTS:4 = ３Ｆ
RETURN -1

;=======================================================================
;宝箱の中身
;=======================================================================
@TRESURING_CALLING_83
#LOCALSIZE 3
LOCAL = FLAG:現M + 10

LOCAL:1 = (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
		CASE 100
			CALL GET_TREASURE, [[アイテム:NapalmBomb]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
		CASE 300
			CALL GET_TREASURE, [[アイテム:Gas Mask Type 6]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
		CASE 301
			CALL GET_TREASURE, [[アイテム:Ｍ249 Minimi]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
		CASE 302
			CALL GET_TREASURE, [[アイテム:Grenade Launcer]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
	ENDSELECT
RETURN 1


;========
;罠置き場
;========
@TRAP_83
SELECTCASE FLAG:現M
	CASE 3
		CALL MESSAGE_WINDOW_D, "", "＞It's a pitfall!"
				CALL DUNGEON_WORP, FLAG:現M-1, FLAG:現X,  FLAG:現Y
	ENDSELECT

;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_83

SELECTCASE FLAG:現M
	CASE 0
		FLAG:エンカウント率 = 5 + FLAG:未遭遇歩数*5
	CASE 1,2
		FLAG:エンカウント率 = 10 + FLAG:未遭遇歩数*5
	CASEELSE 
		FLAG:エンカウント率 = 15 + FLAG:未遭遇歩数*5
ENDSELECT
SIF FLAG:未遭遇歩数 < 5
	FLAG:エンカウント率 = 0
;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_83
RETURN 44, 40, 45, 53 

;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_83
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_83
RETURN CLASS_NUM_COMMON("上級")
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;=======================================================================
@ENEMY_NUM_83, ARG
RETURN ENEMY_NUM_COMMON("上級", ARG)
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_83, ARG
SELECTCASE FLAG:現M
	CASE 0,1,2,
		SELECTCASE RAND:100
			CASE IS < 30
				RETURN [[キャラ:チン]], 40
			CASE IS < 55
				RETURN [[キャラ:レギオン]], 41
			CASE IS < 70
				RETURN [[キャラ:アマゾーン]], 40
			CASE IS < 85
				RETURN [[キャラ:バジリスク]], 42
			CASE IS < 100
				RETURN [[キャラ:ゾンビコップ]], 40
		ENDSELECT
CASEELSE
		SELECTCASE RAND:100
			CASE IS < 30
				RETURN [[キャラ:スプリガン]], 43
			CASE IS < 55
				RETURN [[キャラ:サキュバス]], 44
			CASE IS < 70
				RETURN [[キャラ:ベルセルク]], 45
			CASE IS < 85
				RETURN [[キャラ:パワー]], 45
			CASE IS < 100
				RETURN [[キャラ:ゾンビコップ]], 43
			ENDSELECT
ENDSELECT

;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_83
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:3
	CASE 0
		RETURN [[キャラ:ゾンビコップ]] , 3 + RAND:10 , CSVBASE([[キャラ:ゾンビコップ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:ベルセルク]] , 3 + RAND:10 , CSVBASE([[キャラ:ベルセルク]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:レギオン]] , 3 + RAND:10 , CSVBASE([[キャラ:レギオン]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT