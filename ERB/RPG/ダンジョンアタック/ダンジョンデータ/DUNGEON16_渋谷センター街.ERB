;=======================================================================
;サンプルダンジョン
;ダンジョンのテンプレートみたいなものです
;ダンジョンナンバー0
;=======================================================================




;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_16
;チェックマンクリア
IF 闘技場フラグ:13:0 == 1 && ダンジョンフラグ:16:14 == 0
	RETURN 1
ELSE
	RETURN 0
ENDIF

;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_16
RESULTS = Shibuya town center
;渋谷センター街

;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_16
PRINT Shibuya town center 
;渋谷センター街
SELECTCASE FLAG:現M
	CASE 0
		PRINTL Underground shopping centre B2F
		;地下街
	CASE 1
		PRINTL Underground shopping centre B1F
	CASE 2
		PRINTL Main street
		;表通り
	CASE 3
		PRINTL Gaean Branch Shibuya 1F
		;ガイア教・渋谷支部
	CASE 4
		PRINTL Gaean Branch Shibuya 2F
	CASE 5
		PRINTL Gaean Branch Shibuya 3F
	CASE 6
		PRINTL Gaean Branch Shibuya 4F
	CASEELSE
		PRINTL
ENDSELECT

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_16,ARG
FLAG:現M = 2
FLAG:現X = 1
FLAG:現Y = 6
;CALL AUTOMAP

;=======================================================================
;スタート時イベント
;=======================================================================
@EVENT_DUNGEON_START_16
;T.D.L.の設定ずれやすでにシンジュク到達していることや
;単純に東京を扱う事が多いのでテキストを調整しておきます
IF ダンジョンフラグ:(FLAG:現ダンジョン):10 == 0
	CALL SHOW_PICTURE, "D"
	CALL MESSAGE_WINDOW_D,  "", "＞Shibuya.../＞Once, this was a fancy city full of aspiring young people./＞Now however, gang members with mohawk heads and tuned motorcycles dominate the city during the day/＞and demons roam the city, clearly having had a foothold here for some time."
	;＞渋谷…/＞かつては洒落た若者で賑わう活気のある街だったらしいが/＞現在は白昼堂々と改造バイクを乗り回すモヒカン頭や/＞まるで昔からそこにいたかのような悪魔達が街を跳梁跋扈している
	CALL MESSAGE_WINDOW_D,  "", "＞『Gaean teachings』, aiming to create harmony between humans and demons,/＞propagated by a group of people at the verge of madness,/＞have Shibuya as their main place of influnce./＞And under the guidance of their followers, much the town has been remodeled."
	;『ガイア教』、悪魔と人間との融和を目指すという/＞とても正気の沙汰とは思えない教理を崇拝する集団･･･/＞ここ渋谷は奴らの活動拠点の一つとして、/＞街全体が不気味に造り変えられてしまったのだ
	CALL MESSAGE_WINDOW_D,  "", "＞According to the checkmen, the Gaeans here in Shibuya have one of the star powers./＞If you aim to get that star power,/＞so you can reach the 'new world' the checkmen talked about,/＞you might inevitably clash with the Gaean community..."
	;チェックマンはここ渋谷のガイア教に、星の力の一つがあると言っていた/＞彼らの言っていた「星の力」を手にし、/＞「新たな世界」へと到達するためには/＞おそらくガイア教団との戦闘は避けられないだろう……
	ダンジョンフラグ:(FLAG:現ダンジョン):10 = 1
ENDIF

;=======================================================================
;フロアデータ
;=======================================================================
@MAKE_FLOOR_16
SELECTCASE FLAG:現M
	CASE 0
		;地下街B2F
		FLAG:最大X = 19
		FLAG:最大Y = 13
		;						 0000000000111111111
		;						 0123456789012345678
		CALL MAKE_FLOOR_LINE, 0,"0000000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0       9 3       0"
		CALL MAKE_FLOOR_LINE, 2,"0       2 2       0"
		CALL MAKE_FLOOR_LINE, 3,"0      11111      0"
		CALL MAKE_FLOOR_LINE, 4,"0        1        0"
		CALL MAKE_FLOOR_LINE, 5,"0921112111111     0"
		CALL MAKE_FLOOR_LINE, 6,"0           1     0"
		CALL MAKE_FLOOR_LINE, 7,"0           1111160"
		CALL MAKE_FLOOR_LINE, 8,"0           1     0"
		CALL MAKE_FLOOR_LINE, 9,"0      111111     0"
		CALL MAKE_FLOOR_LINE,10,"0       2 2       0"
		CALL MAKE_FLOOR_LINE,11,"0       1 9       0"
		CALL MAKE_FLOOR_LINE,12,"0000000000000000000"
	CASE 1
		;地下街B1F
		FLAG:最大X = 16
		FLAG:最大Y = 16
		;						 00000000001111111
		;						 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"0000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"0         9 9 90"
		CALL MAKE_FLOOR_LINE, 2,"0         2 2 20"
		CALL MAKE_FLOOR_LINE, 3,"0  1      111110"
		CALL MAKE_FLOOR_LINE, 4,"032111111   1  0"
		CALL MAKE_FLOOR_LINE, 5,"0  1    11211  0"
		CALL MAKE_FLOOR_LINE, 6,"0421           0"
		CALL MAKE_FLOOR_LINE, 7,"0  1    9 9 9  0"
		CALL MAKE_FLOOR_LINE, 8,"0 71116 2 2 2  0"
		CALL MAKE_FLOOR_LINE, 9,"0       11111  0"
		CALL MAKE_FLOOR_LINE,10,"0       1 1 1160"
		CALL MAKE_FLOOR_LINE,11,"0       1 1 1  0"
		CALL MAKE_FLOOR_LINE,12,"0       11111  0"
		CALL MAKE_FLOOR_LINE,13,"0       2 2 2  0"
		CALL MAKE_FLOOR_LINE,14,"0       9 9 9  0"
		CALL MAKE_FLOOR_LINE,15,"0000000000000000"
	CASE 2
	;表通り
		FLAG:最大X = 14
		FLAG:最大Y = 12
		CALL MAKE_FLOOR_LINE, 0,"00000008080000"
		CALL MAKE_FLOOR_LINE, 1,"0      1 1 7 0"
		CALL MAKE_FLOOR_LINE, 2,"0      1 111 0"
		CALL MAKE_FLOOR_LINE, 3,"0      1 1   0"
		CALL MAKE_FLOOR_LINE, 4,"0    6 1 1   0"
		CALL MAKE_FLOOR_LINE, 5,"0    G 1 1   0"
		CALL MAKE_FLOOR_LINE, 6,"81111111111118"
		CALL MAKE_FLOOR_LINE, 7,"0      1 1   0"
		CALL MAKE_FLOOR_LINE, 8,"0      111   0"
		CALL MAKE_FLOOR_LINE, 9,"0        1 7 0"
		CALL MAKE_FLOOR_LINE,10,"0        11118"
		CALL MAKE_FLOOR_LINE,11,"00000000000000"

	CASE 3
		;ガイア教・渋谷支部1F
		FLAG:最大X = 17
		FLAG:最大Y = 17
		;						 00000000001111111
		;						 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"00000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"01111111 11111110"
		CALL MAKE_FLOOR_LINE, 2,"0111   1   2 1  0"
		CALL MAKE_FLOOR_LINE, 3,"0111 3 11111 1 60"
		CALL MAKE_FLOOR_LINE, 4,"01   2 2 111 2 20"
		CALL MAKE_FLOOR_LINE, 5,"0121 1 1 111 1 10"
		CALL MAKE_FLOOR_LINE, 6,"01   1       1 10"
		CALL MAKE_FLOOR_LINE, 7,"01 11111 1 111 10"
		CALL MAKE_FLOOR_LINE, 8,"01   1   2 1   20"
		CALL MAKE_FLOOR_LINE, 9,"01 1 1 T 1 121110"
		CALL MAKE_FLOOR_LINE,10,"02 2 1 2 1     20"
		CALL MAKE_FLOOR_LINE,11,"01111111111121 10"
		CALL MAKE_FLOOR_LINE,12,"01     11111   10"
		CALL MAKE_FLOOR_LINE,13,"01 321 1111121 10"
		CALL MAKE_FLOOR_LINE,14,"01   1 1 2     10"
		CALL MAKE_FLOOR_LINE,15,"011111 1 7 111110"
		CALL MAKE_FLOOR_LINE,16,"00000000000000000"
	CASE 4
		;ガイア教・渋谷支部2F
		FLAG:最大X = 17
		FLAG:最大Y = 17
		;						 00000000001111111
		;						 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"00000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"011111 1111111110"
		CALL MAKE_FLOOR_LINE, 2,"01           2 20"
		CALL MAKE_FLOOR_LINE, 3,"0121 111112121 70"
		CALL MAKE_FLOOR_LINE, 4,"01     111   1  0"
		CALL MAKE_FLOOR_LINE, 5,"0121112111 111110"
		CALL MAKE_FLOOR_LINE, 6,"0U       1     10"
		CALL MAKE_FLOOR_LINE, 7,"011111R121 111210"
		CALL MAKE_FLOOR_LINE, 8,"0    D     U    0"
		CALL MAKE_FLOOR_LINE, 9,"012111 121L111110"
		CALL MAKE_FLOOR_LINE,10,"01     1       D0"
		CALL MAKE_FLOOR_LINE,11,"011111 1112111210"
		CALL MAKE_FLOOR_LINE,12,"0  1   111     10"
		CALL MAKE_FLOOR_LINE,13,"04 121211111 3210"
		CALL MAKE_FLOOR_LINE,14,"02 2           10"
		CALL MAKE_FLOOR_LINE,15,"0111111123 621110"
		CALL MAKE_FLOOR_LINE,16,"00000000000000000"
		;UDLR 一方通行タイル
	CASE 5
		;ガイア教・渋谷支部3F
		FLAG:最大X = 17
		FLAG:最大Y = 17
		;						 00000000001111111
		;						 01234567890123456
		CALL MAKE_FLOOR_LINE, 0,"00000000000000000"
		CALL MAKE_FLOOR_LINE, 1,"032111 4 111 1110"
		CALL MAKE_FLOOR_LINE, 2,"0  1   2   1   10"
		CALL MAKE_FLOOR_LINE, 3,"0111 11111 111 10"
		CALL MAKE_FLOOR_LINE, 4,"0111 11111 111 10"
		CALL MAKE_FLOOR_LINE, 5,"0111 11411 111 10"
		CALL MAKE_FLOOR_LINE, 6,"0  1   1   1   10"
		CALL MAKE_FLOOR_LINE, 7,"01 111 1 111 3 10"
		CALL MAKE_FLOOR_LINE, 8,"02   1 1 1   2 10"
		CALL MAKE_FLOOR_LINE, 9,"0111 6 1 1 111 10"
		CALL MAKE_FLOOR_LINE,10,"0  1 1 G 1 1   10"
		CALL MAKE_FLOOR_LINE,11,"01 121 1 121 1 10"
		CALL MAKE_FLOOR_LINE,12,"02 1   2   1 1 20"
		CALL MAKE_FLOOR_LINE,13,"01111111111111210"
		CALL MAKE_FLOOR_LINE,14,"0  2   2 2   1 10"
		CALL MAKE_FLOOR_LINE,15,"011111 T 1 721 10"
		CALL MAKE_FLOOR_LINE,16,"00000000000000000"
	CASE 6
		;ガイア教・渋谷支部4F
		FLAG:最大X = 11
		FLAG:最大Y = 11
		CALL MAKE_FLOOR_LINE, 0,"00000000000"
		CALL MAKE_FLOOR_LINE, 1,"03 11111110"
		CALL MAKE_FLOOR_LINE, 2,"02 1     10"
		CALL MAKE_FLOOR_LINE, 3,"0111 111 10"
		CALL MAKE_FLOOR_LINE, 4,"0    2 1 10"
		CALL MAKE_FLOOR_LINE, 5,"0111 1 1 10"
		CALL MAKE_FLOOR_LINE, 6,"0111   1 10"
		CALL MAKE_FLOOR_LINE, 7,"01111111 10"
		CALL MAKE_FLOOR_LINE, 8,"02 11111 20"
		CALL MAKE_FLOOR_LINE, 9,"01 11711210"
		CALL MAKE_FLOOR_LINE,10,"00000000000"
	CASEELSE





ENDSELECT


;=======================================================================
;タイル調整
;=======================================================================
@MAKE_FLOOR_LINE_COMMON_16, ARG, ARG:1, ARG:2
#LOCALSIZE 30
;LOCAL:3 宝箱 ダンジョンフラグ:75~99
;LOCAL:4 イベント
;LOCAL:8 メッセンジャー
;(0/0)でLOCALを消去
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
IF ARG == 3
	IF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):(FLAG:現M+75), LOCAL:3)
		DA:(ARG:1):(ARG:2) = 1
	ELSE
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:3 * 10 + 3
	ENDIF
	LOCAL:3++
ENDIF
;イベント扉
IF ARG == 12
	DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:2 * 10 + 12
	LOCAL:2++
ENDIF
;イベント
IF ARG == 4
	DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:4 * 10 + 4
	LOCAL:4++
	SELECTCASE DA:(ARG:1):(ARG:2) / 10
		;ベスフラグ
		CASE 400
			SIF ダンジョンフラグ:(FLAG:現ダンジョン):12
				DA:(ARG:1):(ARG:2) = 1
		;公開処刑
		CASE 501
			SIF ダンジョンフラグ:(FLAG:現ダンジョン):13
				DA:(ARG:1):(ARG:2) = 1
	ENDSELECT
ENDIF
;上り階段
IF ARG == 6
	DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:6 * 100 + 6
	LOCAL:6++
ENDIF
;下り階段
IF ARG == 7
	DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:7 * 100 + 7
	LOCAL:7++
ENDIF
;メッセンジャー
IF ARG == 9
	DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:9 * 10 + 9
	LOCAL:9++
	SELECTCASE DA:(ARG:1):(ARG:2) / 10
	ENDSELECT
ENDIF

;=======================================================================
;タイル定義
;=======================================================================
@DEFINE_TILES_16, ARGS, ARG, ARG:1
;G イベント扉
SIF ARGS == "G"
	RETURN 12
SIF ARGS == "U"
	RETURN 22
SIF ARGS == "D"
	RETURN 32
SIF ARGS == "L"
	RETURN 42
SIF ARGS == "R"
	RETURN 52
;=======================================================================
;指定したマスに進入可能か
;及び、ドアイベント
;=======================================================================
;-----------------------------------------------------------------------
;ドアの表示定義
;-----------------------------------------------------------------------
@TILE_MARK_COMMON_16_2, ARG, ARG:1, ARG:2
SELECTCASE ARG
	CASE 12
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	CASE 22
		RESULTS = ↑
	CASE 32
		RESULTS = ↓
	CASE 42
		RESULTS = →
	CASE 52
		RESULTS = ←
	CASEELSE
		RESULTS = 扉
ENDSELECT
@CAN_ENTER_16,ARG,ARG:1
SELECTCASE DA:ARG:(ARG:1)
	;↑
	CASE 22
		RETURN ARG:1 < FLAG:現Y
	;↓
	CASE 32
		RETURN ARG:1 > FLAG:現Y
	;→
	CASE 42
		RETURN ARG > FLAG:現X
	;←
	CASE 52
		RETURN ARG < FLAG:現X
	CASE 2012
		IF ダンジョンフラグ:(FLAG:現ダンジョン):14 == 2
			CALL MESSAGE_WINDOW_D,  "Guard", "Oh, it's you./You can go on inside."
			;守衛", "おぅ、お前らか/中に入っていいぜ
		ELSE
			CALL MESSAGE_WINDOW_D,  "Guard", "This is the Gaean Branch of Shibuya./Well, what on earth do you want here?"
			;"ここはガイア教団渋谷支部だぜ/てめぇら、一体何のようだ？
			IF !ダンジョンフラグ:(FLAG:現ダンジョン):11
				CALL MESSAGE_WINDOW_D,  "Guard", "...well?/You want to enter?/Then, as usual, show me your card."
				;……あん？/この中に入りたい？/それじゃ、いつも通り、あれ見せろよ
				CALL MESSAGE_WINDOW_D,  "Guard", "You don't have it?/...what's that?/You have no idea where you put it?"
				;あれを持ってないのか？/……あれって何だって？/そんなことも知らねぇでここに入れると思ってんのか？
				CALL MESSAGE_WINDOW_D,  "Guard", "Well, if you want inside, you better find it then!"
				;痛い目を見たくなかったらとっとと失せな！
				RETURN 0
			ELSE
				CALL MESSAGE_WINDOW_D,  "Guard", "...Ah, you got your member card now./Um, sorry about before."
				;…なーんだ、団員証をちゃんと持ってるじゃねーか/窮屈な真似してすまなかったな
				CALL MESSAGE_WINDOW_D,  "Guard", "Well, we didn't always check./But recently, some Temple knights managed to inflitrate./Since then, we have to ask everyone for identification."
				;以前はこんなことしてなかったんだけどよ/つい最近、テンプルナイト共がそれにかこつけて潜入してきたもんで/以来、身元の確認は徹底しろとのお達しなんだ
				CALL MESSAGE_WINDOW_D,  "Guard", "Well, at least one of them survived./And it seems she will be executed in a public show today./I kind of feel sorry for her. Such a young woman and she won't get much older../Um, sorry. You can go in."
				;そういや、そのうちしぶとく生き残った一人を/今日、見せしめに公開処刑するらしいぜ/また年端もいかない小娘だったから気の毒にも感じるがな…/おっとすまないな。中に入っていいぜ
			ENDIF
		ENDIF
	CASE 5012
		IF ダンジョンフラグ:(FLAG:現ダンジョン):13
			CALL MESSAGE_WINDOW_D,  "", "＞This door seems to be locked from the other side."
			;＞反対側から鍵がかけられているようだ
			RETURN 0
		ENDIF
ENDSELECT
CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT


;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_16
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;==========
;イベントの処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4
	CALL EVENT_16
	RETURN RESULT
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_16
	RETURN RESULT
;=======
;ワープゾーン
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6 || DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL WORP_16
	RETURN RESULT
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 9
	CALL MESSAGE_16, 0
	RETURN 1
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_16,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_16
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6 || DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL WORP_16
	RETURN RESULT
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 9
	CALL MESSAGE_16, 0
	RETURN 1

;========
;イベントの処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4
	CALL EVENT_16
	RETURN RESULT
;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y)  % 10 == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1
ENDIF
RETURN 0

;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_16
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	CASE 0
		CALL GET_TREASURE, GETNUM(ITEM, "Life stone"), 3, FLAG:現ダンジョン,0
	CASE 100
		CALL GET_TREASURE, GETNUM(ITEM, "ValueMedicine"), 1, FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	CASE 300
		CALL GET_TREASURE, GETNUM(ITEM, "Lantern Shell"), 10, FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	CASE 301
		CALL GET_TREASURE, GETNUM(ITEM, "ChakraDrop"), 1 , FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	CASE 400
		CALL GET_TREASURE, GETNUM(ITEM, "Bead"), 1, FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	CASE 401
		CALL GET_TREASURE, GETNUM(ITEM, "Medicine"), 1, FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	CASE 500
		CALL GET_TREASURE, GETNUM(ITEM, "火炎の勾玉"), 1, FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	CASE 501
		CALL GET_TREASURE, GETNUM(ITEM, "Life stone"), 2 , FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
	CASE 600
		CALL GET_TREASURE, GETNUM(ITEM, "StIncense"), 1, FLAG:現ダンジョン, FLAG:現M + 75, (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100
ENDSELECT
RETURN 1

@EVENT_16
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	CASE 100
		CALL SHOW_PICTURE, "D"
		CALL MESSAGE_WINDOW_D,  "", "It's a disco.../The interior is crowed with people."
		;ディスコだ…/ディスコ内は人でごった返している
		IF ダンジョンフラグ:(FLAG:現ダンジョン):11 == 0
			CALL MESSAGE_WINDOW_D,  "", "＞It seems you could collect some information here./＞Would you like to ask arround?"
			;ここでなら何か情報収集できそうだ/＞話を聞いてまわろうか？
			CALL INPUT_YN_D,"Yes","No"
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D,  "", "........./...../..."
				CALL MESSAGE_WINDOW_D,  "Drunk youth", "Ohh./Hey you, did you look at me funny!?"
				;酔っ払った若者", "ぅおぃてめぇ/てめぇ、俺らのこと、見てただろ！？
				CALL MESSAGE_WINDOW_D,  "Another drunkard", "You got a problem with that we get drunk during the day, huh!"
				;別の酔っぱらい", "昼間から酔っ払っててなにがわりぃんだ！
				CALL MESSAGE_WINDOW_D,  "Angry youth", "Ugh, that does it!"
				;怒っている若者", "うぜぇんだよ！
				CALL MESSAGE_WINDOW_D,  "", "＞You got involved with a group of drunk youths.."
				;泥酔した若者の集団に絡まれてしまった…
				CALL INPUT_YN_D, "Beat them up", "Apologize and leave"
				;叩きのめす", "謝って場をおさめる
				IF RESULT == 0
					CALL INCREASE_LC, -4
					CALL MESSAGE_WINDOW_D,  "Drunkard", "Hah! This one wants to dance!"
					;酔っぱらい", "てめぇ！　やる気か！
					CALL MESSAGE_WINDOW_D,  "Drunk youth", "We'll rough you up!"
					;ぶっころしてやる！
				ELSE
					CALL INCREASE_LD, 4
					CALL MESSAGE_WINDOW_D,  "Drunkard", "You must be mad if you think you'll get away with that!"
					;ごちゃごちゃうるせー！
					CALL MESSAGE_WINDOW_D,  "Drunk youth", "We'll rough you up!"
				ENDIF
				CALL MESSAGE_WINDOW_D,  "", "＞The drunken youths are attacking!"
				;若者達が襲い掛かってきた！
				FLAG:逃走不可フラグ = 1
				FLAG:会話不能フラグ = 1
				FLAG:先制不意打ちキャンセル = 1
				CALL SET_ENEMY,7,[[キャラ:ハイウェイスター]],33,0,0
				CALL SET_ENEMY,8,[[キャラ:ハイウェイスター]],33,0,0
				CALL SET_ENEMY,9,[[キャラ:ハイウェイスター]],33,0,0
				CALL SET_ENEMY,10,[[キャラ:ハイウェイスター]],33,0,0
				CALL SET_ENEMY,11,[[キャラ:ハイウェイスター]],33,0,0
				CALL BATTLE_START
				CALL SHOW_PICTURE, "D"
				FLAG:逃走不可フラグ = 0
				FLAG:会話不能フラグ = 0
				FLAG:先制不意打ちキャンセル = 0
				CALL MESSAGE_WINDOW_D,  "", @"＞Defeated the youths./＞...some fell out of their pocket during the fighting."
				;若者達を叩きのめした/＞……若者達のポケットから何かが出ている
				CALL MESSAGE_WINDOW_D,  "", "＞It's a membership card for the Gaean community. You take it."
				;ガイア教団の団員証を手に入れた
				CALL MESSAGE_WINDOW_D,  "", @"＞%CALLNAME:MASTER% decided to leave before anyone causes a fuss..."
				;%CALLNAME:MASTER%は騒ぎになる前にその場を離れた……
				ITEM:ガイア教団団員証 = 1
				ダンジョンフラグ:(FLAG:現ダンジョン):11 = 1
			ENDIF
		ELSE
			CALL MESSAGE_WINDOW_D,  "", "＞You have no more business here."
			;もうここに用はない
		ENDIF
	CASE 400
		CALL SHOW_PICTURE, "D"
		CALL MESSAGE_WINDOW_D,  "", "＞This place has a lot of torture tools lying arround./＞It's very messy."
		IF ダンジョンフラグ:(FLAG:現ダンジョン):13 == 0
			CALL MESSAGE_WINDOW_D,  "", "＞Some of the tools and parts of the floor have fresh blood on them."
			CALL MESSAGE_WINDOW_D,  "Voice", "...Gou!...Ah!..."
			CALL MESSAGE_WINDOW_D,  "", "＞Suddenly, you heard bitter groaning that seems to stem from a woman."
			CALL MESSAGE_WINDOW_D,  "Voice", "..Ouch!...Ah!..."
			CALL MESSAGE_WINDOW_D,  "Voice", "......help...."
			CALL MESSAGE_WINDOW_D,  "", "＞However, there is no-one in this room.../＞The voice must come from somewhere else..."
			ダンジョンフラグ:(FLAG:現ダンジョン):12 = 1
		ENDIF
	CASE 500
		CALL SHOW_PICTURE, "D"
		CALL MESSAGE_WINDOW_D,  "", "＞Inside the hall, there was a room partitioned with curtains, with a huge demon inside."
		;ホールの奥、カーテンで仕切られた部屋の中には巨大な悪魔がいた…
		IF ダンジョンフラグ:(FLAG:現ダンジョン):13 == 1
			CALL MESSAGE_WINDOW_D,  "Demon", "...you don't seem to be a mesian.../But who else would inferere with our pleasant masses?/Ah whatever...I don't care what you are./I'll just kill you for obstructing the masses of the Gaeans!"
			;…メシア教徒というわけではないようだが…/しかし楽しい楽しい我らのミサを妨害するとはどういう了見だ？/まぁよかろう…。貴様らが何者であろうが関係はない/ガイア教のミサを妨害した罪は、死を持ってあがなってもらう！
		ELSE
			CALL MESSAGE_WINDOW_D,  "Demon", "……何だ貴様は？/いまは我らのミサの時間だ/用事があるならあとにしてもらおうか……"
			CALL MESSAGE_WINDOW_D,  "Demon", "いや、待て、ちょうどいい/ミサを見て血がたぎっていたところだ/八つ裂きにして我も楽しませてもらうとしようぞ！"
		ENDIF
		CALL MESSAGE_WINDOW_D,  "", "＞Tyrant Balaam appeared!"
		FLAG:逃走不可フラグ = 1
		FLAG:会話不能フラグ = 1
		FLAG:先制不意打ちキャンセル = 1
		;SET_ENEMY,敵のポジション, 敵のキャラ番号、レベル、死体残存フラグ,ボスフラグとなっています
		CALL SET_ENEMY,9,[[キャラ:バラム]],35,0,1
		CFLAG:(CHARANUM-1):ＨＰ補正 = 1000
		CFLAG:(CHARANUM-1):ＭＰ補正 = 8000
		CSTR:(CHARANUM-1):思考パターン = 209
		CFLAG:(CHARANUM-1):行動回数 = 4
		CALL SYNC_STATUS,(CHARANUM-1)
		CALL HEALTH_CHARA,(CHARANUM-1)

		CALL SET_ENEMY,7,[[キャラ:ミュータント]],35,0,0
		CFLAG:(CHARANUM-1):ＨＰ補正 = 150
		CALL SYNC_STATUS,(CHARANUM-1)
		CALL HEALTH_CHARA,(CHARANUM-1)

		CALL SET_ENEMY,8,[[キャラ:ヘル]],35,0,0
		CFLAG:(CHARANUM-1):ＨＰ補正 = 150
		CALL SYNC_STATUS,(CHARANUM-1)
		CALL HEALTH_CHARA,(CHARANUM-1)

		CALL SET_ENEMY,10,[[キャラ:ヘル]],35,0,0
		CFLAG:(CHARANUM-1):ＨＰ補正 = 150
		CALL SYNC_STATUS,(CHARANUM-1)
		CALL HEALTH_CHARA,(CHARANUM-1)

		CALL SET_ENEMY,11,[[キャラ:ミュータント]],35,0,0
		CFLAG:(CHARANUM-1):ＨＰ補正 = 150
		CALL SYNC_STATUS,(CHARANUM-1)
		CALL HEALTH_CHARA,(CHARANUM-1)

		CALL BATTLE_START
		
		FLAG:逃走不可フラグ = 0
		FLAG:会話不能フラグ = 0
		FLAG:先制不意打ちキャンセル = 0

		CALL MESSAGE_WINDOW_D,  "Tyrant Balaam", "…そ…、そんな…馬鹿な…"
		;魔王 バラム", "…そ…、そんな…馬鹿な…
		CALL MESSAGE_WINDOW_D,  "", @"＞Tyrant Balaam was defeated./＞A star shaped jewel appeared from the carcass of Balaam./＞You got the darkness star power!"
		;魔王バラムは息絶えた/＞バラムの死骸から星の形をした宝石が浮き出ている/＞月のピラーを手に入れた！
		CALL MESSAGE_WINDOW_D,  "", @"＞With the loss of Balaam, the Gaeans abandoned their Shibuya branch and ran away.../＞It seems %CALLNAME:MASTER% dealt a heavy blow to the Gaean community here..."
		;バラムを失ったガイア教徒達は渋谷支部を捨てて逃げていった…/＞これで%CALLNAME:MASTER%はガイア教団に完全に目をつけられることになっただろう…
		ダンジョンフラグ:(FLAG:現ダンジョン):13 = 2 
		ダンジョンフラグ:(FLAG:現ダンジョン):14 = 1
		FLAG:脱出スキル禁止 = 0
		IF ダンジョンフラグ:(FLAG:現ダンジョン):12 == 1
			CALL MESSAGE_WINDOW_D,  "", "........./...../..."
			CALL SHOW_PICTURE, "EMPTY"
			CALL SHOW_PICTURE, "NONFLOORD", "Hidden room"
			;アジトの一室
			CALL MESSAGE_WINDOW_D,  "Girl", "...Uooh....I'm saved...."
			;少女", "…うぅ…救世主…さま…
			CALL MESSAGE_WINDOW_D,  "", "＞The girl who had been punished woke up..."
			;暴行を受けていた少女が眼を覚ました…
			CALL MESSAGE_WINDOW_D,  "Girl", "You are....my saviour....aren't you?"
			;あなたは…救世主さま…ですね…
			CALL INPUT_YN_D,"Yes","No"
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D,  "Girl", "Oh...you really are!"
			ELSE
				CALL MESSAGE_WINDOW_D,  "Girl", "You, ...aren't?/No, you must be..."
			ENDIF
			CALL MESSAGE_WINDOW_D,  "Girl", "My...my voice...you heard it?/While I was being tortured, like a dream,/I was hoping that a saviour for me would come..."
			CALL MESSAGE_WINDOW_D,  "Girl", "But...I began to think that it was useless.../That there wouldn't be a saviour for me.../But, you came.../Thank you...thank you so much."
			CALL MESSAGE_WINDOW_D,  "Girl", "But...you are not a mesiac...are you?"
			;しかし……あなたはメシア教徒ではないですよね……？
			CALL INPUT_YN_D,"Yes","No"
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D,  "Girl", "So you aren't after all.../But...you saved me./And you fought against the Gaeans, so this changes nothing."
				;そうですか…やはり…/しかし…あなたがわたしを助けて、/ガイア教団と戦ってくれたのには変わりはありません
			ELSE
				CALL MESSAGE_WINDOW_D,  "Girl", "Is that so.../But..you don't seem to have a job with our organiation.../It's a shame you aren't a Temple Knight.../Ah, no, it's nothing..."
				;そうだったのですか……/しかし…教団内で仕事を持っているわけでは…なさそうですね……/あなたのようなかたがテンプルナイトでないのは惜しい……/いえ、それより……
			ENDIF
			CALL MESSAGE_WINDOW_D,  "Girl", "Please.../Please take me with you.../I...I failed..in my mission here after all."
			;お願いします…/私を共に連れて行ってください…/私は……様の…期待に…応えることが…できませんでした
			CALL MESSAGE_WINDOW_D,  "Girl", "Yes, I...survived. But my friends did not. And I failed./No, after this...I can't go back./But...I won't forgive the Gaeans for this. I won't!"
			;せっかく…造って…いただいたのに…仲間まで失い…/もう帰ることなど…できないでしょう…/ですが…私は…ガイア教団を許すことなど…できません…！
			CALL MESSAGE_WINDOW_D,  "Girl", "As long as they are in Tokyo, it will have no peace./So I want to fight them at your side./...whatever you are...as long as you oppose them...I don't mind.../I'll do anything....And...at your side..."
			;奴らが東京にいる限り…あの地に安息は訪れないでしょう/ですからせめて奴らと戦うあなたの力になりたいのです/…あなたはあなたで…自分の目的を叶えてくれれば…構いませんから…/何でもします…。だから…あなたの…傍に…
			CALL MESSAGE_WINDOW_D,  "", "＞Will you allow the girl to come with you?"
			;少女を連れていきますか？
			CALL INPUT_YN_D,"Yes","No"
			IF RESULT == 0
				CALL MESSAGE_WINDOW_D,  "Girl", "...thank you./I'll do...anything./My name is...Beth...thank you again."
				;…ありがとうございます/なんでも…しますので/私の名前は…ベス…よろしくお願いします
				CALL MESSAGE_WINDOW_D,  "Beth", "Truly..I'm thankful to you from the bottom of my heart."
				;本当に…ありがとうございます…
				CALL MESSAGE_WINDOW_D,  "", "＞Tears formed in Beth's eyes and started to run down her cheeks.."
				;ベスはポロポロと涙を零し始めた
				CALL MESSAGE_WINDOW_D,  "Beth", "Ah, excuse me.../It's just, I'm so happy.../That this is over...that I'm with you..."
				;すいません……/本当に……嬉しくて……/だって…私はあなたと…………
				CALL MESSAGE_WINDOW_D,  "", "＞The former Temple Knight Beth has joined you as a companion."
				;元テンプルナイトの少女・ベスが仲間になった
				CALL ADD_NEW_COMPANION,4501, 500, 1
				SIF RESULT
					CFLAG:(RESULT:1):戦闘参加不可能 = -1
				CFLAG:GETCHARA([[キャラ:ベス]], 0):イベント加入 = 1
			ELSE
				CALL MESSAGE_WINDOW_D,  "Girl", "...is that so...I understand.../...fare well then...best of luck to you../I will stay abit to recover my wounds...then I'll leave..."
				;…そうですか…分かりました/…仕方…ないですよね…/怪我が…治ったら…出ていきます…
				CALL MESSAGE_WINDOW_D,  "", "The girl muttered in a sad voice..."
				;少女はとても悲しそうにそう呟いた……
			ENDIF
		ENDIF
		CALL MESSAGE_WINDOW_D,  "", @"＞For defeating the Gaean senior executive Balaam,/%CALLNAME:MASTER% was paid a bounty of ￥250000."
		;賞金首・ガイア教団幹部・魔王バラムを討伐したことにより%CALLNAME:MASTER%の口座に￥250000が支払われた
		MONEY += 250000
		FLAG:脱出 = 1
	CASE 501
		CALL SHOW_PICTURE, "D"
		CALL MESSAGE_WINDOW_D,  "", "＞When you stepped into the hall,/＞the public execution of a girl was underway."
		;ホールに足を踏み入れると/＞そこでは一人の少女の公開処刑が行われていた
		CALL MESSAGE_WINDOW_D,  "", "＞The clothes of the girl were already ragged and she seemed to be wounded quite severely./＞However, the present Gaeans and demons were still hitting her/＞with fists, whips and magic, continuing their merciless punishment./＞Every time the girl was hit, the surrounding audience cheered."
		;＞すでにぼろぼろになり、動くのもままならない少女に対して/＞ガイア教徒や悪魔達が殴る蹴るは言うに及ばず、鞭や魔法も使い、/＞思い思いの方法で少女に暴行を加えている/＞そして、少女が打たれるたびに、周囲の観客たちから大きな歓声をあがる
		CALL MESSAGE_WINDOW_D,  "", "＞It seemed the girl would not survive this treatment much longer..."
		;この様子では少女が生き絶えるのも時間の問題だろう……
		CALL INPUT_YN_D, "Help the girl", "Sneak past to advance"
		IF RESULT == 0
			CALL MESSAGE_WINDOW_D,  "Gaean", "Whats this?　Who are you?"
			;ガイア教徒", "なんだぁ？　てめーは"
			CALL MESSAGE_WINDOW_D,  "Demon", "Did you come to have fun too?"
			;悪魔", "俺達は今お楽しみなんだぜ？
			CALL MESSAGE_WINDOW_D,  "Female demon", @"Wait!/It's a bad \@ IS_MALE(MASTER) ? boy # girl \@!...let's kill %HIM_HER(MASTER)%!"
			;女悪魔", "いいよいいよ！/乗りが悪い奴はさ…殺しちゃお！
			CALL MESSAGE_WINDOW_D,  "", "＞Gaeans and demons are attacking!"
			;ガイア教徒と悪魔達が襲い掛かってきた！
			FLAG:逃走不可フラグ = 1
			FLAG:会話不能フラグ = 1
			FLAG:先制不意打ちキャンセル = 1
			CALL SET_ENEMY,7,[[キャラ:ハイウェイスター]],33,0,0
			CALL SET_ENEMY,8,[[キャラ:ゴルゴン]],29,0,0
			CALL SET_ENEMY,9,[[キャラ:ミュータント]],32,0,0
			CALL SET_ENEMY,10,[[キャラ:ゴルゴン]],29,0,0
			CALL SET_ENEMY,11,[[キャラ:ハイウェイスター]],33,0,0
			CALL SET_ENEMY,12,[[キャラ:ユキジョロウ]],27,0,0
			CALL SET_ENEMY,13,[[キャラ:ハカイソウ]],28,0,0
			CALL SET_ENEMY,14,[[キャラ:イシュタム]],29,0,0
			CALL SET_ENEMY,15,[[キャラ:ハカイソウ]],28,0,0
			CALL SET_ENEMY,16,[[キャラ:ユキジョロウ]],27,0,0
			CALL BATTLE_START
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D,  "", "＞You defeated them.../＞But there were still many more enemies in the hall./＞More Gaeans and demons are attacking!"
			;悪魔達は力尽きた……/＞しかし、ホールにはまだ多数の敵が残っている……/＞ガイア教徒と悪魔達が襲い掛かってきた！
			CALL SET_ENEMY,7,[[キャラ:ミュータント]],32,0,0
			CALL SET_ENEMY,8,[[キャラ:ゴルゴン]],29,0,0
			CALL SET_ENEMY,9,[[キャラ:ミュータント]],32,0,0
			CALL SET_ENEMY,10,[[キャラ:ゴルゴン]],29,0,0
			CALL SET_ENEMY,11,[[キャラ:ミュータント]],32,0,0
			CALL SET_ENEMY,12,[[キャラ:ユキジョロウ]],27,0,0
			CALL SET_ENEMY,13,[[キャラ:ハカイソウ]],28,0,0
			CALL SET_ENEMY,14,[[キャラ:ヘル]],30,0,0
			CALL SET_ENEMY,15,[[キャラ:ハカイソウ]],28,0,0
			CALL SET_ENEMY,16,[[キャラ:ユキジョロウ]],27,0,0
			CALL BATTLE_START
			CALL SHOW_PICTURE, "D"
			CALL MESSAGE_WINDOW_D,  "", "＞You defeated them too..../＞The group of remaining non-combatants were violently booing you out for what you did."
			;悪魔達は力尽きた……/＞周囲からは激しいブーイングが起こっている……
			FLAG:逃走不可フラグ = 0
			FLAG:会話不能フラグ = 0
			FLAG:先制不意打ちキャンセル = 0
			IF ダンジョンフラグ:(FLAG:現ダンジョン):12 == 1
				CALL MESSAGE_WINDOW_D,  "", @"＞Looking at the punished girl.../＞She was still breathing, but faintly.../＞%CALLNAME:MASTER% hurried to apply some first aid measures."
				;暴行を受けていた少女を見ると……/＞微かではあるがまだ息をしていた……/＞%CALLNAME:MASTER%は応急処置を施すと先に進むことにした
			ELSE
				CALL MESSAGE_WINDOW_D,  "", "＞Looking at the punished girl.../＞It seemed she stopped breathing..."
				;＞暴行を受けていた少女を見ると……/＞既に息絶えていた……
			ENDIF
			ダンジョンフラグ:(FLAG:現ダンジョン):13 = 1
		ELSE
			CALL MESSAGE_WINDOW_D,  "", @"＞%CALLNAME:MASTER% decided not to disturb their public execution./＞Walking softly, you moved past them..."
			;%CALLNAME:MASTER%は公開処刑に熱中する悪魔達に気付かれないように/＞そっとホールを抜けることにした……
			ダンジョンフラグ:(FLAG:現ダンジョン):13 = 2
		ENDIF
		FLAG:脱出スキル禁止 = 1
ENDSELECT
CALL MAKE_FLOOR, FLAG:現ダンジョン
RETURN 1
;=======================================================================
;ワープ
;=======================================================================
@TILE_MARK_COMMON_16_6, ARG, ARG:1, ARG:2
IF ARG == 2006
	RESULTS = 入
ELSE
	RESULTS = 昇
ENDIF
@TILE_MARK_COMMON_16_7, ARG, ARG:1, ARG:2
IF ARG == 3007
	RESULTS = 出
ELSE
	RESULTS = 降
ENDIF
@WORP_16
SELECTCASE DA:(FLAG:現X):(FLAG:現Y)
	CASE 2006
		CALL MESSAGE_WINDOW_D,  "", "＞It's the entrance of the Gaean Branch Shibuya."
		;ガイア教団渋谷支部の入口だ
		CALL INPUT_YN_D, "Enter", "Don't enter"
		SIF !RESULT
			CALL DUNGEON_WORP_ID, 3, 3007, 1
	CASE 3007
		CALL MESSAGE_WINDOW_D,  "", "＞It's the exit of the Gaean Branch Shibuya."
		;ガイア教団渋谷支部の出口だ
		CALL INPUT_YN_D, "Leave", "Stay"
		SIF !RESULT
			CALL DUNGEON_WORP_ID, 2, 2006, 1
	CASEELSE
		IF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
			CALL DUNGEON_UPSTAIRS_ID, FLAG:現M+1, DA:(FLAG:現X):(FLAG:現Y)+1001
		ELSE
			CALL DUNGEON_DOWNSTAIRS_ID, FLAG:現M-1, DA:(FLAG:現X):(FLAG:現Y)-1001
		ENDIF
ENDSELECT
RETURN 1

;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_16
SELECTCASE FLAG:現M
	CASE 0
		FLAG:エンカウント率 = FLAG:未遭遇歩数*4
	CASE 1
		FLAG:エンカウント率 = FLAG:未遭遇歩数*4
	CASE 2
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
	CASE 3
		FLAG:エンカウント率 = FLAG:未遭遇歩数*2
	CASE 4
		FLAG:エンカウント率 = FLAG:未遭遇歩数*2
	CASE 5
		FLAG:エンカウント率 = FLAG:未遭遇歩数*5
	CASE 6
		FLAG:エンカウント率 = FLAG:未遭遇歩数*10
ENDSELECT

SIF FLAG:未遭遇歩数 < 5
	FLAG:エンカウント率 = 0
SIF FLAG:エンカウント率 > 25
	FLAG:エンカウント率 = 25

SIF ダンジョンフラグ:(FLAG:現ダンジョン):14 == 1
	FLAG:エンカウント率 = 0
	
;=======
;メッセンジャー(9)
;=======
@TILE_MARK_COMMON_16_9, ARG, ARG:1, ARG:2
SETCOLOR 0x00ff00
RESULTS = ｍ
@MESSAGE_16, ARG
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	CASE 0
		CALL MESSAGE_WINDOW_D,  "Demon", "バラム様には弱点なんてないんだよ！/でも剣攻撃と電撃は比較的効きやすいんだって！/…このことは内緒だよ！"
		;悪魔
	CASE 1
		CALL MESSAGE_WINDOW_D,  "Demon", "TOKYOの勢力図はいまやガイア教団が圧倒的に優勢だ/メシア教団も打開する策を色々練っているようだがな/俺は別にどちらの味方というわけでもないのだが/我々悪魔にとってはガイア教団が勝ってもらったほうが都合がいいのは確かだな"
	CASE 2
		CALL MESSAGE_WINDOW_D,  "Woman", "ガイア教団のNo,1は、TOKYOの覇者、魔王バエル様よ/次いで池袋のゴズテンノウ様、そしてここ渋谷のバラム様と続いてるのよ"
	CASE 100
		CALL MESSAGE_WINDOW_D,  "破戒僧", "悪魔との共生を認めないメシア教団め…"
	CASE 101
		CALL MESSAGE_WINDOW_D,  "Mohawk head", "渋谷で一番でかいビルがガイア教団の支部さ/3Fは巨大なホールになっていて色々な催し物をやってる/あんたもガイア教徒なら行ってみるといいぜ/ただ、酔っ払ってる教徒や悪魔も多いから気をつけろよ"
	CASE 102
		CALL MESSAGE_WINDOW_D,  "Demon", "東京…、あ！これは昔の呼び名か！/今は私達の間ではTOKYOっていうんだよ！/なんか世紀末っぽくて格好良いよね！"
	CASE 103
		CALL MESSAGE_WINDOW_D,  "Mohawk head", "ここ渋谷のガイア教団支部の指導者は、バラム様だぜ"
	CASE 104
		CALL MESSAGE_WINDOW_D,  "Woman", "新宿にカジノがオープンしたらしいわよ/なんでもオーナーは悪魔だとか…/ちょっと怖いけどすごいものがありそうよね"
	CASE 105
		CALL MESSAGE_WINDOW_D,  "Mohawk head", "この前バラム様が生け捕りにしたテンプルナイト…/…可愛かったなぁ…"
	CASE 106
		CALL MESSAGE_WINDOW_D,  "破戒僧", "ガイア教の教理こそが人々を救うのだ"
	CASE 107
		CALL MESSAGE_WINDOW_D,  "Mohawk head", "テンプルナイトってのはメシア教団の精鋭部隊だ/まぁ俺らガイア教団のガイアーズに比べたらゴミもいいところだけどな"
	CASE 108
		CALL MESSAGE_WINDOW_D,  "Mohawk head", "こっちの地下街にはディスコはないぜ/北側の地下街から入らねーと"
ENDSELECT
;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_16
RETURN 25, 24, 28, 35
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_16
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;=======================================================================
@CLASS_NUM_16
RETURN CLASS_NUM_COMMON("中級")
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;=======================================================================
@ENEMY_NUM_16, ARG
RETURN ENEMY_NUM_COMMON("中級", ARG)

;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_16, ARG
;Human込みLv22-30
;悪魔のみLv24-30
SELECTCASE FLAG:現M
	CASE 0, 1, 2
		SELECTCASE RAND:100
			CASE IS < 15
				;15%でパンクスLv13　ランク3
				RETURN [[キャラ:パンクス]], 24
			CASE IS < 30
				;15%でキンズヘッドLv15　ランク3
				RETURN [[キャラ:キンズヘッド]], 25
			CASE IS < 45
				;15%でメタルLv19　ランク2
				RETURN [[キャラ:メタル]], 26
			CASE IS < 60
				;15%でハイウェイスターLv21　ランク1
				RETURN [[キャラ:ハイウェイスター]], 24
			CASE IS < 70
				;10%でユキジョロウLv21　ランク1
				RETURN [[キャラ:ユキジョロウ]], 24
			CASE IS < 80
				;10%でグーラーLv22　ランク1
				RETURN [[キャラ:グーラー]], 25
			CASE IS < 90
				;10%でイシュタムLv23　ランク1
				RETURN [[キャラ:イシュタム]], 26
			CASE IS < 100
				;10%でヴィーヴルLv23　ランク1
				RETURN [[キャラ:ヴィーヴル]], 26
		ENDSELECT
	CASEELSE
		SELECTCASE RAND:100
			CASE IS < 15
				;15%でフライデイLv24　ランク1
				RETURN [[キャラ:フライデイ]],27
			CASE IS < 30
				;15%でハカイソウLv25　ランク1
				RETURN [[キャラ:ハカイソウ]],28
			CASE IS < 45
				;15%でメタルLv19　ランク3
				RETURN [[キャラ:メタル]],28
			CASE IS < 60
				;15%でハイウェイスターLv21　ランク2
				RETURN [[キャラ:ハイウェイスター]],27
			CASE IS < 70
				;10%でサラシナヒメLv30
				RETURN [[キャラ:サラシナヒメ]],27
			CASE IS < 80
				;10%でマンイーターLv27
				RETURN [[キャラ:マンイーター]],27
			CASE IS < 90
				;10%でアエーシュマ
				RETURN [[キャラ:アエーシュマ]],27
			CASE IS < 100
				;10%でゴルゴンLv29
				RETURN [[キャラ:ゴルゴン]],28
		ENDSELECT
ENDSELECT
;=================================
;特殊調整
;=================================
@SET_ENEMY_STATUS_16, ARG
SELECTCASE NO:ARG
	CASE [[キャラ:ヴィーヴル]]
		ABL:ARG:アイテム2 = 1002
		ABL:ARG:入手確率2 = 15
	CASE [[キャラ:アエーシュマ]]
		ABL:ARG:アイテム1 = 1002
		ABL:ARG:入手確率1 = 10
		ABL:ARG:アイテム2 = 1017
		ABL:ARG:入手確率2 = 5
	CASE [[キャラ:マンイーター]]
		ABL:ARG:アイテム2 = 3501
		ABL:ARG:入手確率2 = 5
		ABL:ARG:アイテム3 = 1018
		ABL:ARG:入手確率3 = 5
ENDSELECT
;=================================
;思考パターン
;=================================
;バラムの思考パターン
@SET_ACTION_209,ARG
IF CFLAG:ARG:行動した回数 == 0
	CALL CHECK_ACTIONABLE,ARG,405
	SIF RESULT == 0
		GOTO DECIDE_ACTION
	CFLAG:ARG:入力行動 = 405
	CALL RANDOM_TARGET,ARG,405
ELSEIF CFLAG:ARG:行動した回数 == 1
	IF RAND:2 == 0
		CALL CHECK_ACTIONABLE,ARG,602
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 602
		CALL RANDOM_TARGET,ARG,602
		RETURN 1
	ELSE
		CALL CHECK_ACTIONABLE,ARG,603
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 603
		CALL RANDOM_TARGET,ARG,603
		RETURN 1
	ENDIF
ELSEIF CFLAG:ARG:行動した回数 == 2
	IF RAND:3 == 0
		CALL CHECK_ACTIONABLE,ARG,706
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 706
		CALL RANDOM_TARGET,ARG,706
		RETURN 1
	ELSEIF RAND:3 == 0
		CALL CHECK_ACTIONABLE,ARG,806
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 806
		CALL RANDOM_TARGET,ARG,806
		RETURN 1
	ELSE
		CALL CHECK_ACTIONABLE,ARG,1006
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 1006
		CALL RANDOM_TARGET,ARG,1006
		RETURN 1
	ENDIF
ELSEIF CFLAG:ARG:行動した回数 == 3
	IF RAND:2 == 0
		CALL CHECK_ACTIONABLE,ARG,505
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 505
		CALL RANDOM_TARGET,ARG,505
		RETURN 1
	ELSE
		CALL CHECK_ACTIONABLE,ARG,0
		SIF RESULT == 0
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = 0
		CALL RANDOM_TARGET,ARG,0
		RETURN 1
	ENDIF
ELSE
		$DECIDE_ACTION
		CALL SEARCH_ACT,ARG
		IF RESULT == 0
			CFLAG:ARG:入力行動 = -1
			CFLAG:ARG:防御フラグ = 1
			RETURN 0
		ENDIF
;		PRINTFORML [{CFLAG:ARG:5}]のスキル数 {RESULT}
		LOCAL = RAND:RESULT
		;スキル番号決定
		LOCAL:1 = S:(10+LOCAL)
;		PRINTFORML [{CFLAG:ARG:5}]のスキル {LOCAL:1}
		$TARGET_LOOP
		
		CALL RANDOM_TARGET,ARG,LOCAL:1
		CALLFORM SKILL_TARGET_{LOCAL:1}
		;ここでスキル番号を行動内容に代入
		SIF CFLAG:ARG:ターゲット == -1
			GOTO DECIDE_ACTION
		CFLAG:ARG:入力行動 = LOCAL:1
ENDIF

;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_16
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:4
	CASE 0
		RETURN [[キャラ:メタル]] , 3 + RAND:10 , CSVBASE([[キャラ:メタル]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 1
		RETURN [[キャラ:パンクス]] , 3 + RAND:10 , CSVBASE([[キャラ:パンクス]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 2
		RETURN [[キャラ:ハカイソウ]] , 3 + RAND:10 , CSVBASE([[キャラ:ハカイソウ]] , GETNUM(BASE , "LV") , 0) + RAND:3
	CASE 3
		RETURN [[キャラ:ハイウェイスター]] , 3 + RAND:10 , CSVBASE([[キャラ:ハイウェイスター]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT
