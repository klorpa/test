
;=======================================================================
;天体博物館
;ボス・ムーウィス（キャロルＪ）
;ダンジョンナンバー89
;
;
;
;フラグ
;ダンジョンフラグ:89:1	クリア状態（0:未クリア、1:始末クリア、2:部下クリア、3:解放クリア、4:他人がクリア）
;ダンジョンフラグ:89:2	カギフラグ（0:未設定、1:おひつじ座、2:かに座、3:やぎ座、4:入手した）
;ダンジョンフラグ:89:3	カギ入手フラグ（0:手に入れてない、1:手に入れた）（未使用）
;ダンジョンフラグ:89:4	施錠したサマナー関係（0:施錠未確認、1:施錠確認済み、2:初回話した、3:カギ入手報告済み）
;ダンジョンフラグ:89:5	報酬フラグ（0:貰ってない、1:貰い待ち、2:貰った）
;ダンジョンフラグ:89:6	扉開錠フラグ（0:開いてない、1:開けた）

;ダンジョンフラグ:89:7	キャロルＪ弾き語り、発展判定、発展度５が最大（0:初回、1:発展度１、2:発展度２、3:発展度３、4:発展度４,5:発展度５）
;ダンジョンフラグ:89:8	キャロルＪ弾き語り、その日に話したか判定、ダンジョン脱出時にリセットする（0:話してない、1:話した）
;ダンジョンフラグ:89:9	発展度５のキャロルＪに合った回数、５回ごとにアイテムもらえる可能性

;時事系列的に天体博物館＞闘技場イベントってことにして、クリア前に闘技場でネミッサを仲間にすると依頼が消えダンジョンボスは他のサマナーに倒される。
;ウラベクエストに関係なくカツオ達はネミッサ入りのＣＯＭＰを手に入れてサマナーとして活動している感じ。

;ネミッサ加入済みの判定に闘技場CLOSSEUM_6_ＶＳ二人組の女.ERBの「闘技場フラグ:6:0 == 1」フラグを
;マヨーネ加入済みの判定にイベントEVENT_1_マヨーネ様のご褒美.ERBの「イベントフラグ:1:0 == 3」フラグを使用しています。
;=======================================================================

;=======================================================================
;攻略条件
;=======================================================================
@PREREQUISITE_DUNGEON_89
;依頼フラグ:29:0 = 1
;依頼を受けていてるか、依頼終了済み
IF 依頼フラグ:33:0 == 1 || 依頼フラグ:33:0 == 2
	RETURN 1
ENDIF
RETURN 0

;=======================================================================
;ダンジョン名
;=======================================================================
@GET_DUNGEON_NAME_89
RESULTS = Astro Museum
;天体博物館

;=======================================================================
;フロア名表示
;=======================================================================
@FLOORNAME_89
PRINT Astro Museum 
;天体博物館
SELECTCASE FLAG:現M
	CASE 0
		PRINTL １Ｆ, Lobby
		;、ロビー
	CASE 1
		PRINTL ２Ｆ
	CASE 2
		PRINTL ３Ｆ
	
	CASEELSE
		PRINTL
ENDSELECT

;=======================================================================
;スタート地点入力
;=======================================================================
@START_DUNGEON_89, ARG
FLAG:現M = 0
FLAG:現X = 9
FLAG:現Y = 12
;CALL AUTOMAP
RETURN 0

;=======================================================================
;攻略開始時のイベント
;=======================================================================
@EVENT_DUNGEON_START_89

;ネミッサ加入しているとダンジョン内のイベントが消える。
IF 闘技場フラグ:6:0 == 1
	;クリア状態、他人がクリアへ
	ダンジョンフラグ:89:1 = 4
ENDIF

;カギの場所
;ネミッサ加入しているか天体博物館クリア状態他人で扉開いてない
IF (闘技場フラグ:6:0 == 1 || ダンジョンフラグ:89:1 == 4) && ダンジョンフラグ:89:6 == 0
	;扉開錠フラグ開けたへ
	ダンジョンフラグ:89:6 = 1
;カギフラグ未設定かつクリアフラグ立ってないで扉開いてない
ELSEIF ダンジョンフラグ:89:2 == 0 && ダンジョンフラグ:89:1 == 0 && ダンジョンフラグ:89:6 == 0
	IF RAND:3 == 0
		;カギの場所おひつじ座へ
		ダンジョンフラグ:89:2 = 1
	ELSEIF RAND:2 == 0
		;カギの場所かに座へ
		ダンジョンフラグ:89:2 = 2
	ELSE
		;カギの場所やぎ座へ
		ダンジョンフラグ:89:2 = 3
	ENDIF
ENDIF

;=======================================================================
;脱出時のイベント
;=======================================================================
@EVENT_DUNGEON_END_89
PRINTFORML 　

;ダンジョン脱出時、報酬フラグ貰い待ちだと賞金と名声を得ます。
IF ダンジョンフラグ:89:5 == 1
	PRINTFORML 　
	;クリア状態始末クリア
	IF ダンジョンフラグ:89:1 == 1 
		PRINTFORML Your completion of the directive and loyalty to the organisation where recognized.
		PRINTFORMW Received ￥40000 and 20 fame.
		;指令の完了と組織への忠誠が認められ、口座に￥40000が支払われ、名声20を得た
		
		MONEY += 40000
		FLAG:名声 += 20
	;クリア状態部下クリア
	ELSEIF ダンジョンフラグ:89:1 == 2
		PRINTFORMW For completing the objective, you received ￥10000 and 20 fame.
		;指令を完了したことにより、口座に￥10000が支払われ、名声20を得た
		
		MONEY += 10000
		FLAG:名声 += 20
	;クリア状態解放クリア
	ELSEIF ダンジョンフラグ:89:1 == 3
		PRINTFORMW For completing the objective, you received ￥10000 and 30 fame.
		;指令を完了したことにより、口座に￥10000が支払われ、名声30を得た
		
		MONEY += 10000
		FLAG:名声 += 30
	ENDIF
	
	
	;報酬フラグ貰ったへ
	ダンジョンフラグ:89:5 = 2
	;依頼終了
	依頼フラグ:33:0 = 2
ENDIF

;=======================================================================
;指定したマスに進入可能か
;=======================================================================
@CAN_ENTER_89, ARG, ARG:1
;SELECTCASE DA:ARG:(ARG:1)
;ENDSELECT

;カギ持ってない、扉開けてない
IF FLAG:現M == 2 && ARG == 6 && ARG:1 == 6 && ダンジョンフラグ:89:2 < 4 && ダンジョンフラグ:89:6 == 0
	PRINTFORMW The door is locked...
	;扉にはカギがかかっている……
	RETURN 0
;カギ持ってる、扉開けてない
ELSEIF FLAG:現M == 2 && ARG == 6 && ARG:1 == 6 && ダンジョンフラグ:89:2 == 4 && ダンジョンフラグ:89:6 == 0
	PRINTFORMW You opened the door with the key in your possession.
	;持っていたカギで扉を開けた……
	;扉開錠フラグ開けたへ
	ダンジョンフラグ:89:6 = 1
	RETURN 0
ENDIF

CALL CAN_ENTER,ARG,ARG:1
RETURN RESULT

;=======================================================================
;フロアデータ
;=======================================================================

;0：壁　1：床もしくは通路　2：扉　3：宝箱　4：イベント　5：罠　6：上り階段 7：下り階段　8：出口　9：その他特殊処理等
;これらの内、3〜9が代入されているマスに、個別の番号を与える処理を実行します。
;この処理を行った後、これらのマスには 「マップ番号*1000 + そのマスがそのマップで左上から数えて何個目のARGタイプのマスか*10 + ARG」が代入されます
;例えば 2003 であった場合、「2番のマップ＝３Ｆで、左上から数えて0個目の3＝宝箱である」という事を意味しています
;尚、0〜2はどのマップにも大量に設置されるので、ここでは基本的に個別の番号を与えません。


@MAKE_FLOOR_89
SELECTCASE FLAG:現M
	CASE 0
	;１階、ロビー
		FLAG:最大X = 19 
		FLAG:最大Y = 15 
							;	  0123456789012345678
		CALL MAKE_FLOOR_LINE, 0, "                   "
		CALL MAKE_FLOOR_LINE, 1, "                   "
		CALL MAKE_FLOOR_LINE, 2, "         6         "
		CALL MAKE_FLOOR_LINE, 3, "  111111111411111  "
		CALL MAKE_FLOOR_LINE, 4, "  111111111111111  "
		CALL MAKE_FLOOR_LINE, 5, "  111111111111111  "
		CALL MAKE_FLOOR_LINE, 6, "       4   4       "
		CALL MAKE_FLOOR_LINE, 7, "     1 4 1 4 1     "
		CALL MAKE_FLOOR_LINE, 8, "     111111111     "
		CALL MAKE_FLOOR_LINE, 9, "     111111111     "
		CALL MAKE_FLOOR_LINE,10, "     111111111     "
		CALL MAKE_FLOOR_LINE,11, "       11111       "
		CALL MAKE_FLOOR_LINE,12, "         8         "
		CALL MAKE_FLOOR_LINE,13, "                   "
		CALL MAKE_FLOOR_LINE,14, "                   "
		
		
	CASE 1
	;２階
		FLAG:最大X = 28 
		FLAG:最大Y = 22
	
							;	  0123456789012345678901234567
		CALL MAKE_FLOOR_LINE, 0, "                           "
		CALL MAKE_FLOOR_LINE, 1, "                           "
		CALL MAKE_FLOOR_LINE, 2, "    1111     6     1111    "
		CALL MAKE_FLOOR_LINE, 3, "   11111 1 11111 1 11111   "
		CALL MAKE_FLOOR_LINE, 4, "  11   1 111111111 1   11  "
		CALL MAKE_FLOOR_LINE, 5, "  1121 1 111   111 1 1211  "
		CALL MAKE_FLOOR_LINE, 6, "  11   1 111 4 111 1   11  "
		CALL MAKE_FLOOR_LINE, 7, "  111111   1 2 1   111111  "
		CALL MAKE_FLOOR_LINE, 8, "  11   1 T 11111 1 1   11  "
		CALL MAKE_FLOOR_LINE, 9, "  1121 1 2   2   2 1 3211  "
		CALL MAKE_FLOOR_LINE,10, "  11   1111111111111   11  "
		CALL MAKE_FLOOR_LINE,11, "   111111111111111111111   "
		CALL MAKE_FLOOR_LINE,12, "   1                   2   "
		CALL MAKE_FLOOR_LINE,13, "   1 4  4  4   4  4  4 1   "
		CALL MAKE_FLOOR_LINE,14, "   111111111111111111111   "
		CALL MAKE_FLOOR_LINE,15, "  11 4  4  4   4  4  4 11  "
		CALL MAKE_FLOOR_LINE,16, "   2                   1   "
		CALL MAKE_FLOOR_LINE,17, "   111111111411111111111   "
		CALL MAKE_FLOOR_LINE,18, "    1111111111111111111    "
		CALL MAKE_FLOOR_LINE,19, "             7             "
		CALL MAKE_FLOOR_LINE,20, "                           "
		CALL MAKE_FLOOR_LINE,21, "                           "
		
		
	CASE 2
	;３階
		FLAG:最大X = 13 
		FLAG:最大Y = 20 
							;	  0123456789012
		CALL MAKE_FLOOR_LINE, 0, "             "
		CALL MAKE_FLOOR_LINE, 1, "             "
		CALL MAKE_FLOOR_LINE, 2, "  111111111  "
		CALL MAKE_FLOOR_LINE, 3, "  111141111  "
		CALL MAKE_FLOOR_LINE, 4, "  111111111  "
		CALL MAKE_FLOOR_LINE, 5, "  111111111  "
		CALL MAKE_FLOOR_LINE, 6, "      2      "
		CALL MAKE_FLOOR_LINE, 7, "  111111111  "
		CALL MAKE_FLOOR_LINE, 8, "  1   1   1  "
		CALL MAKE_FLOOR_LINE, 9, "  1 3 1   1  "
		CALL MAKE_FLOOR_LINE,10, "  1 2 1   1  "
		CALL MAKE_FLOOR_LINE,11, "  11111   1  "
		CALL MAKE_FLOOR_LINE,12, "  1   1   1  "
		CALL MAKE_FLOOR_LINE,13, "  1 3 1   1  "
		CALL MAKE_FLOOR_LINE,14, "  1 2 1   1  "
		CALL MAKE_FLOOR_LINE,15, "  111111111  "
		CALL MAKE_FLOOR_LINE,16, "   1411111   "
		CALL MAKE_FLOOR_LINE,17, "      7      "
		CALL MAKE_FLOOR_LINE,18, "             "
		CALL MAKE_FLOOR_LINE,19, "             "
		
		
	CASE 10
	CASEELSE
ENDSELECT

;=======================================================================
;タイル調整
;=======================================================================
;ARG:1、ARG:2はそれぞれX座標とY座標
;ARGはその座標に現在代入されている番号
;0：壁　1：床もしくは通路　2：扉　3：宝箱　4：イベント　5：罠　6：上り階段 7：下り階段　8：出口　9：その他特殊処理等
;これらの内、3〜9が代入されているマスに、個別の番号を与える処理を実行します。
;この処理を行った後、これらのマスには 「マップ番号*1000 + そのマスがそのマップで左上から数えて何個目のARGタイプのマスか*10 + ARG」が代入されます
;例えば 2003 であった場合、「2番のマップ＝３Ｆで、左上から数えて0個目の3＝宝箱である」という事を意味しています
;尚、0〜2はどのマップにも大量に設置されるので、ここでは基本的に個別の番号を与えません。
;
;何個目のARGタイプのマスなのか、は左上の(0,0)から右まで探し、その後一つ下の段に移って同様に探す、というのを右下まで繰り返して数えます。
@MAKE_FLOOR_LINE_COMMON_89, ARG, ARG:1, ARG:2
#LOCALSIZE 10
;LOCAL:0〜9 それぞれ何個目のARGなのかを記憶しています
;LOCAL:3 宝箱→使用フラグはダンジョンフラグ:70~74
;指定された座標が(0,0)である場合、つまり新しいマップを読み込んだ時はLOCALを消去します
SIF ARG:1 == 0 && ARG:2 == 0
	VARSET LOCAL
SELECTCASE ARG
	CASE 3
		;宝箱は多くの場合、開けたフラグが立っていると床を代入します
		;そのマップでどの宝箱を開けたかは、そのマップの宝箱管理用変数の何番目のビットが立っているかで判断します
		IF GETBIT(ダンジョンフラグ:(FLAG:現ダンジョン):(FLAG:現M+70), LOCAL:3)
			DA:(ARG:1):(ARG:2) = 1
		ELSE
			DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:3 * 10 + 3
		ENDIF
		LOCAL:3++
	CASE 4 TO 9
		DA:(ARG:1):(ARG:2) = FLAG:現M *1000 + LOCAL:ARG * 10 + ARG
		LOCAL:ARG++
	
ENDSELECT

;=======================================================================
;特殊タイル表示
;TILE_MARK_COMMON_{FLAG:現ダンジョン}_X
;=======================================================================
;ARG:1、ARG:2がX座標とY座標
;ARGはそこに代入されている値
;RESULTSに代入した文字がマップに表示されますが、必ず全角1文字分になるようにしてください。そうでないと表示が崩れます。


@TILE_MARK_COMMON_89_4, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	CASE 2 , 4
		RESULTS = ↑
	CASE 1 , 3
		RESULTS = ↓
		
	;メッセンジャーを緑色で"ｍ"と表示
	CASE 0 , 113 , 201
		SETCOLOR 0x00ff00
		RESULTS = ｍ
	CASEELSE
		;特に設定しない場合、"！"と表示されます
		RETURN 1
ENDSELECT


;イベントタイル特殊表示
;末尾の_4はイベントであることを示す4
;@TILE_MARK_COMMON_89_4, ARG , ARG:1 , ARG:2
;SELECTCASE ARG / 10
	;メッセンジャーを緑色で"ｍ"と表示
	;CASE 0 TO 2 , 100 , 101 , 300
	;	SETCOLOR 0x00ff00
	;	RESULTS = ｍ
;	CASEELSE
		;特に設定しない場合、"！"と表示されます
;		RETURN 1
;ENDSELECT

;特殊タイルの表示
;末尾の_9は特殊タイルであることを示す9
@TILE_MARK_COMMON_89_9, ARG , ARG:1 , ARG:2
SELECTCASE ARG / 10
	;CASE 400
		
		
	CASEELSE
		;特に設定しない場合、"■"が表示されます
		RETURN 1
ENDSELECT

;=======================================================================
;[5]調べる　実行時のイベント
;=======================================================================
@EVENT_CHECK_DUNGEON_89

;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
;% 10は10で割ったあまり、つまり下一桁がでてきます
;そして、下一桁はタイルの種類を表す
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_89
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	CALL UPSTAIRS_89
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL DOWNSTAIRS_89
	RETURN RESULT
;========
;イベント
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4
	SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
		;１Ｆイベント
		CASE 0
			;プラネタリウムを閉鎖したサマナー
			CALL DUNGEON_89_Summoner01
		CASE 1
			CALL SHOW_FORCEMOVE, "DD", "D", , ,  "It's a descending escalator..."
			;下りのエスカレーターだ……
		CASE 2
			CALL SHOW_FORCEMOVE, "U", "D", , ,  "It's an ascending escalator./ You can't go down from here..."
			;上りのエスカレーターだ/こちらからは下りることはできない……
		CASE 3
			CALL SHOW_FORCEMOVE, "D", "D", , ,  "It's an descending escalator./ You can't go up from here..."
			;下りのエスカレーターだ/こちらからは上ることはできない……
		CASE 4
			CALL SHOW_FORCEMOVE, "UU", "D", , ,  "It's an ascending escalator..."
			;上りのエスカレーターだ……
			
		;２Ｆイベント
		CASE 100
			PRINTFORMW 「Thank you for visiting Tianhai Tianjin Museum today.」
			;「本日は天海天体博物館へお越しいただきありがとうございます。」
			PRINTFORMW 「Please enjoy yourselves...」
			;「どうぞごゆっくりお楽しみください……。」
			PRINTFORMW The announcement of the museum is flowing...
			;博物館のアナウンスが流れている……
			
		CASE 101
			PRINTFORML おひつじ座　aries
			PRINTFORML East of Pisces and west of Taurus
			PRINTFORMW Second house of the ecliptic a Fall constellation
			;カギの場所おひつじ座の時
			IF ダンジョンフラグ:89:2 == 1
				PRINTFORML 
				PRINTFORMW Something has fallen on your feet!
				;足元に何か落ちている！
				PRINTFORMW You got the key to the planetarium.
				;プラネタリウムのカギを手に入れた
				;カギフラグ手に入れたへ
				ダンジョンフラグ:89:2 = 4
			ENDIF
		CASE 102
			PRINTFORML おうし座　taurus
			PRINTFORML East of Aries and west of Gemini
			PRINTFORMW 黄道上の第３宮　冬の星座
		CASE 103
			PRINTFORML ふたご座　gemini
			PRINTFORML East of Taurus and west of Cancer
			PRINTFORMW 黄道上の第４宮　冬の星座
		CASE 104
			PRINTFORML かに座　cancer
			PRINTFORML ふたご座の東　しし座の西にある
			PRINTFORMW 黄道上の第５宮　春の星座
			;カギの場所かに座の時
			IF ダンジョンフラグ:89:2 == 2
				PRINTFORML 
				PRINTFORMW Something has fallen on your feet!
				PRINTFORMW You got the key to the planetarium.
				;カギフラグ手に入れたへ
				ダンジョンフラグ:89:2 = 4
			ENDIF
		CASE 105
			PRINTFORML しし座　leo
			PRINTFORML おとめ座の西にある
			PRINTFORMW 黄道上の第６宮　春の星座
		CASE 106
			PRINTFORML おとめ座　virgo
			PRINTFORML しし座の東　てんびん座の西にある
			PRINTFORMW 黄道上の第７宮　春の星座


		CASE 107
			PRINTFORML てんびん座　libra
			PRINTFORML おとめ座とさそり座の間にある
			PRINTFORMW 黄道上の第８宮　夏の星座
		CASE 108
			PRINTFORML さそり座　scorpius
			PRINTFORML てんびん座の東　いて座の西にある
			PRINTFORMW 黄道上の第９宮　夏の星座
		CASE 109
			PRINTFORML いて座　sagittarius
			PRINTFORML さそり座とやぎ座の間にある
			PRINTFORMW 黄道上の第１０宮　夏の星座
		CASE 110
			PRINTFORML やぎ座　capricornus
			PRINTFORML いて座の東　みずがめ座の西にある
			PRINTFORMW 黄道上の第１１宮　秋の星座
			;カギの場所やぎ座の時
			IF ダンジョンフラグ:89:2 == 3
				PRINTFORML 
				PRINTFORMW Something has fallen on your feet!
				PRINTFORMW You got the key to the planetarium.
				;カギフラグ手に入れたへ
				ダンジョンフラグ:89:2 = 4
			ENDIF
		CASE 111
			PRINTFORML みずがめ座　aquarius
			PRINTFORML やぎ座の東　うお座の西にある
			PRINTFORMW 黄道上の第１２宮　秋の星座
		CASE 112
			PRINTFORML うお座　pisces
			PRINTFORML みずがめ座の東　おひつじ座の西にある
			PRINTFORMW 黄道上の第１宮　秋の星座
		CASE 113
			;見物人サマナー
			CALL DUNGEON_89_Summoner02
			
		;３Ｆイベント
		CASE 200
			;ボス
			CALL DUNGEON_89_Boss
			
		CASE 201
			;成長するサマナー
			CALL DUNGEON_89_Summoner03
			
	ENDSELECT
ELSE
	PRINTFORMW There seems to be nothing there.
	;何も無いようだ
	RETURN 0
ENDIF

;=======================================================================
;タイル進入時のイベント
;=======================================================================
@EVENT_ENTER_DUNGEON_89,ARG
;ARG = 進入時の向き　0の場合ワープなど
;==========
;出口の処理
;==========
IF DA:(FLAG:現X):(FLAG:現Y) % 10 == 8
	CALL DUNGEON_EXIT
	RETURN 1
;========
;宝の処理
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 3
	CALL TREASURE_89
	RETURN RESULT
;=======
;階段
;=======
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 6
	CALL UPSTAIRS_89
	RETURN RESULT

ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 7
	CALL DOWNSTAIRS_89
	RETURN RESULT

;==========
;扉の処理
;==========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 2
	CALL DUNGEON_DOOR,ARG
	RETURN 1

;========
;イベント
;========
ELSEIF DA:(FLAG:現X):(FLAG:現Y) % 10 == 4
	SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
		;１Ｆイベント
		CASE 0
			;プラネタリウムを閉鎖したサマナー
			CALL DUNGEON_89_Summoner01
			
			RETURN 0
		CASE 1
			CALL SHOW_FORCEMOVE, "DD", "D", , ,  "It's a descending escalator..."
			RETURN 0
		CASE 2
			CALL SHOW_FORCEMOVE, "U", "D", , ,  "It's an ascending escalator./ You can't go down from here..."
			RETURN 0
		CASE 3
			CALL SHOW_FORCEMOVE, "D", "D", , ,  "It's an descending escalator./ You can't go up from here..."
			RETURN 0
		CASE 4
			CALL SHOW_FORCEMOVE, "UU", "D", , ,  "It's an ascending escalator..."
			RETURN 0
		;２Ｆイベント
		CASE 100
			PRINTFORMW 「Thank you for visiting Tianhai Tianjin Museum today.」
			PRINTFORMW 「Please enjoy yourselfes...」
			PRINTFORMW The announcement of the museum is flowing...
			
			RETURN 0
		CASE 101
			PRINTFORML おひつじ座　aries
			PRINTFORML うお座の東　おうし座の西にある
			PRINTFORMW 黄道上の第２宮　秋の星座
			;カギの場所おひつじ座の時
			IF ダンジョンフラグ:89:2 == 1
				PRINTFORML 
				PRINTFORMW Something has fallen on your feet!
				PRINTFORMW You got the key to the planetarium.
				;カギフラグ手に入れたへ
				ダンジョンフラグ:89:2 = 4
			ENDIF
			RETURN 0
		CASE 102
			PRINTFORML おうし座　taurus
			PRINTFORML おひつじ座の東　ふたご座の西にある
			PRINTFORMW 黄道上の第３宮　冬の星座
			RETURN 0
		CASE 103
			PRINTFORML ふたご座　gemini
			PRINTFORML おうし座の東　かに座の西にある
			PRINTFORMW 黄道上の第４宮　冬の星座
			RETURN 0
		CASE 104
			PRINTFORML かに座　cancer
			PRINTFORML ふたご座の東　しし座の西にある
			PRINTFORMW 黄道上の第５宮　春の星座
			;カギの場所かに座の時
			IF ダンジョンフラグ:89:2 == 2
				PRINTFORML 
				PRINTFORMW Something has fallen on your feet!
				PRINTFORMW You got the key to the planetarium.
				;カギフラグ手に入れたへ
				ダンジョンフラグ:89:2 = 4
			ENDIF
			RETURN 0
		CASE 105
			PRINTFORML しし座　leo
			PRINTFORML おとめ座の西にある
			PRINTFORMW 黄道上の第６宮　春の星座
			RETURN 0
		CASE 106
			PRINTFORML おとめ座　virgo
			PRINTFORML しし座の東　てんびん座の西にある
			PRINTFORMW 黄道上の第７宮　春の星座
			RETURN 0

		CASE 107
			PRINTFORML てんびん座　libra
			PRINTFORML おとめ座とさそり座の間にある
			PRINTFORMW 黄道上の第８宮　夏の星座
			RETURN 0
		CASE 108
			PRINTFORML さそり座　scorpius
			PRINTFORML てんびん座の東　いて座の西にある
			PRINTFORMW 黄道上の第９宮　夏の星座
			RETURN 0
		CASE 109
			PRINTFORML いて座　sagittarius
			PRINTFORML さそり座とやぎ座の間にある
			PRINTFORMW 黄道上の第１０宮　夏の星座
			RETURN 0
		CASE 110
			PRINTFORML やぎ座　capricornus
			PRINTFORML いて座の東　みずがめ座の西にある
			PRINTFORMW 黄道上の第１１宮　秋の星座
			;カギの場所やぎ座の時
			IF ダンジョンフラグ:89:2 == 3
				PRINTFORML 
				PRINTFORMW Something has fallen on your feet!
				PRINTFORMW You got the key to the planetarium.
				;カギフラグ手に入れたへ
				ダンジョンフラグ:89:2 = 4
			ENDIF
			RETURN 0
		CASE 111
			PRINTFORML みずがめ座　aquarius
			PRINTFORML やぎ座の東　うお座の西にある
			PRINTFORMW 黄道上の第１２宮　秋の星座
			RETURN 0
		CASE 112
			PRINTFORML うお座　pisces
			PRINTFORML みずがめ座の東　おひつじ座の西にある
			PRINTFORMW 黄道上の第１宮　秋の星座
			RETURN 0
		CASE 113
			;見物人サマナー
			CALL DUNGEON_89_Summoner02
			
			RETURN 0
		;３Ｆイベント
		CASE 200
			;ボス
			CALL DUNGEON_89_Boss
			
			RETURN 0
		CASE 201
			;成長するサマナー
			CALL DUNGEON_89_Summoner03
			
			RETURN 0
	ENDSELECT
ENDIF
RETURN 0

;=======================================================================
;エンカウント率
;=======================================================================
@ENCOUNT_RATE_89
SELECTCASE FLAG:現M
	CASE 0 , 1 , 2 , 3 , 4 , 5
		FLAG:エンカウント率 = FLAG:未遭遇歩数 * 5
	CASEELSE
		FLAG:エンカウント率 = FLAG:未遭遇歩数 * 5
ENDSELECT
SIF FLAG:未遭遇歩数 < 5
	FLAG:エンカウント率 = 0
;=======================================================================
;必要レベル目安
;推奨LV 雑魚最低LV 雑魚最高LV ボスLV 
;=======================================================================
@GET_DUNGEON_LV_89
RETURN 20, 20, 25, 35
;=======================================================================
;エンカウント処理
;=======================================================================
@CHECK_ENCOUNT_89
IF RAND:100 < FLAG:エンカウント率
	CALL ENEMY_TABLE
	RETURN 1
ELSE
	FLAG:未遭遇歩数 += 1
	RETURN 0
ENDIF
;=======================================================================
;敵の種族の種類数
;RETURN X でXが種類の数になります
;あまりいろんなのが混ざっているとCOOPがとりにくくなったり
;敵の攻撃属性が多くなる傾向にあるので、
;種類の数は1・2種類を主体に時々3種類くらいを基本にするのが望ましいと思われます
;=======================================================================
@CLASS_NUM_89
SELECTCASE FLAG:現M
	CASE 0 , 1
		SELECTCASE RAND:100
			CASE IS < 50
				RETURN 1
			CASEELSE
				RETURN 2
		ENDSELECT
	CASE 2 , 3
		SELECTCASE RAND:100
			CASE IS < 50
				RETURN 1
			CASEELSE
				RETURN 2
		ENDSELECT
	CASE 4 , 5
		SELECTCASE RAND:100
			CASE IS < 35
				RETURN 1
			CASEELSE
				RETURN 2
		ENDSELECT
ENDSELECT
;=======================================================================
;敵の総数
;ARGは出現する敵の種類です
;RAND(X, Y)はX〜Y-1のうちのどれかになるRANDです
;なお、ここで帰った数字は下限を種族数、上限を10として自動的に調整されます
;=======================================================================
@ENEMY_NUM_89, ARG
SELECTCASE FLAG:現M
	CASE 0
		RETURN RAND(ARG, ARG*2+1)
	CASE 1 , 2
		RETURN RAND(ARG, ARG*2+2)
	CASE 3 ,4 , 5
		RETURN RAND(ARG, ARG*2+3)
ENDSELECT
;=======================================================================
;敵の種類
;=======================================================================
@ENEMY_89, ARG
SELECTCASE FLAG:現M
	CASE 0
		SELECTCASE RAND:100
			CASE IS < 30
				RETURN [[キャラ:ハーピー]],9
			CASE IS < 35
				RETURN [[キャラ:ハーピー]],10
			CASE IS < 65
				RETURN [[キャラ:アズミ]],8
			CASE IS < 70
				RETURN [[キャラ:アチェリ]],9
			CASE IS < 80
				RETURN [[キャラ:アチェリ]],12
			CASE IS < 90
				RETURN [[キャラ:アチェリ]],13
			CASE IS < 95
				RETURN [[キャラ:セエレ]],10
			CASE IS < 100
				RETURN [[キャラ:セエレ]],12
		ENDSELECT
	CASE 1
		SELECTCASE RAND:100
			CASE IS < 30
				RETURN [[キャラ:ハーピー]],11
			CASE IS < 35
				RETURN [[キャラ:ハーピー]],12
			CASE IS < 65
				RETURN [[キャラ:アズミ]],11
			CASE IS < 70
				RETURN [[キャラ:アズミ]],12
			CASE IS < 80
				RETURN [[キャラ:アチェリ]],13
			CASE IS < 90
				RETURN [[キャラ:アチェリ]],14
			CASE IS < 95
				RETURN [[キャラ:セエレ]],12
			CASE IS < 100
				RETURN [[キャラ:セエレ]],13
		ENDSELECT
	CASE 2
		SELECTCASE RAND:100
			CASE IS < 15
				RETURN [[キャラ:ハーピー]],13
			CASE IS < 25
				RETURN [[キャラ:ハーピー]],14
			CASE IS < 40
				RETURN [[キャラ:アズミ]],13
			CASE IS < 50
				RETURN [[キャラ:アズミ]],14
			CASE IS < 65
				RETURN [[キャラ:アチェリ]],14
			CASE IS < 75
				RETURN [[キャラ:アチェリ]],16
			CASE IS < 90
				RETURN [[キャラ:セエレ]],13
			CASE IS < 100
				RETURN [[キャラ:セエレ]],15
		ENDSELECT
ENDSELECT

;=======================================================================
;宝箱の中身
;=======================================================================
@TREASURE_89
#LOCALSIZE 3
;宝箱のフラグ管理用変数の番号
LOCAL = FLAG:現M + 70
;そのマップの何番目の宝箱か
LOCAL:1 = (DA:(FLAG:現X):(FLAG:現Y) / 10) % 100

;/10はIDを10で割る。つまり、下一桁をなくして、二桁目と三桁目を二桁の数字として取り出している
SELECTCASE DA:(FLAG:現X):(FLAG:現Y) / 10
	;マップ番号*100+そのマップで何番目の宝箱かを示す数値でどの宝箱かを判別します。
	;例えば0の場合、0版のマップの0番目の宝箱であることを表しています。
	;２Ｆ
	CASE 100
		CALL GET_TREASURE_MAG, , 1000 , FLAG:現ダンジョン, LOCAL , LOCAL:1
	;３Ｆ
	CASE 200
		CALL GET_TREASURE, [[アイテム:StIncense]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
	CASE 201
		CALL GET_TREASURE, [[アイテム:ChakraPot]] , 1 , FLAG:現ダンジョン, LOCAL , LOCAL:1
ENDSELECT
RETURN 1



;=======================================================================
;階段置き場
;=======================================================================
;上り階段処理
@UPSTAIRS_89
;SELECTCASE DA:(FLAG:現X):(FLAG:現Y) /10
;	;IDで分岐
;	;１Ｆ上り階段
;	CASE 0
;			;@DUNGEON_UPSTAIRS_ID , 階段を進んだ先のマップ番号 , 移動先のタイルのID
;			;２Ｆ＝マップ番号1番の0番目の下り階段 = 1 * 1000 + 0 * 10 + 7 = 1007へと飛ぶ
;			CALL DUNGEON_UPSTAIRS_ID, 1 , 1007
;	CASE 100
;			CALL DUNGEON_UPSTAIRS_ID, 2 , 2007
;	CASE 200
;			CALL DUNGEON_UPSTAIRS_ID, 3 , 3007
;	CASE 300
;			CALL DUNGEON_UPSTAIRS_ID, 4 , 4007
;	CASEELSE
;ENDSELECT

;入り組んだダンジョンで階段がたくさん入り乱れている状態であれば、上記のように階段毎に指定してあげる必要があります。
;しかしこのエコービルは1つのマップに上り階段が１つ、下り階段が１つ、上の階に進む＝マップ番号が一つ増えるという構造になっています
;なので、下記のように書けば一行で済みます
CALL DUNGEON_UPSTAIRS_ID, FLAG:現M+1 , (FLAG:現M + 1) * 1000 + 7
RETURN 1

;下り階段処理
@DOWNSTAIRS_89
;上り階段同様に、エコービルは下り階段も1行で記述できます
CALL DUNGEON_DOWNSTAIRS_ID, FLAG:現M-1 , (FLAG:現M - 1) * 1000 + 6
RETURN 1
;=======================================================================
;エレベーター定義
;=======================================================================
;ARGはそのエレベーターのタイル
@DUNGEON_ELEVATOR_89, ARG
;RESULT:1〜5にエレベータが存在するマップの番号を代入し、RESULTS:1〜5には表示したい名称を代入、RETURNでは-1を返しておく
RESULT:1 = 0
RESULT:2 = 1
RESULT:3 = 2
RESULT:4 = 3
RESULT:5 = 4
RESULT:6 = 5
RESULTS:1 = １Ｆ
RESULTS:2 = ２Ｆ
RESULTS:3 = ３Ｆ
RESULTS:4 = ４Ｆ
RESULTS:5 = ５Ｆ
RESULTS:6 = ６Ｆ
RETURN -1
;=======================================================================
;輪姦参加悪魔
;=======================================================================
@DUNGEON_RAPE_DEVIL_89
;ランダムにどの悪魔が出るか決定
;RETURN CSV番号 , 人数 , レベル
SELECTCASE RAND:3
	CASE 0 , 1 ,2
		RETURN [[キャラ:セエレ]] , 3 + RAND:10 , CSVBASE([[キャラ:セエレ]] , GETNUM(BASE , "LV") , 0) + RAND:3
ENDSELECT
