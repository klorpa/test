@CALLTRAINEND
TFLAG:CALLTRAIN = 0
@SHOW_USERCOM
#LOCALSSIZE 1
VARSET Q, -1
;派生用
VARSET R, -1
Z= 0
;CALLTRAIN時は表示しない
SIF TFLAG:CALLTRAIN
	RETURN 0
;コマンドを全てチェック
LOCAL:1 = -1
CALL DRAW_INFORMATIONLINE("Act_COM")
FOR LOCAL, 0, 1000
	;名前の長さが0の場合、ないものとみなす
	SIF STRLENS(TRAINNAME:LOCAL) == 0
		CONTINUE
	;派生コマンドは蹴る
	TRYCCALLFORM COMTYPE_{LOCAL}
		SIF RESULT == COMTYPE("派生系")
			CONTINUE
	CATCH
	ENDCATCH
	LOCAL:1++
	;COMABLEをチェック
	CALL COM_ABLE_COMMON_D, LOCAL
	SIF RESULT == 0
		CONTINUE
	;派生コマンドになるかどうかをチェック
	TRYCCALLFORM JUMP_COM{LOCAL}
		LOCAL:2 = RESULT >= 0 ? RESULT # LOCAL
	CATCH
		LOCAL:2 = LOCAL
	ENDCATCH
	;MENU配列に実行可能なコマンドを登録していく
	Q:(LOCAL:1) = LOCAL:2
	;フィルタ関係は派生前でチェックするので注意
	SIF GET_COM_FILTERING(LOCAL, TARGET)
		CONTINUE
	TRYCCALLFORM COMTYPE_{LOCAL}
		SIF CFLAG:(170+RESULT) == 1
			CONTINUE
	CATCH
	ENDCATCH
	;すでに表示されているコマンドがここまで来た場合は帰る
	SIF MATCH(R, LOCAL:2)
		CONTINUE
	;コマンド表示
	TRYCCALLFORM COMNAME{LOCAL:2}
		PRINTFORMC %RESULTS%[{LOCAL:1,3}]
	CATCH
		PRINTFORMC %TRAINNAME_E(LOCAL:2)%[{LOCAL:1,3}]
	ENDCATCH
	;ここまできたら表示されたものとして登録される
	R:(LOCAL:1) = LOCAL:2
	;改行処理
	CALL CHECK_NEWLINE
NEXT

;USERCOM表示の前に一度、改行する
IF (Z % TRAINPRINTCPL())
	Z = 0
	PRINTL 
ENDIF
CALL DRAW_INFORMATIONLINE("Filter_COM")
;フィルタされている系統の表示
SETCOLOR COLOR("gray")
FOR LOCAL,0,16
	SELECTCASE LOCAL
		CASE 13,14
			CONTINUE
		CASEELSE
			IF CFLAG:TARGET:(CFLAGNAME:(170+LOCAL)) == 0
				SETCOLOR COLOR("aqua")
			ELSE
				SETCOLOR COLOR("light-gray")
			ENDIF
			;SIF CFLAG:TARGET:(CFLAGNAME:(170+LOCAL)) == 0 && (LOCAL != 15 && コマンド数:LOCAL > 0)
			;	CONTINUE
			;SIF LOCAL == 15 && CFLAG:TARGET:(CFLAGNAME:(170+LOCAL)) == 0
			;SIF CFLAG:TARGET:(CFLAGNAME:(170+LOCAL)) == 0 && コマンド数:LOCAL == 0
			;SIF LOCAL == 15 && Z != 0
			;	PRINTL
			PRINTFORMC  %SUBSTRING(CFLAGNAME_E(170+LOCAL),,STRLENS(CFLAGNAME_E(170+LOCAL))-8)%[{700+LOCAL}]
			CALL CHECK_NEWLINE
	ENDSELECT
NEXT
RESETCOLOR

IF (Z % TRAINPRINTCPL())
	Z = 0
	PRINTL 
ENDIF

CALL DRAW_INFORMATIONLINE("Ex_COM")
IF !CFLAG:TARGET:オプションフィルタ
	A = 0
	Z = 0
	PRINTC History Status[800]
	CALL CHECK_NEWLINE
	PRINTC Hygiene Status[801]
	CALL CHECK_NEWLINE
	PRINTC Status[802]
	CALL CHECK_NEWLINE
	B = 11
	REPEAT 50
		C = B+COUNT
		;媚薬、利尿剤、ビデオカメラ、新妻プレイ、羞恥プレイ、強精神薬使用時は覗く
		SIF C == 26
			CONTINUE
		SIF C == 27
			CONTINUE
		SIF C == 28
			CONTINUE
		SIF C == 30
			CONTINUE
		SIF C == 33
			CONTINUE
		SIF C == 34
			CONTINUE
		SIF C == 40
			CONTINUE
	REND
		PRINTC Clothes Options[803]
		CALL CHECK_NEWLINE
	IF HAVE_PENIS(PLAYER) || HAVE_PENIS(TARGET)
		IF ITEM:45 > 0
			PRINTC Condom Settings[804]
			CALL CHECK_NEWLINE
		ENDIF
		PRINTC Auto-cum Target[805]
		CALL CHECK_NEWLINE
	ENDIF
	IF ASSIPLAY
		PRINTC Status(Assistant)[806]
		CALL CHECK_NEWLINE
	ELSE
		PRINTC Status(Master)[806]
		CALL CHECK_NEWLINE
	ENDIF

	IF (!(CFLAG:初期主導権 || TALENT:快感の否定 || TALENT:抵抗 || TALENT:抑圧) && TALENT:オトコ ) || ABL:従順 > 2
		PRINTFORMC \@ TEQUIP:主導権 ? Get # Pass　 \@Initiative[808]
		CALL CHECK_NEWLINE
	ENDIF

	IF ASSI >= 0
		IF ASSIPLAY && TEQUIP:25 == 0
			PRINTC Switch to Master[900]
		ELSEIF TEQUIP:25 == 0
			PRINTC Switch to Assistant[900]
		ENDIF
		CALL CHECK_NEWLINE
	ENDIF

	PRINTFORMC MAG-absorption(\@(CFLAG:ＭＡＧ非被吸収 == 1) ? OFF # ON \@)[902]
	CALL CHECK_NEWLINE


	IF ASSIPLAY
		PRINTFORMC MAG-transfer(\@(CFLAG:ASSI:ＭＡＧ非被吸収 == 1) ? OFF # ON \@)[906]
		CALL CHECK_NEWLINE
	ELSE
		PRINTFORMC MAG-transfer(\@(CFLAG:MASTER:ＭＡＧ非被吸収 == 1) ? OFF # ON \@)[906]
		CALL CHECK_NEWLINE
	ENDIF

	PRINTC Register Train Menu[980]
	CALL CHECK_NEWLINE
	FOR LOCAL, 0, 5
		SIF GLOBALS:LOCAL == "" || GLOBALS:LOCAL == @"調教メニュー{LOCAL+1}"
			GLOBALS:LOCAL = Training Menu {LOCAL+1}
		SIF !GETBIT(CFLAG:調教メニュー表示設定, LOCAL)
			CONTINUE
		PRINTFORMC %GLOBALS:LOCAL%[{981+LOCAL}]
		CALL CHECK_NEWLINE
	NEXT
	FOR LOCAL, 0, 3
		LOCALS = %AUTO_SPLIT(CSTR:(71+LOCAL),"|",10)%
		SIF LOCALS == "" || !GETBIT(CFLAG:調教メニュー表示設定, LOCAL+5)
			CONTINUE
		PRINTFORMC %LOCALS%[{986+LOCAL}]
		CALL CHECK_NEWLINE
	NEXT
	;IF FLAG:550 > 0
	;	PRINTC 調教メニュー表示[991]
	;	CALL CHECK_NEWLINE
	;	PRINTC 調教メニュー実行[992]
	;	CALL CHECK_NEWLINE
	;ENDIF

	PRINTC Command Filter [993]
	CALL CHECK_NEWLINE
ENDIF
PRINTC End training[999]
IF PREVCOM >= 0
	PRINTL 
	PRINT ＜Last training command:
	PRINTS TRAINNAME_S(TSTR:0)
	SIF TFLAG:前回の調教者 == ASSI
		PRINT (Assistant)
	PRINTL ＞
ENDIF

@USERCOM
#LOCALSIZE 2
IF RESULT < 0 || RESULT > 999
	RETURN 0
ELSEIF (RESULT >= 700 && RESULT <= 712) || RESULT == 715
	INVERTBIT CFLAG:TARGET:(RESULT + 170 - 700), 0
ELSEIF RESULT == 800
	CALL SHOW_EQUIP
	RETURN 1
ELSEIF RESULT == 801
	CALL STAIN_INFO
	RETURN 1
ELSEIF RESULT == 802
	CALL SHOW_CHARADATA,TARGET
	WAIT
	RETURN 1
ELSEIF RESULT == 803
	CALL CLOTHES_CHANGE,TARGET
	RETURN 1
ELSEIF RESULT == 804 && ( HAVE_PENIS(PLAYER) || HAVE_PENIS(TARGET) ) && ITEM:45 > 0
	CALL CONDOM_SETTING
	RETURN 1
ELSEIF RESULT == 805 && ( HAVE_PENIS(PLAYER) || HAVE_PENIS(TARGET) )
	CALL SAMENPLACE_SETTING
	RETURN 1
ELSEIF RESULT == 806
	CALL SHOW_CHARADATA,PLAYER
	WAIT
	RETURN 1
ELSEIF RESULT == 808 && ((!(CFLAG:初期主導権 || TALENT:快感の否定 || TALENT:抵抗 || TALENT:抑圧) && TALENT:オトコ ) || ABL:従順 > 2)
	INVERTBIT TEQUIP:主導権 , 0
	RETURN 1
ELSEIF RESULT == 900 && ASSI > 0 && TEQUIP:25 == 0
	IF ASSIPLAY
		ASSIPLAY = 0
		PLAYER = MASTER
	ELSE
		ASSIPLAY = 1
		PLAYER = ASSI
	ENDIF
	RETURN 1
ELSEIF RESULT == 902
	IF CFLAG:ＭＡＧ非被吸収 == 0
		CFLAG:ＭＡＧ非被吸収 = 1
	ELSE
		CFLAG:ＭＡＧ非被吸収 = 0
	ENDIF
	RETURN 1
ELSEIF RESULT == 906
	IF ASSIPLAY
		IF CFLAG:ASSI:ＭＡＧ非被吸収 == 0
			CFLAG:ASSI:ＭＡＧ非被吸収 = 1
		ELSE
			CFLAG:ASSI:ＭＡＧ非被吸収 = 0
		ENDIF
	ELSE
		IF CFLAG:MASTER:ＭＡＧ非被吸収 == 0
			CFLAG:MASTER:ＭＡＧ非被吸収 = 1
		ELSE
			CFLAG:MASTER:ＭＡＧ非被吸収 = 0
		ENDIF
	ENDIF
	RETURN 1
ELSEIF RESULT == 980
	CALL COMSEQ_REGISTER
	RETURN 1
ELSEIF RANGE(RESULT, 981, 988)
	CALL COMSEQ_TRAIN, RESULT - 981
	RETURN 1
;ELSEIF RESULT == 991 && FLAG:550 > 0
;	CALL COMSEQ_SHOW
;	RETURN 1
;ELSEIF RESULT == 992 && FLAG:550 > 0
;	CALL COMSEQ_TRAIN
;	RETURN 1
ELSEIF RESULT == 993
	CALL COM_FILTER, TARGET
	RETURN 1
ELSEIF RESULT == 999
	BEGIN AFTERTRAIN
	RETURN 1
ELSEIF Q:RESULT >= 0
	DOTRAIN Q:RESULT
ENDIF
RETURN 0

@SHOW_EQUIP
T = TARGET
CALL SHOW_INFO_JUEL
IF PREVCOM >= 0
	PRINT Last training command:
	;前回調教コマンド
	PRINTS TRAINNAME_S(TSTR:0)
	SIF TFLAG:前回の調教者 == ASSI
		PRINT (Assistant)
		;助手
	PRINTL 
ENDIF
SIF TEQUIP:26
	PRINTL [Aphrodiasc]
SIF TEQUIP:27
	PRINTL [Diuretic]
IF TEQUIP:34 == 1
	PRINTL [強精神薬反動中]
ELSEIF TEQUIP:34 > 1
	PRINTL [強精神薬効果発揮中]
ENDIF

SIF TEQUIP:28
	PRINTFORML [Recording video({TFLAG:70 - 1}/10)]
	;ビデオ撮影中
SIF TEQUIP:29
	PRINTL [野外プレイ中]
SIF TEQUIP:46 >= 1
	PRINTFORML [野次馬{TEQUIP:46}人]
SIF TEQUIP:30
	PRINTL [羞恥プレイ中]
SIF TEQUIP:31
	PRINTL [お風呂場プレイ中]
SIF TEQUIP:33
	PRINTL [新妻プレイ中]
SIF TEQUIP:25
	PRINTL [触手調教中]

IF TEQUIP:13 && TEQUIP:25
	PRINTL [Tentacle insertion]
ELSEIF TEQUIP:13
	PRINTL [Vibrator]
ENDIF

IF TEQUIP:14 && TEQUIP:25
	PRINTL [Tentacle anal insertion]
ELSEIF TEQUIP:14
	PRINTL [Anal vibrator]
ENDIF

IF TEQUIP:11 && TEQUIP:25
	PRINTL [触手クリ責め]
ELSEIF TEQUIP:11
	PRINTL [Clit cap]
ENDIF

IF TEQUIP:16 && TEQUIP:25
	PRINTL [Tentacle nipple tease]
ELSEIF TEQUIP:16
	PRINTL [Nipple caps]
ENDIF
IF TEQUIP:17 && TEQUIP:25
	PRINTL [Tentacle milking]
ELSEIF TEQUIP:17
	PRINTL [Milker]
ENDIF
SIF TEQUIP:24
	PRINTL [乳房電極装備中]

IF TEQUIP:12 && TEQUIP:25
	PRINTL [触手ペニス責め]
ELSEIF TEQUIP:12
	PRINTL [Onahole]
ENDIF

IF TEQUIP:32
	PRINTL [Shower]
ENDIF

IF TEQUIP:19 && TEQUIP:25
	PRINTL [Bound(tentacles)]
ELSEIF TEQUIP:19
	PRINTL [Bound(rope)]
ENDIF

IF TEQUIP:21 && TEQUIP:25
	PRINTL [触手浣腸]
ELSEIF TEQUIP:21
	PRINTL [浣腸＋アナルプラグ装備中]
ENDIF

SIF TEQUIP:36
	PRINTL [触手口辱中]

SIF TEQUIP:18
	PRINTL [アイマスク装備中]
SIF TEQUIP:20
	PRINTL [Ball-gag equipped]
	;ボールギャグ装備中
SIF TEQUIP:15
	PRINTL [Anal beads]
SIF TEQUIP:22
	PRINTL [Balloon dilator]
SIF TEQUIP:23
	PRINTL [アナル電極装備中]

SIF TEQUIP:41
	PRINTL [Wooden horse]
SIF TEQUIP:42
	PRINTL [Ejacultation suppressed]
	
SIF TEQUIP:PLAYER:26
	PRINTL [Aphrodiasc(Trainer)]
	;調教者
	
IF TEQUIP:PLAYER:13 && TEQUIP:25
	PRINTL [Tentacle insertion(Trainer)]
ELSEIF TEQUIP:PLAYER:13
	PRINTL [Vibrator(Trainer)]
ENDIF

IF TEQUIP:PLAYER:14 && TEQUIP:25
	PRINTL [Tentacle anal insertion(Trainer)]
ELSEIF TEQUIP:PLAYER:14
	PRINTL [Anal vibrator(Trainer)]
ENDIF

IF TEQUIP:PLAYER:11 && TEQUIP:25
	PRINTL [触手クリ責め(Trainer)]
ELSEIF TEQUIP:PLAYER:11
	PRINTL [Clit cap(Trainer)]
ENDIF

IF TEQUIP:PLAYER:16 && TEQUIP:25
	PRINTL [Tentacle nipple tease(Trainer)]
ELSEIF TEQUIP:PLAYER:16
	PRINTL [Nipple caps(Trainer)]
ENDIF
IF TEQUIP:PLAYER:17 && TEQUIP:25
	PRINTL [Tentacle milking(Trainer)]
ELSEIF TEQUIP:PLAYER:17
	PRINTL [Milker(Trainer)]
ENDIF

IF TEQUIP:PLAYER:12 && TEQUIP:25
	PRINTL [触手ペニス責め(Trainer)]
ELSEIF TEQUIP:PLAYER:12
	PRINTL [Onahole(Trainer)]
ENDIF

SIF TEQUIP:PLAYER:15
	PRINTL [Anal beads(Trainer)]

SIF EX:0
	PRINTFORM Ｃorgasm:{EX:0} times　
	;絶頂	回
SIF EX:1
	PRINTFORM Ｖorgasm:{EX:1} times　
SIF EX:2
	PRINTFORM Ａorgasm:{EX:2} times　
SIF EX:3
	PRINTFORM Ｂorgasm:{EX:3} times　
SIF EX:4
	PRINTFORM 　Milk spray:{EX:4} times　
	;噴乳
SIF EX:5
	PRINTFORM 　Ejaculation:{EX:5} times　
SIF EX:0 || EX:1 || EX:2 || EX:3 || EX:4 || EX:5
	PRINTL 
SIF EX:PLAYER:0
	PRINTFORM Trainer Ｃorgasm:{EX:PLAYER:0} times　
SIF EX:PLAYER:1
	PRINTFORM Trainer Ｖorgasm:{EX:PLAYER:1} times　
SIF EX:PLAYER:2
	PRINTFORM Trainer Ａorgasm:{EX:PLAYER:2} times　
SIF EX:PLAYER:3
	PRINTFORM Trainer Ｂorgasm:{EX:PLAYER:3} times　
SIF EX:PLAYER:0 || EX:PLAYER:1 || EX:PLAYER:2 || EX:PLAYER:3 || EX:PLAYER:4 || EX:PLAYER:5
	PRINTL 

WAIT

@CONDOM_SETTING
A = 0
B = 0
C = 0
T = 0
PRINTW Please select a character to edit condom settings
;コンドームの設定を行う対象を選択してください
PRINTL  
REPEAT 3
	IF (HAVE_PENIS(MASTER)) && A == 0
		PRINTFORM [{COUNT}] %CALLNAME:MASTER%
		IF TEQUIP:MASTER:35 && (FLAG:98 & 4)
			PRINTL (装着済み、常備)
		ELSEIF TEQUIP:MASTER:35
			PRINTL (装着済み)
		ELSE
			PRINTL (Raw)
			;未装着
		ENDIF
			A = 1
	ELSEIF ASSI >= 0
		IF HAVE_PENIS(ASSI) && B == 0
			PRINTFORM [{COUNT}] %CALLNAME:ASSI%
			IF TEQUIP:ASSI:35 && (FLAG:98 & 2)
				PRINTL (装着済み、常備)
			ELSEIF TEQUIP:ASSI:35
				PRINTL (装着済み)
			ELSE
				PRINTL (Raw)
			ENDIF
			B = 1
		ENDIF
	ELSEIF HAVE_PENIS(TARGET) && C == 0
		PRINTFORM [{COUNT}] %CALLNAME:TARGET%
		IF TEQUIP:TARGET:35 && (FLAG:98 & 1)
			PRINTL (装着済み、常備)
		ELSEIF TEQUIP:TARGET:35
			PRINTL (装着済み)
		ELSE
			PRINTL (Raw)
		ENDIF
			C = 1
	ENDIF
REND
PRINTL [100]Return
$INPUT_LOOP
INPUT
IF RESULT == 0
	IF A == 1
		T = MASTER
	ELSEIF B == 1
		T = ASSI
	ELSEIF C == 1
		T = TARGET
	ENDIF
ELSEIF RESULT == 1
	IF B == 1
		T = ASSI
	ELSEIF C == 1
		T = TARGET
	ENDIF
ELSEIF RESULT == 2
	T = TARGET
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF

PRINTL Please select the codom setting
;コンドームの設定をしてください
PRINTL  
	PRINTFORM Configuration Target:%CALLNAME:T%
	IF TEQUIP:T:35
		PRINT (装着済み
		IF (T == MASTER && (FLAG:98 & 4)) || (T == ASSI && (FLAG:98 & 2)) || (T == TARGET && (FLAG:98 & 1))
			PRINTL 、常備)
		ELSE
			PRINTL )
		ENDIF
	ELSE
		PRINTL (Raw)
	ENDIF
PRINTL [0]  Always wear a condom
;コンドームを常に装着する
PRINTL [1]  Wear a condom
;コンドームを装着する
PRINTL [2]  Don't wear a condom
;コンドームを外す
PRINTL [100]Return

$INPUT_LOOP1
INPUT
IF RESULT == 0
	IF TEQUIP:T:35 == 0
		ITEM:45 -= 1
		TEQUIP:T:35 = 1
	ENDIF
	SIF T == MASTER
		FLAG:98 |= 4
	SIF T == ASSI
		FLAG:98 |= 2
	SIF T == TARGET
		FLAG:98 |= 1
	PRINTFORMW %CALLNAME:T%は、コンドームを常につけるようにした
ELSEIF RESULT == 1
	IF TEQUIP:T:35 == 0
		;淫乱・娼婦持ち
		IF TALENT:PLAYER:4 || TALENT:PLAYER:7
			IF PLAYER != T
				IF RAND:2 == 0
					PRINTFORMW %CALLNAME:PLAYER%は淫らな笑みを浮かべてコンドームを取り出すと、%CALLNAME:T%のペニスに口で装着させた…
				ELSE
					PRINTFORMW %CALLNAME:PLAYER%は慣れた手つきでコンドームを咥えると、そのまま口で%CALLNAME:T%のペニスに装着させた…
				ENDIF
			ELSEIF PLAYER == MASTER && ASSI > 0
				SELECTCASE RAND:3
					CASE 0
						PRINTFORMW %CALLNAME:PLAYER%はコンドームを取り出すと、%CALLNAME:ASSI%に口で%CALLNAME:T%のペニスに装着させた…
					CASE 1
						PRINTFORMW %CALLNAME:ASSI%は慣れた手つきでコンドームを咥えると、そのまま口で%CALLNAME:T%のペニスに装着した…
					CASE 2
						PRINTFORMW %CALLNAME:PLAYER%はコンドームを取り出すと、%CALLNAME:TARGET%に口で%CALLNAME:T%のペニスに装着させた…
				ENDSELECT
			ELSE
				PRINTFORMW %CALLNAME:PLAYER%はコンドームを取り出すと、%CALLNAME:TARGET%に口で%CALLNAME:T%のペニスに装着させた…
			ENDIF
		ELSE
			PRINTFORMW %CALLNAME:T%は、コンドームをつけた
		ENDIF
		ITEM:45 -= 1
		TEQUIP:T:35 = 1
	ELSEIF (T == MASTER && (FLAG:98 & 4)) || (T == ASSI && (FLAG:98 & 2)) || (T == TARGET && (FLAG:98 & 1))
		PRINTFORMW %CALLNAME:T%は、コンドームを常につけないようにした
		SIF T == MASTER
			FLAG:98 &= 3
		SIF T == ASSI
			FLAG:98 &= 5
		SIF T == TARGET
			FLAG:98 &= 6
	ELSE
		PRINTFORMW %CALLNAME:T%は、既にコンドームをつけている
	ENDIF
ELSEIF RESULT == 2
	PRINTFORMW %CALLNAME:T%はコンドームを外した
	SIF T == MASTER
		FLAG:98 &= 3
	SIF T == ASSI
		FLAG:98 &= 5
	SIF T == TARGET
		FLAG:98 &= 6
	TEQUIP:T:35 = 0
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP1
ENDIF


@CHECK_NEWLINE
Z += 1
IF Z == TRAINPRINTCPL()
	PRINTL 
	Z = 0
ENDIF

@COM_FILTER, ARG
;改行用カウントをリセットしておく
LOCAL:1 = 0
DRAWLINE
IF CFLAG:ARG:利用フィルタ == 1
	PRINTL 【Common Setting 1】
	;【共通設定1】
ELSEIF CFLAG:ARG:利用フィルタ == 2
	PRINTL 【Common Setting 2】
ELSEIF CFLAG:ARG:利用フィルタ == 3
	PRINTL 【Common Setting 3】
ELSEIF CFLAG:ARG:利用フィルタ == 4
	PRINTFORML 【%CALLNAME:ARG%s individual Setting】
ELSE
	PRINTFORML 【No active filter】
	;【フィルター未使用】
ENDIF
;ページの決定で、LOCAL:3は開始のTRAIN、LOCAL:4が終了位置
IF LOCAL:3 == 0
	LOCAL:4 = 101
ELSEIF LOCAL:3 == 101
	LOCAL:4 = 1000
;ELSE
;	LOCAL:4 = 1000
ENDIF
;フィルターかかってるコマンドは灰色に
SETCOLOR 102, 102, 102
FOR LOCAL, LOCAL:3, LOCAL:4
	SIF CFLAG:ARG:利用フィルタ == 0
		BREAK
	;名前の長さが0の場合、ないものとみなす
	SIF STRLENS(TRAINNAME:LOCAL) == 0
		CONTINUE
	;共通設定モードの時は共通設定
	;フィルタリングフラグが立ってないとき、通常表示、つまり立ってる時だけ灰色
	IF GET_COM_FILTERING(LOCAL, ARG)
		PRINTFORMC %TRAINNAME_E(LOCAL)%[{LOCAL,3}]
	ELSE
		PRINTFORMCD %TRAINNAME_E(LOCAL)%[{LOCAL,3}]
	ENDIF
	;改行処理
	LOCAL:1 += 1
	SIF (LOCAL:1 % TRAINPRINTCPL()) == 0
		PRINTL 
NEXT
IF (LOCAL:1 % TRAINPRINTCPL())
	PRINTL 
	LOCAL:1 = 0
ENDIF
RESETCOLOR
DRAWLINE
SETCOLOR 102, 255, 255
;現在ページも色で表す
IF LOCAL:3 == 0
	PRINTC [1000] Page 1
ELSE
	PRINTCD [1000] Page 1
ENDIF
IF LOCAL:3 == 101
	PRINTC [1001] Page 2
ELSE
	PRINTCD [1001] Page 2
ENDIF
;IF LOCAL:3 == 300
;	PRINTCD [1002] ページ3
;ELSE
;	PRINTC [1002] ページ3
;ENDIF
PRINTL 
;色を戻す
RESETCOLOR
DRAWLINE
PRINTL [1003] Return
SETCOLOR 102, 255, 255
IF CFLAG:ARG:利用フィルタ == 0
	PRINTL [1004] Use no filter
	;利用しない
ELSE
	PRINTDL [1004] Use no filter
ENDIF
IF CFLAG:ARG:利用フィルタ == 1
	PRINTL [1005] Common Setting 1
ELSE
	PRINTDL [1005] Common Setting 1
ENDIF
IF CFLAG:ARG:利用フィルタ == 2
	PRINTL [1006] Common Setting 2
ELSE
	PRINTDL [1006] Common Setting 2
ENDIF
IF CFLAG:ARG:利用フィルタ == 3
	PRINTL [1007] Common Setting 3
ELSE
	PRINTDL [1007] Common Setting 3
ENDIF
IF CFLAG:ARG:利用フィルタ == 4
	PRINTL [1008] Individual Setting
	;個別設定
ELSE
	PRINTDL [1008] Individual Setting
ENDIF
SIF CFLAG:ARG:利用フィルタ
	PRINTDL [1009] Reset
RESETCOLOR
$INPUT_LOOP
INPUT
IF RESULT == 1000
	LOCAL:3 = 0
ELSEIF RESULT == 1001
	LOCAL:3 = 101
;ELSEIF RESULT == 1002
;	LOCAL:3 = 300
ELSEIF RESULT == 1003
	RETURN 1
ELSEIF RESULT == 1004
	PRINTFORMW All filters disabled
	;フィルターを利用しません
	CFLAG:ARG:利用フィルタ = 0
ELSEIF RESULT == 1005
	PRINTW Common Setting 1 is used.
	;共通設定1を利用します
	CFLAG:ARG:利用フィルタ = 1
ELSEIF RESULT == 1006
	PRINTW Common Setting 2 is used.
	CFLAG:ARG:利用フィルタ = 2
ELSEIF RESULT == 1007
	PRINTW Common Setting 3 is used.
	CFLAG:ARG:利用フィルタ = 3
ELSEIF RESULT == 1008
	PRINTW Individual Settings will be used
	;個別設定を利用します
	CFLAG:ARG:利用フィルタ = 4
ELSEIF RESULT == 1009 && CFLAG:ARG:利用フィルタ
	PRINTL Reset all current settings.
	;現在表示している設定をリセットします
	PRINTL Are you sure?
	;本当によろしいですか？
	CALL INPUT_YN, "Yes", "No"
	IF RESULT == 0
		FOR LOCAL, SET_COM_FILTERING_NUM(ARG), SET_COM_FILTERING_NUM(ARG)+13
			IF CFLAG:ARG:利用フィルタ == 4
				CFLAG:ARG:LOCAL = 0
			ELSE
				FLAG:LOCAL = 0
			ENDIF
		NEXT
		PRINTW Reset complete
		;リセットしました
	ENDIF
ELSEIF RESULT < 0 || RESULT > 1000 || !CFLAG:ARG:利用フィルタ
	GOTO INPUT_LOOP
ELSEIF STRLENS(TRAINNAME:RESULT)
	CALL INVERT_COM_FILTERING(RESULT, ARG)
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART


@GET_COM_FILTERING(ARG, ARG:1 = -123456789)
#FUNCTION
;隠し番号入力で対象をTARGETにもとい、省略でTARGET
SIF ARG:1 == -123456789
	ARG:1 = TARGET
SIF ARG:1 < -1 || ARG:1 >= CHARANUM
	THROW "ARG:1の値が異常です"
SIF ARG < 0 || ARG >1008
	THROW "ARGの値が異常です"
;使うCFLAGの決定
LOCAL = ARG / 63 + SET_COM_FILTERING_NUM(ARG:1)
;そのCFLAGの何番目のBITか
LOCAL:1 = ARG % 63
;フラグチェック
IF CFLAG:(ARG:1):利用フィルタ == 0
	RETURNF 0
ELSEIF CFLAG:(ARG:1):利用フィルタ < 4
	SIF GETBIT(FLAG:LOCAL, LOCAL:1)
		RETURNF 1
ELSE
	SIF GETBIT(CFLAG:(ARG:1):LOCAL, LOCAL:1)
		RETURNF 1
ENDIF
RETURNF 0
@INVERT_COM_FILTERING(ARG, ARG:1 = -123456789)
;隠し番号入力で対象をTARGETにもとい、省略でTARGET
SIF ARG:1 == -123456789
	ARG:1 = TARGET
SIF ARG:1 < -1 || ARG:1 >= CHARANUM
	PRINTW ARG:1の値が異常です
SIF ARG < 0 || ARG >1008
	PRINTW ARGの値が異常です
;使うFLAGの決定
LOCAL = ARG / 63 + SET_COM_FILTERING_NUM(ARG:1)
;そのCFLAGの何番目のBITか
LOCAL:1 = ARG % 63
;フラグ反転
IF CFLAG:(ARG:1):利用フィルタ < 4
	INVERTBIT FLAG:LOCAL, LOCAL:1
ELSE
	INVERTBIT CFLAG:(ARG:1):LOCAL, LOCAL:1
ENDIF
RETURN 0

;フィルタリングのFLAGの先頭の位置を返す
@SET_COM_FILTERING_NUM(ARG)
#FUNCTION
IF CFLAG:ARG:利用フィルタ == 1
	RETURNF GETNUM(FLAG, "フィルター共通設定1")
ELSEIF CFLAG:ARG:利用フィルタ == 2
	RETURNF GETNUM(FLAG, "フィルター共通設定2")
ELSEIF CFLAG:ARG:利用フィルタ == 3
	RETURNF GETNUM(FLAG, "フィルター共通設定3")
ELSEIF CFLAG:ARG:利用フィルタ == 4
	RETURNF GETNUM(CFLAG, "コマンドフィルター")
ELSE
	RETURNF -1
ENDIF
