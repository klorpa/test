;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:STRIP_THEATER.ERB
;	Facility	:ストリップ劇場での労役に関する関数群です。
;
;	Licence		:ライセンスフリー
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		----					新規作成
;	002		2011/09/08		黒天					処女(再生処女)喪失処理を関数を呼ぶように修正
;													ＮＴＲイベントで一部の処理の対象がACTORではなくLOCALになっていたのを修正
;	003		2013/11/24		ひみつ					リスト表示数設定
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;==========================================================
;@MANAGEMENT_STRIP_THEATER
; ストリップ劇場の一日の活動報告処理
;<引数>
;	ARG - 店舗番号
;<戻り値>
;	なし
;==========================================================
@MANAGEMENT_STRIP_THEATER,ARG
#LOCALSIZE 5
#DIM GUEST, 1
#DIM ACTOR, 1
#DIM DIFFICULTY, 1
#DIM ACT, 10
#DIM REWARD,1
#DIM EXCITE, 1
#DIM SUCCESS, 1
#DIM PLAY_TOTAL, 1
;変数の初期化
VARSET ACT, -1
VARSET LOCAL, 0
;LOCAL:12 = 0
GUEST = 0
;LOCAL:3 = 0
;REPEAT CHARANUM
;演者のリスト作成 及び　基本の観客数の設定
FOR LOCAL, 0 , CHARANUM
	IF CFLAG:LOCAL:労役店舗 == ARG+1
		ACT:(CFLAG:LOCAL:劇場出演順番-1) = LOCAL
		;基本観客数
		GUEST += CFLAG:LOCAL:劇場人気/20 + 1
	ENDIF
NEXT
;演者がいない場合はここで終了
IF MATCH(ACT, -1) == 10
	PRINTFORML 
	PRINTFORML 本日は休演です
	PRINTFORML 維持費 ￥{店舗("規模",ARG) * 500}
	MONEY -= 店舗("規模",ARG) * 500
	PRINTL 
	RETURN 0
ENDIF
;REND
;IF ACTOR_TOTAL > 0
GUEST /= 10
;客数の範囲は10〜規模*100
GUEST = LIMIT(GUEST, 10, 店舗("規模",ARG) * 100)
;SIF GUEST <= 10
;	GUEST = 10
;SIF GUEST >= 店舗("規模",ARG) * 100
;	GUEST = 店舗("規模",ARG) * 100
;ENDIF
;客興奮度
EXCITE = 10
;おひねり金額
REWARD = 0
FOR LOCAL, 0, 店舗("規模",ARG)
	;一応異常値は蹴る
	SIF ACT:LOCAL < 0
		CONTINUE
	;現在の演者を設定
	ACTOR = ACT:LOCAL
	SKIPDISP GETBIT(FLAG:ストリップ劇場報告設定, ARG)
	PRINTL
	DRAWLINE
	PRINTFORM 　演目{LOCAL+1}　%CALLNAME:ACTOR% 観客{GUEST}人
	PRINTFORML
	;----------------------------
	;メッセージの表示
	;----------------------------
	LOCAL:1 = TARGET
	TARGET = ACTOR
	TFLAG:13 = 43
	CALL EVENT_MESSAGE_STRIP, ARG , GUEST
	CALL KOJO_MESSAGE_EVENT
	SKIPDISP GETBIT(FLAG:ストリップ劇場報告設定, ARG)
	TARGET = LOCAL:1
	;-------------------------
	;難易度設定
	;-------------------------
	;基本値
	;ストリップ
	IF CFLAG:ACTOR:劇場演目 == 0
		DIFFICULTY = 12
	;ポールダンス
	ELSEIF CFLAG:ACTOR:劇場演目 == 1
		DIFFICULTY = 18
	;オナニーショー
	ELSE
		DIFFICULTY = 24
	ENDIF
	;ABLによる難易度修正
	DIFFICULTY -= ABL:ACTOR:欲望
	DIFFICULTY -= ABL:ACTOR:露出癖
	DIFFICULTY -= ABL:ACTOR:マゾっ気
	;TALENTによる難易度修正
	SIF TALENT:ACTOR:恋慕
		DIFFICULTY += 2
	SIF TALENT:ACTOR:淫乱
		DIFFICULTY -= 2
	SIF TALENT:ACTOR:服従
		DIFFICULTY -= 1
	SIF TALENT:ACTOR:親愛
		DIFFICULTY += 4
	SIF TALENT:ACTOR:娼婦
		DIFFICULTY -= 4
	SIF TALENT:ACTOR:隷属
		DIFFICULTY -= 2
	SIF TALENT:ACTOR:崩壊
		DIFFICULTY += 10
	SIF TALENT:ACTOR:プライド高い
		DIFFICULTY += 1
	SIF TALENT:ACTOR:プライド低い
		DIFFICULTY -= 1
	SIF TALENT:ACTOR:保守的
		DIFFICULTY += 1
	SIF TALENT:ACTOR:好奇心
		DIFFICULTY -= 1
	SIF TALENT:ACTOR:一線越えない
		DIFFICULTY += 2
	SIF TALENT:ACTOR:目立ちたがり
		DIFFICULTY -= 1
	SIF TALENT:ACTOR:貞操観念
		DIFFICULTY += 2
	SIF TALENT:ACTOR:貞操無頓着
		DIFFICULTY -= 2
	SIF TALENT:ACTOR:恥じらい
		DIFFICULTY += 1
	SIF TALENT:ACTOR:恥薄い
		DIFFICULTY -= 1
	SIF DIFFICULTY < 0
		DIFFICULTY = 0
	;-------------------------
	;成功判定
	;最大10回判定で、失敗あるいは大失敗が出るまで連続して試行(回数はPLAY_TOTALに取得)
	;試行中は客の興奮度(EXCITE)/人数(GUEST)/報酬(REWARD)が変動する
	;また、演目の成功度をSUCCESSに取得する
	;-------------------------
	;上演回数
	PLAY_TOTAL = 0
	;成功度
	SUCCESS = 0
	DRAWLINE
	DO
		PLAY_TOTAL++
		;成功判定Ａ
		LOCAL:1 = RAND:6 + 1
		;成功判定Ｂ
		LOCAL:2 = RAND:6 + 1
		PRINTFORML 成功判定{PLAY_TOTAL}回目　観客{GUEST}人
		PRINTFORM 難易度:{DIFFICULTY} 判定:{LOCAL:1} + {LOCAL:2}
		IF LOCAL:1 + LOCAL:2 == 12  || LOCAL:1 + LOCAL:2 - DIFFICULTY >= 5
			PRINTL 　<<大成功>>
			GUEST += RAND:5 + 16
			SIF GUEST >= 店舗("規模",ARG) * 100
				GUEST = 店舗("規模",ARG) * 100
			EXCITE += RAND:30
			SIF EXCITE >=  100
				EXCITE =  100
			SUCCESS += RAND:10+1
			IF CFLAG:ACTOR:引継ぎ回数 && FLAG:5 < 9 && CHECK_TIMELIMIT(0,DAY < 51,1)
				REWARD += MAX(0,RAND:(GUEST*EXCITE*LIMIT(CFLAG:ACTOR:劇場人気/20+100, 100, 500)/(10000 * 店舗("規模",ARG))+1)/100)
			ELSE
				REWARD += RAND:(GUEST*EXCITE*LIMIT(CFLAG:ACTOR:劇場人気/20+100, 100, 500)/(10000 * 店舗("規模",ARG))+1)
			ENDIF
		ELSEIF LOCAL:1 + LOCAL:2 == 2  || LOCAL:1 + LOCAL:2 - DIFFICULTY <= -5
			PRINTL 　<<大失敗>>
			GUEST -= RAND:5 + 6
			SIF GUEST <= 0
				GUEST = 1
			EXCITE -= RAND:20
			SIF EXCITE <= 0
				EXCITE = 1
			SUCCESS -= RAND:5+1
			BREAK
		ELSEIF LOCAL:1 + LOCAL:2 - DIFFICULTY >= 0
			PRINTL 　<<成功>>
			GUEST += RAND:10 + 1
			SIF GUEST >= 店舗("規模",ARG) * 100
				GUEST = 店舗("規模",ARG) * 100
			EXCITE += RAND:20
			SIF EXCITE >=  100
				EXCITE =  100
			SUCCESS += RAND:5+1
			IF CFLAG:ACTOR:引継ぎ回数 && FLAG:5 < 9 && CHECK_TIMELIMIT(0,DAY < 51,1)
				REWARD += MAX(0,RAND:(GUEST*EXCITE*LIMIT(CFLAG:ACTOR:劇場人気/40+50, 50, 250)/(10000 * 店舗("規模",ARG))+1)/100)
			ELSE
				REWARD += RAND:(GUEST*EXCITE*LIMIT(CFLAG:ACTOR:劇場人気/40+50, 50, 250)/(10000 * 店舗("規模",ARG))+1)
			ENDIF
		ELSE
			PRINTL 　<<失敗>>
			GUEST -= RAND:5 + 1
			SIF GUEST <= 0
				GUEST = 1
			EXCITE -= RAND:10
			SIF EXCITE <= 0
				EXCITE = 1
			SUCCESS -= RAND:3+1
			BREAK
		ENDIF
		DIFFICULTY ++
	LOOP PLAY_TOTAL < 10
	
	CALL 帳簿閲覧_労役, ACTOR, GUEST, 0, 5

;	IF FLAG:ストリップ劇場報告設定
;		NOSKIP
;			PRINTFORML 演目[{LOCAL+1}] %CALLNAME:ACTOR,16, LEFT% 観客:{GUEST,3}人 Stamina({BASE:ACTOR:体力,5} - {LOCAL:3 * (100 + PLAY_TOTAL), 4} = {BASE:ACTOR:体力-LOCAL:3 * (100 + PLAY_TOTAL), 5})
;		ENDNOSKIP
;	ENDIF
	;----------------------------
	;体力の消費とストレスの加算
	;LOCAL:3は否定の珠の加算にも用いる
	;上演回数(PLAY_TOTAL)に比例する
	;----------------------------
	DRAWLINE
	;ストリップ
	IF CFLAG:ACTOR:劇場演目 == 0
		LOCAL:3 = 3
	;ポールダンス
	ELSEIF CFLAG:ACTOR:劇場演目 == 1
		LOCAL:3 = 5
	;オナニーショー
	ELSE
		LOCAL:3 = 7
	ENDIF
	PRINTFORML Stamina　　　　　{BASE:ACTOR:体力,10}   - {LOCAL:3 * (100 + PLAY_TOTAL), 6}    = {BASE:ACTOR:体力-LOCAL:3 * (100 + PLAY_TOTAL), 10}
	BASE:ACTOR:体力 -= LOCAL:3 * (100 + PLAY_TOTAL)
	IF ACTOR != MASTER
		PRINTFORML Stress　　　{CFLAG:ACTOR:ストレス値,10}   + {LOCAL:3, 6}    = {CFLAG:ACTOR:ストレス値+LOCAL:3, 10}
		CFLAG:ACTOR:ストレス値 += LOCAL:3
	ENDIF

	IF FLAG:ストリップ劇場報告設定
		NOSKIP
			PRINTFORML 演目[{LOCAL+1}] %CALLNAME:ACTOR,16, LEFT% 観客:{GUEST,3}人 Stamina({BASE:ACTOR:体力,5} - {LOCAL:3 * (100 + PLAY_TOTAL), 4} = {BASE:ACTOR:体力-LOCAL:3 * (100 + PLAY_TOTAL), 5})
		ENDNOSKIP
	ENDIF

	;------------------------------
	;経験と球追加
	;PLAY_TOTAL 上演回数
	;EXCITE 客の興奮度
	;GUEST 客の人数
	;LOCAL:1 欲情の珠獲得数
	;LOCAL:2 恥情の珠獲得数
	;------------------------------
	PRINTFORML OutExposeEXP　{EXP:ACTOR:野外露出経験,10}   + {PLAY_TOTAL,6}    = {EXP:ACTOR:野外露出経験+PLAY_TOTAL,10}
	EXP:ACTOR:野外露出経験 += PLAY_TOTAL
	IF CFLAG:ACTOR:劇場演目 == 2
		PRINTFORML MasturbEXP　{EXP:ACTOR:自慰経験,10}   + {PLAY_TOTAL,6}    = {EXP:ACTOR:自慰経験+PLAY_TOTAL,10}
		EXP:ACTOR:自慰経験 += PLAY_TOTAL
	ENDIF
	SELECTCASE CFLAG:ACTOR:劇場演目
		CASE 0
			LOCAL:1 = GUEST*(PLAY_TOTAL)*ABL:ACTOR:欲望*(100+EXCITE)/100
			LOCAL:2 = GUEST*(PLAY_TOTAL)*ABL:ACTOR:露出癖*(100+EXCITE)/100
		CASE 1
			LOCAL:1 = GUEST*(PLAY_TOTAL)*ABL:ACTOR:欲望*2*(100+EXCITE)/100
			LOCAL:2 = GUEST*(PLAY_TOTAL)*ABL:ACTOR:露出癖*2*(100+EXCITE)/100
		CASE 2
			LOCAL:1 = GUEST*(PLAY_TOTAL)*ABL:ACTOR:欲望*3/2*(100+EXCITE)/100
			LOCAL:2 = GUEST*(PLAY_TOTAL)*ABL:ACTOR:露出癖*3/2*(100+EXCITE)/100
	ENDSELECT
	PRINTFORML 欲情の珠　　　{JUEL:ACTOR:欲情,10}   + {LOCAL:1,6}    = {JUEL:ACTOR:欲情+LOCAL:1,10}
	PRINTFORML 恥情の珠　　　{JUEL:ACTOR:恥情,10}   + {LOCAL:2,6}    = {JUEL:ACTOR:恥情+LOCAL:2,10}
	JUEL:ACTOR:欲情 += LOCAL:1
	JUEL:ACTOR:恥情 += LOCAL:2
	IF ACTOR != MASTER && GUEST*LOCAL:3/MAX(ABL:ACTOR:露出癖, 1)
		PRINTFORML 否定の珠　　　{JUEL:ACTOR:100,10}   + {GUEST*LOCAL:3/MAX(ABL:ACTOR:露出癖, 1),6}    = {JUEL:ACTOR:100+GUEST*LOCAL:3/MAX(ABL:ACTOR:露出癖, 1),10}
		JUEL:ACTOR:100 += GUEST*LOCAL:3/MAX(ABL:ACTOR:露出癖, 1)
	ENDIF
	PRINTFORMW 劇場人気　　　{CFLAG:ACTOR:劇場人気, 10}   \@SUCCESS >=0 ? + # - \@ {ABS(SUCCESS), 6}    = {CFLAG:ACTOR:劇場人気+SUCCESS, 10}
	CFLAG:ACTOR:劇場人気 += SUCCESS
	;経験と球追加ここまで

;ＮＴＲ進行
IF GETBIT(FLAG:ＮＴＲ機能 , 5)
	;専有奴隷の首輪
	IF (CFLAG:ACTOR:その他 == 1116 || CFLAG:ACTOR:その他2 == 1116 || CFLAG:ACTOR:その他3 == 1116)
		GOTO SKIP_NTR
	ENDIF
	v = 0
	SIF TALENT:ACTOR:ＮＴＲ == 2
		x = 30
	SIF CFLAG:ACTOR:現ＮＴＲ進行度 > 10
		x = 30
	SIF TALENT:ACTOR:10 || TALENT:ACTOR:14 || TALENT:ACTOR:111 || TALENT:ACTOR:112
		z += 10
	SIF TALENT:ACTOR:145
		z += 10
	y = CFLAG:ACTOR:放置日数
	SIF y > 20
		y = 20
	SIF (CFLAG:ACTOR:その他 == 1117 || CFLAG:ACTOR:その他2 == 1117 || CFLAG:ACTOR:その他3 == 1117)
		PRINTFORMW %CALLNAME:ACTOR%は何とも云えない甘い匂いを纏っている……

	IF RAND:2 == 0
		CFLAG:ACTOR:(2089) +=(ABL:ACTOR:欲望 + ABL:ACTOR:露出癖 + ABL:ACTOR:セックス中毒 + ABL:ACTOR:自慰中毒) * (2-CFLAG:ACTOR:構ったフラグ) + CFLAG:ACTOR:放置日数*10
	ELSE
		CFLAG:ACTOR:(2088) -=(ABL:ACTOR:欲望 + ABL:ACTOR:露出癖 + ABL:ACTOR:セックス中毒 + ABL:ACTOR:自慰中毒) * (2-CFLAG:ACTOR:構ったフラグ) + CFLAG:ACTOR:放置日数*10
	ENDIF
	CFLAG:ACTOR:淫乱度 += GUEST / 2
	IF RAND:100 < 10 + y + x || RAND:100 < x || (CFLAG:ACTOR:その他 == 1117 || CFLAG:ACTOR:その他2 == 1117 || CFLAG:ACTOR:その他3 == 1117) || TALENT:ACTOR:73 == 1
		IF TALENT:ACTOR:ＮＴＲ == 2
			PRINTFORML %CALLNAME:ACTOR%はチンピラ達に声をかけられた……
		ELSE
			PRINTFORML %CALLNAME:ACTOR%は劇場のファンに声をかけられた……	
		ENDIF
		PRINTFORML ・
		PRINTFORML ・・
		PRINTFORMW ・・・

		IF RAND:2 == 0
			CFLAG:ACTOR:(2089) +=(ABL:ACTOR:欲望 + ABL:ACTOR:露出癖 + ABL:ACTOR:セックス中毒 + ABL:ACTOR:自慰中毒) * (2-CFLAG:ACTOR:構ったフラグ) + CFLAG:ACTOR:放置日数*10
		ELSE
			CFLAG:ACTOR:(2088) -=(ABL:ACTOR:欲望 + ABL:ACTOR:露出癖 + ABL:ACTOR:セックス中毒 + ABL:ACTOR:自慰中毒) * (2-CFLAG:ACTOR:構ったフラグ) + CFLAG:ACTOR:放置日数*10
		ENDIF
			CFLAG:ACTOR:淫乱度 += GUEST / 2

		IF RAND:100 < 10 + y + x - 10 || TALENT:ACTOR:ＮＴＲ == 2 || (CFLAG:ACTOR:その他 == 1117 || CFLAG:ACTOR:その他2 == 1117 || CFLAG:ACTOR:その他3 == 1117) || TALENT:ACTOR:73 == 1

			CFLAG:ACTOR:淫乱度 +=  CFLAG:ACTOR:現ＮＴＲ進行度 * 10 + EXCITE * 5

			IF CFLAG:ACTOR:淫ＮＴＲ進行度 == 0
				CFLAG:ACTOR:淫ＮＴＲ進行度 ++
				PRINTFORMW %CALLNAME:ACTOR%は興奮した劇場のファンに路地裏に連れ込まれた……

			ELSEIF RAND:120 < 10 + z + x + y - 10 || TALENT:ACTOR:ＮＴＲ == 2 || (CFLAG:ACTOR:その他 == 1117 || CFLAG:ACTOR:その他2 == 1117 || CFLAG:ACTOR:その他3 == 1117) || TALENT:ACTOR:73 == 1
			CFLAG:ACTOR:淫乱度 += ( CFLAG:ACTOR:現ＮＴＲ進行度 * 10 + EXCITE * 5)*2
				w = 1
				IF TALENT:ACTOR:ＮＴＲ == 2
					PRINTFORMW %CALLNAME:ACTOR%はチンピラ達にマンションの一室に連れ込まれた……
				ELSE
					PRINTFORMW %CALLNAME:ACTOR%が劇場のファンに案内された先には複数の男達が待ちうけていた……
				ENDIF
				
				IF CFLAG:ACTOR:淫ＮＴＲ進行度 < 2 && ! TALENT:ACTOR:ＮＴＲ == 2
					CFLAG:ACTOR:淫ＮＴＲ進行度 = 2
				ENDIF
				IF CFLAG:ACTOR:108 == 1
					IF RAND:100 < CFLAG:ACTOR:現ＮＴＲ進行度 * 5
						CFLAG:ACTOR:108 -= 1
						PRINTFORML %CALLNAME:ACTOR%の子宮内避妊結界が男達の一人に解除されてしまったようだ……
					ENDIF
				ENDIF
				IF RAND:100 < CFLAG:ACTOR:現ＮＴＲ進行度 * 5
					IF CFLAG:ACTOR:109 == 0
						CFLAG:ACTOR:109 += 1
						PRINTFORML %CALLNAME:ACTOR%は男達の一人に排卵誘発剤を飲まされた……
					ENDIF
				ENDIF
				IF w == 1					
					v = x + y + z
					SIF v > 40
						v = 32
					w = 0
				ENDIF
				IF TALENT:ACTOR:ＮＴＲ == 2
					PRINTFORMW %CALLNAME:ACTOR%はチンピラ達に玩具を扱うかのように犯された……
					PRINTFORMW チンピラ達は%CALLNAME:ACTOR%の中出しされている様子を撮影し、裏ビデオとして流したようだ
				ELSE
					PRINTFORMW %CALLNAME:ACTOR%は周りを取り囲んだ男達に輪姦された……
					PRINTFORMW 輪姦されている%CALLNAME:ACTOR%の生々しい様子がカメラで撮影されている……
				ENDIF

				EXP:ACTOR:63 +=((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2) + 1)
				PRINTFORML %EXPNAME_E(63)%＋{((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2) + 1)}

			ELSE
				PRINTFORMW %CALLNAME:ACTOR%は興奮した劇場のファンに路地裏で強姦された……
			ENDIF

			EXP:ACTOR:0 += (EXCITE/10 + v) + 1
			EXP:ACTOR:1 += (EXCITE/15 + v/2) + 1
			EXP:ACTOR:2 += (((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2)/3) + 1)
			EXP:ACTOR:キス経験 += (EXCITE/10 + v) + 1
			;ふたなりかオトコ
			SIF TALENT:ACTOR:139 || TALENT:ACTOR:140
				EXP:ACTOR:3 += (EXCITE/15 + v/2) + 1
			;男の娘はケツマンコで妊娠
			SIF TALENT:ACTOR:男の娘 && CFLAG:ACTOR:108 == 0
				CFLAG:ACTOR:105 += (EXCITE/10 + v) + 1
			EXP:ACTOR:4 += ((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2) + 1)
			;CreampieEXP
			IF CFLAG:ACTOR:108 == 0
				EXP:ACTOR:5 += (EXCITE/10 + v) + 1
				CFLAG:ACTOR:105 += (EXCITE/10 + v) + 1
			ENDIF
			EXP:ACTOR:20 += ((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2) + (EXCITE/12 + v) + 1+ 1)
			EXP:ACTOR:22 += (EXCITE/10 + v)/2+1

			PRINTFORML %EXPNAME_E(0)%＋{(EXCITE/10 + v) + 1}

			PRINTFORML %EXPNAME_E(1)%＋{(EXCITE/15 + v/2) + 1}
			PRINTFORML %EXPNAME_E(2)%＋{ (((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2)/3) + 1)}
			PRINTFORML KissEXP＋{(EXCITE/10 + v) + 1}
			SIF (TALENT:ACTOR:ふたなり || TALENT:ACTOR:オトコ)
				PRINTFORML %EXPNAME_E(3)%＋{(EXCITE/15 + v/2) + 1}

			PRINTFORML %EXPNAME_E(4)%＋{ ((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2) + 1)}

			IF CFLAG:ACTOR:108 == 0
				PRINTFORML %EXPNAME_E(5)%＋{(EXCITE/10 + v) + 1}
			ELSE
				PRINTFORML <避妊結界>
			ENDIF

			PRINTFORML %EXPNAME_E(20)%＋{((EXCITE/10 + v) + 1) + ((EXCITE/15 + v/2) + (EXCITE/10 + v)/2 + 1+ 1)}
			PRINTFORML %EXPNAME_E(22)%＋{(EXCITE/10 + v)/2+1}
			PRINTFORML GangbangEXP +{1}
			EXP:ACTOR:輪姦経験 += 1	
			;処女/再生処女喪失
			CALL 処女喪失, ACTOR
			CALL 処女喪失イベント, ACTOR
			PRINTW

		ELSE
		PRINTFORMW %CALLNAME:ACTOR%は劇場のファンとしばらく会話をしていたようだ……

		ENDIF

	ENDIF
ENDIF
$SKIP_NTR

NEXT
SKIPDISP 0
;取得おひねりと入場料入手処理
DRAWLINE
PRINTFORML 収入合計: 入場料 {GUEST}魔貨 おひねり￥{REWARD}
IF REWARD >= 店舗("規模",ARG) * 500
	PRINTL 入手したおひねりから維持費を出した
	REWARD -= 店舗("規模",ARG) * 500
	PRINTFORML 入手金額 {GUEST}魔貨 ￥{REWARD}
	MONEY += REWARD
	MONEY:1 += GUEST
ELSE
	PRINTL 入手したおひねりから維持費を出した
	REWARD = 店舗("規模",ARG) * 500 - REWARD
	PRINTFORML 入手金額 {GUEST}魔貨 維持費￥{REWARD}
	MONEY -= REWARD
	MONEY:1 += GUEST
ENDIF
PRINTFORML 
;==========================================================
;@ARRANGE_MANAGEMENT_STRIP_THEATER
; 劇場の演者および演目の設定処理
;<引数>
;	ARG - 店舗番号
;<戻り値>
;	なし
;==========================================================
@ARRANGE_MANAGEMENT_STRIP_THEATER,ARG
#DIM ACT, 10
#DIM LINE, 1
CLEARLINE 9
LINE = LINECOUNT
$START
CLEARLINE LINECOUNT - LINE
VARSET ACT, -1
;演者のリスト作成
FOR LOCAL, 0 , CHARANUM
	SIF CFLAG:LOCAL:労役店舗 == ARG+1
		ACT:(CFLAG:LOCAL:劇場出演順番-1) = LOCAL
NEXT

CUSTOMDRAWLINE = 
FOR LOCAL, 0, 店舗("規模",ARG)
	PRINTFORM [{LOCAL}] 演目{LOCAL+1}　\@ACT:LOCAL >= 0 ? %CALLNAME:(ACT:LOCAL),16,LEFT% # 休演\@
	IF ACT:LOCAL < 0
		PRINTL 
		CONTINUE
	ENDIF
	SELECTCASE CFLAG:(ACT:LOCAL):劇場演目
		CASE 0
			PRINTL 　ストリップ
		CASE 1
			PRINTL 　ポールダンス
		CASE 2
			PRINTL 　オナニーショー
	ENDSELECT
NEXT
DRAWLINE
PRINTL [1000]Return
$INPUT_LOOP_1
INPUT
IF RESULT == 1000
	CLEARLINE LINECOUNT - LINE
	CLEARLINE 4
	RETURN 0
ELSEIF RESULT < 0 || RESULT >= 店舗("規模",ARG)
	GOTO INPUT_LOOP_1
ELSEIF ACT:RESULT >= 0
	LOCAL = ACT:RESULT
	PRINTFORML %CALLNAME:LOCAL%の演目を変更しますか？
	CALL INPUT_SELECT(CFLAG:LOCAL:劇場演目 != 0, "ストリップに変更", (CFLAG:LOCAL:劇場演目 != 1) * 2, "ポールダンスに変更" ,(CFLAG:LOCAL:劇場演目 != 2) * 3, "オナニーショーに変更",4, "休演させる" , 9, "Return")
	SELECTCASE RESULT
		CASE 1, 2, 3
			CFLAG:LOCAL:劇場演目=  RESULT - 1
		CASE 4
			CFLAG:LOCAL:労役フラグ = 0
			CFLAG:LOCAL:劇場出演順番 = 0
			CFLAG:LOCAL:労役店舗 = 0
			CFLAG:LOCAL:劇場演目= 0
	ENDSELECT
	GOTO START
ENDIF
LOCAL:1 = RESULT
;REPEAT CHARANUM
;	IF CFLAG:COUNT:劇場出演順番 == (LOCAL:1)+1 && CFLAG:COUNT:労役店舗 == ARG+1
;		CFLAG:COUNT:労役フラグ = 0
;		CFLAG:COUNT:劇場出演順番 = 0
;		CFLAG:COUNT:労役店舗 = 0
;		DE:(50+RESULT):ARG = -1
;		DE:(60+RESULT):ARG = -1
;	ENDIF
;REND
CUSTOMDRAWLINE = 

P = 0
;表示させるキャラを抽出（Q:*に表示するキャラの登録番号、LOCAL:2に人数）
VARSET Q, -1
A = 0
REPEAT CHARANUM
	SIF WORKABLE(ARG,COUNT) == 0
		CONTINUE
	;SIF CFLAG:COUNT:労役フラグ != 0 && CFLAG:COUNT:労役フラグ != 10+ARG
	SIF CFLAG:COUNT:労役フラグ != 0
		CONTINUE
	SIF CFLAG:COUNT:この場に居ないフラグ == 1
		CONTINUE
	Q:A = COUNT
	A += 1
REND
LOCAL:2 = A

$PRINT_LIST
DRAWLINE
PRINTFORML この店舗で労役させたいキャラを選んでください
DRAWLINE
CALL MANAGEMENT_CHARALIST,ARG,0
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1007]Previous page\@
PRINTLC [1000]Return
PRINTFORMLC \@(P >= (LOCAL:2 - 1) / リスト表示数()) ? %" " * 16% # [1009]Next page\@

$INPUT_LOOP_2
INPUT
IF RESULT == 1000
	GOTO START
ELSEIF RESULT == 1007
	IF P > 0
		P -= 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_2
	ENDIF
ELSEIF RESULT == 1009
	LOCAL:3 = (LOCAL:2 - 1) / リスト表示数()
	IF P < LOCAL:3
		P += 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_2
	ENDIF
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MATCH(Q,RESULT) == 0
	GOTO INPUT_LOOP_2
ENDIF
LOCAL:4 = RESULT

PRINTFORML このキャラが行う演目を選んでください
PRINTLC [0]ストリップ
PRINTLC [1]ポールダンス
PRINTLC [2]オナニーショー
$INPUT_LOOP_3
INPUT
IF RESULT < 0 || RESULT >= 3
	GOTO INPUT_LOOP_3
ENDIF
CFLAG:(LOCAL:4):労役フラグ = 10+ARG
CFLAG:(LOCAL:4):劇場出演順番 = LOCAL:1+1
CFLAG:(LOCAL:4):労役店舗 = ARG+1
CFLAG:(LOCAL:4):劇場演目= RESULT
GOTO START
;DE:(50+(LOCAL:1)):ARG = CFLAG:(LOCAL:4):キャラ固有の番号
;DE:(60+(LOCAL:1)):ARG = RESULT
;==========================================================
;@EXTEND_MANAGEMENT_STRIP_THEATER
; 劇場の規模拡張処理
;<引数>
;	ARG - 店舗番号
;<戻り値>
;	なし
;==========================================================
@EXTEND_MANAGEMENT_STRIP_THEATER,ARG
PRINTFORML 現在、この店舗のＬＶは{店舗("規模",ARG)}です。

SELECTCASE 店舗("規模",ARG)
	CASE IS < 5
		LOCAL = 2000
	CASE IS < 10
		LOCAL = 3000
ENDSELECT
IF MONEY < LOCAL
	PRINTFORML 拡張するには￥{LOCAL}必要です。資金が足りません。
	RETURN 0
ELSEIF 店舗("規模",ARG) >= 10
	PRINTFORML この店舗はこれ以上拡張できません。
	RETURN 0
ELSE
	PRINTFORML ￥{LOCAL}で拡張出来ます。拡張しますか？
ENDIF

CALL INPUT_YN,"YES","NO"
SIF RESULT == 1
	RETURN 0
CALL 店舗演算("規模",ARG,1,"+")
MONEY -= LOCAL
;==========================================================
;@EXTEND_MANAGEMENT_STRIP_THEATER
; ストリップ劇場労役時の口上
; メッセージとしてはCALL EVENT_MESSAGEから呼ばれる
; が、ARGに引数を入れた場合、観客人数と劇場番号だけ渡して帰る
;<引数>
;	ARG		- 店舗番号
;	ARG:1	- 観客人数
;<戻り値>
;	なし
;==========================================================
@EVENT_MESSAGE_STRIP, ARG = -1, ARG:1
#DIM GUEST, 1
#DIM THEATER, 1
IF ARG >= 0
	THEATER = ARG
	GUEST = ARG:1
	RETURN 0
ENDIF
SELECTCASE CFLAG:TARGET:劇場演目
	CASE 0
		;ストリップ
		PRINTFORM %CALLNAME:TARGET%は
		;恋慕or親愛
		IF TALENT:TARGET:恋慕 || TALENT:TARGET:親愛
			;親愛かつ妻or夫なら、1/4の確率で最愛の人とか言い出す
			;主人の性別で更に分岐させてもいいんですが、女キャラに「最愛の妻」とか言われると違和感がw
			IF (RAND:4 == 0) && TALENT:TARGET:親愛 && (TALENT:TARGET:妻 || TALENT:TARGET:夫)
				PRINTFORM 最愛の人
			ELSE
				PRINTFORM %CALLNAME:MASTER%
			ENDIF
			PRINTFORM 以外の
		;服従or隷属
		ELSEIF TALENT:TARGET:服従 || TALENT:TARGET:隷属
			PRINTFORM 主人でもない
		ELSE
			PRINTFORM 見ず知らずの
		ENDIF
		PRINTFORM 相手に己の裸体を見せることに
		;恋慕で抵抗を感じ始め、親愛だと抵抗が強まる
		IF TALENT:TARGET:恋慕 || TALENT:TARGET:親愛
			SIF TALENT:TARGET:親愛
				PRINTFORM 強い
			PRINTFORM 抵抗と
		ENDIF
		;露出癖のLvで変化。[マゾ][恥じらい]があれば、そっち優先。
		IF TALENT:TARGET:マゾ
			PRINTFORM 被虐的な快楽
		ELSEIF TALENT:TARGET:恥じらい
			PRINTFORM 強い羞恥
		ELSEIF ABL:TARGET:露出癖 >= 7
			PRINTFORM 強烈な快美感
		ELSEIF ABL:TARGET:露出癖 >= 5
			PRINTFORM 快美感
		ELSEIF ABL:TARGET:露出癖 >= 3
			PRINTFORM 羞恥
		ELSE
			PRINTFORM 嫌悪
		ENDIF
		PRINTFORML を感じながら、
		;露出癖Lv7以上or淫乱or娼婦
		IF ABL:TARGET:露出癖 >= 7 || TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
			PRINTFORM 淫らに肢体をくねらせつつ
		ELSE
			;露出癖Lv2以下で文追加
			SIF ABL:TARGET:露出癖 < 3
				PRINTFORM 躊躇うような
			PRINTFORM ぎこちない仕草で
		ENDIF
		;巨乳or爆乳
		IF  TALENT:TARGET:巨乳 || TALENT:TARGET:爆乳
			PRINTFORM 肉感的な体を隠す
		;小柄体型
		ELSEIF TALENT:TARGET:小柄体型
			PRINTFORM 幼さの残る体を隠す
		ELSE
			PRINTFORM 身にまとう
		ENDIF
		PRINTFORMW 衣服を脱ぎ始めた…
		LOCAL:11 = 3
	CASE 1
;ポールダンス
		PRINTFORM %CALLNAME:TARGET%は
		;最大観客数の９割以上
		IF GUEST >= 店舗("規模", THEATER) * 90
			PRINTFORM 満員の
		;最大観客数の７割以上
		ELSEIF GUEST >= 店舗("規模", THEATER) * 70
			PRINTFORM 大勢の客で溢れる
		;最大客数の３割未満
		;ELSEIF GUEST < 店舗("規模", THEATER) * 30 
		;	PRINTFORM 空席の目立つ
		ENDIF
		;露出癖Lv3以上
		IF ABL:TARGET:露出癖 >= 3
			PRINTFORM 観客席を見て
			;観客数が少ないと対応変化
			IF GUEST < 店舗("規模", THEATER) * 30
				IF TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
					PRINTFORM 残念そうに
				ELSE
					PRINTFORM 安堵したように
				ENDIF
			;淫乱or娼婦
			ELSEIF TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
				PRINTFORM 淫らに
			;露出癖Lv5以上
			ELSEIF ABL:TARGET:露出癖 >= 5
				PRINTFORM にっこりと
			ELSE
				PRINTFORM ぎこちなく
			ENDIF
			PRINTFORM 微笑むと、
		ELSE
			PRINTFORM 観客席を見ようともせず、
		ENDIF
		;淫乱or娼婦
		IF TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
			PRINTFORML 快楽に染まった顔で
		;露出癖Lv5以上
		ELSEIF ABL:TARGET:露出癖 >= 5
			PRINTFORML 普段と変わらない仕草で
		ELSE
			PRINTFORML 羞恥に染まった顔のまま
		ENDIF
		;オトコと男の娘以外で胸関係の表記追加
		IF (TALENT:TARGET:オトコ == 0) && (TALENT:TARGET:男の娘 == 0)
			IF TALENT:TARGET:絶壁
				PRINTFORM 平坦な胸と
			ELSE
				SIF TALENT:TARGET:巨乳 || TALENT:TARGET:爆乳
					PRINTFORM 大きな
				SIF TALENT:TARGET:貧乳
					PRINTFORM 小さな
				PRINTFORM 乳房と
			ENDIF
		ENDIF
		;棒の有無で表記が変化
		IF TALENT:TARGET:オトコ || TALENT:TARGET:男の娘 || TALENT:TARGET:ふたなり
			IF TALENT:TARGET:小柄体型 || TALENT:TARGET:小人体型
				PRINTFORM 小振りな
			ELSEIF TALENT:TARGET:大柄
				PRINTFORM 太い
			ELSEIF TALENT:TARGET:巨体
			;ネタでしかないのでコメントアウト。マーラいないし。
			;	SIF マーラ様以外
			;		PRINTFORM ご立派様のように
				PRINTFORM 巨大な
			ENDIF
			PRINTFORM 肉棒
		ELSE
			;重ねると長くなり過ぎるので、どちらか一方のみ
			IF TALENT:TARGET:セックス狂
				PRINTFORM 開発されきった
			ELSEIF TALENT:TARGET:小柄体型
				PRINTFORM 幼い
			;ELSE
			;	PRINTFORM 
			ENDIF
			PRINTFORM 性器
		ENDIF
		PRINTFORM を晒し、
		;尻尾があると文追加
		IF TALENT:TARGET:尾
			PRINTFORM 尻尾を
			IF TALENT:TARGET:恥じらい
				PRINTFORM 恥ずかしげに
			ELSEIF TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
				PRINTFORM 扇情的に
			ELSE
				PRINTFORM ゆらゆらと
			ENDIF
			PRINTFORM 揺らしながら、
		ELSEIF TALENT:TARGET:小柄体型 || TALENT:TARGET:貧乳
			PRINTFORM 幼さを感じさせる
		ELSEIF TALENT:TARGET:巨乳 || TALENT:TARGET:爆乳
			PRINTFORM 肉感的な
		ELSEIF TALENT:TARGET:魅力 || TALENT:TARGET:謎の魅力
			PRINTFORM 魅力的な
		ENDIF
		PRINTFORM 裸体を
		;露出癖Lv5以上or淫乱or娼婦
		SIF (ABL:TARGET:露出癖 >= 5) || TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
			PRINTFORM 淫靡に
		PRINTFORM ポールに絡ませて
		;尾があると文が長くなり過ぎるので、ここで改行
		SIF TALENT:TARGET:尾
			PRINTFORML 
		PRINTFORMW 踊り始めた…
		LOCAL:11 = 5
	CASE 2
;オナニーショー
		PRINTFORM %CALLNAME:TARGET%は
		;最大観客数の９割以上
		IF GUEST >= 店舗("規模", THEATER) * 90
			PRINTFORM 満員の
		;最大観客数の７割以上
		ELSEIF GUEST >= 店舗("規模", THEATER) * 70
			PRINTFORM 大勢の客で溢れる
		;最大客数の３割未満
		;ELSEIF GUEST < 店舗("規模",ARG) * 30 
		;	PRINTFORM 空席の目立つ
		ENDIF
		;露出癖Lv3以上で観客席を見れるようになる
		IF ABL:TARGET:露出癖 >= 3
			PRINTFORM 観客席を見て
			;観客数が少ないと対応変化
			IF GUEST < 店舗("規模", THEATER) * 30
				IF TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
					PRINTFORM 残念そうに
				ELSE
					PRINTFORM 安堵したように
				ENDIF
			;淫乱or娼婦
			ELSEIF TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
				PRINTFORM 淫らに
			;露出癖Lv5以上
			ELSEIF ABL:TARGET:露出癖 >= 5
				PRINTFORM にっこりと
			ELSE
				PRINTFORM ぎこちなく
			ENDIF
			PRINTFORM 微笑むと、
		ELSE
			PRINTFORM 観客席を見ようともせず、
		ENDIF
		;淫乱or娼婦
		IF TALENT:TARGET:淫乱 || TALENT:TARGET:娼婦
			PRINTFORML 快楽に染まった顔で
		;露出癖Lv5以上
		ELSEIF ABL:TARGET:露出癖 >= 5
			PRINTFORML 普段と変わらない仕草で
		ELSE
			PRINTFORML 羞恥に染まった顔のまま
		ENDIF
		PRINTFORM 自らの裸体を晒し、
		;ランダムで４パターンに分岐。
		IF RAND:4 == 0
			;ない乳は揉めないので分岐
			IF TALENT:TARGET:絶壁
				PRINTFORM 平らな胸で自己主張する
				IF TALENT:TARGET:Ｂ敏感
					PRINTFORM 普通より敏感な
				ELSEIF TALENT:TARGET:淫乳
					PRINTFORM 開発されきった
				ENDIF
				PRINTFORM 乳首を弄り、
				SIF TALENT:TARGET:母乳体質
					PRINTFORML そこから母乳を滲ませながら、
			ELSE
				SIF TALENT:TARGET:巨乳 || TALENT:TARGET:爆乳
					PRINTFORM 大きな
				SIF TALENT:TARGET:貧乳
					PRINTFORM 小ぶりな
				PRINTFORM 乳房を揉みしだき、
				SIF TALENT:TARGET:母乳体質
					PRINTFORML 先端から母乳を噴出させながら、
			ENDIF
		;[Male][Crossdresser]の場合はスキップ、ふたなりの場合は、スジが棒の下に隠れてると解釈
		ELSEIF (RAND:4 == 1) && (TALENT:TARGET:オトコ == 0) && (TALENT:TARGET:男の娘 == 0)
			IF TALENT:TARGET:処女
				PRINTFORM 未使用の
			ELSEIF TALENT:TARGET:セックス狂
				PRINTFORM 開発されきった
			ENDIF
			SIF TALENT:TARGET:小柄体型
				PRINTFORM 幼い
			PRINTFORM 性器に
			;処女膜がない場合はバイブオナニーに
			IF (TALENT:TARGET:処女 == 0) && (TALENT:TARGET:再生処女 == 0)
				;バイブ購入の有無で分けるべきかもしれませんが、とりあえず劇場側から一時レンタルしてると解釈
				PRINTFORM バイブを挿入し、それを
				SIF ABL:TARGET:Ｖ感覚 >= 5
					PRINTFORM 子宮まで届きそうな勢いで激しく
				PRINTFORM 前後させて
			ELSE
				PRINTFORM 指を挿し入れ、
				;長くなるので、ここで改行
;				IF TALENT:TARGET:処女 || TALENT:TARGET:再生処女
					PRINTFORML 膜を傷つけないように注意しながら、
;				ENDIF
				IF TALENT:TARGET:自慰狂い || TALENT:TARGET:セックス狂 || ABL:TARGET:自慰中毒 >= 7
					PRINTFORM 激しく
				ELSEIF ABL:TARGET:自慰中毒 >= 3
					PRINTFORM ゆっくりと
				ELSE
					PRINTFORM つたなく
				ENDIF
				PRINTFORM 指を動かして
			ENDIF
		;Ａ感覚Lv5以上or尻穴狂いorＡSensitiveでアナニーに発展
		ELSEIF (RAND:4 == 2) && ((ABL:TARGET:Ａ感覚 >= 5) || TALENT:TARGET:尻穴狂い || TALENT:TARGET:Ａ敏感)
				IF TALENT:TARGET:Ａ敏感
					PRINTFORM 普通より敏感な
				ELSEIF TALENT:TARGET:尻穴狂い
					PRINTFORM 開発されきった
				ENDIF
				PRINTFORM 尻穴に
				;Ａ感覚Lv2までは躊躇いがある感じで
				SIF ABL:TARGET:Ａ感覚 < 3
					PRINTFORM 躊躇いがちに
				PRINTFORM 指を挿し入れ、
				;Ａ感覚Lv5以上で、自慰狂いがあるか、自慰中毒Lv5以上で躊躇がなくなる
				IF (ABL:TARGET:Ａ感覚 >= 5) && (TALENT:TARGET:自慰狂い || ABL:TARGET:自慰中毒 >= 5) 
					PRINTFORM 激しく
				ELSEIF ABL:TARGET:Ａ感覚 >= 3
					PRINTFORM ゆっくりと
				ELSE
					PRINTFORM 恐る恐る
				ENDIF
				PRINTFORM 抜き挿しして

		ELSE
			;棒の有無で内容分岐
			IF (TALENT:TARGET:オトコ == 0) && (TALENT:TARGET:男の娘 == 0) && (TALENT:TARGET:ふたなり == 0)
				SIF TALENT:TARGET:自慰狂い
					PRINTFORM 開発されきった
				SIF TALENT:TARGET:小柄体型 || TALENT:TARGET:小人体型
					PRINTFORM 小さく
				SIF TALENT:TARGET:Ｃ敏感
					PRINTFORM 普通より
				PRINTFORM 敏感な肉豆に
				SIF ABL:TARGET:Ｃ感覚 < 3
					PRINTFORM 躊躇いがちに
				PRINTFORM 触れて、
				IF ABL:TARGET:自慰中毒 >= 3 || TALENT:TARGET:自慰狂い
					PRINTFORM 慣れた
				ELSE
					PRINTFORM 慣れない
				ENDIF
				PRINTFORM 手付きで
			ELSE
				IF TALENT:TARGET:小柄体型 || TALENT:TARGET:小人体型
					PRINTFORM 小振りな
				ELSEIF TALENT:TARGET:大柄
					PRINTFORM 太い
				ELSEIF TALENT:TARGET:巨体
					;ネタでしかないのでコメントアウト。マーラいないし。
					;SIF マーラ様以外
					;	PRINTFORM ご立派様のように
					PRINTFORM 巨大な
				ENDIF
				PRINTFORM 肉棒を
				IF ABL:TARGET:自慰中毒 >= 3 || TALENT:TARGET:自慰狂い
					PRINTFORM 慣れた
				ELSE
					PRINTFORM 慣れない
				ENDIF
				PRINTFORM 手付きでしごいて
			ENDIF
		ENDIF
		PRINTFORMW 自らを慰めた…
ENDSELECT
