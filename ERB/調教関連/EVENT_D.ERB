;-------------------------------------------------
;EVENT_D.ERB、EVENT_DEPENDENCE、依存度システムの処理
;依存度はプラスに大きいと主人に対して慕う形で依存し、
;マイナスに大きいと主人を畏怖する形で依存していく
;依存度ベクトルは各調教コマンドごとで指示されており、増加と減少で処理は異なっている
;-------------------------------------------------
@MASTER_DEPENDENCE_CHECK

;依存度のベクトル処理
;0は関数終了、崩壊も依存度は変化しない
IF TFLAG:33 == 0 || TALENT:9 == 1
	PRINTL <No dependency change>
	;依存度変化無し
	RETURN 0
;1は依存度が0以上で増加、0未満で減少
ELSEIF TFLAG:33 == 1
	SIF CFLAG:2 >= 0
		CALL MASTER_DEPENDENCE_PLUS
	SIF CFLAG:2 < 0
		CALL MASTER_DEPENDENCE_MINUS
;2は増加、但し服従、隷属持ちは減少
ELSEIF TFLAG:33 == 2
	IF TALENT:5 || TALENT:8
		CALL MASTER_DEPENDENCE_MINUS
	ELSE
		CALL MASTER_DEPENDENCE_PLUS
	ENDIF
;3は減少、但し恋慕、親愛持ちは増加
ELSEIF TFLAG:33 == 3
	IF TALENT:3 || TALENT:6
		CALL MASTER_DEPENDENCE_PLUS
	ELSE
		CALL MASTER_DEPENDENCE_MINUS
	ENDIF
;4は強制増加
ELSEIF TFLAG:33 == 4
	CALL MASTER_DEPENDENCE_PLUS
;5は強制減少
ELSEIF TFLAG:33 == 5
	CALL MASTER_DEPENDENCE_MINUS
ENDIF

RETURN 0


;依存度増加処理
@MASTER_DEPENDENCE_PLUS

R = 0

;ABL:従順をみる
R += ABL:0*2

;ABL:奉仕精神をみる
R += ABL:7

;難易度を見る
;EASY
IF FLAG:4 == 1
	R += 1
;NORMAL
ELSEIF FLAG:4 == 2
	R += 0
;HARD
ELSEIF FLAG:4 == 3
	R -= 1
ENDIF

;素質による増減
;陥落素質
;恋慕
SIF TALENT:3 && ASSIPLAY == 0
	R += 5
;親愛
SIF TALENT:4 && ASSIPLAY == 0
	R += 7
;淫乱
SIF TALENT:5 && GET_RELATION(TARGET, PLAYER) > 100
	R += 3
;娼婦
SIF TALENT:6 && GET_RELATION(TARGET, PLAYER) > 100
	R += 5
;服従
SIF TALENT:7
	R += 10
;隷属
SIF TALENT:8
	R += 20

;その他素質
;自慰狂い
SIF TALENT:74
	R += 1
;セックス狂
SIF TALENT:75
	R += 1
;尻穴狂い
SIF TALENT:76
	R += 1
;淫乳
SIF TALENT:77
	R += 1
;妄信
SIF TALENT:86
	R += 2


;主人の魅力
SIF TALENT:PLAYER:92
	R += 2
;主人の魅惑
SIF TALENT:PLAYER:93
	R += 3
;主人の謎の魅力
SIF TALENT:PLAYER:94
	R += 5
;主人の治療
SIF TALENT:PLAYER:135
	R += 2
;主人の鼓舞
SIF TALENT:PLAYER:136
	R += 2
;主人の人気
SIF TALENT:PLAYER:145
	R += 3



;依存度補正を加算
R += TFLAG:30

;結果が0未満の場合は0にする
SIF R < 0
	R = 0
SIF ASSIPLAY == 0
	CFLAG:2 += R
BASE:忠誠度 += LIMIT(R/2,1,10)
TCVAR:TARGET:獲得忠誠度 += LIMIT(R/2,1,10)
;PRINTFORML Loyalty +{LIMIT(R/2,1,10)}

;好感度処理
CFLAG:PLAYER:(CFLAG:TARGET:キャラ固有の番号 + 2100) += LIMIT(R/2,1,10)
SIF PLAYER != MASTER && CFLAG:PLAYER:構ったフラグ == 0
	CFLAG:PLAYER:(CFLAG:TARGET:キャラ固有の番号 + 2100) += LIMIT(R/2,1,10)
CFLAG:TARGET:(CFLAG:PLAYER:キャラ固有の番号 + 2100) += LIMIT(R/2,1,10)
SIF PLAYER != MASTER && CFLAG:TARGET:構ったフラグ == 0
	CFLAG:TARGET:(CFLAG:PLAYER:キャラ固有の番号 + 2100) += LIMIT(R/2,1,10)

;IF R > 0
;	PRINTL <依存度増加>
;ELSE
;	PRINTL <依存度変化無し>
;ENDIF
SIF ASSIPLAY == 0
	TCVAR:TARGET:獲得依存度 = R
R = 0
TFLAG:30 = 0


RETURN 0



;依存度減少処理
@MASTER_DEPENDENCE_MINUS

R = 0

;ABL:従順をみる
R += ABL:0*2

;ABL:マゾっ気をみる
R += ABL:9

;難易度を見る
;EASY
IF FLAG:4 == 1
	R += 1
;NORMAL
ELSEIF FLAG:4 == 2
	R += 0
;HARD
ELSEIF FLAG:4 == 3
	R -= 1
ENDIF

;素質による増減
;陥落素質
;恋慕
SIF TALENT:3
	R += 10
;親愛
SIF TALENT:4
	R += 20
;淫乱
M = NO:MASTER
SIF TALENT:5 && GET_RELATION(TARGET, MASTER) > 100
	R += 3
;娼婦
SIF TALENT:6 && GET_RELATION(TARGET, MASTER) > 100
	R += 5
;服従
SIF TALENT:7
	R += 5
;隷属
SIF TALENT:8
	R += 7

;その他素質
;自慰狂い
SIF TALENT:74
	R += 1
;セックス狂
SIF TALENT:75
	R += 1
;尻穴狂い
SIF TALENT:76
	R += 1
;淫乳
SIF TALENT:77
	R += 1
;サド
SIF TALENT:83
	R -= 1
;マゾ
SIF TALENT:84
	R += 1
;妄信
SIF TALENT:86
	R += 2


;主人の魅力
SIF TALENT:MASTER:92
	R += 2
;主人の魅惑
SIF TALENT:MASTER:93
	R += 3
;主人の謎の魅力
SIF TALENT:MASTER:94
	R += 5
;主人の治療
SIF TALENT:MASTER:135
	R += 2
;主人の鼓舞
SIF TALENT:MASTER:136
	R += 2
;主人の人気
SIF TALENT:MASTER:145
	R += 3


;依存度補正を加算
R += TFLAG:30

;結果が0未満の場合は0にする
SIF R < 0
	R = 0
SIF ASSIPLAY == 0 || 陥落(TARGET)
	CFLAG:2 -= R
BASE:忠誠度 += LIMIT(R/2,1,10)
TCVAR:TARGET:獲得忠誠度 += LIMIT(R/2,1,10)
;PRINTFORML Loyalty +{LIMIT(R/2,1,10)}

;IF 陥落(TARGET)
	;好感度処理
	CFLAG:PLAYER:(CFLAG:TARGET:キャラ固有の番号 + 2100) -= LIMIT(R/2,1,10)
	SIF PLAYER != MASTER && CFLAG:PLAYER:構ったフラグ == 0
		CFLAG:PLAYER:(CFLAG:TARGET:キャラ固有の番号 + 2100) -= LIMIT(R/2,1,10)
	CFLAG:TARGET:(CFLAG:PLAYER:キャラ固有の番号 + 2100) -= LIMIT(R/2,1,10)
	SIF PLAYER != MASTER && CFLAG:TARGET:構ったフラグ == 0
		CFLAG:TARGET:(CFLAG:PLAYER:キャラ固有の番号 + 2100) -= LIMIT(R/2,1,10)
;ENDIF


;IF R > 0
;	PRINTL <依存度減少>
;ELSE
;	PRINTL <依存度変化無し>
;ENDIF
SIF ASSIPLAY == 0 || 陥落(TARGET)
	TCVAR:TARGET:獲得依存度 = -R
R = 0
TFLAG:30 = 0


RETURN 0



