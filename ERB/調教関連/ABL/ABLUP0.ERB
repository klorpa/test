;従順の可否
@ISABLUP0
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:0 >= 10
	RETURN 0
ELSEIF ABL:0 >= 5 && !陥落(TARGET) && TARGET != MASTER
	RETURN 0
ENDIF

CALL DECIDE_ABLUP0

IF Z == 0
	RETURN 0
ELSE
	RETURN 1
ENDIF



;Obedience
@ABLUP0
$ABLUP_LOOP
DRAWLINE
;恋慕、親愛、服従、隷属はMAXLV10
IF ABL:0 >= 10
	PRINTW The abiltiy has been MAXed already.
	RETURN 0
ELSEIF ABL:0 >= 5 && !陥落(TARGET) && TARGET != MASTER
	PRINTW The abiltiy has been MAXed already.
	RETURN 0
ENDIF

CALL DECIDE_ABLUP0


;ＬＶ４から５に上げるときはAbnormalEXP必要（素質：[臆病][素直][Affection][親愛]なら無視できる）
IF ABL:0 == 4 && TALENT:10 == 0 && TALENT:13 == 0 && TALENT:3 == 0 && TALENT:6 == 0
	PRINTS EXPNAME_E(50)
	PRINTL  required
	;あり、かつ
;ＬＶ７から８に上げるときはAbnormalEXP3以上必要（素質：[臆病][素直][Affection][親愛]なら無視できる）
ELSEIF ABL:0 == 7 && TALENT:10 == 0 && TALENT:13 == 0 && TALENT:3 == 0 && TALENT:6 == 0
	PRINTS EXPNAME_E(50)
	PRINTL 3 or higher required
	;以上、かつ
ENDIF

PRINTFORM [0] - %PALAMNAME_E(11)% Orbs×{A}....
IF I == 0
	PRINT ＯＫ
ELSE
	SIF I & 1
		PRINT Not Enough Orbs 
	SIF I & 2
		PRINT Not Enough EXP 
ENDIF
PRINTL 

PRINTFORM [1] - %PALAMNAME_E(5)% Orbs×{B}....

IF J == 0
	PRINT ＯＫ
ELSE
	SIF J & 1
		PRINT Not Enough Orbs 
	SIF J & 2
		PRINT Not Enough EXP 
ENDIF
PRINTL 

IF C > 0
	;欲情の珠で上げる
	SIF JUEL:6 < C
		K |= 1

	PRINTFORM [2] - %PALAMNAME_E(6)% Orbs×{C}....

	IF K == 0
		PRINT ＯＫ
	ELSE
		SIF K & 1
			PRINT Not Enough Orbs 
		SIF K & 2
			PRINT Not Enough EXP 
	ENDIF
	PRINTL 
ELSE
	K = 256
ENDIF

IF D > 0
	;屈服の珠で上げる
	SIF JUEL:7 < D
		L |= 1

	PRINTFORM [3] - %PALAMNAME_E(7)% Orbs×{D}....

	IF L == 0
		PRINT ＯＫ
	ELSE
		SIF L & 1
			PRINT Not Enough Orbs 
		SIF L & 2
			PRINT Not Enough EXP 
	ENDIF
	PRINTL 
ELSE
	L = 256
ENDIF

IF Z == 0
	WAIT
	RETURN
ENDIF

PRINTL [100] - Quit

PRINTFORMLC   %" " * 42%
PRINTLC [200] - Auto-Upgrade ability
;Auto-Upgrade ability

INPUT
IF (RESULT < 0 || RESULT > 3) && RESULT != 100 && RESULT != 200
	RETURN 0
ELSEIF I != 0 && RESULT == 0
	PRINTL Conditions are not met
	;条件を満たしていません
	RETURN 0
ELSEIF J != 0 && RESULT == 1
	PRINTL Conditions are not met
	;条件を満たしていません
	RETURN 0
ELSEIF K == 256 && RESULT == 2
	RESTART
ELSEIF K != 0 && RESULT == 2
	PRINTL Conditions are not met
	;条件を満たしていません
	RETURN 0
ELSEIF L == 256 && RESULT == 3
	RESTART
ELSEIF L != 0 && RESULT == 3
	PRINTL Conditions are not met
	;条件を満たしていません
	RETURN 0
ELSEIF RESULT == 100
	RETURN 0
ELSEIF RESULT == 200
	CALL AUTOMAX, 0
	RETURN 0
ENDIF

ABL:0 += 1

IF RESULT == 0
	JUEL:11 -= A
ELSEIF RESULT == 1
	JUEL:5 -= B
ELSEIF RESULT == 2
	JUEL:6 -= C
ELSEIF RESULT == 3
	JUEL:7 -= D
ENDIF

PRINTS ABLNAME_E(0)
PRINT  ability level became 
;のレベルが
PRINTV ABL:0
PRINTW .
;になりました
GOTO ABLUP_LOOP

;従順LvUP判定
@DECIDE_ABLUP0
;条件別にＯＫかダメかを記録する
I = 0
J = 0
K = 0
L = 0
;能力アップ可能なら1にする
Z = 0

IF ABL:0 == 0
	A = 10
	B = 10
	C = 150
	D = 100
ELSEIF ABL:0 == 1
	A = 150
	B = 100
	C = 450
	D = 600
ELSEIF ABL:0 == 2
	A = 500
	B = 300
	C = 1000
	D = 1200
ELSEIF ABL:0 == 3
	A = 3000
	B = 3000
	C = 0
	D = 0
ELSEIF ABL:0 == 4
	A = 6000
	B = 6000
	C = 0
	D = 0
ELSEIF ABL:0 == 5
	A = 8000
	B = 10000
	C = 0
	D = 0
ELSEIF ABL:0 == 6
	A = 10000
	B = 15000
	C = 0
	D = 0
ELSEIF ABL:0 == 7
	A = 14000
	B = 20000
	C = 0
	D = 0
ELSEIF ABL:0 == 8
	A = 18000
	B = 25000
	C = 0
	D = 0
ELSEIF ABL:0 == 9
	A = 25000
	B = 35000
	C = 0
	D = 0
ENDIF


;恋慕
SIF TALENT:3
	TIMES B , 0.80
;親愛
SIF TALENT:6
	TIMES B , 0.60
;服従
SIF TALENT:5
	TIMES A , 0.70
;隷属
SIF TALENT:8
	TIMES A , 0.40

;一線越えない
IF TALENT:27 && (TALENT:3 == 0 && TALENT:4 == 0 && TALENT:5 == 0 && 陥落() < 2)
	TIMES A , 2.00
	TIMES B , 2.00
	TIMES C , 2.00
	TIMES D , 2.00
ENDIF

;ＬＶ４から５に上げるときはAbnormalEXP必要（素質：[臆病][素直][Affection][親愛]なら無視できる）
IF ABL:0 == 4 && TALENT:10 == 0 && TALENT:13 == 0 && TALENT:3 == 0 && TALENT:6 == 0
	IF EXP:50 == 0
		;AbnormalEXPがない
		I |= 2
		J |= 2
		K |= 2
		L |= 2
	ENDIF
;ＬＶ７から８に上げるときはAbnormalEXP3以上必要（素質：[臆病][素直][Affection][親愛]なら無視できる）
ELSEIF ABL:0 == 7 && TALENT:10 == 0 && TALENT:13 == 0 && TALENT:3 == 0 && TALENT:6 == 0
	IF EXP:50 < 3
		;AbnormalEXPが3未満
		I |= 2
		J |= 2
		K |= 2
		L |= 2
	ENDIF
ENDIF

;恐怖の珠で上げる
SIF JUEL:11 < A
	I |= 1

IF I == 0
	Z = 1
ENDIF

;恭順の珠で上げる
SIF JUEL:5 < B
	J |= 1

IF J == 0
	Z = 1
ENDIF

IF C
	;欲情の珠で上げる
	SIF JUEL:6 < C
		K |= 1

	IF K == 0
		Z = 1
	ENDIF
ELSE
	K = 256
ENDIF

IF D
	;屈服の珠で上げる
	SIF JUEL:7 < D
		L |= 1

	IF L == 0
		Z = 1
	ENDIF
ELSE
	L = 256
ENDIF
RETURN 1

