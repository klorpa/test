;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		2011/03/25		UNP						売却・陥落条件表示の作成
;	002		2013/11/24		ひみつ					料理2+歌唱2の判定を修正
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#

;============================================================================
;ネタバレっぽい要素をステータス画面に表示する
;============================================================================

;============================================================================
;表示用の汎用関数
;条件に合致している場合とそうでない場合で色を変える
;そもそも表示するかどうかのチェックも含めて統合
;ARGS:0 表示文字列, ARG:0 達成条件, ARG:1 0=通常 1=経験等一度超えると取り返しが付かない物
;
;返り値 達成条件を満たしていれば1, それ以外の場合は0
;表示の時に
;============================================================================
@SPOILER_PRINT_FLAG( ARGS:0, ARG:0, ARG:1=0)
#FUNCTION
#LOCALSIZE 10

LOCAL:0 = GETCOLOR()
LOCAL:1 = 0xFFFFFF
LOCAL:2 = 0x444444
LOCAL:3 = 0xFFFF00
LOCAL:4 = 0x440000
	
;達成条件によって色を変える
IF ARG:0
	;Accomplishment
	SETCOLOR LOCAL:(ARG:1 * 2 + 1)
	LOCAL:9 = 1
ELSE
	;未達成
	SETCOLOR LOCAL:(ARG:1 * 2 + 2)
	LOCAL:9 = 0
ENDIF
PRINTFORM %ARGS:0% 
SETCOLOR LOCAL:0
RETURNF LOCAL:9

;============================================================================
;売却条件の達成状況表示
;TALENTに対象キャラクターが設定されている前提
;============================================================================
@SPOILER_PRINT_SELL
#LOCALSIZE 5

IF CFLAG:売却可能 < 2
	;売却条件の表示
	IF CFLAG:売却可能 == 0
		;基礎条件
		PRINT Sale Condition： 
		LOCAL:0 = SPOILER_PRINT_FLAG("Obedience + Desire 6; ", (ABL:従順+ABL:欲望)>=6, 0)
		LOCAL:1 = 1
		IF ((TALENT:11 && TALENT:5 == 0 && TALENT:8 == 0) || TALENT:12)
			LOCAL:0 = SPOILER_PRINT_FLAG("Obedience 4; ", (ABL:従順>=4), 0) && LOCAL:0
			LOCAL:1 += 1
		ENDIF
		IF (TALENT:20 || TALENT:32 || TALENT:34)
			LOCAL:0 = SPOILER_PRINT_FLAG("Desire 4; ", (ABL:欲望>=4), 0) && LOCAL:0
			LOCAL:1 += 1
		ENDIF
		
		;IF LOCAL:0
		;	CALLF SPOILER_PRINT_FLAG("(OK)",1)
		;ELSE
		;	CALLF SPOILER_PRINT_FLAG(@"({LOCAL:0}/{LOCAL:1})",0)
		;ENDIF
		
		PRINTL
		;追加条件
		PRINT Additional Condition： 
		LOCAL:2 =0
		LOCAL:2 += SPOILER_PRINT_FLAG("Total Sensitivity 8; ",  ABL:3+ABL:4+ABL:5+ABL:6 >= 8)
		LOCAL:2 += SPOILER_PRINT_FLAG("Total Sensitivity 13; ",  ABL:3+ABL:4+ABL:5+ABL:6 >= 13)
		LOCAL:2 += SPOILER_PRINT_FLAG("Obedience 4; ",  ABL:従順 >= 4)
		LOCAL:2 += SPOILER_PRINT_FLAG("Desire 4; ",  ABL:欲望 >= 4)
		PRINTL 
		PRINTFORM %" ",23%
		LOCAL:2 += SPOILER_PRINT_FLAG("Technique 2 + Servicing 2; ", ABL:技巧 >= 2 && ABL:奉仕精神 >= 2)
		LOCAL:2 += SPOILER_PRINT_FLAG("Technique 4 + Servicing 4; ", ABL:技巧 >= 4 && ABL:奉仕精神 >= 4)
		PRINTL 
		PRINTFORM %" ",23%
		LOCAL:2 += SPOILER_PRINT_FLAG("Exhibitionism 3 + Masturb addict; ",  ABL:露出癖 >= 3 && ABL:自慰中毒)
		LOCAL:2 += SPOILER_PRINT_FLAG("Masochism 3; ",  ABL:マゾっ気 >= 3)
		LOCAL:2 += SPOILER_PRINT_FLAG("Semen addict; ",  ABL:精液中毒)
		PRINTL 
		PRINTFORM %" ",23%
		LOCAL:2 += SPOILER_PRINT_FLAG("Beast addict; ",  ABL:獣姦中毒)
		LOCAL:2 += SPOILER_PRINT_FLAG("Tentacle addict; ",  ABL:触手中毒)
		LOCAL:2 += SPOILER_PRINT_FLAG("Gangbang addict; ",  ABL:輪姦中毒)
		LOCAL:2 += SPOILER_PRINT_FLAG("Kiss addict; ",  ABL:キス中毒)
		PRINTL 
		PRINTFORM %" ",23%
		;実際の判定はＢＬっ気3+レズ中毒でもいいけど、それは珍しい状況なので表記としてはこうする
		IF IS_MALE(TARGET)
			LOCAL:2 += SPOILER_PRINT_FLAG("ＢＬism 3 + ＢＬ addict; ",  (ABL:レズっ気 >= 3 && ABL:レズ中毒) || (ABL:ＢＬっ気 >= 3 && ABL:ＢＬ中毒))
		ELSE
			LOCAL:2 += SPOILER_PRINT_FLAG("Lesbianism 3 + Lesbian addict; ",  (ABL:レズっ気 >= 3 && ABL:レズ中毒) || (ABL:ＢＬっ気 >= 3 && ABL:ＢＬ中毒))
		ENDIF
		LOCAL:2 += SPOILER_PRINT_FLAG("Cooking 2 + Singing 2; ",  ABL:料理技能 >= 2 && ABL:歌唱技能 >= 2)
		PRINTL 
		PRINTFORM %" ",23%
		LOCAL:2 += SPOILER_PRINT_FLAG("Submission seal 3 + Pleasure seal 3; ",  MARK:屈服刻印 >= 3 && MARK:快楽刻印 >= 3)
		LOCAL:2 += SPOILER_PRINT_FLAG("Submission seal 3 + Pain seal 3; ",  MARK:屈服刻印 >= 3 && MARK:苦痛刻印 >= 3)
		
		LOCAL:3 = MIN(6,CSVBASE(NO:TARGET,GETNUM(BASE,"LV"),0)/10 + (ABL:種族 == 0))
		IF LOCAL:2 > LOCAL:3
			CALLF SPOILER_PRINT_FLAG("(OK)",1)
		ELSE
			CALLF SPOILER_PRINT_FLAG(@"({LOCAL:2}/{LOCAL:3+1})",0)
		ENDIF
		PRINTL
	ENDIF

	;助手条件の表示
	;実判定は素質オトコを見ているけど、後々のためIS_MALEを使う
	PRINT Assistant Condition： 
	LOCAL:3 = SPOILER_PRINT_FLAG(@"Available-for-sale; ", CFLAG:売却可能==1)
	
	LOCAL:3  = ABL:0+ABL:1+ABL:2+(IS_MALE(TARGET) ? ABL:ＢＬっ気 # ABL:レズっ気)
	LOCALS = (OK)
	SIF LOCAL:3<12
		LOCALS = ({LOCAL:3}/12)
	
	;リアル
	IF IS_MALE(TARGET)
		CALLF SPOILER_PRINT_FLAG(@"Obedience + Desire + Technique + ＢＬism %LOCALS%", LOCAL:3>=12)
	ELSE
		CALLF SPOILER_PRINT_FLAG(@" Obedience + Desire + Technique + Lesbianism %LOCALS%", LOCAL:3>=12)
	ENDIF
	
	PRINTL
	DRAWLINE
ELSE
ENDIF	
RETURN

;============================================================================
;陥落条件の達成状況表示
;TALENTに対象キャラクターが設定されている前提
;依存度の達成状況だけはサードアイが無いとわからないようにした
;（条件表を見る代わりというつもりなので、攻略を有利になってしまうと良くないので）
;AbnormalEXPを少なく保つなど、一度超えると元に戻せない高難度条件は色を変えて表示。赤字があると達成不可
;陥落はさせられるけど上位に行けない場合も色変えた方がいいかも
;============================================================================
@SPOILER_PRINT_FALLTALENT
#LOCALSIZE 1

	;同性愛の経験処理は現在の性別と関係してないけど、表記としてはわかりやすいようにこうしておく
	IF IS_MALE(TARGET)
		LOCALS = ＢＬ
	ELSE
		LOCALS = Lesbian
	ENDIF

IF TALENT:3 == 0 && TALENT:4 == 0 && TALENT:5 == 0 && TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0 && TALENT:9 == 0
	;陥落素質がない状態（３通りの取得条件を表示）
	;恋慕
	PRINT Affection Condition： 
	CALLF SPOILER_PRINT_FLAG("Obedience 3; ", ABL:従順 >= 3)
	CALLF SPOILER_PRINT_FLAG("Servicing 3; ", ABL:奉仕精神 >= 3)
	CALLF SPOILER_PRINT_FLAG("Submission seal 3; ", MARK:屈服刻印 == 3)
	CALLF SPOILER_PRINT_FLAG("Rejection seal 0; ", MARK:反発刻印 == 0)
	CALLF SPOILER_PRINT_FLAG("ServicPlsEXP 200; ", EXP:奉仕快楽経験 >= 200)
	PRINTL 
	PRINTFORM %" ",22%
	CALLF SPOILER_PRINT_FLAG("Less than 2 Abnormal EXP; ", EXP:異常経験 - CFLAG:初期異常経験 < 2, 1)
	IF (TALENT:MASTER:覚)
		LOCAL:0 = SPOILER_PRINT_FLAG("More than 1000 Dependance", CFLAG:依存度 >= 1000)
	ELSE
		PRINT More than 1000 Dependance?
	ENDIF
	PRINTL
	;淫乱
	;@EVENTCHECK_Tのコメントには同性愛経験2000というのがあるけど実装されていない
	PRINT Lewdness Condition： 
	CALLF SPOILER_PRINT_FLAG("Desire 3; ", ABL:欲望 >= 3)
	CALLF SPOILER_PRINT_FLAG("Pleasure seal 3; ", MARK:快楽刻印 == 3)
	CALLF SPOILER_PRINT_FLAG("Submission seal 3; ", MARK:屈服刻印 == 3)
	CALLF SPOILER_PRINT_FLAG("Rejection seal 0; ", MARK:反発刻印 == 0)
	CALLF SPOILER_PRINT_FLAG("More than 10 Sensitivity; ", ABL:3+ABL:4+ABL:5+ABL:6 >= 10)
	PRINTL 
	PRINTFORM %" ",21%
	CALLF SPOILER_PRINT_FLAG("ClimaxEXP 50; ", EXP:絶頂経験 >= 50)
	CALLF SPOILER_PRINT_FLAG(@"(More than 3 Abnormal EXP or More than 1 Abnormal EXP + %LOCALS% EXP1000)", (EXP:異常経験 >= 3)||(EXP:異常経験 >= 1 && (EXP:40 >= 1000 || EXP:41 >= 1000)))
	PRINTL
	;服従
	PRINT Submissive Condition： 
	CALLF SPOILER_PRINT_FLAG("Obedience 3; ", ABL:従順 >= 3)
	CALLF SPOILER_PRINT_FLAG("Masochism 3; ", ABL:マゾっ気 >= 3)
	CALLF SPOILER_PRINT_FLAG("Submission seal 3; ", MARK:屈服刻印 == 3)
	CALLF SPOILER_PRINT_FLAG("Rejection seal 0; ", MARK:反発刻印 == 0)
	CALLF SPOILER_PRINT_FLAG("Less than 10 Sensitivity; ", ABL:3+ABL:4+ABL:5+ABL:6 < 10, 1)
	PRINTL 
	PRINTFORM %" ",23%
	CALLF SPOILER_PRINT_FLAG("More than 200 Maso EXP + Bondage EXP; ", EXP:苦痛快楽経験+EXP:緊縛経験 >= 200)
	CALLF SPOILER_PRINT_FLAG("More than 2 Abnormal EXP", EXP:異常経験 >= 2)	
	IF (TALENT:MASTER:覚)
		CALLF SPOILER_PRINT_FLAG("Less than -1000 Dependance", -CFLAG:依存度 >= 1000)
	ELSE
		PRINT Less than -1000 Dependance?
	ENDIF
	PRINTL
	DRAWLINE
ELSEIF TALENT:6 == 0 && TALENT:7 == 0 && TALENT:8 == 0 && TALENT:9 == 0
	;上位陥落素質への条件表示
	IF TALENT:恋慕
		PRINT Love Condition： 
		CALLF SPOILER_PRINT_FLAG("Obedience 5; ", ABL:従順 >= 5)
		CALLF SPOILER_PRINT_FLAG("Servicing 5; ", ABL:奉仕精神 >= 5)
		CALLF SPOILER_PRINT_FLAG("Rejection seal 0; ", MARK:反発刻印 == 0)
		CALLF SPOILER_PRINT_FLAG("Love EXP 200; ", EXP:愛情経験 >= 200)
		CALLF SPOILER_PRINT_FLAG("ServicPlsEXP 400; ", EXP:奉仕快楽経験 >= 400)
		PRINTL 
		PRINTFORM %" ",17%
		CALLF SPOILER_PRINT_FLAG("Less than 5 abnormal experience; ", EXP:異常経験- CFLAG:初期異常経験 < 5, 1)
		IF (TALENT:MASTER:覚)
			CALLF SPOILER_PRINT_FLAG("More than 3000 Dependance", CFLAG:依存度 >= 3000)
		ELSE
			PRINT More than 3000 Dependance?
		ENDIF
		PRINTL
		DRAWLINE
	ELSEIF TALENT:淫乱
		PRINT Whore Condition： 
		CALLF SPOILER_PRINT_FLAG("Desire 5; ", ABL:欲望 >= 5)
		CALLF SPOILER_PRINT_FLAG("More than 20 Sensitivity; ", ABL:3+ABL:4+ABL:5+ABL:6 >= 20)
		CALLF SPOILER_PRINT_FLAG("Repulsion seal 0; ", MARK:反発刻印 == 0)
		CALLF SPOILER_PRINT_FLAG("Climax experience >200; ", EXP:絶頂経験 >= 200)
		PRINTL 
		PRINTFORM %" ",18%
		CALLF SPOILER_PRINT_FLAG(@"(More than 6 Abnormal EXP, More than 4 Abnormal EXP + %LOCALS% EXP2000 or More than 3 Abnormal EXP + %LOCALS% EXP3000)", (EXP:異常経験 >= 6)||(EXP:異常経験 >= 4 && (EXP:40 >= 2000 || EXP:41 >= 2000))||(EXP:異常経験 >= 3 && (EXP:40 >= 3000 || EXP:41 >= 3000)) )
		PRINTL
		DRAWLINE
	ELSEIF TALENT:服従
		;@EVENTCHECK_TのコメントにはLesbianEXPもしくはＢＬEXP1500未満というのあるけど実装されていない
		PRINT Slave Condition： 
		CALLF SPOILER_PRINT_FLAG("Obedience 5; ", ABL:従順 >= 5)
		CALLF SPOILER_PRINT_FLAG("Masochism 5; ", ABL:マゾっ気 >= 5)
		CALLF SPOILER_PRINT_FLAG("Less than 15 Sensitivity; ", ABL:3+ABL:4+ABL:5+ABL:6 < 15, 1)
		CALLF SPOILER_PRINT_FLAG("Rejection seal 0; ", MARK:反発刻印 == 0)
		PRINTL 
		PRINTFORM %" ",18%
		CALLF SPOILER_PRINT_FLAG("More than 500 Maso EXP + Bondage EXP; ", EXP:苦痛快楽経験+EXP:緊縛経験 >= 500)
		CALLF SPOILER_PRINT_FLAG("More than 5 Abnormal EXP", EXP:異常経験 >= 5)	
		IF (TALENT:MASTER:覚)
			CALLF SPOILER_PRINT_FLAG("Less than -3000 Dependance", -CFLAG:依存度 >= 3000)
		ELSE
			PRINT Less than -3000 Dependance?
		ENDIF
		PRINTL
		DRAWLINE
	ENDIF
ENDIF
RETURN
