;--------------------------------------------
;ARG:1が真の場合、表示を行わずに終わるので
;表示せずに内容だけ知りたいときはどうぞ
;@EVENTCOMから呼ばれ、COMに渡す前に実行判定を行う
;なお、判定失敗はSELECTCOMにいくまえに必ず判定失敗するコマンドであるところの999を代入し
;コマンドを不成立にする形にしてある
;---------------------------------------------
@COM_ORDER, ARG, ARG:1
#LOCALSIZE 4
#LOCALSSIZE 6
#DIM CHARA, 2
;書式は 
;LOCALS ABLなどの属性
;LOCALS:1 表示名
;LOCALS:2 数値
;LOCALS:3 キャラクター

;とりあえず、現状はこの組み合わせで通しておく
CHARA = TARGET
CHARA:1 = PLAYER
;フラグ初期化
TFLAG:実行判定値 = 0
TFLAG:TALENT実行判定値 = 0
TFLAG:ABL実行判定値 = 0
TFLAG:PALAM実行判定値 = 0
TFLAG:MARK実行判定値 = 0
TFLAG:EXTRA実行判定値 = 0
TFLAG:RELATION実行判定値 = 100
TSTR:1 = 
TRYCCALLFORM COM_ORDER_{SELECTCOM}, CHARA, CHARA:1
CATCH
	RETURN 1
ENDCATCH
VARSET RESULTS
SIF !STRCOUNT(TSTR:1, "判定無視") && !STRCOUNT(TSTR:1, "共通回避")
	CALL COM_ORDER_COMMON, CHARA, CHARA:1
;相性はここでかける
TFLAG:実行判定値 = TFLAG:実行判定値 * TFLAG:RELATION実行判定値 / 100
IF ARG:1
	SIF STRCOUNT(TSTR:1, "判定無視") > 0
		RETURN 1
	CALLFORM COM_ORDER_NUM_{ARG}
	RETURN TFLAG:実行判定値 > RESULT, TFLAG:実行判定値, RESULT
ENDIF
SPLIT TSTR:1, "/", RESULTS
DRAWLINE
PRINTFORML Obedience test
DRAWLINE
IF STRCOUNT(TSTR:1, "判定無視") > 0
	SPLIT TSTR:1, "/", LOCALS
	PRINTFORMW %LOCALS:1%　<判定無視>
	RETURN 1
ENDIF
VARSET LOCAL
FOR LOCAL:1, 0, 5
	LOCAL = 0
	;初回カウント
	LOCAL:2 = 0
	;字数カウント
	LOCAL:3 = 0
	WHILE RESULTS:LOCAL != ""
		IF !STRCOUNT(RESULTS:LOCAL, AUTO_SPLIT("TALENT/ABL/MARK/PALAM/EXTRA", "/", LOCAL:1))
			LOCAL++
			CONTINUE
		ENDIF
		SIF !LOCAL:2
			PRINTFORM %AUTO_SPLIT("Traits/Abilities/Seals/Param/Others", "/", LOCAL:1), 13, LEFT%　{TFLAG:@"%AUTO_SPLIT("TALENT/ABL/MARK/PALAM/EXTRA", "/", LOCAL:1)%実行判定値",4}　
		SPLIT RESULTS:LOCAL, "_", LOCALS
		LOCALS:4 = 
		IF LOCAL:2++
			PRINT 　
		;	IF TOINT(LOCALS:2) > 0
		;		LOCALS:4 += " + "
		;	ELSE
		;		LOCALS:4 += " - "
		;	ENDIF
		ENDIF
		IF LOCALS:3 == "PLAYER"
			LOCALS:4 += "調教者の"
		ELSEIF LOCALS:4 == "ASSI"
			LOCALS:4 += "助手の"
		ENDIF
		IF LOCAL:3 > 66
			LOCAL:3 = 0
			PRINTL 
			PRINTFORM %" "*14%
		ENDIF
		LOCALS:4 += LOCALS:1 + @"({TOINT(LOCALS:2)})"
		LOCAL:3 += STRLENS(LOCALS:4)
		PRINTFORM %LOCALS:4%
		ARRAYREMOVE RESULTS, LOCAL, 1
	WEND
	SIF LOCAL:2
		PRINTL 
NEXT
LOCAL = 0
;初回カウント
LOCAL:1 = 0
WHILE RESULTS:LOCAL != ""
	IF !STRCOUNT(RESULTS:LOCAL, "RELATION")
		LOCAL++
		CONTINUE
	ENDIF
	SPLIT RESULTS:LOCAL, "_", LOCALS
	SIF LOCAL:1++ == 0
		PRINTFORM Compatibility  %TOSTR(TFLAG:RELATION実行判定値, "0'.'00")%　
	PRINTFORM %LOCALS:1%　
	ARRAYREMOVE RESULTS, LOCAL, 1
WEND
PRINTL 
CALLFORM COM_ORDER_NUM_{ARG}
DRAWLINE
PRINTFORM Total value {TFLAG:実行判定値, 7}　
IF TFLAG:実行判定値 > RESULT
	PRINT >
ELSEIF TFLAG:実行判定値 < RESULT
	PRINT <
ELSE
	PRINT =
ENDIF
PRINTFORML 　Target value {RESULT,7}　\@TFLAG:実行判定値 >= RESULT ? <Success>#<Failure>\@
DRAWLINE
WAIT
RETURN TFLAG:実行判定値 >= RESULT
;---------------------------------------------------------------------------
;COM_ORDERの内容を追加していき、数値の加算してTFLAGにしまっていく
;TALENT_処女_-20_TARGET/ABL_従順(Lv5)_10_TARGET/　といった形でしまっていく
;---------------------------------------------------------------------------
@ADD_COM_ORDER, ARGS, ARGS:1, ARG, ARG:1 = -99, ARG:2 = -99
#LOCALSSIZE 1
#LOCALSIZE 1
;判定無視が入っている場合は打ち切り
SIF STRCOUNT(TSTR:1, "判定無視")
	RETURN 0
;省略時の調整
SIF ARG:1 == -99
	ARG:1 = TARGET
SIF ARG:2 == -99
	ARG:2 = PLAYER
IF ARGS == "判定無視"
	TSTR:1 = %ARGS%/%ARGS:1%
	RETURN 0
ENDIF
IF ARGS == "共通回避"
	TSTR:1 += "共通回避/"
	RETURN 0
ENDIF
IF ARGS == "TALENT"
	SELECTCASE ARGS:1
		CASE "恋慕"
			SIF !Affection(ARG:1, ARG:2)
				RETURN 0
		CASE "Love"
			SIF !Love(ARG:1, ARG:2)
				RETURN 0
		CASE "淫乱"
			SIF !Lewd(ARG:1, ARG:2)
				RETURN 0
		CASE "娼婦"
			SIF !Whore(ARG:1, ARG:2)
				RETURN 0
		CASE "服従"
			SIF !Submissive(ARG:1, ARG:2)
				RETURN 0
		CASE "隷属"
			SIF !Slave(ARG:1, ARG:2)
				RETURN 0
		CASEELSE
			SIF !TALENT:(ARG:1):(ARGS:1)
			RETURN 0
	ENDSELECT
ENDIF
SIF ARGS == "ABL" && !ABL:(ARG:1):(ARGS:1)
	RETURN 0
SIF ARGS == "MARK" && !MARK:(ARG:1):(ARGS:1)
	RETURN 0
SIF ARGS == "PALAM" && !GETPALAMLV(PALAM:(ARG:1):(ARGS:1), 5)
	RETURN 0
SIF ARGS:1 == "危険日" && 危険日(ARG:1) < 2
	RETURN 0
SIF ARGS:1 == "Aphrodisiac" && !TEQUIP:(ARG:1):Aphrodisiac
	RETURN 0
IF ARGS == "STAIN" && !TALENT:(ARG:1):汚れ無視
	LOCAL = 0
	SIF ARGS:1 != "ヴァギナ" && GET_STAIN(ARGS:1, "愛液", ARG:2)
		LOCAL = 1
	SIF GET_STAIN(ARGS:1, "精液", ARG:2)
		LOCAL = 1
	SIF ARGS:1 != "アナル" && GET_STAIN(ARGS:1, "アナル", ARG:2)
		LOCAL = 1
	SIF GET_STAIN(ARGS:1, "粘液", ARG:2)
		LOCAL = 1
	SIF LOCAL == 0
		RETURN 0
ENDIF
SELECTCASE ARGS
	CASE "TALENT"
		LOCAL = ARG
		TSTR:1 += @"%ARGS%_%TALENTNAME_S(ARGS:1)%_{ARG}_"
	CASE "ABL"
		LOCAL = ARG*ABL:(ARG:1):(ARGS:1)
		TSTR:1 += @"%ARGS%_%ABLNAME_S(ARGS:1)%LV{ABL:(ARG:1):(ARGS:1)}_{ARG*ABL:(ARG:1):(ARGS:1)}_"
	CASE "MARK"
		LOCAL = ARG*MARK:(ARG:1):(ARGS:1)
		TSTR:1 += @"%ARGS%_%MARKNAME_S(ARGS:1)%LV{MARK:(ARG:1):(ARGS:1)}_{ARG*MARK:(ARG:1):(ARGS:1)}_"
	CASE "PALAM"
		LOCAL = GETPALAMLV(PALAM:(ARG:1):(ARGS:1),5)
		TSTR:1 += @"%ARGS%_%PALAMNAME_S(ARGS:1)%LV{GETPALAMLV(PALAM:(ARG:1):(ARGS:1),5)}_{GETPALAMLV(PALAM:(ARG:1):(ARGS:1),5)}_"
	CASE "STAIN"
		LOCAL = 0
		SIF ARGS:1 != "ヴァギナ" && GET_STAIN(ARGS:1, "愛液", ARG:2)
			LOCAL -= 1
		SIF GET_STAIN(ARGS:1, "精液", ARG:2)
			LOCAL -= 3
		SIF ARGS:1 != "アナル" && GET_STAIN(ARGS:1, "アナル", ARG:2)
			LOCAL -= 7
		SIF GET_STAIN(ARGS:1, "粘液", ARG:2)
			LOCAL -= 15
		IF TALENT:(ARG:1):汚臭鈍感
			LOCAL /= 3
		ELSEIF TALENT:(ARG:1):汚臭敏感
			LOCAL *= 2
		ENDIF
		LOCAL = LOCAL * ARG / 100
		IF LOCAL < 0
			TSTR:1 += "EXTRA_"
			SIF ARG:2 == ASSI
				TSTR:1 += "助手の"
			TSTR:1 += @"汚れ_{LOCAL}_"
			;IF TALENT:(ARG:1):汚臭鈍感
			;	TSTR:1 += @"汚れ鈍感_{LOCAL}_"
			;ELSEIF TALENT:(ARG:1):汚臭敏感
			;	TSTR:1 += @"EXTRA_汚れ敏感_{LOCAL}_"
			;ELSE
			;	TSTR:1 += @"EXTRA_汚れ_{LOCAL}_"
			;ENDIF
			ARGS = EXTRA
		ELSE
			RETURN 0
		ENDIF
	CASE "EXTRA"
		SELECTCASE ARGS:1
			CASE "危険日"
				TSTR:1 += "EXTRA_"
				TSTR:1 += @"\@危険日(ARG:1) == 3 ? 満月# \@発情中_10_"
				TSTR:1 += "発情中"
				LOCAL =  10
			CASE "忠誠度"
				LOCAL = LIMIT(BASE:忠誠度,0,1000) / 50 - 10
				TSTR:1 += @"EXTRA_%ARGS:1%_{LOCAL}_"
		CASEELSE
			TSTR:1 += @"EXTRA_%ARGS:1%_{ARG}_"
			LOCAL =  ARG
		ENDSELECT
	CASE "RELATION"
		;相性による修正
		LOCAL = LIMIT(100+(GET_RELATION(ARG:1, ARG:2) - 100)/2 , 50, 150)
		TFLAG:RELATION実行判定値 = TFLAG:RELATION実行判定値 * LOCAL / 100
		TSTR:1 += "RELATION_"
		SELECTCASE LOCAL
			CASE 50
				TSTR:1 += "Terrible"
			CASE 51 TO 75
				TSTR:1 += "Poor"
			CASE 76 TO 99
				TSTR:1 += "Bad"
			CASE 100
				TSTR:1 += "Normal"
			CASE 100 TO 124
				TSTR:1 += "Good"
			CASE 125 TO 149
				TSTR:1 += "Great"
			CASE 150
				TSTR:1 += "Excellent"
		ENDSELECT
		;TSTR:1 += @"%AUTO_SPLIT(" /(調教者)/(助手)/(奴隷)", "/", EQUALCHECK_TURN(1, PLAYER == ARG:2, ASSI == ARG:2, TARGET == ARG:2))%_{LOCAL}_"
		TSTR:1 += "(" + CALLNAME:(ARG:2) + ")_"
		LOCAL = 0
ENDSELECT

IF ARG:1 == PLAYER
	TSTR:1 += "PLAYER/"
ELSEIF ARG:1 == ASSI
	TSTR:1 += "ASSI/"
ELSE
	TSTR:1 += "TARGET/"
ENDIF

TFLAG:実行判定値 += LOCAL
TFLAG:@"%ARGS%実行判定値" += LOCAL


;--------------------------
;能力チェック
;--------------------------
@COM_ORDER_COMMON, ARG, ARG:1
CALL ADD_COM_ORDER, "ABL", "従順", 4, ARG, ARG:1
CALL ADD_COM_ORDER, "ABL", "マゾっ気", 2, ARG, ARG:1
;レズ調教
IF IS_LESBIAN(ARG:1, ARG)
	CALL ADD_COM_ORDER, "ABL", "レズっ気", 3, ARG, ARG:1
	CALL ADD_COM_ORDER, "ABL", "レズ中毒", 3, ARG, ARG:1
	CALL ADD_COM_ORDER, "TALENT", "両刀", 10, ARG, ARG:1
	CALL ADD_COM_ORDER, "TALENT", "好奇心", 7, ARG, ARG:1
	CALL ADD_COM_ORDER, "TALENT", "保守的", -13, ARG, ARG:1
;ＢＬ調教
ELSEIF IS_GAY(PLAYER, TARGET)
	CALL ADD_COM_ORDER, "ABL", "ＢＬっ気", 3, ARG, ARG:1
	CALL ADD_COM_ORDER, "ABL", "ＢＬ中毒", 3, ARG, ARG:1
	CALL ADD_COM_ORDER, "TALENT", "両刀", 10, ARG, ARG:1
	CALL ADD_COM_ORDER, "TALENT", "好奇心", 7, ARG, ARG:1
	CALL ADD_COM_ORDER, "TALENT", "保守的", -13, ARG, ARG:1
ELSE
	CALL ADD_COM_ORDER, "TALENT", "好奇心", 5, ARG, ARG:1
	CALL ADD_COM_ORDER, "TALENT", "保守的", -10, ARG, ARG:1
ENDIF
;--------------------------
;刻印チェック
;--------------------------
CALL ADD_COM_ORDER, "MARK", "苦痛刻印", 5, ARG, ARG:1
IF TALENT:ARG:プライド高い
	LOCAL = 4
ELSEIF TALENT:ARG:プライド低い
	LOCAL = 1
ELSE
	LOCAL = 2
ENDIF
CALL ADD_COM_ORDER, "MARK", "屈服刻印", 3 * LOCAL, ARG, ARG:1
CALL ADD_COM_ORDER, "MARK", "反発刻印", -3 * LOCAL, ARG, ARG:1
;--------------------------
;ＰＡＬＡＭチェック
;--------------------------
CALL ADD_COM_ORDER, "PALAM", "恭順", 3, ARG, ARG:1
CALL ADD_COM_ORDER, "PALAM", "恐怖", 3, ARG, ARG:1
;--------------------------
;危険日チェック
;--------------------------
CALL ADD_COM_ORDER, "EXTRA", "危険日", 10, ARG, ARG:1
;--------------------------
;忠誠度チェック
;--------------------------
CALL ADD_COM_ORDER, "EXTRA", "忠誠度", 0, ARG, ARG:1
;--------------------------
;--------------------------
;素質チェック
;--------------------------
CALL ADD_COM_ORDER, "TALENT", "服従", 10, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "隷属", 20, ARG, ARG:1
SIF !TALENT:ARG:服従 && TALENT:ARG:隷属
	CALL ADD_COM_ORDER, "TALENT", "反抗的", -5, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "気丈", -5, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "素直", 5, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "プライド高い", -15, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "プライド低い", 5, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "目立ちたがり", 2, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "抑圧", -10, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "抵抗", -10, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "弱味", 12, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "即落ち", 10, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "妄信", 8, ARG, ARG:1
;--------------------------
;調教者の素質チェック
;--------------------------
;調教者の能力、素質
;ABL:サドっ気
CALL ADD_COM_ORDER, "ABL", "サドっ気", 2, ARG:1, ARG
CALL ADD_COM_ORDER, "TALENT", "魅惑", 6, ARG:1, ARG
CALL ADD_COM_ORDER, "TALENT", "謎の魅力", 8, ARG:1, ARG
CALL ADD_COM_ORDER, "TALENT", "サド", 3, ARG:1, ARG
;--------------------------
;助手の素質チェック
;--------------------------
;助手の素質
;鼓舞
SIF ASSI > 0 && !ASSIPLAY
	CALL ADD_COM_ORDER, "TALENT", "鼓舞", 1, ARG:1, ARG


