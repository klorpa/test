;-------------------------------------------------
;クンニ
;愛撫系コマンド　レベル1
;-------------------------------------------------
@JUMP_COM1
;前回の調教がフェラチオかクンニ強制かシックスナインだとシックスナインに
IF GROUPMATCH(PREVCOM, 81 , 120 , 501)
	CALL COM_ABLE_COMMON, 501
	SIF RESULT == 1
		RETURN 501
ENDIF
;前回の調教がパイズリかパイズリフェラか６９パイズリだと６９パイズリに
IF GROUPMATCH(PREVCOM, 82 , 88, 508)
	CALL COM_ABLE_COMMON, 508
	SIF RESULT == 1
		RETURN 508
ENDIF
;羞恥プレイ中は岩清水に
SIF TEQUIP:30 == 1
	RETURN 502
RETURN -1
@ACT_COM1
PRINTL Cunnilingus
TSTR:0 = クンニ
;CALL KOJO_MESSAGE_COM

;調教者→調教対象
TFLAG:28 = 2
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、Willpower
DOWNBASE:TARGET:体力 += 5
DOWNBASE:TARGET:気力 += 50

SOURCE:TARGET:快Ｃ = 100
SOURCE:TARGET:液体 = 60
SOURCE:TARGET:露出 = 10
SOURCE:TARGET:逸脱 = 15
SOURCE:TARGET:反感 = 15

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:32 || TALENT:34
	SOURCE:TARGET:鬱屈 += 20

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:52
	TIMES SOURCE:TARGET:快Ｃ , 1.50

;猫舌では減少
SIF TALENT:PLAYER:54
	TIMES SOURCE:TARGET:快Ｃ , 0.75

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;奴隷のＶ⇔調教者の口の汚れが移動
CALL MOVE_STAIN("ヴァギナ", TARGET, "口", PLAYER)

;-------------------------------------------------
;吸精処理
;-------------------------------------------------
IF CFLAG:PLAYER:ＭＡＧ非被吸収 == 0
	LOCAL = (RAND:4+1) * BASE:LV
	CALL DRAIN_MAG,PLAYER,LOCAL,TARGET
ELSEIF CFLAG:TARGET:ＭＡＧ非被吸収 == 0
	LOCAL = (RAND:4+1) * BASE:LV
	CALL DRAIN_MAG,TARGET,LOCAL,PLAYER
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
A = 3
IF IS_LESBIAN(TARGET,PLAYER)
	CALL ADDS_EXP("レズ経験", A, TARGET)
	CALL ADDS_EXP("レズ経験", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
	CALL ADDS_EXP("ＢＬ経験", A, TARGET)
	CALL ADDS_EXP("ＢＬ経験", A, PLAYER)
ENDIF

;調教者が猫舌持ちの場合は、調教者にSadoEXPを追加
IF TALENT:PLAYER:54
	CALL ADDS_EXP("嗜虐快楽経験", 1, PLAYER)
ENDIF

;依存度ベクトル
TFLAG:33 = 1

RETURN 1


;===================================================
;調教タイプ
;===================================================
@COMTYPE_1
RETURN COMTYPE("愛撫系")