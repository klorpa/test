;-------------------------------------------------
;オナホール使う
;道具使用コマンド、レベル2
;触手ペニス責めさせる
;-------------------------------------------------
@CHECK_COM_ABLE51

;オナホール実行判定
;触手調教中はダメ
SIF TEQUIP:25
	RETURN 0

;解除はいつでも可能
SIF TEQUIP:PLAYER:12
	RETURN 1

;アイテムを持っているか、アイテムなし設定でないとダメ
SIF !EXIST_ITEM(3)
	RETURN 0


;小人体型持ちは、双方が調教者と調教対象ならOK
;それ以外の場合は主人が禁断の知識持ちじゃないとダメ
SIF TALENT:PLAYER:142 && PLAYER != MASTER && TALENT:MASTER:144 == 0
	RETURN 0

SIF !USE_PENIS(PLAYER)
	RETURN 0

;新妻プレイ中はダメ
SIF TEQUIP:33
	RETURN 0
RETURN 1

@CHECK_COM_ABLE175

;触手ペニス責め
SIF TEQUIP:25 == 0
	RETURN 0

;オトコかふたなりじゃないとダメ
SIF !HAVE_PENIS(PLAYER)
	RETURN 0
RETURN 1

@ACT_COM51
;-------------------------------------------------
;特殊派生処理
;-------------------------------------------------
IF TEQUIP:25
	PRINTL 触手ペニス責めさせる
	TSTR:0 = 触手ペニス責めさせる
	SELECTCOM = 175
ELSE
	PRINTL Use Onahole
	TSTR:0 = オナホール使う
ENDIF
;CALL KOJO_MESSAGE_COM

;着脱の処理
IF TEQUIP:PLAYER:12 == 0
	TEQUIP:PLAYER:12 = 1
ELSE
	TEQUIP:PLAYER:12 = 0
ENDIF

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、Willpower
LOSEBASE:0 += 10
LOSEBASE:1 += 10

SOURCE:4 = 10
SOURCE:13 = 10
SOURCE:17 = 10

SOURCE:PLAYER:道具快Ｃ = 200
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
;SIF TALENT:32 || TALENT:34
;	SOURCE:16 += 30

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;触手ペニス責めなら、触手体液がペニスに付く
SIF TEQUIP:25
	CALL SET_STAIN("ペニス", "粘液", PLAYER)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
A = 1
IF IS_LESBIAN(TARGET,PLAYER)
;	PRINTFORML %EXPNAME:40%+{A}
;	PRINTFORML %EXPNAME:40%(調教者)+{A}
;	EXP:40 += A
;	EXP:PLAYER:40 += A
	CALL ADDS_EXP("レズ経験", A, TARGET)
	CALL ADDS_EXP("レズ経験", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
;	PRINTFORML %EXPNAME:41%+{A}
;	PRINTFORML %EXPNAME:41%(調教者)+{A}
;	EXP:41 += A
;	EXP:PLAYER:41 += A
	CALL ADDS_EXP("ＢＬ経験", A, TARGET)
	CALL ADDS_EXP("ＢＬ経験", A, PLAYER)
	
	
ENDIF

;触手召喚時、TentacleEXPのリセットをする
;SIF TEQUIP:25 == 1
;	T = 0

;依存度ベクトル
TFLAG:33 = 1


RETURN 1

;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM51
;オナホール装備中
IF TEQUIP:25
	PRINTL ＜触手ペニス責め中(調教者)＞
ELSE
	PRINTL ＜オナホール装備中(調教者)＞
ENDIF
;-------------------------------------------------
;射精ゲージチェック
;-------------------------------------------------
B = 1500

;プレイヤーのABL:Ｃ感覚をみる
B *= 100 + ABL:PLAYER:Ｃ感覚*((ABL:PLAYER:Ｃ感覚+1)/2) * 25
B /= 100

;再装填処理中は射精ゲージは20分の1に
SIF CFLAG:PLAYER:9 > 0
	B /= 20
SIF HAVE_PENIS(PLAYER)
	BASE:PLAYER:2 += B
;-------------------------------------------------
;射精チェック
;-------------------------------------------------
;射精先タイプの設定
TCVAR:PLAYER:ペニス使用対象 = -1
TCVAR:PLAYER:射精箇所 = -1
;射精先選択の可・不可
TFLAG:704 = 0


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

SOURCE:PLAYER:道具快Ｃ += 100
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;TentacleEXP加算
SIF TEQUIP:25 == 1
	CALL ADDS_EXP("触手経験", 1, PLAYER)
;	T += 1


RETURN 1

;-------------------------------------------------
;調教時テキスト
;-------------------------------------------------
;51,オナホール使う
;-------------------------------------------------
@MESSAGE_COM51
;装着時
IF TEQUIP:PLAYER:12
	PRINTFORMW %CALLNAME:PLAYER%はペニスをオナホールに挿入した
;解除時
ELSE
	PRINTFORMW %CALLNAME:PLAYER%はオナホールを外した
ENDIF

;-------------------------------------------------
;175,触手ペニス責めさせる
;-------------------------------------------------
@MESSAGE_COM175
;装着時
IF TEQUIP:PLAYER:12
	PRINTFORM いやらしく肉ヒダをうねらせた触手が%CALLNAME:PLAYER%の
	;汚れ分岐
	IF  GROUPMATCH(1, GET_STAIN("ペニス", "愛液", TARGET), GET_STAIN("ペニス", "母乳", TARGET), GET_STAIN("ペニス", "精液", TARGET), GET_STAIN("ペニス", "破瓜の血", TARGET)) > 2
		PRINT 様々な体液にまみれた
	ELSEIF GET_STAIN("ペニス", "愛液", PLAYER)
		PRINT 愛液にまみれた
	ELSEIF GET_STAIN("手", "精液", PLAYER)
		PRINT 精液でドロドロになった
	;セックス系を直前にしていないなら乱数分岐
	ELSEIF RAND:3 == 0
		PRINT いきり立つ
	ELSEIF TEQUIP:PLAYER:下着（下） == 620
		PRINT ピアスがいやらしく光る
	ELSE
		PRINT 
	ENDIF
	PRINTFORMW ペニスに巻き付き、しごき始めた
;解除時
ELSE
	PRINTFORMW 触手は%CALLNAME:PLAYER%のペニスを解放した
ENDIF

;===================================================
;調教タイプ
;===================================================
@COMTYPE_51
RETURN COMTYPE("対調教者道具系")

