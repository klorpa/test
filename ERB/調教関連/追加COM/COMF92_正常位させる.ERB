
@CHECK_COM_ABLE92
;正常位させる実行判定

;調教者のヴァギナが使えないとダメ
SIF !USE_VAGINA(PLAYER) 
	RETURN 0
	
;調教対象のペニスが使えないとダメ
SIF  !USE_PENIS(TARGET) && !USE_PBAND(TARGET)
	RETURN 0

;触手調教中はダメ
SIF TEQUIP:25
	RETURN 0

;縄・拘束中はダメ
SIF TEQUIP:19
	RETURN 0

;三角木馬騎乗中はダメ
SIF TEQUIP:41
	RETURN 0

;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF !EXIST_ITEM(20)
		RETURN 0
ENDIF

RETURN 1


@ACT_COM92
;正常位させる

PRINTL Missionary
;正常位させる
TSTR:0 = 正常位させる

;-------------------------------------------------
;実行決定
;-------------------------------------------------
;CALL KOJO_MESSAGE_COM

;調教者←→調教対象
TFLAG:28 = 3
;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:0 += 50
LOSEBASE:1 += 100

SOURCE:0 = 400
IF TALENT:PLAYER:小柄体型
	TIMES SOURCE:快Ｃ , 1.50
	SOURCE:痛み += 50
ELSEIF TALENT:PLAYER:小人体型
	TIMES SOURCE:快Ｃ , 2.50
	SOURCE:痛み += 100
ENDIF
IF TALENT:大柄
	TIMES SOURCE:快Ｃ , 1.50
	SOURCE:痛み += 50
ELSEIF TALENT:巨体
	TIMES SOURCE:快Ｃ , 2.50
	SOURCE:痛み += 100
ENDIF

SOURCE:5 = 500
SOURCE:14 = 1000
SOURCE:12 = 500
SOURCE:18 = 500
SIF IS_MALE(TARGET) && (TALENT:快感の否定 == 0 || ABL:欲望 > 2)
	SOURCE:18 /= 10

;調教者に快Ｖ
SOURCE:PLAYER:1 = 400

;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------
;抑圧、抵抗持ちには、抑鬱ソース追加
SIF TALENT:32 || TALENT:34
	SOURCE:16 += 700

SIF (TALENT:PLAYER:0 == 1 || TALENT:PLAYER:1 == 1)
	TFLAG:31 = 1
	
;調教者が処女か再生処女（AbnormalEXPは一回のみ）
IF (TALENT:PLAYER:0 == 1 || TALENT:PLAYER:1 == 1)
	TIMES SOURCE:13 , 20.00
	TIMES SOURCE:14 , 3.00
	PRINTFORM ＜%CALLNAME:PLAYER%は
	SIF TALENT:PLAYER:1 == 1
		PRINT 再生
	PRINTL 処女を失いました＞
	;TALENT:PLAYER:0 = 0
	;TALENT:PLAYER:1 = 0

	;AbnormalEXP追加
	SIF !IS_MALE(TARGET)
		CALL ADDS_ABNORMAL_EXP("Take Virginity（Same Sex）", TARGET)
	SIF !IS_MALE(TARGET)
		CALL ADDS_ABNORMAL_EXP("Lost virginity（Same sex）", PLAYER)
ENDIF
;-------------------------------------------------
;射精チェック
;-------------------------------------------------

;射精先タイプの設定
TCVAR:PLAYER:ペニス使用対象 = TARGET
TCVAR:PLAYER:射精箇所 = 14
;射精先選択の可・不可
TFLAG:704 = 0

;-------------------------------------------------
;汚れの処理
;-------------------------------------------------
;射精に関係なく行われる処理はここから
;膣内に破瓜の血の汚れを付加
SIF TALENT:PLAYER:0 == 1 || TALENT:PLAYER:1 == 1
	CALL SET_STAIN("膣内", "破瓜の血", PLAYER)
;奴隷の膣内⇔調教者のＰの汚れが移動
CALL MOVE_STAIN("膣内", TARGET, "ペニス", PLAYER)

;-------------------------------------------------
;吸精処理
;-------------------------------------------------
IF CFLAG:PLAYER:ＭＡＧ非被吸収 == 0
	LOCAL = (RAND:10+1) * BASE:LV
	CALL DRAIN_MAG,PLAYER,LOCAL,TARGET
ELSEIF CFLAG:TARGET:ＭＡＧ非被吸収 == 0
	LOCAL = (RAND:10+1) * BASE:LV
	CALL DRAIN_MAG,TARGET,LOCAL,PLAYER
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;調教者のV経験
CALL ADDS_EXP("Ｖ経験", 1, PLAYER)

;PRINTFORML ServicPlsEXP(調教者) + 1
;EXP:PLAYER:奉仕快楽経験 += 1
;PRINTFORML SexEXP(調教者) + 1
;EXP:PLAYER:性交経験 += 1
CALL ADDS_EXP("奉仕快楽経験", 1, PLAYER)
CALL ADDS_EXP("性交経験", 1, PLAYER)
;BestialEXP
IF (TALENT:TARGET:獣 || TALENT:TARGET:鳥) && !(TALENT:PLAYER:獣 || TALENT:PLAYER:鳥)
	CALL ADDS_EXP("獣姦経験", 1, PLAYER)
	
ENDIF

;処女で同性の場合AbnormalEXP
IF TALENT:PLAYER:処女 && !IS_MALE(TARGET)
	CALL ADDS_ABNORMAL_EXP("Take Virginity（Same Sex）", TARGET)
	CALL ADDS_ABNORMAL_EXP("Lost virginity（Same sex）", PLAYER)
ENDIF
;近親相姦の場合AbnormalEXP付与
IF 近親チェック(PLAYER , TARGET)
	CALL ADDS_ABNORMAL_EXP( @"Incest（%RESULTS:0%）", PLAYER)
	CALL ADDS_ABNORMAL_EXP( @"Incest（%RESULTS:1%）", TARGET)
ENDIF
;LesbianEXP
A = 4
IF IS_LESBIAN(TARGET,PLAYER)
;	PRINTFORML %EXPNAME:40%+{A}
;	PRINTFORML %EXPNAME:40%(調教者)+{A}
;	EXP:40 += A
;	EXP:PLAYER:40 += A
	CALL ADDS_EXP("レズ経験", A, TARGET)
	CALL ADDS_EXP("レズ経験", A, PLAYER)
ELSEIF IS_GAY(TARGET,PLAYER)
;	PRINTFORML %EXPNAME:41%+{A}
;	PRINTFORML %EXPNAME:41%(調教者)+{A}
;	EXP:41 += A
;	EXP:PLAYER:41 += A
	CALL ADDS_EXP("ＢＬ経験", A, TARGET)
	CALL ADDS_EXP("ＢＬ経験", A, PLAYER)
	
	
ENDIF



;依存度ベクトル
TFLAG:33 = 1

;依存度補正値(主人かつ恋慕、淫乱、服従で1。親愛、娼婦、隷属で2）
IF (TALENT:3 || TALENT:4 || TALENT:5) && ASSIPLAY == 0
	TFLAG:30 += 1
ELSEIF (TALENT:6 || TALENT:7 || TALENT:8) && ASSIPLAY == 0
	TFLAG:30 += 2
ENDIF

;奉仕快楽フラグ
TFLAG:100 = 1

RETURN 1

@MESSAGE_COM92
	IF TEQUIP:主導権
		PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%を\@TALENT:獰猛 ? 乱暴に#\@組み敷き、
		PRINTFORM \@GET_STAIN("ヴァギナ", "精液", PLAYER) ? 精液が零れている#\@\@ TALENT:PLAYER:小柄体型 || TALENT:PLAYER:小人体型 ? スジ # 秘裂\@にペニスをあてがい
		;「TARGETが真性のサドか」「サドっ気持ちか」で分岐
		IF (TALENT:サド || ABL:サドっ気 >= 5 || TALENT:獰猛) && RAND:3 == 0
			PRINT 亀頭を子宮口にぶつけるように
		ELSEIF TALENT:サド || (ABL:サドっ気 >= 4 && RAND:2 == 0)
			PRINTFORM %CALLNAME:PLAYER%を焦らすようにゆっくりと
		ELSEIF RAND:2 == 0
			PRINT そのまま
		ENDIF
		;「TARGETが真性のサドか」「サドっ気持ちか」で分岐
		IF (TALENT:サド || ABL:サドっ気 >= 5 || TALENT:獰猛) && RAND:3 == 0
			PRINT 突き入れると、
		ELSEIF ABL:サドっ気 >= 3 && RAND:2 == 0
			PRINT 挿入すると、
		ELSE
			PRINT 入れると、
		ENDIF
		;恋慕で追加
		SIF (TALENT:恋慕 || TALENT:親愛) && RAND:3 >= 1
			PRINTFORM %CALLNAME:PLAYER%にキスをしながら
		;「TARGETが真性のサドか」「サドっ気持ちか」で分岐
		IF (TALENT:サド || ABL:サドっ気 >= 5 || TALENT:獰猛) && RAND:3 == 0
			PRINTFORMW カリ首で引っ掻くように、%CALLNAME:PLAYER%の膣内を蹂躙した…
		ELSEIF TALENT:サド || (ABL:サドっ気 >= 3 && RAND:2 == 0) || TALENT:獰猛
			PRINTFORMW 激しく腰を打ちつけた…
		ELSE
			PRINTFORMW ゆっくりと腰を前後に動かした…
		ENDIF
	ELSE
		PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の前に寝そべると
		;調教者が淫乱
		IF ABL:PLAYER:欲望 > 3
			PRINTFORM 秘裂を見せ付けるように大きく股を開き、
		ENDIF
		PRINTFORM 勃起したペニスを
		SIF EXP:PLAYER:Ｖ経験 == 0
			PRINT 男を受け入れた事のない
		PRINTFORMW 自らのヴァギナに受け入れた…
	ENDIF
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
@COM_ORDER_92, ARG, ARG:1
IF IS_LOOKSLIKE_MALE(ARG) && !IS_LOOKSLIKE_MALE(ARG:1) && TEQUIP:ARG:主導権
	CALL ADD_COM_ORDER, "判定無視", CALLNAME:ARG+"主導＋正常行為"
	RETURN 0
ENDIF
CALL ADD_COM_ORDER, "ABL", "欲望", 2, ARG, ARG:1
CALL ADD_COM_ORDER, "MARK", "快楽刻印", 2, ARG, ARG:1
CALL ADD_COM_ORDER, "PALAM", "欲情", 2, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "快感に素直", 2, ARG, ARG:1
SIF !TALENT:ARG:淫乱 && !TALENT:ARG:娼婦
	CALL ADD_COM_ORDER, "TALENT", "快感の否定", -2, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "淫乱", 10, ARG, ARG:1
CALL ADD_COM_ORDER, "TALENT", "娼婦", 15, ARG, ARG:1
;オトコだと逆らいにくい
SIF IS_MALE(TARGET)
	CALL ADD_COM_ORDER, "EXTRA", "Male", 15, ARG, ARG:1
;調教者
CALL ADD_COM_ORDER, "TALENT", "処女", -15, ARG:1, ARG
CALL ADD_COM_ORDER, "TALENT", "貞操観念", -10, ARG:1, ARG
CALL ADD_COM_ORDER, "TALENT", "貞操無頓着", 5, ARG:1, ARG
CALL ADD_COM_ORDER, "ABL", "技巧", 4, ARG:1, ARG
CALL ADD_COM_ORDER, "RELATION", , , ARG, ARG:1
;-------------------------------------------------
;実行値
;-------------------------------------------------
@COM_ORDER_NUM_92
RETURN 15
;===================================================
;調教タイプ
;===================================================
@COMTYPE_92
RETURN COMTYPE("調教者奉仕系")


