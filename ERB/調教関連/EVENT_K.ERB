;調教時キャラ別口上を一括管理

;-----------------------------------------------------------------------------------
;口上用システムファイルの存在判定
;EVENT_K.ERB(つまりこのファイル)が存在しているかを判定
;口上が使用可能で口上機能を能動的にOFFにしていない場合には、自動的にスイッチが入る
;各口上ファイル内でキャラフラグを制御するので、
;EVENT_Kxx.ERB(キャラ別口上ファイル)を途中で削除してもエラーはでない
;ただし、口上をONにした状態でこのファイルを削除するとエラーが発生する
;あっても邪魔にはならないので削除しない方が良いだろう
;-----------------------------------------------------------------------------------
@EVENTSHOP
#PRI
SIF FLAG:7 == 0
	FLAG:7 = 2
	
@EVENTTRAIN
;調教開始時口上呼び出し一括管理用
;オプションで口上非表示の場合スキップ
SIF FLAG:7 < 0
	RETURN 0
;口上表示頻度が０（本来あり得ない）の場合２に
SIF FLAG:7 == 0
	FLAG:7 = 2

;初回調教時、口上セレクト機能を起動
SIF CFLAG:201 == 0
	CALL 口上セレクト設定
;SIF CFLAG:201 == 0 && CFLAG:口上セレクト == -1
;	CALL 口上セレクト_汎用

CALL 口上呼び出し , "EVENTTRAIN"
SIF RESULT == 0
	CALL EVENTTRAIN_MESSAGE_CLOTHES
	

@EVENTEND
;調教終了時口上の呼び出し一括管理用
;オプションで口上非表示の場合スキップ
SIF FLAG:7 < 0
	RETURN 0

CALL 口上呼び出し , "EVENTEND"

;@EVENTTURNEND
;#LATER
;;ターン終了時口上（？）の呼び出し一括管理用
;SIF TARGET < 0
;	RETURN 0
;;個別処理呼び出し試行
;TRYCCALLFORM EVENTTURNEND_K{NO:TARGET}
;CATCH
;	TRYCALLFORM EVENTTURNEND_K{ABL:会話タイプ}
;ENDCATCH


;--------------------------------------------------
;@KOJO_MESSAGE_COM関係
;コマンド実行時に出力されます
;--------------------------------------------------
@KOJO_MESSAGE_COM
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL TRAIN_MESSAGE
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_COM"
SIF RESULT == 0 && FLAG:6 > 0
	CALL TRAIN_MESSAGE

;--------------------------------------------------
;@KOJO_MESSAGE_RINKAN関係
;輪姦がメッセージが出るたびに出力されます
;ARG:1	輪姦の種類
;ARG:2	ハードの有無　１有/０無
;--------------------------------------------------
@KOJO_MESSAGE_RINKAN,ARG:1,ARG:2
IF FLAG:7 <= 0
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_RINKAN", -1,ARG:1,ARG:2

RETURN RESULT
;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG関係
;パラメータ変動をトリガーにした口上
;パラメータ変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_A
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL PALAM_MESSAGE_A
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_PALAMCNG_A"
SIF RESULT == 0 && FLAG:6 > 0
	CALL PALAM_MESSAGE_A


;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG関係
;パラメータ変動をトリガーにした口上
;パラメータ変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_B
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL PALAM_MESSAGE_B
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_PALAMCNG_B"
SIF RESULT == 0 && FLAG:6 > 0
	CALL PALAM_MESSAGE_B


;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG関係
;パラメータ変動をトリガーにした口上
;パラメータ変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_C
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL PALAM_MESSAGE_C
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_PALAMCNG_C"
SIF RESULT == 0 && FLAG:6 > 0
	CALL PALAM_MESSAGE_C


;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG関係
;パラメータ変動をトリガーにした口上
;パラメータ変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_D
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL PALAM_MESSAGE_D
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_PALAMCNG_D"
SIF RESULT == 0 && FLAG:6 > 0
	CALL PALAM_MESSAGE_D



;-------------------------------------------------
;@KOJO_MESSAGE_PALAMCNG関係
;パラメータ変動をトリガーにした口上
;パラメータ変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_E
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL PALAM_MESSAGE_E
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_PALAMCNG_E"
SIF RESULT == 0 && FLAG:6 > 0
	CALL PALAM_MESSAGE_E


;-------------------------------------------------
;@KOJO_MESSAGE_MARKCNG関係
;刻印変動をトリガーにした口上
;刻印変動後に口上を発動します
;-------------------------------------------------
@KOJO_MESSAGE_MARKCNG
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL MARK_MESSAGE
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_MARKCNG"
SIF RESULT == 0 && FLAG:6 > 0
	CALL MARK_MESSAGE

;-------------------------------------------------
;@KOJO_MESSAGE_EVENT関係
;イベント時の口上、イベント開始時に出力します
;-------------------------------------------------
@KOJO_MESSAGE_EVENT
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL EVENT_MESSAGE
	RETURN 0
ENDIF

CALL 口上呼び出し , "KOJO_MESSAGE_EVENT"
SIF RESULT == 0 && FLAG:6 > 0
	CALL EVENT_MESSAGE

;-------------------------------------------------
;@KOJO_MESSAGE_EVENT関係
;イベント時の口上、イベント開始時に出力します
;-------------------------------------------------
@KOJO_MESSAGE_LOSE_COLOSSEUM
IF FLAG:7 <= 0
	SIF FLAG:6 > 0
		CALL MESSAGE_LOSE_COLOSSEUM
	RETURN 0
ENDIF

CALL 口上呼び出し, @"KOJO_MESSAGE_LOSE_COLOSSEUM_{TFLAG:闘技場敗北}"
SIF RESULT == 0 && FLAG:6 > 0
	CALL MESSAGE_LOSE_COLOSSEUM


;-------------------------------------------------
;@KOJO_MESSAGE_ENDING関係
;イベント時の口上、イベント開始時に出力します
;-------------------------------------------------
@KOJO_MESSAGE_ENDING
CALL 口上呼び出し , "KOJO_MESSAGE_ENDING"
RETURN RESULT


@口上セレクト設定
#DIM カウント , 1
#DIM リスト , 70
#LOCALSIZE 1
#LOCALSSIZE 1
VARSET リスト , -1
カウント = 0
FOR LOCAL , 0 , 70
	IF LOCAL < 50
		LOCALS = K{NO}
	ELSEIF LOCAL < 60
		LOCALS = PUB{ABL:会話タイプ}
	ELSE
		TRYCCALLFORM 特殊口上条件_SP{LOCAL - 60}
			SIF RESULT
				LOCALS = SP{LOCAL-60}
		CATCH
			CONTINUE
		ENDCATCH
	ENDIF
	SIF LOCAL % 50 != 0 && LOCAL < 60
		LOCALS += @"_{LOCAL%50}"
	TRYCCALLFORM セレクト口上説明_%LOCALS%, 1
		SIF RESULT == -1
			CONTINUE
		;[IF_DEBUG]
		;	PRINTFORML セレクト口上説明_%LOCALS%
		;[ENDIF]
		リスト:カウント = LOCAL
		カウント ++
	CATCH
	ENDCATCH
NEXT
;[IF_DEBUG]
;	PRINTFORML 口上は{カウント}個見つかりました
;	DRAWLINE
;[ENDIF]
IF カウント > 1 || MAXARRAY(リスト) >= 60
	PRINTFORML Please choose dialogue to apply
	FOR LOCAL , 0 , カウント
		IF リスト:LOCAL < 50
			LOCALS = K{NO}
		ELSEIF リスト:LOCAL < 60
			LOCALS = PUB{ABL:会話タイプ}
		ELSE
			LOCALS = SP{リスト:LOCAL-60}
		ENDIF
		SIF リスト:LOCAL % 50 != 0 && リスト:LOCAL < 60
			LOCALS += @"_{リスト:LOCAL%50}"
		PRINTFORM [{リスト:LOCAL}]
		CALLFORM セレクト口上説明_%LOCALS%
	NEXT
	PRINTL [99] Use none
	$INPUT_LOOP
	INPUT
	IF RESULT == 99
		CFLAG:口上セレクト = -1
	ELSEIF RESULT >= 0 && RESULT < 70 && MATCH(リスト , RESULT)
		CFLAG:口上セレクト = RESULT
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF カウント == 1
	CFLAG:口上セレクト = リスト:0
ELSE
	CFLAG:口上セレクト = 0
ENDIF

@口上呼び出し, ARGS , ARG = -1 , ARG:1 , ARG:2 , ARG:3
#LOCALSSIZE 1
SIF ARG == -1
	ARG = TARGET
LOCALS = %口上関数末尾文字(ARG)%
;SIF ARG == -1
;	ARG = TARGET
;IF CFLAG:ARG:口上セレクト < 50
;	LOCALS = _K{NO:ARG}
;ELSEIF CFLAG:ARG:口上セレクト < 60
;	LOCALS = _PUB{ABL:会話タイプ}
;ELSE
;	LOCALS = _SP{CFLAG:ARG:口上セレクト - 60}
;ENDIF
;SIF CFLAG:ARG:口上セレクト % 50 != 0 && CFLAG:ARG:口上セレクト < 60
;	LOCALS += @"_{CFLAG:ARG:口上セレクト%50}"
;[IF_DEBUG]
;	PRINTFORML 呼び出す口上は %ARGS%%LOCALS% です
;[ENDIF]
IF ARGS == "BATTLE_EVENT" || ARGS == "ENEMY_BATTLE_EVENT"
	TRYCCALLFORM %ARGS%%LOCALS% , ARG:1 , ARG:2 , ARG:3
	CATCH
		TRYCCALLFORM %ARGS%_PUB{ABL:ARG:会話タイプ} , ARG:1 , ARG:2 , ARG:3
		CATCH
			RETURN 0
		ENDCATCH
	ENDCATCH
ELSEIF ARGS == "KOJO_MESSAGE_RINKAN"
	TRYCCALLFORM %ARGS%%LOCALS%, ARG:1 , ARG:2
	CATCH
		TRYCCALLFORM %ARGS%_PUB{ABL:ARG:会話タイプ}, ARG:1 , ARG:2
		CATCH
			RETURN 0
		ENDCATCH
	ENDCATCH
	RETURN RESULT
;ELSEIF ARGS == "BATTLE_MESSAGE"
;	TRYCCALLFORM %ARGS%%LOCALS% , ARG:1 , ARG:2
;	CATCH
;		TRYCCALLFORM %ARGS%_PUB{ABL:ARG:会話タイプ} , ARG:1 , ARG:2 , ARG:3
;		CATCH
;			TRYCCALLFORM %ARGS%_PUB{ABL:ARG:会話タイプ} , ARG:1 , ARG:2
;			CATCH
;				RETURN 0
;			ENDCATCH
;		ENDCATCH
;	ENDCATCH
ELSEIF ARGS == "BATTLE_MESSAGE"
	TRYCCALLFORM %ARGS%%LOCALS% , ARG:1 , ARG:2
	CATCH
		TRYCCALLFORM %ARGS%_PUB{ABL:ARG:会話タイプ} , ARG:1 , ARG:2
		CATCH
			RETURN 0
		ENDCATCH
	ENDCATCH
ELSEIF ARGS == "BATTLE_BOYCOTT_MESSAGE"
	TRYCCALLFORM %ARGS%%LOCALS% , ARG:1 , ARG:2
	CATCH
		TRYCCALLFORM %ARGS%_PUB{ABL:ARG:会話タイプ} , ARG:1 , ARG:2
		CATCH
			RETURN 0
		ENDCATCH
	ENDCATCH
ELSEIF ARGS == "存在"
	LOCAL:2 = SELECTCOM
	LOCAL:3 = ASSI
	LOCAL:4 = TARGET
	SELECTCOM = 65535
	ASSI = 0
	TARGET = 0
	SKIPDISP 1
	TRYCCALLFORM KOJO_MESSAGE_COM_K{NO:ARG}
		LOCAL = 1
	CATCH
		TRYCCALLFORM KOJO_MESSAGE_COM_PUB{ABL:ARG:会話タイプ}
			LOCAL = 2
		CATCH
			LOCAL = 0
		ENDCATCH
	ENDCATCH
	IF CFLAG:ARG:口上セレクト % 50 != 0 && CFLAG:ARG:口上セレクト < 60
		TRYCCALLFORM KOJO_MESSAGE_COM_K{NO:ARG}_{CFLAG:ARG:口上セレクト%50}
			LOCAL = 1
		CATCH
		ENDCATCH
	ENDIF
	SKIPDISP 0
	SELECTCOM = LOCAL:2
	ASSI = LOCAL:3
	TARGET = LOCAL:4
	RETURN LOCAL
ELSE
	TRYCCALLFORM %ARGS%%LOCALS%
	CATCH
		TRYCCALLFORM %ARGS%_PUB{ABL:ARG:会話タイプ}
		CATCH
			RETURN 0
		ENDCATCH
	ENDCATCH
ENDIF
SIF ARGS == "KOJO_MESSAGE_ENDING"
	RETURN RESULT
RETURN 1

@口上関数末尾文字 , ARG
#FUNCTIONS
#LOCALSSIZE 1
SIF ARG == -1
	ARG = TARGET
IF CFLAG:ARG:口上セレクト < 50
	LOCALS = _K{NO:ARG}
ELSEIF CFLAG:ARG:口上セレクト < 60
	LOCALS = _PUB{ABL:ARG:会話タイプ}
ELSE
	LOCALS = _SP{CFLAG:ARG:口上セレクト - 60}
ENDIF
SIF CFLAG:ARG:口上セレクト % 50 != 0 && CFLAG:ARG:口上セレクト < 60
	LOCALS += @"_{CFLAG:ARG:口上セレクト%50}"
RETURNF LOCALS
