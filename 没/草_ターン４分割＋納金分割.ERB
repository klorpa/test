
@EVENTCOM
#PRI
REPEAT 35
	TFLAG:COUNT = 0
REND
TFLAG:100 = 0


@EVENTCOMEND
#PRI

IF BASE:0 <= 0
	PRINTFORMW ………………
	PRINTFORMW …………
	PRINTFORMW ……
	PRINTFORMW （激しい調教によって、%CALLNAME:TARGET%は気絶しました　調教を終了します）
	;従順ダウン
	IF ABL:0 > 0
		ABL:0 -= 1
		PRINTFORML %CALLNAME:TARGET%の従順が１下がった
	ENDIF
	;体力回復停止フラグ
	CFLAG:6 += 2
	SIF TALENT:147
		CFLAG:6 -= 1
	CFLAG:10 += 10
	BASE:0 = 1
	BEGIN AFTERTRAIN
ELSEIF BASE:0 < 500
	PRINTW （体力が限界に来ています　調教を終了します）
	BEGIN AFTERTRAIN
ENDIF


@EVENTTURNEND
#PRI
REPEAT CHARANUM
	CALL HEALTH_CHARA,COUNT
	;気力の回復
	BASE:COUNT:1 = MAXBASE:COUNT:1

	;労役中のキャラは体力回復処理無し
	SIF CFLAG:COUNT:12 > 0
		CONTINUE

	;体力回復停止処理
	IF CFLAG:COUNT:6 > 0
		CFLAG:COUNT:6 -= 1
		CONTINUE
	ENDIF

	;体力の回復（午後の調教後は夜の休みが入るので回復大きい）
	IF TIME == 3
		A = MAXBASE:COUNT:0 / 5
	ELSE
		A = MAXBASE:COUNT:0 / 15
	ENDIF

	;回復早い、回復遅い、蓬莱人
	IF TALENT:COUNT:130
		TIMES A , 1.50
	ELSEIF TALENT:COUNT:131
		TIMES A , 0.75
	ELSEIF TALENT:COUNT:147
		TIMES A , 1.80
	ENDIF

	;日光浴、月光浴、吸血鬼
	IF TIME == 0
		SIF TALENT:COUNT:133
			A += 100
	ELSE
		SIF TALENT:COUNT:132
			TIMES A , 1.50
		SIF TALENT:COUNT:134
			A += 100
	ENDIF

	;治療
	IF ASSI >= 0
		SIF TALENT:ASSI:135
		A += 150
	ENDIF

	;休憩フラグ（休憩フラグが経っている、もしくは調教対象でない）
	IF FLAG:0 || TARGET != COUNT
		A += 100
		CFLAG:COUNT:10 -= 1
	ENDIF

	;ショップで休憩を選択時
	IF FLAG:0 == 2
		TIMES A , 2.00
		CFLAG:COUNT:10 -= 1
	ENDIF

	BASE:COUNT:0 += A
	SIF BASE:COUNT:0 > MAXBASE:COUNT:0
		BASE:COUNT:0 = MAXBASE:COUNT:0

	;ストレス値が0未満の場合は0にする
	SIF CFLAG:COUNT:10 < 0
		CFLAG:COUNT:10 = 0

REND

CALL EVENTCHECK_ABL
FLAG:0 = 0

;午後なら次の日、午前なら午後にする
IF TIME == 3
	;日付変更時のイベント呼び出し
	CALL EVENT_NEXTDAY
	FLAG:ダンジョン探索済み = 0
	DAY = DAY+1
	PRINTW 一日が終わった……
	TIME = 0
	;バッドエンドの判定
	SIF DAY > DAY:1 && FLAG:5 == 0 && MONEY < MONEY:2
		CALL BADENDING_1
	SIF DAY > DAY:1 && FLAG:5 == 0
		CALL SHOP_COM_555

	;朝になった時のイベント呼び出し
	CALL EVENT_NEWDAY
	CALL EVENT_NEWDAY_SELF
ELSE
	TIME = 1
ENDIF


BEGIN SHOP
