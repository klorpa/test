
@SHOPCOMABLE_122
RESULTS = Enchance Demon
LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
	SIF ABL:LOCAL:種族 > 0 && ABL:LOCAL:種族 < 46
		LOCAL:1 += 1
NEXT
IF LOCAL:1 > 0
	RETURN 1
ENDIF
RETURN 0

@SHOP_COM_122

P = 0
;表示させるキャラを抽出（Q:*に表示するキャラの登録番号、LOCAL:2に人数）
VARSET Q, -1
A = 0
REPEAT CHARANUM
	SIF COUNT == MASTER
		CONTINUE
	SIF CFLAG:COUNT:労役フラグ
		CONTINUE
	SIF CFLAG:COUNT:この場に居ないフラグ == 1
		CONTINUE
	SIF CFLAG:COUNT:フィルタリングフラグ
		CONTINUE

	Q:A = COUNT
	A += 1
REND
LOCAL:2 = A




$PRINT_LIST
DRAWLINE
PRINTFORML ＭＡＧを使って強化するキャラを選んでください　＜page.{P}＞
DRAWLINE
CALL SHOW_CHARA_LIST
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1007]Previous page\@
PRINTLC [1000]Return
PRINTFORMLC \@(P >= LOCAL:2 / 20) ? %" " * 16% # [1009]Next page\@

$INPUT_LOOP_1
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1007
	IF P > 0
		P -= 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF RESULT == 1009
	LOCAL = LOCAL:2 / 20
	IF P < LOCAL
		P += 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF MATCH(Q,RESULT,0) < 1
	GOTO INPUT_LOOP_1
ENDIF

CALL ENHANCE,RESULT

GOTO PRINT_LIST

@ENHANCE,ARG
DRAWLINE
PRINTFORML %NAME:ARG%をどうしますか？	MAG:{BASE:ARG:ＭＡＧ}
PRINTL

IF ABL:ARG:種族 != 45
	PRINTFORML [1]Stat enhancement(Necessary MAG:{POWER(CFLAG:ARG:能力強化回数+1,2)*10*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)})
ELSE
	PRINTFORML [1]Stat enhancement(Necessary MAG:{POWER(CFLAG:ARG:能力強化回数+1,2)*5*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)})
ENDIF
PRINTL [2]Skill enhancement
PRINTL [3]スキル変異
PRINTL [4]Skill deletion
PRINTFORML [5]Skill acquisition(Necessary MAG:{100*(CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0))})
;PRINTL [6]Attack enhancement
;PRINTL [7]Resistance enhancement
PRINTL [8]Stamina and willpower enhancement
SIF ABL:ARG:種族 == 45
	PRINTFORML [9]Zomaレベルアップ(Necessary MAG:{2*(BASE:ARG:LV)*(BASE:ARG:LV+1)*(BASE:ARG:LV+2)/6})
DRAWLINE
PRINTL [0]Return

$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 1
	IF ABL:ARG:種族 == 45 && BASE:ARG:ＭＡＧ < POWER(CFLAG:ARG:能力強化回数+1,2)*5*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)
		PRINTFORMW Not enough MAG。
		GOTO INPUT_LOOP
	ENDIF
	IF ABL:ARG:種族 != 45 && BASE:ARG:ＭＡＧ < POWER(CFLAG:ARG:能力強化回数+1,2)*10*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)
		PRINTFORMW Not enough MAG。
		GOTO INPUT_LOOP
	ENDIF
	CALL ENHANCE_BASE,ARG
ELSEIF RESULT == 2
	IF TALENT:ARG:ペルソナ使い
		PRINTFORMW ペルソナ使いはSkill enhancementを出来ません
		GOTO INPUT_LOOP
	ENDIF
	CALL MAG_SKILL_RANKUP,ARG
ELSEIF RESULT == 3
	IF TALENT:ARG:ペルソナ使い
		PRINTFORMW ペルソナ使いはスキル変異を出来ません
		GOTO INPUT_LOOP
	ENDIF
	CALL MAG_SKILL_MUTATION,ARG
ELSEIF RESULT == 4
	IF TALENT:ARG:ペルソナ使い
		PRINTFORMW ペルソナ使いは消去を出来ません
		GOTO INPUT_LOOP
	ENDIF
	CALL MAG_SKILL_DELETE,ARG
ELSEIF RESULT == 5
	IF TALENT:ARG:ペルソナ使い
		PRINTFORMW ペルソナ使いはスキル習得を出来ません
		GOTO INPUT_LOOP
	ENDIF
	IF 100*(CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)) > BASE:ARG:ＭＡＧ
		PRINTW Not enough MAG
		CLEARLINE 2
		GOTO INPUT_LOOP
	ENDIF
	FOR LOCAL,1,FLAG:スキル数+1
		LOCALS = スキル{LOCAL}
		SIF ABL:ARG:LOCALS == 0
			GOTO OK_5
	NEXT
	PRINTW 空いているスキル枠がありません
	GOTO INPUT_LOOP
	$OK_5
	;スキル習得用関数呼び出し、引数は対象キャラ、空きスキル枠
	CALL MAG_LEARN_SKILL,ARG,LOCALS
ELSEIF RESULT == 8
	CALL MAG_GROWTH,ARG
ELSEIF RESULT == 9 && ABL:ARG:種族 == 45
	IF 2*(BASE:ARG:LV)*(BASE:ARG:LV+1)*(BASE:ARG:LV+2)/6 > BASE:ARG:ＭＡＧ
		PRINTW Not enough MAG
		CLEARLINE 2
		GOTO INPUT_LOOP
	ENDIF
	PRINTFORMW %NAME:ARG%のレベルが1上がりました
	BASE:ARG:ＭＡＧ -= 2*(BASE:ARG:LV)*(BASE:ARG:LV+1)*(BASE:ARG:LV+2)/6
	BASE:ARG:LV += 1
	CALLFORM EVENT_LEVELUP,ARG,1
	TRYCALLFORM EVENT_LEVELUP_{NO:ARG},1
	CALL SYNC_STATUS,ARG
	
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART




;===================================================
;Stat enhancement
;===================================================
@ENHANCE_BASE,ARG
PRINTL □Stat enhancement
IF ABL:ARG:種族 != 45
	PRINTFORML どの能力を強化しますか？(Necessary MAG:{POWER(CFLAG:ARG:能力強化回数+1,2)*10*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)})
ELSE
	PRINTFORML どの能力を強化しますか？(Necessary MAG:{POWER(CFLAG:ARG:能力強化回数+1,2)*5*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)})
ENDIF
FOR LOCAL,1,7
	PRINTFORML [{LOCAL}] %GET_BASESTATUS_E(LOCAL),4% {BASE:ARG:(GET_BASESTATUS(LOCAL)),3} + {CFLAG:ARG:(GET_BASESTATUS(LOCAL) + "強化回数"),3}
NEXT
DRAWLINE
PRINTL [0]Return

$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT > 6 || RESULT < 1
	GOTO INPUT_LOOP
ELSE
	PRINTFORMW %NAME:ARG%の%GET_BASESTATUS_E(RESULT)%が1上がった
	CFLAG:ARG:能力強化回数 += 1
	CFLAG:ARG:(GET_BASESTATUS(RESULT) + "強化回数") += 1
	IF ABL:ARG:種族 != 45
		CALL CONTROL_MAG,ARG,-POWER(CFLAG:ARG:能力強化回数,2)*10*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)
	ELSE
		CALL CONTROL_MAG,ARG,-POWER(CFLAG:ARG:能力強化回数,2)*5*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)
	ENDIF
ENDIF

IF ABL:ARG:種族 != 45 && BASE:ARG:ＭＡＧ < POWER(CFLAG:ARG:能力強化回数+1,2)*10*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)
	PRINTW ＭＡＧが足りなくなったので終了します。
	RETURN 0
ENDIF
IF ABL:ARG:種族 == 45 && BASE:ARG:ＭＡＧ < POWER(CFLAG:ARG:能力強化回数+1,2)*5*CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0)
	PRINTW ＭＡＧが足りなくなったので終了します。
	RETURN 0
ENDIF
RESTART


;===================================================
;Skill enhancement
;===================================================
@MAG_SKILL_RANKUP,ARG
PRINTL □Skill enhancement
DRAWLINE
PRINTL どのスキルをランクアップしますか？

FOR LOCAL,0,FLAG:スキル数
	LOCALS = スキル{LOCAL+1}
	IF ABL:ARG:LOCALS > 0
		RESULT = 0
		TRYCALLFORM SKILL_RANKUP_{ABL:ARG:LOCALS}
		SIF RESULT == 0
			SETCOLOR 0x404040
		CALLFORM SKILL_NAME_{ABL:ARG:LOCALS},ARG
		PRINTFORM 　[{LOCAL+1}]%RESULTS,20,LEFT%
		RESETCOLOR
	ENDIF
	SIF LOCAL % 4 == 3
		PRINTL
NEXT

DRAWLINE
PRINTL [0]Return


$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN 0
ELSEIF RESULT < 1 || RESULT > FLAG:スキル数
	GOTO INPUT_LOOP
ELSE
	LOCALS = スキル{RESULT}
	RESULT = 0
	VARSET D,-1
	LOCAL:1 = 0
	TRYCALLFORM SKILL_RANKUP_{ABL:ARG:LOCALS}
	IF RESULT == 0
		REUSELASTLINE ランクアップできないスキルです。
		GOTO INPUT_LOOP
	ENDIF
	LOCAL:1 = RESULT
	CALLFORM SKILL_RANK_{ABL:ARG:LOCALS}
	LOCAL:2 = RESULT
	$PRINT_LIST
	FOR LOCAL,1,LOCAL:1+1
		CALLFORM SKILL_NAME_{D:LOCAL}
		CALLFORM SKILL_RANK_{D:LOCAL}
		PRINTFORML [{LOCAL}] %RESULTS,20,LEFT% {(POWER(RESULT,2)-POWER(LOCAL:2,2))*500}MAG
	NEXT
	DRAWLINE
	PRINTL [0]Return
	$INPUT_LOOP1
	INPUT
	IF RESULT == 0
		RESTART
	ELSEIF D:RESULT > 0
		LOCAL = D:RESULT
		IF CHECK_SKILL(ARG,LOCAL) > 0
			REUSELASTLINE 既に習得しているスキルです。
			GOTO INPUT_LOOP1
		ENDIF
		
		CALLFORM SKILL_RANK_{LOCAL}
		IF BASE:ARG:ＭＡＧ < (POWER(RESULT,2)-POWER(LOCAL:2,2))*500
			REUSELASTLINE Not enough MAG
			GOTO INPUT_LOOP1
		ENDIF
		CALLFORM SKILL_EXPLAIN_{LOCAL}
		DRAWLINE
		PRINTL よろしいですか？
		PRINTL [0]Yes [1]No
		$INPUT_LOOP2
		INPUT
		IF RESULT == 0
			CALLFORM SKILL_RANK_{LOCAL}
			CALL CONTROL_MAG,ARG,-(POWER(RESULT,2)-POWER(LOCAL:2,2))*500
			CALLFORM SKILL_NAME_{ABL:ARG:LOCALS}
			PRINTFORM %NAME:ARG%は一瞬のひらめきで%RESULTS%を
			
			
			;D:(RAND:(LOCAL:1))に登録された番号のスキルを習得
			ABL:ARG:LOCALS = LOCAL
			CALLFORM SKILL_NAME_{ABL:ARG:LOCALS}
			PRINTFORMW %RESULTS%へとランクアップさせた！
		ELSEIF RESULT == 1
			GOTO PRINT_LIST
		ELSE
			GOTO INPUT_LOOP2
		ENDIF
		
	ELSE
		GOTO INPUT_LOOP1
	ENDIF
	
ENDIF
RESTART


;===================================================
;スキル変異
;===================================================
@MAG_SKILL_MUTATION,ARG
PRINTL □スキル変異
DRAWLINE
PRINTL どのスキルを変異しますか？



FOR LOCAL,0,FLAG:スキル数
	LOCALS = スキル{LOCAL+1}
	IF ABL:ARG:LOCALS > 0
		CALLFORM SKILL_NAME_{ABL:ARG:LOCALS},ARG
		PRINTFORM 　[{LOCAL+1}]%RESULTS,20,LEFT%
		CALLFORM SKILL_RANK_{ABL:ARG:LOCALS}
		LOCALS = ({POWER(RESULT,2)*250}MAG)
		PRINTFORM %LOCALS,11%
	ENDIF
	SIF LOCAL % 3 == 2 || LOCAL+1 == FLAG:スキル数
		PRINTL 
NEXT
DRAWLINE
PRINTL [0]Return

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN 0
ELSEIF RESULT < 1 || RESULT > FLAG:スキル数
	GOTO INPUT_LOOP
ELSE
	LOCALS = スキル{RESULT}
	SIF ABL:ARG:LOCALS < 1
		GOTO INPUT_LOOP
	CALLFORM SKILL_RANK_{ABL:ARG:LOCALS}
	LOCAL:2 = RESULT
	IF BASE:ARG:ＭＡＧ < POWER(LOCAL:2,2)*250
		REUSELASTLINE Not enough MAG
		GOTO INPUT_LOOP
	ENDIF
	
	;変異候補リスト作成
	VARSET D,-1
	LOCAL:1 = 0
	FOR LOCAL,1,3000
		RESULT = 0
		TRYCALLFORM SKILL_RANK_{LOCAL}
		;ランク±1まで、0は変異不可
		SIF RESULT == 0 || RESULT < LOCAL:2-1 || RESULT > LOCAL:2+1
			CONTINUE
		SIF CHECK_SKILL(ARG,LOCAL) == 1
			CONTINUE
		D:(LOCAL:1) = LOCAL
		LOCAL:1 += 1
	NEXT
	CALL CONTROL_MAG,ARG,-POWER(LOCAL:2,2)*250
	CALLFORM SKILL_NAME_{ABL:ARG:LOCALS}
	PRINTFORM %NAME:ARG%は%RESULTS%を忘れ、
	
	;D:(RAND:(LOCAL:1))に登録された番号のスキルを習得
	ABL:ARG:LOCALS = D:(RAND:(LOCAL:1))
	CALLFORM SKILL_NAME_{ABL:ARG:LOCALS}
	PRINTFORMW %RESULTS%を習得した！
ENDIF
CALL SYNC_STATUS,ARG
CALL HEALTH_CHARA,ARG

;===================================================
;Skill acquisition
;===================================================
@MAG_LEARN_SKILL,ARG,ARGS
PRINTL □Skill acquisition
DRAWLINE
PRINTL どのスキルを習得しますか？

;習得可能なスキルをリスト化
VARSET D,-1
LOCAL:1 = 1
FOR LOCAL,1,3000

	SIF CHECK_SKILL(ARG,LOCAL) == 1
		CONTINUE
	RESULT = 0
	TRYCALLFORM SKILL_RANK_{LOCAL}
	SIF RESULT != 1
		CONTINUE
	RESULT = 23
	TRYCALLFORM SKILL_SUCCESSION_TYPE_{LOCAL}
	SIF RESULT == 23 || (RESULT != 22 && TALENT:ARG:(GET_SUCCESSION(RESULT)) == 0)
		CONTINUE
	RESULT = 1
	TRYCALLFORM 継承部位_{LOCAL},ARG
	SIF RESULT == 0
		CONTINUE
		
	
	D:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$PRINT_LIST
FOR LOCAL,1,LOCAL:1
	CALLFORM SKILL_NAME_{D:LOCAL}
	PRINTFORMLC [{LOCAL}] %RESULTS%
NEXT
CUSTOMDRAWLINE ・
PRINTL [0]Return
PRINTFORML 


$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF D:RESULT > 0
	DRAWLINE
	LOCAL = RESULT
	CALLFORM SKILL_NAME_{D:LOCAL}
	PRINTFORML □%RESULTS%
	CALLFORM SKILL_EXPLAIN_{D:LOCAL}
	DRAWLINE
	PRINTL 習得しますか？
	PRINTL [0]Yes [1]No
	$INPUT_LOOP1
	INPUT
	IF RESULT == 0
		ABL:ARG:ARGS = D:LOCAL
		CALL CONTROL_MAG,ARG,-100*(CSVBASE(NO:ARG,GETNUM(BASE,"LV"),0))
	ELSEIF RESULT == 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP1
	ENDIF
ELSE
	GOTO INPUT_LOOP
ENDIF
RETURN 0

;======================================
;Skill deletion
;======================================
@MAG_SKILL_DELETE,ARG
PRINTL □Skill deletion
DRAWLINE
PRINTL どのスキルを消去しますか？

FOR LOCAL,0,FLAG:スキル数
	LOCALS = スキル{LOCAL+1}
	IF ABL:ARG:LOCALS > 0
		CALLFORM SKILL_NAME_{ABL:ARG:LOCALS},ARG
		PRINTFORM 　[{LOCAL+1}]%RESULTS,20,LEFT% 
		CALLFORM SKILL_RANK_{ABL:ARG:LOCALS}
		LOCALS = ({POWER(RESULT,2)*200}MAG)
		PRINTFORM %LOCALS,11%
	ENDIF
	SIF LOCAL % 3 == 2 || LOCAL+1 == FLAG:スキル数
		PRINTL 
NEXT
DRAWLINE
PRINTL [0]Return

$INPUT_LOOP
INPUT

IF RESULT == 0
	RETURN 0
ELSEIF RESULT < 1 || RESULT > FLAG:スキル数
	GOTO INPUT_LOOP
ELSE
	LOCALS = スキル{RESULT}
	SIF ABL:ARG:LOCALS < 1
		GOTO INPUT_LOOP
	CALLFORM SKILL_RANK_{ABL:ARG:LOCALS}
	LOCAL:2 = RESULT
	IF BASE:ARG:ＭＡＧ < POWER(LOCAL:2,2)*200
		REUSELASTLINE Not enough MAG
		CLEARLINE 2
		GOTO INPUT_LOOP
	ENDIF
	
	CALL CONTROL_MAG,ARG,-POWER(LOCAL:2,2)*200
	CALLFORM SKILL_NAME_{ABL:ARG:LOCALS}
	PRINTFORM %NAME:ARG%は%RESULTS%を忘れた。
	ABL:ARG:LOCALS = 0
ENDIF
CALL SYNC_STATUS,ARG
CALL HEALTH_CHARA,ARG







@MAG_GROWTH,ARG
DRAWLINE
PRINTL □Stamina and willpower enhancement
DRAWLINE
PRINTL どちらを強化しますか？
PRINTFORML %NAME:ARG% ＭＡＧ：{BASE:ARG:ＭＡＧ,6}
PRINTFORML [1]Stamina (Current maximum:{MAXBASE:ARG:体力}) (+100 / {POWER(MAXBASE:ARG:体力/100,2)*5}MAG)
PRINTFORML [2]Willpower (Current maximum:{MAXBASE:ARG:気力}) (+100 / {POWER(MAXBASE:ARG:気力/100,2)*10}MAG)
DRAWLINE
PRINTL [0]Return

$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 1
	IF BASE:ARG:ＭＡＧ < POWER(MAXBASE:ARG:体力/100,2)*5
		PRINTW Not enough MAG
		GOTO INPUT_LOOP
	ENDIF
	CALL CONTROL_MAG,ARG,-POWER(MAXBASE:ARG:体力/100,2)*5
	MAXBASE:ARG:体力 += 100
	PRINTFORMW %NAME:ARG%の体力が{MAXBASE:ARG:体力}になりました
ELSEIF RESULT == 2
	IF BASE:ARG:ＭＡＧ < POWER(MAXBASE:ARG:気力/100,2)*10
		PRINTW Not enough MAG
		GOTO INPUT_LOOP
	ENDIF
	CALL CONTROL_MAG,ARG,-POWER(MAXBASE:ARG:気力/100,2)*10
	MAXBASE:ARG:気力 += 100
	PRINTFORMW %NAME:ARG%の気力が{MAXBASE:ARG:気力}になりました
ELSE
	GOTO INPUT_LOOP
ENDIF


