;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;
;	Module		:DEVIL_COMPENDIUM.ERB
;	Facility	:悪魔全書一般の処理
;
;	Licence		:ライセンスフリー。
;
;	Modification Data:
;
;	Edit	Date			Author					Reason
;	001		20XX/XX/XX		N鳥						新規作成
;	002		2011/03/ 8		P						一部表記ずれを対策
;_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/_/#
;================================================================
;悪魔全書予定地
;解析度:1 〜 名称識別、会話可能 100 〜 アナライズ可能 1000〜全書呼び出し可能




;=============================================================
;悪魔全書
;=============================================================
@DEVIL_COMPENDIUM
;LOCAL ループ用
;LOCAL:1 選択種族
;LOCAL:2 選択した悪魔
;LOCAL:3 召喚した悪魔の番号
;LOCAL:4 召喚費用
;LOCAL:5 召喚可能フラグ
;LOCAL:6 既に仲魔にいるか

;LOCAL:10
;LOCAL:20
;LOCAL:30
;C 閲覧可能な種族のリスト
;D 選択した種族のリスト
DRAWLINE
PRINTL [1] 閲覧
PRINTL [2] 逆引き検索
PRINTL [3] 悪魔全書登録
DRAWLINE
PRINTL [0] Return
$INPUT_LOOP
INPUT
IF RESULT == 0
	RETURN 0
ELSEIF RESULT == 2
	CALL REVERSE_LOOKUP_DEVIL
	RETURN 0
ELSEIF RESULT == 3
	CALL REGISTRATION_COMPENDIUM
	RETURN 0
ELSEIF RESULT > 3
	GOTO INPUT_LOOP
ENDIF

$COMPENDIUM_START
CUSTOMDRAWLINE ･

;リスト初期化
VARSET C, 0
FOR LOCAL,1,17
	C:LOCAL = 1
NEXT

FOR LOCAL,1,5001
	IF EXISTCSV(LOCAL,0)
		SIF FLAG:(20000+LOCAL) > 0
			C:(CSVABL(LOCAL,GETNUM(ABL,"種族"),0)) = 1
	ENDIF
NEXT

;HalfDemon・Element（？）・Zomaは閲覧不可
C:36 = 0
C:40 = 0
C:45 = 0

PRINTL どの種族の悪魔を閲覧しますか？
FOR LOCAL,1,99
	SIF C:LOCAL != 1
		CONTINUE
	PRINTFORMLC [{LOCAL,2}] %STR:LOCAL,6,LEFT%
NEXT
PRINTL

DRAWLINE
PRINTL [0] Return
$INPUT_LOOP1
INPUT
IF RESULT == 0
	RESTART
ELSEIF C:RESULT != 1
	GOTO INPUT_LOOP1
ENDIF

LOCAL:1 = RESULT

LOCAL:10 = 0
LOCAL:20 = 0
LOCAL:30 = 2
;リストの作成
VARSET D, -1
FOR LOCAL,1,5001
	IF EXISTCSV(LOCAL,0) && CSVABL(LOCAL,GETNUM(ABL,"種族"),0) == LOCAL:1
;		SIF CSVCFLAG(LOCAL,(GETNUM(CFLAG,"全書召喚不可")),0) == 1 || FLAG:(20000+LOCAL) < 1000 || (CSVCFLAG(LOCAL,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL) == 0)
;			CONTINUE
		SIF FLAG:(20000+LOCAL) < 1
			CONTINUE
		FOR LOCAL:10,0,5000
			IF D:(LOCAL:10) == -1
					D:(LOCAL:10) = LOCAL
					BREAK
			ELSEIF CSVBASE(LOCAL,GETNUM(BASE,"LV"),0) <= CSVBASE(D:(LOCAL:10),GETNUM(BASE,"LV"),0)
				CONTINUE
			ELSE 
					FOR LOCAL:20,4999,LOCAL:10,-1
						SIF D:(LOCAL:20-1) > -1
							D:(LOCAL:20) = D:(LOCAL:20-1)
					NEXT
					D:(LOCAL:10) = LOCAL
					BREAK
			ENDIF
		NEXT
	ENDIF
NEXT

$PRINT_LIST
LOCAL:6 = 0
;リストを表示
DRAWLINE
PRINTFORML □%STR:(LOCAL:1)%
DRAWLINE
FOR LOCAL,0,5000
	SIF D:LOCAL == -1
		BREAK
	;召喚費用を算出
	LOCAL:4 = MAX(POWER(CSVBASE(D:LOCAL,GETNUM(BASE,"LV"),0),3),CSVBASE(D:LOCAL,GETNUM(BASE,"LV"),0) * 300)
	;精霊は2.5倍
	SIF LOCAL:1 == 13
		TIMES LOCAL:4 , 2.50
	SIF FLAG:(20000+D:LOCAL) < 100
		SETCOLOR 0x404040
	IF FLAG:(20000+D:LOCAL) < 1000
		LOCAL:4 *= 2 * 1000
		LOCAL:4 /= MAX(1,FLAG:(20000+D:LOCAL))
	ENDIF
	;---- EDIT 002 MOD START -------------------------
	;解析度を右揃えに変更
	PRINTFORM [{LOCAL,3}] %CSVCALLNAME(D:LOCAL,0),16,LEFT%Lv{CSVBASE(D:LOCAL,GETNUM(BASE,"LV"),0),3,RIGHT}　{FLAG:(20000+D:LOCAL)/10,3,RIGHT}％
	;---- EDIT 002 MOD ENDで -------------------------
	IF CSVTALENT(D:LOCAL, GETNUM(TALENT, "オトコ"),0)
		IF CSVTALENT(D:LOCAL, GETNUM(TALENT, "男の娘"),0)
			PRINT ？　
		ELSE
			PRINT ♂　
		ENDIF
	ELSE
		IF CSVTALENT(D:LOCAL, GETNUM(TALENT, "ふたなり"),0)
			PRINT 双　
		ELSE
			PRINT ♀　
		ENDIF
	ENDIF
	IF FLAG:(20000+D:LOCAL) < 100
		PRINTFORML  (詳細不明)
		RESETCOLOR
	ELSEIF CSVCFLAG(D:LOCAL,(GETNUM(CFLAG,"全書召喚不可")),0) == 1 || (CSVCFLAG(D:LOCAL,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+D:LOCAL) == 0) || LOCAL:1 > 18
		PRINTFORML  (召喚不可)
	ELSEIF FLAG:(20000+D:LOCAL) < 100
		PRINTFORML (解析度不足)
	ELSE
		PRINTFORML ￥{LOCAL:4,10,RIGHT}
	ENDIF
NEXT
DRAWLINE
PRINTL [1000] Return
$INPUT_LOOP2
INPUT
IF RESULT == 1000
	GOTO COMPENDIUM_START
ELSEIF RESULT < 0 || RESULT >= 5000 || D:RESULT == -1 || FLAG:(20000+D:RESULT) < 100
	GOTO INPUT_LOOP2
;ELSEIF 

ENDIF

LOCAL:2 = D:RESULT
LOCAL:3 = CHARANUM

SIF CSVCFLAG(LOCAL:2 , GETNUM(CFLAG,"１体のみ"),0) && FINDCHARA(NO,LOCAL:2) > 0
	LOCAL:9 = 1

;選んだ悪魔が既に仲魔である場合、後に渡すフラグを立てておく
SIF FINDCHARA_NO_C(LOCAL:2) > -1
	LOCAL:6 = 1

;一旦悪魔をキャラ登録
CALL ADD_NEW_COMPANION, LOCAL:2, 0, -1
CALL STRFLAG_NUM_CPD(LOCAL:3,"REFER")
;初期化する能力を記述
CFLAG:(LOCAL:3):初期LV = BASE:(LOCAL:3):LV
BASE:(LOCAL:3):ＥＸＰ = BASE:(LOCAL:3):LV* (BASE:(LOCAL:3):LV+1) * (BASE:(LOCAL:3):LV-1) * 5 /3

CFLAG:(LOCAL:3):能力強化回数 = 0
FOR LOCAL,0,6
	CFLAG:(LOCAL:3):(GET_BASESTATUS(LOCAL) + "強化回数") = 0
NEXT
CALL SYNC_STATUS,LOCAL:3
CALL HEALTH_CHARA,LOCAL:3

$PRINT_STATUS
CUSTOMDRAWLINE =
U = LOCAL:3
LOCALS = \@ CSTR:U:種族名 != "" ? %CSTR:U:種族名% # %STR:(ABL:U:種族)% \@
PRINTFORML %LOCALS%　　%NAME:U%　　
TRYCCALLFORM SHOW_INFO_PAGE_{LOCAL:30}
CATCH
ENDCATCH

;召喚費用を算出
LOCAL:4 = MAX(POWER(BASE:(LOCAL:3):LV,3),BASE:(LOCAL:3):LV*300, POWER(CSVBASE(LOCAL:2 , GETNUM(BASE , "LV"),0) ,3) ,CSVBASE(LOCAL:2,GETNUM(BASE,"LV"),0) * 300)
;精霊は2.5倍
SIF LOCAL:1 == 13
	TIMES LOCAL:4 , 2.50
IF FLAG:(20000+LOCAL:2) < 1000
	LOCAL:4 *= 2 * 1000
	LOCAL:4 /= MAX(1,FLAG:(20000+LOCAL:2))
ENDIF

;FUSION.ERBからページ送り他を輸入

;ステータスの表示切替用
PRINTFORMLC \@ LOCAL:30 > 1 ? [1007]Prev. Page # %" " * 16% \@
PRINTFORMLC                 
PRINTFORMLC \@ LOCAL:30 < 3 ? [1009]Next. Page # %" " * 16% \@
PRINTL
PRINTL

IF CSVCFLAG(LOCAL:2,(GETNUM(CFLAG,"全書召喚不可")),0) == 1 || (CSVCFLAG(LOCAL:2,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL:2) == 0) || FLAG:(20000+LOCAL:2) < 100 || LOCAL:1 > 18
	PRINTFORM [9] Return
	LOCAL:5 = 0
ELSE
	PRINTFORM この悪魔を喚び出しますか？（Summoning cost:￥{LOCAL:4}) [0]Yes [9]No
	LOCAL:5 = 1
ENDIF

SIF STRFLAG_NUM_CPD_FIND("NO",NO:(LOCAL:3)) > -1
	PRINT  [10] 登録内容を消去する
PRINTL

$INPUT_LOOP3
INPUT
IF RESULT == 1007 && LOCAL:30 > 1
	LOCAL:30 -= 1
	GOTO PRINT_STATUS
ELSEIF RESULT == 1009 && LOCAL:30 < 3
	LOCAL:30 += 1
	GOTO PRINT_STATUS
ELSEIF RESULT <= FLAG:スキル数 && RESULT > 0
	LOCALS = スキル{RESULT}
	IF ABL:U:LOCALS > 0
		CALLFORM SKILL_EXPLAIN_{ABL:U:LOCALS}
		WAIT
		GOTO PRINT_STATUS
	ELSE
		GOTO INPUT_LOOP3
	ENDIF
ELSEIF RESULT == 9
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ELSEIF RESULT == 10 &&STRFLAG_NUM_CPD_FIND("NO",NO:(LOCAL:3)) > -1
	CALL STRFLAG_NUM_CPD(LOCAL:3,"CLEAR")
	PRINTFORML %LOCALS%%NAME:U%の登録内容を消去しました
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ELSEIF RESULT == 0 && LOCAL:5 == 1
ELSE
	GOTO INPUT_LOOP3
ENDIF

;悪魔のレベル>主人のレベルだった場合駄目
IF LOCAL:9
	PRINTW 残念だがこの悪魔は２体以上仲魔にできん
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

IF LOCAL:6 == 1
	PRINTW 同じ仲魔がいるようだな
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;悪魔のレベル>主人のレベルだった場合駄目
IF BASE:(LOCAL:3):LV > BASE:MASTER:LV || CSVBASE(LOCAL:2,GETNUM(BASE,"LV"),0) > BASE:MASTER:LV
	PRINTW 残念だが……お前のレベルでは扱えん
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;ＣＯＭＰ容量が一杯
IF FLAG:ＣＯＭＰ容量 < NUM_NAKAMA()+ソフト容量()
	PRINTW ＣＯＭＰの空き容量が不足しているようだな
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;足りなければ不可能
IF MONEY < LOCAL:4
	PRINTW 資金が不足しているようだな
	DELCHARA LOCAL:3
	GOTO PRINT_LIST
ENDIF

;￥を消費
MONEY -= LOCAL:4

;作成時のセリフを表示
TRYCCALLFORM FUSION_MESSAGE_K{NO:(LOCAL:3)},LOCAL:3
CATCH
	TRYCALLFORM FUSION_MESSAGE_PUB{ABL:(LOCAL:3):会話タイプ},LOCAL:3
ENDCATCH
PRINTFORMW %STR:(LOCAL:1)% %CALLNAME:(LOCAL:3)%が仲魔になった

;種族リストに戻るよ
GOTO PRINT_LIST



;=============================================================
;悪魔解析度増加処理
;=============================================================
@INCREASE_ANALYZE,ARG,ARG:1
;ARG　増加させたい悪魔の登録番号
;ARG:1 増加させたい値

SIF NUM_HAVESKILL([[スキル:民俗学]])
	ARG:1 *= 2
FLAG:(20000+ARG) += ARG:1
SIF FLAG:(20000+ARG) > 1000
	FLAG:(20000+ARG) = 1000


