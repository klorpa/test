
@REGISTRATION_COMPENDIUM
;LOCAL:1 種族
;LOCAL:2 キャラ番号
;LOCAL:3 キャラ登録番号
;LOCAL:4 既存の全書データ呼び出し用キャラ登録番号
;LOCAL:5 キャラリストのページ送り用
;LOCAL:6 キャラ比較のページ送り用
;LOCAL:7 能力表示のページ送り用
;LOCAL:10 登録可否

;キャラリストを表示

P = 0
;表示させるキャラを抽出（Q:*に表示するキャラの登録番号、LOCAL:5に人数）
VARSET Q, -1
A = 0
REPEAT CHARANUM
	SIF ABL:COUNT:種族 == 0 || NO:COUNT == 4509
		CONTINUE
	SIF CFLAG:COUNT:この場に居ないフラグ == 1
		CONTINUE
	Q:A = COUNT
	A += 1
REND
LOCAL:5 = A

$PRINT_LIST
DRAWLINE
PRINTFORML 悪魔全書に登録したい悪魔を選んでください　＜page.{P}＞　%NAME:MASTER% LV:{BASE:MASTER:LV,3}
DRAWLINE
CALL SHOW_CHARA_LIST
DRAWLINE
PRINTFORMLC \@(P <= 0) ? %" " * 16% # [1001]Previous page\@
PRINTLC [1000]Return
PRINTFORMLC \@(P >= (LOCAL:5 - 1) / 20) ? %" " * 16% # [1009]Next page\@

$INPUT_LOOP_1
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001
	IF P > 0
		P -= 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF RESULT == 1009
	IF P < ((LOCAL:5 - 1) / 20)
		P += 1
		GOTO PRINT_LIST
	ELSE
		GOTO INPUT_LOOP_1
	ENDIF
ELSEIF RESULT < 0 || RESULT >= CHARANUM || MATCH(Q,RESULT,0) == 0
	GOTO INPUT_LOOP_1
ENDIF

;選択したキャラの能力を表示。

LOCAL:1 = ABL:RESULT:種族
LOCAL:2 = NO:RESULT
LOCAL:3 = RESULT
U = RESULT
LOCAL:7 = 2

;比較用のキャラ追加
LOCAL:4 = CHARANUM
CALL ADD_NEW_COMPANION,LOCAL:2,0,-1
CALL STRFLAG_NUM_CPD(LOCAL:4,"REFER")
CALL SYNC_STATUS,LOCAL:4
CALL HEALTH_CHARA,LOCAL:4

$PRINT_STATUS
CUSTOMDRAWLINE =
PRINTFORML  \@ U != LOCAL:4 ? 全書の登録内容 # %" " * 18% \@
LOCALS = \@ CSTR:U:種族名 != "" ? %CSTR:U:種族名% # %STR:(ABL:U:種族)% \@
PRINTFORML %LOCALS%　　%NAME:U%　　
TRYCCALLFORM SHOW_INFO_PAGE_{LOCAL:7}
CATCH
ENDCATCH


;ステータスの表示切替用
PRINTFORMLC \@ LOCAL:7 == 2 ? [1007]Prev. Page # %" " * 16% \@
PRINTFORMLC                 
PRINTFORMLC \@ LOCAL:7 == 1 ? [1009]Next. Page # %" " * 16% \@
PRINTL 
PRINTFORMLC \@ U != LOCAL:4 && LOCAL:4 > -1 ? [1010]全書の登録内容 # %" " * 18% \@
PRINTFORMLC \@ U != LOCAL:3 && LOCAL:4 > -1 ? [1011]現在のステータス # %" " * 18% \@
PRINTL

;登録するかどうか
IF CSVCFLAG(LOCAL:2,(GETNUM(CFLAG,"全書召喚不可")),0) == 1 || (CSVCFLAG(LOCAL:2,(GETNUM(CFLAG,"合体条件有り")),0) == 1 && FLAG:(10000+LOCAL:2) == 0) || LOCAL:1 > 18
	PRINTFORML この悪魔は悪魔全書から召喚できないため、登録できません
	PRINTFORML 　　　[9] Return
	LOCAL:10 = 0
ELSE
	PRINTFORM \@ U == LOCAL:3 ? この悪魔を登録しますか？ # この内容に上書きしますか？ \@
	PRINTL [0]Yes [9]No
	LOCAL:10 = 1
ENDIF
$INPUT_LOOP
INPUT
IF RESULT == 1007 && LOCAL:7 == 2
	LOCAL:7 = 1
	GOTO PRINT_STATUS
ELSEIF RESULT == 1009 && LOCAL:7 == 1
	LOCAL:7 = 2
	GOTO PRINT_STATUS
ELSEIF RESULT == 1010 && U != LOCAL:4
	U = LOCAL:4
	GOTO PRINT_STATUS
ELSEIF RESULT == 1011 && U != LOCAL:3
	U = LOCAL:3
	GOTO PRINT_STATUS
ELSEIF RESULT <= FLAG:スキル数 && RESULT > 0
	LOCALS = スキル{RESULT}
	IF ABL:U:LOCALS > 0
		CALLFORM SKILL_EXPLAIN_{ABL:U:LOCALS}
		WAIT
		GOTO PRINT_STATUS
	ELSE
		GOTO INPUT_LOOP
	ENDIF
ELSEIF RESULT == 9
	DELCHARA LOCAL:4
	RESTART
ELSEIF RESULT == 0 && LOCAL:10 == 1
	DELCHARA LOCAL:4
ELSE
	GOTO INPUT_LOOP
ENDIF

CALL STRFLAG_NUM_CPD(LOCAL:3,"ADD")



@STRFLAG_NUM_CPD,ARG = -1,ARGS
;使用SAVESTR:2000〜6999。埋まりきることはまず無いと思います
;ARGSがADDの場合、キャラ登録番号ARGのキャラの各種能力をSAVESTRに記載する。同じキャラ番号のが既にあるなら上書きする
;ARGSがREFERの場合、キャラ登録番号ARGのキャラ番号のSAVESTRがある場合それを参照してABL等に上書きする
;ARGSがCLEARの場合、最後のSAVESTRをキャラ登録番号ARGのSAVESTRに上書きして最後のを削除する。セーブデータ圧縮及び処理時間短縮のための処理

IF ARG < 0 || ARG >= CHARANUM
	STR:(-1) = "登録番号が異常です"
	RETURN
ENDIF

LOCAL:5 = GET_CPD_STRFLAG_NUM("")

IF ARGS == "CLEAR"
	LOCAL = STRFLAG_NUM_CPD_FIND("NO",NO:ARG)
	SIF LOCAL == -1
		RETURN
	LOCAL:1 = STRFLAG_NUM_CPD_FIND("",-1) - 1
	SAVESTR:LOCAL = %SAVESTR:(LOCAL:1)%
	SAVESTR:(LOCAL:1) = 
ELSEIF ARGS == "ADD"
	LOCAL = STRFLAG_NUM_CPD_FIND("NO",NO:ARG)
	SIF LOCAL == -1
		LOCAL = STRFLAG_NUM_CPD_FIND("",-1)
	SAVESTR:LOCAL = %NAME:ARG%,{NO:ARG}
	FOR LOCAL:1,2,LOCAL:5
		IF GETNUM(ABL,GET_CPD_STRFLAG(LOCAL:1)) > 0
			SAVESTR:LOCAL = %SAVESTR:LOCAL%,{ABL:ARG:GET_CPD_STRFLAG(LOCAL:1)}
		ELSEIF GETNUM(BASE,GET_CPD_STRFLAG(LOCAL:1)) > 0
			SAVESTR:LOCAL = %SAVESTR:LOCAL%,{BASE:ARG:GET_CPD_STRFLAG(LOCAL:1)}
		ELSEIF GETNUM(CFLAG,GET_CPD_STRFLAG(LOCAL:1)) > 0
			SAVESTR:LOCAL = %SAVESTR:LOCAL%,{CFLAG:ARG:GET_CPD_STRFLAG(LOCAL:1)}
		ELSE
			SAVESTR:LOCAL = %SAVESTR:LOCAL%,想定外の動作です
		ENDIF
	NEXT	
ELSEIF ARGS == "REFER"
	LOCAL = STRFLAG_NUM_CPD_FIND("NO",NO:ARG)
	SIF LOCAL == -1
		RETURN
	SPLIT SAVESTR:LOCAL, ",", LOCALS
	FOR LOCAL:1,2,LOCAL:5
		IF GETNUM(ABL,GET_CPD_STRFLAG(LOCAL:1)) > 0
			ABL:ARG:GET_CPD_STRFLAG(LOCAL:1) = TOINT(LOCALS:(LOCAL:1))
		ELSEIF GETNUM(BASE,GET_CPD_STRFLAG(LOCAL:1)) > 0
			BASE:ARG:GET_CPD_STRFLAG(LOCAL:1) = TOINT(LOCALS:(LOCAL:1))
		ELSEIF GETNUM(CFLAG,GET_CPD_STRFLAG(LOCAL:1)) > 0
			CFLAG:ARG:GET_CPD_STRFLAG(LOCAL:1) = TOINT(LOCALS:(LOCAL:1))
		ELSE
			STR:(-1) = "想定外の動作が発生しました。{LOCAL:5}"
		ENDIF
	NEXT	
ENDIF

RETURN


@STRFLAG_NUM_CPD_FIND(ARGS,ARG)
#FUNCTION
;ARGSがARGなSAVESTRを探し、そのSAVESTR番号を返す
;ARGが-1なら、空のSAVESTR番号で一番小さい番号を返す
FOR LOCAL,2000,7000
	IF SAVESTR:LOCAL == ""
		SIF ARG == -1
			RETURNF LOCAL
		RETURNF -1
	ENDIF
	SPLIT SAVESTR:LOCAL, ",", LOCALS
	SIF ARGS != "" && TOINT(LOCALS:(GET_CPD_STRFLAG_NUM(ARGS))) == ARG
		RETURNF LOCAL
NEXT

RETURNF -2

@GET_CPD_STRFLAG,ARG = -1
#FUNCTIONS
#DIMS REGISTRATIONS,100
;STRFLAG_NUM_CPDでSAVESTRにARG番目に記載される能力を返す
;力〜運はBASEとCFLAG:強化回数を加えたものが基本値となることに注意
;GETNUM(ABL or BASE or CFLAG,RESULTS)にかけて使用する

;ARGS = NAME,NO,LV,St,St強化回数,In,In強化回数,Ma,Ma強化回数,En,En強化回数,Ag,Ag強化回数,Lu,Lu強化回数,ＥＸＰ,能力強化回数,変異,変異レベル,スキル1,スキル2,スキル3,スキル4,スキル5,スキル6,スキル7,スキル8,習得スキル1,習得スキル2,習得スキル3,習得スキル4,習得スキル5,習得スキル6,習得スキル7,習得スキル8,習得スキル9,習得スキル10,習得スキル11,習得スキル12,習得スキル13,習得スキル14,習得スキル15,習得スキル16,習得スキル17,習得スキル18,習得スキル19,習得スキル20,習得LV1,習得LV2,習得LV3,習得LV4,習得LV5,習得LV6,習得LV7,習得LV8,習得LV9,習得LV10,習得LV11,習得LV12,習得LV13,習得LV14,習得LV15,習得LV16,習得LV17,習得LV18,習得LV19,習得LV20,変更相性1,変更相性値1,攻撃相性,射程,攻撃範囲
REGISTRATIONS = NAME,NO,LV,St,St強化回数,In,In強化回数,Ma,Ma強化回数,En,En強化回数,Ag,Ag強化回数,Lu,Lu強化回数,ＥＸＰ,能力強化回数,変異,変異レベル,スキル1,スキル2,スキル3,スキル4,スキル5,スキル6,スキル7,スキル8,習得スキル1,習得スキル2,習得スキル3,習得スキル4,習得スキル5,習得スキル6,習得スキル7,習得スキル8,習得スキル9,習得スキル10,習得スキル11,習得スキル12,習得スキル13,習得スキル14,習得スキル15,習得スキル16,習得スキル17,習得スキル18,習得スキル19,習得スキル20,習得LV1,習得LV2,習得LV3,習得LV4,習得LV5,習得LV6,習得LV7,習得LV8,習得LV9,習得LV10,習得LV11,習得LV12,習得LV13,習得LV14,習得LV15,習得LV16,習得LV17,習得LV18,習得LV19,習得LV20,変更相性1,変更相性値1,攻撃相性,射程,攻撃範囲
;SPLIT ARGS, ",", LOCALS
SPLIT REGISTRATIONS, ",", LOCALS
LOCALS:99 = %REGISTRATIONS%

IF ARG < 0 || ARG > 99 || LOCALS:ARG == ""
	RETURNF "登録されていません"
ELSE
	RETURNF LOCALS:ARG
ENDIF


@GET_CPD_STRFLAG_NUM(ARGS)
#FUNCTION
;STRFLAG_NUM_CPDでSAVESTRにARGSが何番目に記載されるかを返す
SPLIT GET_CPD_STRFLAG(99), ",", LOCALS

FOR LOCAL, 0, 100
	IF LOCALS:LOCAL == ARGS
		RETURNF LOCAL
	ENDIF
NEXT

RETURNF -1
