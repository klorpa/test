;================================================================
;魔晶装備
;================================================================
@MAKE_MASHO
FLAG:合体予定悪魔 = -1
;LOCAL ループ用
;LOCAL:1 素材の登録番号
LOCAL:1 = -1
;LOCAL:2 悪魔の表示用変数
;LOCAL:3 合体結果の剣枠
;LOCAL:4 作成する魔晶装備の種類
;G 合体剣のフラグを管理する変数 20-25:バッドステータス村正用

$START_MAKE
LOCAL:6 = 0

;キャラリストを表示
CALL INPUT_CHARA_LIST("素体にする悪魔を選んでください","CASTING_MASHO")
SIF RESULT == 1000
	RETURN 0
LOCAL:1 = RESULT


LOCAL:3 = -1
;空いている合体剣枠を参照
FOR LOCAL,0,50
	IF DC:LOCAL:49 == 0
		LOCAL:3 = LOCAL
		BREAK
	ENDIF
NEXT
IF LOCAL:3 == -1
	PRINTW これ以上合体剣を持てません
	RETURN 0
ENDIF

;合体剣の種類を決める
;習得スキルによってGを設定
VARSET G,-1
G:0 = 1
FOR LOCAL,1,FLAG:スキル数+1
	LOCALS = スキル{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による条件
		TRYCALLFORM SKILL_TYPE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
		G:RESULT = 1
	ENDIF
NEXT
;剣撃以外の物理は無し
G:1 = -1
G:2 = -1
G:3 = -1


;Nerve・精神は特殊
G:8 = -1
G:9 = -1
FOR LOCAL,1,FLAG:スキル数+1
	LOCALS = スキル{LOCAL}
	IF ABL:(LOCAL:1):LOCALS > 0
		RESULT = 0
		;相性による条件
		TRYCCALLFORM SKILL_ADDTIONAL_STATE_{ABL:(LOCAL:1):LOCALS},LOCAL:1
			SELECTCASE GET_STATE(RESULT)
				CASE "POISON"
					G:20 = 1
				CASE "SLEEP"
					G:21 = 1
				CASE "PANIC"
					G:22 = 1
				CASE "HAPPY"
					G:23 = 1
				CASE "BIND"
					G:24 = 1
				CASE "CHARM"
					G:25 = 1
			ENDSELECT
		CATCH
		ENDCATCH
	ENDIF
NEXT

;剣のタイプを選択
$PRINT_TYPE
DRAWLINE
PRINTL 剣の攻撃相性を選択してください
FOR LOCAL,0,FLAG:相性数
	SIF G:LOCAL > 0
		PRINTFORM [{LOCAL}] 【%GET_TYPE(LOCAL)%】
	SIF G:LOCAL > 0 && LOCAL == 5
		PRINT & FREEZE ２０％
	SIF G:LOCAL > 0 && LOCAL == 6
		PRINT & SHOCK ２０％
	SIF G:LOCAL > 0
		PRINTL
NEXT
SIF G:20 > 0
	PRINTL [20] 【Slash】& POISON ２０％
SIF G:21 > 0
	PRINTL [21] 【Slash】& SLEEP ２５％
SIF G:22 > 0
	PRINTL [22] 【Slash】& PANIC ２５％
SIF G:23 > 0
	PRINTL [23] 【Slash】& HAPPY ３５％
SIF G:24 > 0
	PRINTL [24] 【Slash】& BIND ２５％
SIF G:25 > 0
	PRINTL [25] 【Slash】& CHARM ２０％
DRAWLINE
PRINTL [100]Cancel

$INPUT_TYPE
INPUT

IF RESULT == 100
	RESTART
ELSEIF G:RESULT > 0
	;DCにデータを入力
	;まずは名称と相性、追加効果を決めておく
	SELECTCASE RESULT
		CASE 0
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 1
		CASE 4,16
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 2
		CASE 5
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 3
			DC:(LOCAL:3):45 = GET_STATE_NUM("FREEZE")
			DC:(LOCAL:3):46 = 5
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 6
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 4
			DC:(LOCAL:3):45 = GET_STATE_NUM("SHOCK")
			DC:(LOCAL:3):46 = 6
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 7
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 5
		CASE 10,11
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT
		CASE 12 TO 15
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = RESULT - 6
		CASE 20
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 12
			DC:(LOCAL:3):45 = GET_STATE_NUM("POISON")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 21
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 13
			DC:(LOCAL:3):45 = GET_STATE_NUM("SLEEP")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 22
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 14
			DC:(LOCAL:3):45 = GET_STATE_NUM("PANIC")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 23
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 15
			DC:(LOCAL:3):45 = GET_STATE_NUM("HAPPY")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 35
			DC:(LOCAL:3):48 = 95
		CASE 24
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 16
			DC:(LOCAL:3):45 = GET_STATE_NUM("BIND")
			DC:(LOCAL:3):46 = 8
			DC:(LOCAL:3):47 = 25
			DC:(LOCAL:3):48 = 95
		CASE 25
			DC:(LOCAL:3):40 = 0
			DC:(LOCAL:3):49 = 17
			DC:(LOCAL:3):45 = GET_STATE_NUM("CHARM")
			DC:(LOCAL:3):46 = 9
			DC:(LOCAL:3):47 = 20
			DC:(LOCAL:3):48 = 95
		CASE 17
			DC:(LOCAL:3):40 = RESULT
			DC:(LOCAL:3):49 = 18
	ENDSELECT
	;基本能力値修正、戦闘能力値修正を決定
	;基本
	DC:(LOCAL:3):0 += (ABL:(LOCAL:1):従順 + ABL:(LOCAL:1):欲望 + ABL:(LOCAL:1):技巧 +3)/6
	DC:(LOCAL:3):1 += (ABL:(LOCAL:1):欲望+1)/2
	DC:(LOCAL:3):3 += (ABL:(LOCAL:1):欲望+1)/2
	DC:(LOCAL:3):6 += (ABL:(LOCAL:1):従順+1)/2
	DC:(LOCAL:3):4 += (ABL:(LOCAL:1):従順+1)/2
	DC:(LOCAL:3):2 += (ABL:(LOCAL:1):技巧+1)/2
	DC:(LOCAL:3):5 += (ABL:(LOCAL:1):技巧+1)/2
	;戦闘
	DC:(LOCAL:3):10 = BASE:(LOCAL:1):LV + BASE:(LOCAL:1):力*2
	DC:(LOCAL:3):11 = BASE:(LOCAL:1):LV*8/10 + BASE:(LOCAL:1):速さ*8/10 + BASE:(LOCAL:1):運*4/10
	
	;攻撃形態は素材のものをコピー
;	DC:(LOCAL:3):41 = ABL:(LOCAL:1):最低攻撃回数
	DC:(LOCAL:3):41 = 1
;	DC:(LOCAL:3):42 = ABL:(LOCAL:1):最大攻撃回数
	DC:(LOCAL:3):42 = 1
	DC:(LOCAL:3):43 = ABL:(LOCAL:1):射程
	DC:(LOCAL:3):44 = ABL:(LOCAL:1):攻撃範囲
	
	;射程・攻撃範囲によって命中と攻撃に修正が入る
	SELECTCASE DC:(LOCAL:3):43
		CASE 2
			TIMES DC:(LOCAL:3):11 , 0.8
		CASE 3
			TIMES DC:(LOCAL:3):11 , 0.6
	ENDSELECT
	SELECTCASE DC:(LOCAL:3):44
		CASE 2
			TIMES DC:(LOCAL:3):10 , 0.7
			TIMES DC:(LOCAL:3):11 , 0.8
		CASE 3
			TIMES DC:(LOCAL:3):10 , 0.4
			TIMES DC:(LOCAL:3):11 , 0.5
	ENDSELECT
	
	DC:(LOCAL:3):39 = GET_EQUIPNUM("剣")
	
	;追加効果付きの剣は修正を加える
	SELECTCASE DC:(LOCAL:3):49
		CASE 3,4
			TIMES DC:(LOCAL:3):10 , 0.95
			TIMES DC:(LOCAL:3):11 , 0.95
		CASE 20 TO 25,17
			TIMES DC:(LOCAL:3):10 , 0.90
			TIMES DC:(LOCAL:3):11 , 0.90
	ENDSELECT
	;Def相性
	FOR LOCAL,0,FLAG:相性数
		DC:(LOCAL:3):(20+LOCAL) = 100
	NEXT
	SIF DC:(LOCAL:3):40 == 17
		TIMES DC:(LOCAL:3):10 , 0.75
	
ELSE
	CLEARLINE 1
	GOTO INPUT_TYPE
ENDIF

;合体結果の剣を表示
CALL 装備説明,(2450+LOCAL:3+450*DC:(LOCAL:3):39)



;合体するかどうか　→　合体実行（実際には既に終わっている）
PRINT この剣を作りますか？ 

CALL INPUT_YN,"Yes","No",2

SELECTCASE RESULT
	CASE 0
		ITEM:(2450+LOCAL:3+450*DC:(LOCAL:3):39) = 1
	CASE 1
		REPEAT 50
			DC:(LOCAL:3):COUNT = 0
		REND
		RESTART
ENDSELECT

PRINTFORMW %STR:(DC:(LOCAL:3):49+100)%を手に入れた

CALL FUSION_LETTER,LOCAL:1,1

;合体素材の悪魔を消去
CALL キャラ削除, LOCAL:1

ITEM:無銘の刀 -= 1

RETURN 0

GOTO START_MAKE


;==========================================================================
;魔晶装備を素材に戻す
;==========================================================================
@DEL_MASHO
PRINTFORML どの装備を素材に戻しますか？　＜page.{P}＞
DRAWLINE
;持っているアイテムを表示
VARSET G,-1
FOR LOCAL,2000,VARSIZE("ITEM")
	IF 魔晶装備(LOCAL)
		IF ITEM:LOCAL > 0
			PRINTFORMLC [{LOCAL}]%STR:((DC:(LOCAL-2450-450*((LOCAL-2000)/500)):49)+100)%
			G:LOCAL = 1
		ENDIF
	ELSE
	ENDIF
NEXT

$PRINT_LIST
DRAWLINE
PRINTL [1000] Return
$INPUT_LOOP_1
INPUT
IF RESULT == 1000
		RETURN 0
ELSEIF RESULT < 0 || RESULT >= VARSIZE("G")
	GOTO INPUT_LOOP_1
ELSEIF G:RESULT != 1
	GOTO INPUT_LOOP_1
ENDIF
LOCAL:2 = RESULT
CALL 装備説明,LOCAL:2

PRINTL この装備を素材に戻しますか？
PRINTL [0]Yes [1]No
$INPUT_LOOP2
INPUT
IF RESULT == 1
	RESTART
ELSEIF RESULT != 0
	GOTO INPUT_LOOP2
ENDIF


ITEM:無銘の刀 += 1

FOR LOCAL,0,50
	DC:(LOCAL:2-2450-450*((LOCAL:2-2000)/500)):LOCAL = 0
NEXT

ITEM:(LOCAL:2) -= 1


;================================================================
;材料にできる悪魔の条件
;================================================================
@CASTING_MASHO
SIF 契約(COUNT) > 0
	RETURN 0
SIF ABL:COUNT:種族 == 0 || ABL:COUNT:種族 > 44
	RETURN 0
SIF CFLAG:COUNT:労役フラグ == 3
	RETURN 0
SIF CFLAG:COUNT:この場に居ないフラグ == 1
	RETURN 0
SIF CFLAG:COUNT:合体不可
	RETURN 0
SIF EQUIP:COUNT:剣 != 0
	RETURN 0
SIF EQUIP:COUNT:銃 != 0
	RETURN 0
SIF EQUIP:COUNT:頭 != 0
	RETURN 0
SIF EQUIP:COUNT:胴 != 0
	RETURN 0
SIF EQUIP:COUNT:腕 != 0
	RETURN 0
SIF EQUIP:COUNT:足 != 0
	RETURN 0
RETURN 1
